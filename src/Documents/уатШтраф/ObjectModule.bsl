#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка 
		ИЛИ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка на дубли номера постановления
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатШтраф.Ссылка
	|ИЗ
	|	Документ.уатШтраф КАК уатШтраф
	|ГДЕ
	|	НЕ уатШтраф.ПометкаУдаления
	|	И уатШтраф.НомерПостановления = &НомерПостановления
	|	И уатШтраф.Ссылка <> &Ссылка");
	
	Запрос.УстановитьПараметр("НомерПостановления", НомерПостановления);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстНСТР = НСтр("en='Specified number of resolution already exists! Document: ""%1""';ru='Указанный номер постановления уже существует! Документ: ""%1""'");
		ТекстНСТР = СтрШаблон(ТекстНСТР, Выборка.Ссылка);
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
	КонецЕсли;
			
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
		Водитель = ДанныеЗаполнения.Водитель1;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		Колонна = уатОбщегоНазначения.МестонахождениеТС(ДанныеЗаполнения.ТранспортноеСредство, ТекущаяДата()).Колонна;
		Организация = ДанныеЗаполнения.Организация;
		Подразделение = ДанныеЗаполнения.Подразделение;
		ТС = ДанныеЗаполнения.ТранспортноеСредство;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
КонецПроцедуры

#КонецОбласти
