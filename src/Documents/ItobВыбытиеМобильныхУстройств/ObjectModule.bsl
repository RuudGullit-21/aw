
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ItobЦентрСпутниковогоМониторинга.МобильныйКлиент") Тогда
		МодульМобильныйКлиентСлужебный = ОбщегоНазначения.ОбщийМодуль("мкМобильныйКлиентСлужебный");		
	    МодульМобильныйКлиентСлужебный.ПроверитьНаНаличиеБолееПозднихДокументовРегистрации(ЭтотОбъект, Отказ);
	КонецЕсли;	
	
	Если Терминал.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент") Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Если Не Статус = Перечисления.мкСтатусыРегистрацииМобильныхУстройств.Новый Тогда
				ТекстСообщения = "Данные мобильного клиента отправлены на регистрацию. Отмена проведения документа запрещена.";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Статус = Перечисления.мкСтатусыРегистрацииМобильныхУстройств.Успешно;
		ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Статус = Перечисления.мкСтатусыРегистрацииМобильныхУстройств.Новый;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтарыйОбъектПривязки = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ItobЦентрСпутниковогоМониторинга.МобильныйКлиент") Тогда
		МодульМобильныйКлиентСлужебный = ОбщегоНазначения.ОбщийМодуль("мкМобильныйКлиентСлужебный");		
	    Если Не МодульМобильныйКлиентСлужебный.ОтменаРегистрацииРазрешена(ЭтотОбъект, СтарыйОбъектПривязки) Тогда	
			ТекстСообщения = "Терминал не привязан к объектам мониторинга.";
    		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	
	Если Статус = Перечисления.мкСтатусыРегистрацииМобильныхУстройств.Успешно Тогда
		// регистр ItobПривязкиТрекеров
		Движение = Движения.ItobПривязкиТрекеров.Добавить();
		Движение.Период = Дата;
		Движение.Объект = СтарыйОбъектПривязки;
		Движение.ТерминалУстановлен = Ложь;
		Движение.Терминал = Терминал;
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ItobПеремещениеМобильногоУстройства")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ItobПоступлениеМобильныхУстройств") Тогда
		Терминал = ДанныеЗаполнения.Терминал;
		Если КонецДня(ДанныеЗаполнения.Дата) >= КонецДня(ТекущаяДатаСеанса()) Тогда
			Дата = КонецДня(ДанныеЗаполнения.Дата) + 1;
		Иначе
			Дата = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли