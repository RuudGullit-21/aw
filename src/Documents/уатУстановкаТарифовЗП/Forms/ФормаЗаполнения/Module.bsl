
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ДатаСреза", ДатаСреза);
	Параметры.Свойство("флФизическиеЛица", флФизическиеЛица);
	
	Заголовок = ?(флФизическиеЛица, "Физические лица", "Сотрудники");
	
	ЗагрузитьНастройки();
	СформироватьТаблицуСотрудниковСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда 
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбранныеСотрудники = Новый Массив;
	Для Каждого ТекСтрока Из Сотрудники Цикл
		Если ТекСтрока.ФлагВыбора Тогда
			ВыбранныеСотрудники.Добавить(ТекСтрока.Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	ОповеститьОВыборе(ВыбранныеСотрудники);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого ТекСтрока Из Сотрудники Цикл
		ТекСтрока.ФлагВыбора = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для Каждого ТекСтрока Из Сотрудники Цикл
		ТекСтрока.ФлагВыбора = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиКомпоновкиНастройкиОтбор

&НаКлиенте
Процедура НастройкиКомпоновкиНастройкиОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	СформироватьТаблицуСотрудниковСервер();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "СотрудникиСотрудник" Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(Неопределено, Элементы.Сотрудники.ТекущиеДанные.Сотрудник);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()
	
	ХранилищеНастроекДанныхФорм.Сохранить(
		"Документ.уатУстановкаТарифовЗП.Форма.ФормаЗаполнения",
		ИмяНастройкиСохранения(),
		Новый ХранилищеЗначения(НастройкиКомпоновки.Настройки.Отбор));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМакетКомпоновки()
	Если флФизическиеЛица Тогда
		Возврат Документы.уатУстановкаТарифовЗП.ПолучитьМакет("КомпоновкаДанныхФизическиеЛица");
	Иначе
		Возврат Документы.уатУстановкаТарифовЗП.ПолучитьМакет("КомпоновкаДанныхСотрудники");
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройки()
	
	МакетКомпоновки            = ПолучитьМакетКомпоновки();
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(МакетКомпоновки, УникальныйИдентификатор);
	НастройкиКомпоновки.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	НастройкиКомпоновки.ЗагрузитьНастройки(МакетКомпоновки.НастройкиПоУмолчанию);
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда 
		НастройкиОтбораСотрудников = ХранилищеНастроекДанныхФорм.Загрузить(
			"Документ.уатУстановкаТарифовЗП.Форма.ФормаЗаполнения", ИмяНастройкиСохранения());
		Если НастройкиОтбораСотрудников <> Неопределено 
			И ТипЗнч(НастройкиОтбораСотрудников) = Тип("ХранилищеЗначения") 
			И Не НастройкиОтбораСотрудников.Получить() = Неопределено Тогда
			
			Для Каждого ТекОтбор Из НастройкиОтбораСотрудников.Получить().Элементы Цикл 
				НовыйОтбор = НастройкиКомпоновки.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовыйОтбор, ТекОтбор);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	НастройкиКомпоновки.Восстановить();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТаблицуСотрудниковСервер()
	
	ВыбранныеСотрудники = Новый Массив;
	Для Каждого ТекСтрока Из Сотрудники Цикл
		Если ТекСтрока.ФлагВыбора Тогда
			ВыбранныеСотрудники.Добавить(ТекСтрока.Маршрут);
		КонецЕсли;
	КонецЦикла;
	Сотрудники.Очистить();
	
	Настройки = НастройкиКомпоновки.ПолучитьНастройки();
	Если НЕ флФизическиеЛица Тогда
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаСреза", ДатаСреза);
	КонецЕсли;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ПолучитьМакетКомпоновки(),
		Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаСотрудников);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Для Каждого Сотрудник Из ТаблицаСотрудников Цикл
		НоваяСтрока = Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Сотрудник);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ИмяНастройкиСохранения()
	Если флФизическиеЛица Тогда
		ИмяНастройки = "уатУстановкаТарифовЗП_НастройкиКомпоновщикаСотрудников_Отбор";
	Иначе
		ИмяНастройки = "уатУстановкаТарифовЗП_НастройкиКомпоновщикаФизЛиц_Отбор";
	КонецЕсли;
	Возврат ИмяНастройки;
КонецФункции

#КонецОбласти
