
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатДокументФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	АвторизованныйПользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	Если ТипЗнч(АвторизованныйПользователь) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда 
		Если Параметры.Ключ.Пустая() Тогда 
			Объект.Ответственный = АвторизованныйПользователь;
			
			Если ТипЗнч(АвторизованныйПользователь.ОбъектАвторизации) = Тип("СправочникСсылка.КонтактныеЛица") 
					И ТипЗнч(АвторизованныйПользователь.ОбъектАвторизации.ОбъектВладелец) = Тип("СправочникСсылка.Контрагенты") Тогда 
				Объект.Контрагент = АвторизованныйПользователь.ОбъектАвторизации.ОбъектВладелец;
			ИначеЕсли ТипЗнч(АвторизованныйПользователь.ОбъектАвторизации) = Тип("СправочникСсылка.Контрагенты") Тогда 
				Объект.Контрагент = АвторизованныйПользователь.ОбъектАвторизации;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.Ответственный.Доступность	 = Ложь;
		Элементы.Контрагент.Доступность		 = Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.уатТС) Тогда
		Элементы.РасходыЗаполнитьНомерБортовогоУстройстваТС.Видимость = Ложь;
	КонецЕсли;
	
	// уатУправлениеАвтотранспортом.МодификацияКонфигурации
	уатМодификацияКонфигурацииПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец уатУправлениеАвтотранспортом.МодификацияКонфигурации
	
	УстановитьСвязиПараметровВыбора();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// уатУправлениеАвтотранспортом.МодификацияКонфигурации
	уатМодификацияКонфигурацииКлиентПереопределяемый.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец уатУправлениеАвтотранспортом.МодификацияКонфигурации
	
	уатЗащищенныеФункцииКлиент.уатДокументФормаЭлементаПриОткрытии(Отказ, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Если Объект.Организация = ОрганизацияПередИзменением Тогда
		Возврат;
	КонецЕсли;
	ОрганизацияПередИзменением = Объект.Организация;
	
	ПриИзмененииКонтрагентаИлиОрганизации();

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		ПриИзмененииКонтрагентаИлиОрганизации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорКонтрагента;
	
	Если ДоговорПередИзменением <> Объект.ДоговорКонтрагента Тогда
		
		ПриИзмененииДоговора();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	мВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком");
	
	уатОбщегоНазначенияТиповыеКлиент.уатНачалоВыбораЗначенияДоговораКонтрагента(Объект, ЭтотОбъект, Элемент, Объект.Контрагент, 
	Объект.ДоговорКонтрагента, мВидДоговора, СтандартнаяОбработка);
КонецПроцедуры 

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВидДоговораДоступныеЗначения = Новый Массив;
	
	мВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком");
	ВидДоговораДоступныеЗначения.Добавить(мВидДоговора);
	
	ПараметрыФормы = Новый Структура();   
	ПараметрыФормы.Вставить("Владелец", Объект.Контрагент);
	ПараметрыФормы.Вставить("ВидДоговораДоступныеЗначения", ВидДоговораДоступныеЗначения);
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура РасходыТСПриИзменении(Элемент)
	ТекСтрока = Элементы.Расходы.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекСтрока.ТС)
		И ТипЗнч(ТекСтрока.ТС) = Тип("СправочникСсылка.уатТС")
		И уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ТекСтрока.ТС, "ПринадлежностьТС")
		= ПредопределенноеЗначение("Перечисление.уатПринадлежностьТС.Собственное") Тогда
		МестонахождениеТС = уатОбщегоНазначения.МестонахождениеТС(ТекСтрока.ТС, ТекСтрока.ДатаОперации);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьНомерБортовогоУстройстваТС(Команда)
	ЗаполнитьНомерБортовогоУстройстваТССервер(Объект.Расходы)
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// уатУправлениеАвтотранспортом.МодификацияКонфигурации
&НаКлиенте
Процедура Подключаемый_уатВыполнитьКоманду(Команда)
	
	уатМодификацияКонфигурацииКлиентПереопределяемый.ВыполнитьПодключаемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры
// Конец уатУправлениеАвтотранспортом.МодификацияКонфигурации

&НаКлиенте
Процедура ЗагрузитьОтчеты(Команда)
	
	Если Объект.Расходы.Количество() > 0 Тогда
		ТекстНСТР = НСтр("en='Clear refueling table before download?';ru='Очистить таблицу расходов перед загрузкой?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗагрузитьОтчетыИзФайлаВопрос", ЭтотОбъект),
		ТекстНСТР, РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
	КонецЕсли;
	
	ЗагрузитьОтчетыИзФайлаПродолжение();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТСПоНомеруУстройства(Команда)
	
	Для Каждого ТекСтрока Из Объект.Расходы Цикл
		
		ТекСтрока.ТС = НайтиТСПоНомеруУстройства(ТекСтрока.ДатаОперации, ТекСтрока.ЭлектронноеСредство);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриИзмененииКонтрагентаИлиОрганизации()
	
	ДанныеОбменаССервером = Новый Структура("Организация, Контрагент, ДоговорКонтрагента, Дата");
	ЗаполнитьЗначенияСвойств(ДанныеОбменаССервером, Объект);
	
	// Получим данные с сервера
	ДанныеОбменаССервером.ДоговорКонтрагента = Объект.ДоговорКонтрагента;
	ЗначенияДляЗаполнения = ИзменениеКонтрагентаСервер(ДанныеОбменаССервером);
	Объект.ДоговорКонтрагента = ЗначенияДляЗаполнения.ДоговорКонтрагента;
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорКонтрагента;
	
	УстановитьСвязиПараметровВыбора();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвязиПараметровВыбора()
	
	НовыйМассив = Новый Массив();
	
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация");
	НовыйМассив.Добавить(НоваяСвязь);
	
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Контрагент");
	НовыйМассив.Добавить(НоваяСвязь);
	
	Элементы.ДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменениеКонтрагентаСервер(ДанныеДляЗаполнения)
	СтруктураПараметровДляПолученияДоговора =
		уатЗаполнениеДокументов.ПолучитьСтруктуруПараметровДляПолученияДоговораПокупки();
	
	ЗначенияДляЗаполнения = уатОбщегоНазначенияСервер.ПриИзмененииЗначенияКонтрагента(ДанныеДляЗаполнения,
							СтруктураПараметровДляПолученияДоговора);
	Возврат ЗначенияДляЗаполнения;
КонецФункции

&НаКлиенте
Процедура ПриИзмененииДоговора()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = уатОбщегоНазначенияТиповыеСервер.ПолучитьЗначениеРеквизита(Объект.ДоговорКонтрагента, "Организация");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Объект.Контрагент = уатОбщегоНазначенияТиповыеСервер.ПолучитьЗначениеРеквизита(Объект.ДоговорКонтрагента, "Владелец");
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиТСПоНомеруУстройства(ДатаОперации, НомерУстройства)
	
	ТС = Справочники.уатТС.ПустаяСсылка();
	СтруктураОтбора = Новый Структура("ВнешняяСистема", Справочники.уатВнешниеСистемы.Автодор);
	
	ЗаписьРегистра = РегистрыСведений.уатИсторияЗакрепленияБортовыхУстройств.СрезПоследних(ДатаОперации, СтруктураОтбора);
	
	Для Каждого ТекСтрока Из ЗаписьРегистра Цикл
		
		Если ТекСтрока.НомерБортовогоУстройства = НомерУстройства Тогда
			ТС = ТекСтрока.ТС;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат ТС;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьНомерБортовогоУстройстваТССервер(Знач Расходы)
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	тТС.ТС КАК ТС,
	               |	тТС.ЭлектронноеСредство КАК ЭлектронноеСредство,
	               |	тТС.ДатаОперации КАК ДатаОперации
	               |ПОМЕСТИТЬ втТС
	               |ИЗ
	               |	&тТС КАК тТС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	уатТС.Ссылка КАК Ссылка,
	               |	втТС.ЭлектронноеСредство КАК ЭлектронноеСредство,
	               |	втТС.ДатаОперации КАК ДатаОперации
	               |ИЗ
	               |	Справочник.уатТС КАК уатТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТС КАК втТС
	               |		ПО уатТС.Ссылка = втТС.ТС
	               |			И (уатТС.НомерБортовогоУстройстваАвтодор = """")
	               |			И (втТС.ЭлектронноеСредство <> """")";
	
	Запрос.УстановитьПараметр("тТС", Расходы.Выгрузить());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.НомерБортовогоУстройстваАвтодор = Выборка.ЭлектронноеСредство;
		СправочникОбъект.ДополнительныеСвойства.Вставить("ДатаОперации", Выборка.ДатаОперации);
		СправочникОбъект.ДополнительныеСвойства.Вставить("ЗаписьНомераБортовогоУстройтваТС", Истина);
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗагрузитьОтчетыИзФайлаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Расходы.Очистить();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗагрузитьОтчетыИзФайлаПродолжение(); 
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыИзФайлаПродолжение()
	Попытка
		ПараметрыОткрытия = Новый Структура();
		ОткрытьФорму("Документ.уатОтчетСистемыАвтодор.Форма.ФормаЗагрузки",ПараметрыОткрытия,ЭтотОбъект,,,, Новый ОписаниеОповещения("ЗагрузитьОтчетыИзФайлаЗавершение", ЭтотОбъект),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Исключение
		ТекстНСТР = НСтр("ru = 'Ошибка при заполнении табличной части ""Расходы"": '; en = 'Error while filling tabular section ""Refueling"": '");
		ТекстНСТР = ТекстНСТР + ОписаниеОшибки();
		ПоказатьПредупреждение(Неопределено, ТекстНСТР);
		Возврат;
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыИзФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
		
		Для Каждого ТекСтрока Из Результат Цикл
			Если ТекСтрока.Свойство("ЭлектронноеСредство") Тогда
				ТС = НайтиТСПоНомеруУстройства(ТекСтрока.ДатаОперации, Лев(ТекСтрока.ЭлектронноеСредство,19));
			КонецЕсли;
			НоваяЗаправка = Объект.Расходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗаправка, ТекСтрока);
			НоваяЗаправка.ТС          = ТС;
		КонецЦикла;
		Модифицированность = Истина;
	КонецЕсли;
	
	Объект.Расходы.Сортировать("ДатаОперации");
	
КонецПроцедуры

#КонецОбласти
