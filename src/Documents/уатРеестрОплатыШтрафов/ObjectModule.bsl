#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		Если ЗначениеЗаполнено(IDРеестра)
			И Штрафы.Количество() <> 0 Тогда
			
			Если ЗначениеЗаполнено(Штрафы[0].Штраф.УчетнаяЗапись) Тогда
				УчетныеЗаписиСервисаШтрафов = Штрафы[0].Штраф.УчетнаяЗапись;
			Иначе
				УчетныеЗаписиСервисаШтрафов = Штрафы[0].Штраф.ТС.УчетнаяЗаписьCервисаШтрафов;
			КонецЕсли;
		
			ТекстОшибки = "";
			уатИнтеграции_проф.ШтрафовНет_УдалениеРеестра(УчетныеЗаписиСервисаШтрафов, Ссылка, IDРеестра, ТекстОшибки);
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Иначе
				IDРеестра = "";
			КонецЕсли
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(IDРеестра) 
		И Штрафы.Количество() <> 0 Тогда
		
		Если ЗначениеЗаполнено(Штрафы[0].Штраф.УчетнаяЗапись) Тогда
			УчетныеЗаписиСервисаШтрафов = Штрафы[0].Штраф.УчетнаяЗапись;
		Иначе
			УчетныеЗаписиСервисаШтрафов = Штрафы[0].Штраф.ТС.УчетнаяЗаписьCервисаШтрафов;
		КонецЕсли;
		
		ТекстОшибки   = "";
		ДанныеРеестра = Новый Соответствие();
		IDРеестра     = уатИнтеграции_проф.ШтрафовНет_СозданиеРеестра(УчетныеЗаписиСервисаШтрафов, Ссылка, ТекстОшибки, ДанныеРеестра);
		Если НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
			Если ТипЗнч(ДанныеРеестра) = Тип("Соответствие") Тогда
				СуммаОплачиваемыхПостановлений = ДанныеРеестра.Получить("amount");
				ИтоговаяСтоимость              = ДанныеРеестра.Получить("totalAmount");
				Комиссия                       = СуммаОплачиваемыхПостановлений - ИтоговаяСтоимость;
				ДатаСозданияРеестра            = уатИнтеграции_проф.ПреобразоватьДатуИзСтроки(ДанныеРеестра.Получить("createdAt"));
			КонецЕсли;
			
			items = ДанныеРеестра.Получить("items");
			Если ТипЗнч(items) = Тип("Массив") Тогда
				Для Каждого ТекСтрока Из items Цикл
					uin = ТекСтрока.Получить("uin");
					Для Каждого ТекСтрокаШтраф Из Штрафы Цикл
						Если ТекСтрокаШтраф.Штраф.НомерПостановления = uin Тогда
							ТекСтрокаШтраф.Статус        = уатИнтеграции_проф.УстановитьТекущееСостояниеОплатыПостановления(ТекСтрока.Получить("status"));
							ТекСтрокаШтраф.ПричинаОтказа = уатИнтеграции_проф.УстановитьПричинуОтказаОплатыПостановления(ТекСтрока.Получить("inactiveReason"));
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			Конецесли;

		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти


#КонецЕсли