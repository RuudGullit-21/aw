#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		АвтоТест = Истина;
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("ТолькоПросмотр") И Параметры.ТолькоПросмотр = Истина Тогда
		ТолькоПросмотр = Истина;
		Элементы.ФормаОК.Доступность = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("флМоточасы") И Параметры.флМоточасы = Истина Тогда
		Элементы.ПробегПоОдометру.Заголовок = НСтр("en='Operation time by counter';ru='Наработка по счетчику'");
		Элементы.ПробегПоGPS.Заголовок      = НСтр("en='Result by monitoring data';ru='Наработка по мониторингу'");
		Элементы.ПробегПоОдометру.Подсказка = НСтр("en='Operation time on moto hours counter, h';ru='Наработка по счетчику моточасов, ч'");
		Элементы.ПробегПоGPS.Подсказка      = НСтр("en='Operation time by monitoring system sensor, h';ru='Наработка по датчику системы мониторинга, ч'");
		Заголовок = НСтр("en='Input operation time on moto hours';ru='Ввод наработки'");
	КонецЕсли;
	
	Если Параметры.Свойство("ПробегПоОдометру") Тогда
		ПробегПоОдометру = Параметры.ПробегПоОдометру;
	КонецЕсли;
	Если Параметры.Свойство("ПробегПоGPS") Тогда
		ПробегПоGPS = Параметры.ПробегПоGPS;
	КонецЕсли;
	
	// сброс настроек размеров формы и положения окна до стандартных
	КлючОбъекта = "Документ.уатПутевойЛист.Форма.ВводЗначенийПробега/НастройкиОкна";
	ХранилищеСистемныхНастроек.Удалить(КлючОбъекта,"", ИмяПользователя());
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если АвтоТест тогда
		Возврат;
	КонецЕсли;
	
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(Неопределено,
			НСтр("en='Immediate opening for this object is prohibited!';ru='Непосредственное открытие для данного объекта запрещено!'"), 10);
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	мРезультат = Новый Структура("ПробегПоОдометру, ПробегПоGPS", ПробегПоОдометру, ПробегПоGPS);
	Закрыть(мРезультат);
КонецПроцедуры

#КонецОбласти
