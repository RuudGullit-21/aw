
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ТС") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ГСМ") Тогда
		ГСМ = Параметры.ГСМ;
	Иначе
		ГСМ = Параметры.ТС.Модель.ОсновноеТопливо;
	КонецЕсли;
	
	// формирование заголовка формы
	Если Параметры.Свойство("ЭтоТЖ") И Параметры.ЭтоТЖ = ИСТИНА Тогда
		Заголовок = "Расшифровка нормы расхода ТЖ";
	Иначе
		Заголовок = "Расшифровка нормы расхода ГСМ";
	КонецЕсли;
		
	// получаем аналоги
	ЭтоПЛ = Параметры.Свойство("ЗаправкиГСМ");
	ЭтоТехПЛ = ЭтоПЛ И (ТипЗнч(Параметры.ПЛ) = Тип("ДокументСсылка.уатТехнологическийПутевойЛист"));
	тзАналогиГСМ = уатОбщегоНазначения.уатПолучитьАналогиГСМ(ГСМ);
	ВидимостьТблОстаткиОбороты = ЭтоПЛ И тзАналогиГСМ.Количество() > 0;
			
	// Флаг для отображения надписи "Не проведен" в таблице "Остатки и обороты с учетом аналогов"
	Если ЭтоТехПЛ Тогда
		ПЛпроведен = Параметры.ПЛ.Проведен;
	ИначеЕсли ЭтоПЛ Тогда
		ПЛпроведен = Параметры.ПЛ.Проведен И Параметры.ПЛ.Рассчитан;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокНормРасхода") Тогда
		Для Каждого ТекЭл Из Параметры.СписокНормРасхода Цикл
			НоваяСтрока = РасшифровкаНорм.Добавить();
			НоваяСтрока.Норма = ТекЭл.Представление;
			НоваяСтрока.Значение = Формат(ТекЭл.Значение, "ЧДЦ=3");
		КонецЦикла;
	КонецЕсли;
	
	Если ВидимостьТблОстаткиОбороты Тогда
		флТЖ = (Параметры.Свойство("ЭтоТЖ") И Параметры.ЭтоТЖ = ИСТИНА);
		
		Если флТЖ Тогда
			ТипКолонкиКоличество = Новый ОписаниеТипов("Число");
		Иначе
			ТочностьОстТоплива = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(Параметры.ПЛ.Организация,
				ПланыВидовХарактеристик.уатПраваИНастройки.ТочностьОстатковТоплива);
			ТипКолонкиКоличество = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, ТочностьОстТоплива));
		КонецЕсли;
			
		тблЗаправкиГСМ = Новый ТаблицаЗначений;
		тблЗаправкиГСМ.Колонки.Добавить("ГСМ", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		тблЗаправкиГСМ.Колонки.Добавить("Количество", ТипКолонкиКоличество);
		Для Каждого ТекДок Из Параметры.ЗаправкиГСМ Цикл
			НоваяСтрока = тблЗаправкиГСМ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекДок);
		КонецЦикла;
		
		тблСливыГСМ = Новый ТаблицаЗначений;
		тблСливыГСМ.Колонки.Добавить("ГСМ", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		тблСливыГСМ.Колонки.Добавить("Количество", ТипКолонкиКоличество);
		Если Параметры.Свойство("СливыГСМ") Тогда
			Для Каждого ТекДок Из Параметры.СливыГСМ Цикл
				НоваяСтрока = тблСливыГСМ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекДок);
			КонецЦикла;
		КонецЕсли;
				
		Если Параметры.Свойство("ЭтоТЖ") И Параметры.ЭтоТЖ = ИСТИНА Тогда
			Элементы.ГруппаОстаткиГСМТЖ.Заголовок = "Остатки и обороты ТЖ с учетом аналогов";
			Элементы.ОстаткиГСМГСМ.Заголовок = "ТЖ";
			
			Запрос = Новый Запрос(		
			"ВЫБРАТЬ
			|	ЗаправкиГСМ.ГСМ КАК ГСМ,
			|	ЗаправкиГСМ.Количество КАК Количество
			|ПОМЕСТИТЬ ЗаправкиГСМ
			|ИЗ
			|	&ЗаправкиГСМ КАК ЗаправкиГСМ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СливыГСМ.ГСМ КАК ГСМ,
			|	СливыГСМ.Количество КАК Количество
			|ПОМЕСТИТЬ СливыГСМ
			|ИЗ
			|	&СливыГСМ КАК СливыГСМ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уатОборотыТЖнаТС.ГСМ КАК ГСМ,
			|	СУММА(уатОборотыТЖнаТС.КоличествоРасходОборот) КАК Количество
			|ПОМЕСТИТЬ РасходТЖ
			|ИЗ
			|	РегистрНакопления.уатОборотыТЖнаТС.Обороты(
			|			,
			|			,
			|			Регистратор,
			|			ТС = &ТС
			|				И ГСМ В (&ГСМ)) КАК уатОборотыТЖнаТС
			|ГДЕ
			|	уатОборотыТЖнаТС.Регистратор = &ПЛ
			|
			|СГРУППИРОВАТЬ ПО
			|	уатОборотыТЖнаТС.ГСМ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ОстаткиГСМ.ТЖ КАК ГСМ,
			|	ОстаткиГСМ.КоличествоОстаток КАК НачОстаток,
			|	ОстаткиГСМ.КоличествоОстаток КАК КонОстаток,
			|	0 КАК РасходФакт,
			|	0 КАК Выдано,
			|	0 КАК Сдано
			|ПОМЕСТИТЬ ТаблицаДанные
			|ИЗ
			|	РегистрНакопления.уатОстаткиТЖнаТС.Остатки(
			|			&ДатаВыезда,
			|			ТЖ В (&ГСМ)
			|				И ТС = &ТС) КАК ОстаткиГСМ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РасходТЖ.ГСМ,
			|	0,
			|	-РасходТЖ.Количество,
			|	РасходТЖ.Количество,
			|	0,
			|	0
			|ИЗ
			|	РасходТЖ КАК РасходТЖ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗаправкиГСМ.ГСМ,
			|	0,
			|	ЗаправкиГСМ.Количество,
			|	0,
			|	ЗаправкиГСМ.Количество,
			|	0
			|ИЗ
			|	ЗаправкиГСМ КАК ЗаправкиГСМ
			|ГДЕ
			|	ЗаправкиГСМ.ГСМ В(&ГСМ)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СливыГСМ.ГСМ,
			|	0,
			|	-СливыГСМ.Количество,
			|	0,
			|	0,
			|	СливыГСМ.Количество
			|ИЗ
			|	СливыГСМ КАК СливыГСМ
			|ГДЕ
			|	СливыГСМ.ГСМ В(&ГСМ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаДанные.ГСМ КАК ГСМ,
			|	СУММА(ТаблицаДанные.НачОстаток) КАК НачОстаток,
			|	СУММА(ТаблицаДанные.КонОстаток) КАК КонОстаток,
			|	СУММА(ТаблицаДанные.РасходФакт) КАК РасходФакт,
			|	СУММА(ТаблицаДанные.Выдано) КАК Выдано,
			|	СУММА(ТаблицаДанные.Сдано) КАК Сдано
			|ИЗ
			|	ТаблицаДанные КАК ТаблицаДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаДанные.ГСМ");
			
			// добавляем к аналогам основное ГСМ
			НоваяСтрока = тзАналогиГСМ.Добавить();
			НоваяСтрока.ГСМ    = ГСМ;
			НоваяСтрока.Аналог = ГСМ;
			
			Запрос.УстановитьПараметр("ДатаВыезда", Параметры.ДатаВыезда);
			Запрос.УстановитьПараметр("ПЛ", Параметры.ПЛ);
			Запрос.УстановитьПараметр("ТС", Параметры.ТС);
			Запрос.УстановитьПараметр("ГСМ", тзАналогиГСМ.ВыгрузитьКолонку("Аналог"));
			Запрос.УстановитьПараметр("ЗаправкиГСМ", тблЗаправкиГСМ);
			Запрос.УстановитьПараметр("СливыГСМ", тблСливыГСМ);
			
			тблОстаткиИОбороты = Запрос.Выполнить().Выгрузить();
			ОстаткиИОборотыГСМ.Загрузить(тблОстаткиИОбороты);
			
		Иначе
			Элементы.ГруппаОстаткиГСМТЖ.Заголовок = "Остатки и обороты ГСМ с учетом аналогов";
			Элементы.ОстаткиГСМГСМ.Заголовок = "ГСМ";
			
			ТекФормат = "ЧДЦ=" + ТочностьОстТоплива;
			Элементы.ОстаткиГСМКоличество.Формат = ТекФормат;
			Элементы.ОстаткиГСМВыдано.Формат     = ТекФормат;
			Элементы.ОстаткиГСМСдано.Формат      = ТекФормат;
			Элементы.ОстаткиГСМРасходФакт.Формат = ТекФормат;
			Элементы.ОстаткиГСМКонОстаток.Формат = ТекФормат;
			
			Запрос = Новый Запрос(		
			"ВЫБРАТЬ
			|	ЗаправкиГСМ.ГСМ КАК ГСМ,
			|	ЗаправкиГСМ.Количество КАК Количество
			|ПОМЕСТИТЬ ЗаправкиГСМ
			|ИЗ
			|	&ЗаправкиГСМ КАК ЗаправкиГСМ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СливыГСМ.ГСМ КАК ГСМ,
			|	СливыГСМ.Количество КАК Количество
			|ПОМЕСТИТЬ СливыГСМ
			|ИЗ
			|	&СливыГСМ КАК СливыГСМ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уатРасходГСМ.ГСМ КАК ГСМ,
			|	СУММА(уатРасходГСМ.РасходПоФактуОборот) КАК Количество
			|ПОМЕСТИТЬ РасходГСМнаТС
			|ИЗ
			|	РегистрНакопления.уатРасходГСМнаТС.Обороты(
			|			,
			|			,
			|			Регистратор,
			|			ТС = &ТС
			|				И ГСМ В (&ГСМ)) КАК уатРасходГСМ
			|ГДЕ
			|	уатРасходГСМ.Регистратор = &ПЛ
			|
			|СГРУППИРОВАТЬ ПО
			|	уатРасходГСМ.ГСМ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ОстаткиГСМ.ГСМ КАК ГСМ,
			|	ОстаткиГСМ.КоличествоОстаток КАК НачОстаток,
			|	ОстаткиГСМ.КоличествоОстаток КАК КонОстаток,
			|	0 КАК РасходФакт,
			|	0 КАК Выдано,
			|	0 КАК Сдано
			|ПОМЕСТИТЬ ТаблицаДанные
			|ИЗ
			|	РегистрНакопления.уатОстаткиГСМнаТС.Остатки(
			|			&ДатаВыезда,
			|			ГСМ В (&ГСМ)
			|				И ТС = &ТС) КАК ОстаткиГСМ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РасходГСМнаТС.ГСМ,
			|	0,
			|	-РасходГСМнаТС.Количество,
			|	РасходГСМнаТС.Количество,
			|	0,
			|	0
			|ИЗ
			|	РасходГСМнаТС КАК РасходГСМнаТС
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗаправкиГСМ.ГСМ,
			|	0,
			|	ЗаправкиГСМ.Количество,
			|	0,
			|	ЗаправкиГСМ.Количество,
			|	0
			|ИЗ
			|	ЗаправкиГСМ КАК ЗаправкиГСМ
			|ГДЕ
			|	ЗаправкиГСМ.ГСМ В(&ГСМ)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СливыГСМ.ГСМ,
			|	0,
			|	-СливыГСМ.Количество,
			|	0,
			|	0,
			|	СливыГСМ.Количество
			|ИЗ
			|	СливыГСМ КАК СливыГСМ
			|ГДЕ
			|	СливыГСМ.ГСМ В(&ГСМ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаДанные.ГСМ КАК ГСМ,
			|	СУММА(ТаблицаДанные.НачОстаток) КАК НачОстаток,
			|	СУММА(ТаблицаДанные.КонОстаток) КАК КонОстаток,
			|	СУММА(ТаблицаДанные.РасходФакт) КАК РасходФакт,
			|	СУММА(ТаблицаДанные.Выдано) КАК Выдано,
			|	СУММА(ТаблицаДанные.Сдано) КАК Сдано
			|ИЗ
			|	ТаблицаДанные КАК ТаблицаДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаДанные.ГСМ
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТаблицаДанные.ГСМ = &ОсновноеТопливо УБЫВ,
			|	ТаблицаДанные.ГСМ");
			
			// добавляем к аналогам основное ГСМ
			НоваяСтрока = тзАналогиГСМ.Добавить();
			НоваяСтрока.ГСМ    = ГСМ;
			НоваяСтрока.Аналог = ГСМ;
			
			Запрос.УстановитьПараметр("ДатаВыезда", Параметры.ДатаВыезда);
			Запрос.УстановитьПараметр("ПЛ", Параметры.ПЛ);
			Запрос.УстановитьПараметр("ТС", Параметры.ТС);
			Запрос.УстановитьПараметр("ГСМ", тзАналогиГСМ.ВыгрузитьКолонку("Аналог"));
			Запрос.УстановитьПараметр("ОсновноеТопливо", Параметры.ТС.Модель.ОсновноеТопливо);
			Запрос.УстановитьПараметр("ЗаправкиГСМ", тблЗаправкиГСМ);
			Запрос.УстановитьПараметр("СливыГСМ", тблСливыГСМ);
			
			тблОстаткиИОбороты = Запрос.Выполнить().Выгрузить();
			ОстаткиИОборотыГСМ.Загрузить(тблОстаткиИОбороты);
			
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаОстаткиГСМТЖ.Видимость = ВидимостьТблОстаткиОбороты И НЕ ЭтоТехПЛ;
	Элементы.ГруппаПредупреждение.Видимость = Параметры.Свойство("НеПересчитыватьРасходГСМ") И Параметры.НеПересчитыватьРасходГСМ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(Неопределено,
			НСтр("en='Immediate opening for this object is prohibited!';ru='Непосредственное открытие для данного объекта запрещено!'"), 10);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
