
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	флМЛ = (Параметры.Свойство("ИмяДокумента") И Параметры.ИмяДокумента = "уатМаршрутныйЛист");
	
	Для Сч = 1 По 5 Цикл
		Если Параметры.Свойство("СпецРабота" + Сч) Тогда
			ЭтотОбъект["СпецРабота" + Сч] = Параметры["СпецРабота" + Сч];
		КонецЕсли;
		Если Параметры.Свойство("КоличествоСпецРаботы" + Сч) Тогда 
			Если НЕ флМЛ И ЭтотОбъект["СпецРабота" + Сч].Временный Тогда 
				ЭтотОбъект["КоличествоСпецРаботы" + Сч] = уатОбщегоНазначения.уатВремяВЧЧ_ММ(Параметры["КоличествоСпецРаботы" + Сч]);
			Иначе
				ЭтотОбъект["КоличествоСпецРаботы" + Сч] = Параметры["КоличествоСпецРаботы" + Сч];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
	Если НЕ флМЛ И Параметры.Свойство("ТС") Тогда
		Если Параметры.ТС.Модель.НаличиеСпидометра Тогда
			НадписьПояснение = НСтр("en='Value of total mileage is reduced by mileage of special. works in calculation of fuels consumption.';ru='Значение общего пробега уменьшается на пробег спец. работ при расчете расхода ГСМ.'");
		Иначе
			НадписьПояснение = НСтр("en='Value of time reduced by the time of spec. works in calculation of fuels consumption.';ru='Значение времени в работе уменьшается на время спец. работ при расчете расхода ГСМ.'");
		КонецЕсли;
	Иначе
		Элементы.НадписьПояснение.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьОтборПараметровВыработки();
	ЗаполнитьДопВыработкуПоУмолчанию();
	УстановитьВидимостьДоступность();
	
	// По логике установка ТолькоПросмотр = Истина для формы должна блокировать все контролы.
	// Но нет, 1С считает по другому. Поэтому приходится писать этот костыль
	Если ТолькоПросмотр Тогда
		Элементы.ГруппаВерх.ТолькоПросмотр = Истина;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпецРабота1ПриИзменении(Элемент)
	
	КоличествоСпецРаботы1 = 0;
	УстановитьВидимостьДоступность();
		
КонецПроцедуры

&НаКлиенте
Процедура СпецРабота2ПриИзменении(Элемент)
	
	КоличествоСпецРаботы2 = 0;
	УстановитьВидимостьДоступность();
		
КонецПроцедуры

&НаКлиенте
Процедура СпецРабота3ПриИзменении(Элемент)
	
	КоличествоСпецРаботы3 = 0;
	УстановитьВидимостьДоступность();
		
КонецПроцедуры

&НаКлиенте
Процедура СпецРабота4ПриИзменении(Элемент)
	
	КоличествоСпецРаботы4 = 0;
	УстановитьВидимостьДоступность();
		
КонецПроцедуры

&НаКлиенте
Процедура СпецРабота5ПриИзменении(Элемент)
	
	КоличествоСпецРаботы5 = 0;
	УстановитьВидимостьДоступность();
		
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСпецРаботы1ПриИзменении(Элемент)
	
	Если ЭтоВременнаяСпецРабота(СпецРабота1) Тогда 
		уатЗащищенныеФункцииСервер.КонтрольВводаВремени(КоличествоСпецРаботы1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСпецРаботы2ПриИзменении(Элемент)
	
	Если ЭтоВременнаяСпецРабота(СпецРабота2) Тогда 
		уатЗащищенныеФункцииСервер.КонтрольВводаВремени(КоличествоСпецРаботы2);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСпецРаботы3ПриИзменении(Элемент)
	
	Если ЭтоВременнаяСпецРабота(СпецРабота3) Тогда 
		уатЗащищенныеФункцииСервер.КонтрольВводаВремени(КоличествоСпецРаботы3);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСпецРаботы4ПриИзменении(Элемент)
	
	Если ЭтоВременнаяСпецРабота(СпецРабота4) Тогда 
		уатЗащищенныеФункцииСервер.КонтрольВводаВремени(КоличествоСпецРаботы4);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСпецРаботы5ПриИзменении(Элемент)
	
	Если ЭтоВременнаяСпецРабота(СпецРабота5) Тогда 
		уатЗащищенныеФункцииСервер.КонтрольВводаВремени(КоличествоСпецРаботы5);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьДанные(Команда)
	
	Если Не ДанныеУказаныВерно() Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	Для Сч = 1 По 5 Цикл
		ПараметрыЗакрытия.Вставить("СпецРабота" + Сч, ЭтотОбъект["СпецРабота" + Сч]);
		Если НЕ флМЛ И ЭтоВременнаяСпецРабота(ЭтотОбъект["СпецРабота" + Сч]) Тогда
			ПараметрыЗакрытия.Вставить("КоличествоСпецРаботы" + Сч, уатОбщегоНазначения.уатВремяВСекунды(ЭтотОбъект["КоличествоСпецРаботы" + Сч]));
		Иначе
			ПараметрыЗакрытия.Вставить("КоличествоСпецРаботы" + Сч, ЭтотОбъект["КоличествоСпецРаботы" + Сч]);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Для Сч = 1 По 5 Цикл 
		СпецРабота = ЭтотОбъект["СпецРабота" + Формат(Сч, "ЧГ=0")];
		
		КоличествоТекст = НСтр("en='Quantity';ru='Количество'");
		Если ЗначениеЗаполнено(СпецРабота) Тогда
			КоличествоТекст = КоличествоТекст + ", " + СпецРабота.ЕдиницаИзмерения;
		КонецЕсли;
		
		Если СпецРабота.Временный Тогда 
			КоличествоФорматРедактирования = "ЧДЦ=2; ЧРД=:; ЧГ=";
		Иначе 
			КоличествоФорматРедактирования = "ЧЦ=7; ЧДЦ=1";
		КонецЕсли;
		
		КоличествоВидимость = НЕ СпецРабота.ОтображениеВВидеФлага;
		КоличествоФлагВидимость = СпецРабота.ОтображениеВВидеФлага;
		
		ЭлементКоличествоСпецРаботы = Элементы["КоличествоСпецРаботы" + Формат(Сч, "ЧГ=0")];
		ЭлементКоличествоСпецРаботы.Заголовок = КоличествоТекст;
		ЭлементКоличествоСпецРаботы.ФорматРедактирования = КоличествоФорматРедактирования;
		ЭлементКоличествоСпецРаботы.Видимость = НЕ СпецРабота.ОтображениеВВидеФлага;
		
		ЭлементКоличествоСпецРаботыФлаг = Элементы["КоличествоСпецРаботы" + Формат(Сч, "ЧГ=0") + "Флаг"];
		ЭлементКоличествоСпецРаботыФлаг.Видимость = СпецРабота.ОтображениеВВидеФлага;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоВременнаяСпецРабота(Знач СпецРабота)
	
	Возврат СпецРабота.Временный;
	
КонецФункции // ЭтоВременнаяСпецРабота()

&НаСервере
Функция ДанныеУказаныВерно()
	
	Отказ = Ложь;
	
	Для СчВнешний = 1 По 5 Цикл 
		//Если ЗначениеЗаполнено(ЭтотОбъект["СпецРабота"+Формат(СчВнешний, "ЧГ=0")]) И Не ЗначениеЗаполнено(ЭтотОбъект["КоличествоСпецРаботы"+Формат(СчВнешний, "ЧГ=0")]) Тогда 
		//	ТекстНСТР = СтрШаблон(НСтр("en='Not specified number of special work %1';ru='Не указано количество специальной работы %1'"), Формат(СчВнешний, "ЧГ=0"));
		//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР,, "КоличествоСпецРаботы"+Формат(СчВнешний, "ЧГ=0"),, Отказ);
		//КонецЕсли;
		Если Не ЗначениеЗаполнено(ЭтотОбъект["СпецРабота"+Формат(СчВнешний, "ЧГ=0")]) И ЗначениеЗаполнено(ЭтотОбъект["КоличествоСпецРаботы"+Формат(СчВнешний, "ЧГ=0")]) Тогда 
			ТекстНСТР = СтрШаблон(НСтр("en='Not specified special work %1';ru='Не указана специальная работа %1'"), Формат(СчВнешний, "ЧГ=0"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР,, "СпецРабота"+Формат(СчВнешний, "ЧГ=0"),, Отказ);
		КонецЕсли;
		
		Для СчВнутр = СчВнешний+1 По 5 Цикл 
			Если ЗначениеЗаполнено(ЭтотОбъект["СпецРабота"+Формат(СчВнешний, "ЧГ=0")]) И ЭтотОбъект["СпецРабота"+Формат(СчВнешний, "ЧГ=0")] = ЭтотОбъект["СпецРабота"+Формат(СчВнутр, "ЧГ=0")] Тогда 
				ТекстНСТР = НСтр("en='This special work has already been specified earlier.';ru='Данная специальная работа уже была указана ранее.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР,, "СпецРабота"+Формат(СчВнутр, "ЧГ=0"),, Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДопВыработкуПоУмолчанию()
	Для Сч = 1 По 5 Цикл
		Если ЗначениеЗаполнено(ЭтотОбъект["СпецРабота" + Сч]) Тогда
			Возврат; // выработка уже указана, не заполняем
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатПараметрыВыработки.Ссылка
	|ИЗ
	|	Справочник.уатПараметрыВыработки КАК уатПараметрыВыработки
	|ГДЕ
	|	(НЕ &флМЛ И уатПараметрыВыработки.ИспользоватьВСпецработахПЛ
	|	ИЛИ &флМЛ И уатПараметрыВыработки.ИспользоватьВДопВыработкеМЛ)
	|	И НЕ уатПараметрыВыработки.ПометкаУдаления
	|	И уатПараметрыВыработки.СпособВводаЗначений = ЗНАЧЕНИЕ(Перечисление.уатСпособыВводаЗначенийВыработкивМЛ_уэ.ВТабличнойЧасти)");
	Запрос.УстановитьПараметр("флМЛ", флМЛ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сч = 1;
	Пока Сч <= 5 И Выборка.Следующий() Цикл
		ЭтотОбъект["СпецРабота" + Сч] = Выборка.Ссылка;
		
		Сч = Сч + 1;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПараметровВыработки()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатПараметрыВыработки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.уатПараметрыВыработки КАК уатПараметрыВыработки
	|ГДЕ
	|	НЕ уатПараметрыВыработки.ПометкаУдаления
	|	И (НЕ &флМЛ
	|				И (уатПараметрыВыработки.ДействуетНаТС
	|					ИЛИ уатПараметрыВыработки.ДействуетНаСотрудников)
	|			ИЛИ &флМЛ
	|				И уатПараметрыВыработки.СпособВводаЗначений = ЗНАЧЕНИЕ(Перечисление.уатСпособыВводаЗначенийВыработкивМЛ_уэ.ВТабличнойЧасти)
	|				И (уатПараметрыВыработки.ДействуетНаСотрудниковМЛ
	|					ИЛИ уатПараметрыВыработки.ДействуетНаТСМЛ))");
	Запрос.УстановитьПараметр("флМЛ", флМЛ);
	мсвДоступныеПВ = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	мсвПараметрыВыбора = Новый Массив;
	мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", мсвДоступныеПВ));
	
	Для Сч = 1 По 5 Цикл
		Элементы["СпецРабота" + Сч].ПараметрыВыбора = Новый ФиксированныйМассив(мсвПараметрыВыбора);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
