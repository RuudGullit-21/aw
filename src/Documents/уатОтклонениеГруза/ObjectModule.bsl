
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатЗаказГрузоотправителя") Или 
			ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатЗаказГрузоотправителя") Тогда
		Организация   = ДанныеЗаполнения.Организация;
		Заказчик      = ДанныеЗаполнения.Контрагент;
		
		НовСтрока = Заказы.Добавить();
		НовСтрока.ЗаказГрузоотправителя = ДанныеЗаполнения.Ссылка;
		НовСтрока.ПричинаОтклонения     = ПредопределенноеЗначение("Справочник.уатПричиныЗакрытияЗаказов.ЗакрытУспешно");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Проверка на уникальность заказов.
	ТЗ = Заказы.Выгрузить(, "ЗаказГрузоотправителя");
	ТЗ.Свернуть("ЗаказГрузоотправителя");
	Если Не ТЗ.Количество() = Заказы.Количество() Тогда
		ТекстНСТР = НСтр("en='Tabular section""Orders"" have duplicate lines.';ru='В табличной части ""Заказы"" присутствуют дубли строк.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР, ЭтотОбъект,,, Отказ);
		Возврат;
	КонецЕсли;
	
	// Проверка заказов на вхождение в уже проведенные закрытия.
	мЗапрос = Новый Запрос();
	мЗапрос.УстановитьПараметр("мсвЗаказов", Заказы.Выгрузить(, "ЗаказГрузоотправителя"));
	мЗапрос.УстановитьПараметр("Ссылка",     Ссылка);
	
	мЗапрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатОтклонениеГрузаЗаказы.Ссылка,
	|	уатОтклонениеГрузаЗаказы.ЗаказГрузоотправителя
	|ИЗ
	|	Документ.уатОтклонениеГруза.Заказы КАК уатОтклонениеГрузаЗаказы
	|ГДЕ
	|	уатОтклонениеГрузаЗаказы.ЗаказГрузоотправителя В(&мсвЗаказов)
	|	И уатОтклонениеГрузаЗаказы.Ссылка <> &Ссылка
	|	И уатОтклонениеГрузаЗаказы.Ссылка.Проведен";
	
	мВыборка = мЗапрос.Выполнить().Выбрать();
	
	Пока мВыборка.Следующий() Цикл 
		ОшибочныйЗаказ = Заказы.Найти(мВыборка.ЗаказГрузоотправителя, "ЗаказГрузоотправителя");
		Если ОшибочныйЗаказ = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		ИндексСтроки = Заказы.Индекс(ОшибочныйЗаказ);
		
		ТекстНСТР = НСтр("en='Ordering in the string ""%1"" already closed by document ""%2""';ru='Заказ в строке ""%1"" уже закрыт документом ""%2""'");
		ТекстНСТР = СтрШаблон(ТекстНСТР, ИндексСтроки + 1, мВыборка.Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстНСТР, 
			ЭтотОбъект, 
			"Заказы["+Формат(ИндексСтроки, "ЧН=0; ЧГ=0")+"].ЗаказГрузоотправителя",
			, 
			Отказ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	Документы.уатОтклонениеГруза.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	уатПроведение.ОтразитьЗаказыГрузоотправителей(ДополнительныеСвойства, Движения, Отказ);
	уатПроведение_уэ.ОтразитьГрузыКПеревозке(ДополнительныеСвойства, Движения, Отказ);
	уатПроведение.ОтразитьПричиныЗакрытияЗаказов(ДополнительныеСвойства, Движения, Отказ);
	уатПроведение_уэ.ОтразитьСтатусыГрузов(ДополнительныеСвойства, Движения, Отказ);
	
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Документы.уатОтклонениеГруза.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

#КонецОбласти
