&НаКлиенте
Перем ВалютаРасчетовПередИзменением;


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатДокументФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	НесколькоВалют = ?(Объект.Мультивалютный, 1, 0);
	
	УстановитьОтборДоговоры();
	ВывестиСуммовыеИтогиДокумента();
	ОбновитьДинамическиеКолонки();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатДокументФормаЭлементаПриОткрытии(Отказ, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	УстановитьАвтоотметкуНезаполненныхСтатей();
	УстановитьВидимость();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьДинамическиеКолонки();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьОтборДоговоры();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		ТекСтрока.Валюта    = Неопределено;
		ТекСтрока.Курс      = Неопределено;
		ТекСтрока.Кратность = Неопределено;
	КонецЦикла;
	
	УстановитьОтборДоговоры();
	ОбновитьДинамическиеКолонки();
КонецПроцедуры

&НаКлиенте
Процедура НесколькоВалютПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Объект.Мультивалютный = (НесколькоВалют = 1);
	
	Если Объект.Мультивалютный Тогда
		Объект.ВалютаДокумента = Неопределено;
		
	Иначе
		Объект.ВалютаДокумента = мВалютаРегламентированногоУчета;
		СтруктураКурс = уатОбщегоНазначенияТиповые.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
		Объект.Курс = СтруктураКурс.Курс;
		Объект.Кратность = СтруктураКурс.Кратность;
		ВалютаПриИзменении(Неопределено);
		
	КонецЕсли;
	
	УстановитьОтборДоговоры();
	РассчитатьСуммуДокумента();
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	Если Объект.ВалютаДокумента = ВалютаРасчетовПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКурс = уатОбщегоНазначенияТиповые.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
	
	Объект.Курс      = СтруктураКурс.Курс;
	Объект.Кратность = СтруктураКурс.Кратность;
	
	УстановитьОтборДоговоры();
	РассчитатьСуммуДокумента();
	
	Для Каждого СтрокаТЧ Из Объект.Состав Цикл
		Если уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтрокаТЧ.Договор, "ВалютаВзаиморасчетов") <> Объект.ВалютаДокумента Тогда
			СтрокаТЧ.Договор   = Неопределено;
			СтрокаТЧ.Валюта    = Неопределено;
			СтрокаТЧ.Курс      = 0;
			СтрокаТЧ.Кратность = 0;
		КонецЕсли;
	КонецЦикла;
	
	ВалютаРасчетовПередИзменением = Объект.ВалютаДокумента;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_РасшифровкаПлатежа

&НаКлиенте
Процедура СоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	УстановитьОграничениеТипаСделка();
	УстановитьОтборЗаказов();
КонецПроцедуры

&НаКлиенте
Процедура СоставПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьСуммуДокумента();
	УстановитьАвтоотметкуНезаполненныхСтатей();
КонецПроцедуры

&НаКлиенте
Процедура СоставПослеУдаления(Элемент)
	УстановитьАвтоотметкуНезаполненныхСтатей();
КонецПроцедуры

&НаКлиенте
Процедура СоставДоговорПриИзменении(Элемент)
	СтрокаПлатеж = Элементы.Состав.ТекущиеДанные;
	
	СтрокаПлатеж.Валюта = уатОбщегоНазначенияТиповыеСервер.ПолучитьЗначениеРеквизита(СтрокаПлатеж.Договор, "ВалютаВзаиморасчетов");
	СоставВалютаПриИзменении(Неопределено);
	
	УстановитьОграничениеТипаСделка();
	УстановитьОтборЗаказов();
	СтрокаПлатеж.Сделка = Неопределено;
	СтрокаПлатеж.ЗаказНаТС = Неопределено;
	
	ОбновитьДинамическиеКолонки();
КонецПроцедуры

&НаКлиенте
Процедура СоставСделкаПриИзменении(Элемент)
	СтрокаПлатеж = Элементы.Состав.ТекущиеДанные;
	СтрокаПлатеж.ЗаказНаТС = Неопределено;
	
	мсвЗаказы = ЗаказыПоСделке(СтрокаПлатеж.Сделка);
	Если мсвЗаказы.Количество() = 1 Тогда
		СтрокаПлатеж.ЗаказНаТС = мсвЗаказы[0];
	КонецЕсли;
	
	УстановитьОтборЗаказов();
КонецПроцедуры

&НаКлиенте
Процедура СоставУвеличениеДолгаПриИзменении(Элемент)
	ОбновитьДинамическиеКолонки();
КонецПроцедуры

&НаКлиенте
Процедура СоставУменьшениеДолгаПриИзменении(Элемент)
	ОбновитьДинамическиеКолонки();
КонецПроцедуры

&НаКлиенте
Процедура СоставВалютаПриИзменении(Элемент)
	СтрокаПлатеж = Элементы.Состав.ТекущиеДанные;
	СтруктураКурсаВзаиморасчетов = уатОбщегоНазначенияТиповые.ПолучитьКурсВалюты(СтрокаПлатеж.Валюта, Объект.Дата);
	СтрокаПлатеж.Курс            = СтруктураКурсаВзаиморасчетов.Курс;
	СтрокаПлатеж.Кратность       = СтруктураКурсаВзаиморасчетов.Кратность;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ОбновитьДинамическиеКолонки()
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		ТекСтрока.Разница = ТекСтрока.УвеличениеДолга - ТекСтрока.УменьшениеДолга;
		ТекСтрока.СделкаАвтоотметка =
			(ТекСтрока.Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоНакладным
			ИЛИ ТекСтрока.Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам
			ИЛИ ТекСтрока.Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказамНаТС);
		ТекСтрока.РасчетыПоНакладнымИлиСчетам = (ТекСтрока.Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоНакладным
			ИЛИ ТекСтрока.Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДоговоры()
	мсвСвязиПараметровВыбора = Новый Массив;
	мсвСвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Контрагент"));
	мсвСвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	Если НЕ Объект.Мультивалютный Тогда
		мсвСвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.ВалютаВзаиморасчетов", "Объект.ВалютаДокумента"));
	КонецЕсли;
	Элементы.СоставДоговор.СвязиПараметровВыбора = Новый ФиксированныйМассив(мсвСвязиПараметровВыбора);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОграничениеТипаСделка()
	СтрокаПлатеж = Элементы.Состав.ТекущиеДанные;
	
	мсвПараметрыВыбора = Новый Массив;
	
	ВедениеВзаиморасчетов = уатОбщегоНазначенияТиповыеСервер.ПолучитьЗначениеРеквизита(СтрокаПлатеж.Договор, "ВедениеВзаиморасчетов");
	ВидДоговора = уатОбщегоНазначенияТиповыеСервер.ПолучитьЗначениеРеквизита(СтрокаПлатеж.Договор, "ВидДоговора");
	
	Если ВедениеВзаиморасчетов = ПредопределенноеЗначение("Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоНакладным") Тогда
		Если ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком") Тогда
			Элементы.СоставСделка.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.уатПоступлениеТоваровУслуг");
		Иначе
			Элементы.СоставСделка.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.уатРеализацияУслуг");
		КонецЕсли;
	ИначеЕсли ВедениеВзаиморасчетов = ПредопределенноеЗначение("Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам") Тогда
		Если ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком") Тогда
			Элементы.СоставСделка.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.уатСчетНаОплатуПоставщика");
		Иначе
			Элементы.СоставСделка.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.уатСчетНаОплатуПокупателю");
		КонецЕсли;
	ИначеЕсли ВедениеВзаиморасчетов = ПредопределенноеЗначение("Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказамНаТС") Тогда
		Элементы.СоставСделка.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.уатЗаказГрузоотправителя");
		Если ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") Тогда
			мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ДоговорКонтрагента", СтрокаПлатеж.Договор));
		КонецЕсли;
	Иначе
		Элементы.СоставСделка.ОграничениеТипа = Новый ОписаниеТипов("Неопределено");
	КонецЕсли;
	
	Элементы.СоставСделка.ПараметрыВыбора = Новый ФиксированныйМассив(мсвПараметрыВыбора);
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуДокумента()
	Объект.СуммаПриход = 0;
	Объект.СуммаРасход = 0;
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		Если Объект.Мультивалютный Тогда
			ТекСуммаПриход = уатОбщегоНазначенияТиповые.ПересчитатьИзВалютыВВалюту(ТекСтрока.УвеличениеДолга,
				ТекСтрока.Валюта,    Объект.ВалютаДокумента,
				ТекСтрока.Курс,      Объект.Курс,
				ТекСтрока.Кратность, Объект.Кратность);
			ТекСуммаРасход = уатОбщегоНазначенияТиповые.ПересчитатьИзВалютыВВалюту(ТекСтрока.УменьшениеДолга,
				ТекСтрока.Валюта,    Объект.ВалютаДокумента,
				ТекСтрока.Курс,      Объект.Курс,
				ТекСтрока.Кратность, Объект.Кратность);
		Иначе
			ТекСуммаПриход = ТекСтрока.УвеличениеДолга;
			ТекСуммаРасход = ТекСтрока.УменьшениеДолга;
		КонецЕсли;
		
		Объект.СуммаПриход = Объект.СуммаПриход + ТекСуммаПриход;
		Объект.СуммаРасход = Объект.СуммаРасход + ТекСуммаРасход;
	КонецЦикла;
	
	ВывестиСуммовыеИтогиДокумента();
КонецПроцедуры

&НаСервере
Процедура ВывестиСуммовыеИтогиДокумента()
	Если Не Объект.Мультивалютный Тогда
		СуммаДокумента = Объект.СуммаПриход - Объект.СуммаРасход;
		
		Если НЕ ЗначениеЗаполнено(Объект.ВалютаДокумента) Тогда 
			Элементы.НадписьКурс.Видимость = Ложь;
		Иначе
			//СтруктураКурс = уатОбщегоНазначенияТиповые.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
			НадписьКурс = "(" + Формат(Объект.Курс, "ЧДЦ=4; ЧРД=.; ЧН=0.0000; ЧГ=0") + ")";
			Элементы.НадписьКурс.Видимость = Истина;
		КонецЕсли;
		
		// удаляем мультивалютные реквизиты
		мсвУдалитьРеквизиты = Новый Массив;
		Для Сч = 1 по 1000 Цикл //различных валют и курсов может быть несколько, примем гипотетический максимум = 1000
			ТекПолеИтого = Элементы.Найти("Всего" + Сч);
			Если ТекПолеИтого = Неопределено Тогда
				Прервать;
			КонецЕсли;
			
			мсвУдалитьРеквизиты.Добавить("Всего" + Сч);
			мсвУдалитьРеквизиты.Добавить("ВалютаРасчетов" + Сч);
			
			Элементы.Удалить(Элементы["Всего" + Сч]);
			Элементы.Удалить(Элементы["ВалютаРасчетов" + Сч]);
		КонецЦикла;

		Если мсвУдалитьРеквизиты.Количество() > 0 Тогда
			ИзменитьРеквизиты(, мсвУдалитьРеквизиты);
		КонецЕсли;
		
	Иначе
		тблИтогиПоВалютам = Объект.Состав.Выгрузить();
		тблИтогиПоВалютам.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
		Для Каждого ТекСтрока Из тблИтогиПоВалютам Цикл
			ТекСтрока.Сумма = ТекСтрока.УвеличениеДолга - ТекСтрока.УменьшениеДолга;
		КонецЦикла;
		
		тблИтогиПоВалютам.Свернуть("Валюта, Курс, Кратность", "Сумма");
		тблИтогиПоВалютам.Сортировать("Валюта, Курс, Кратность");
		
		// добавляем мультивалютные реквизиты
		мсвДобавитьРеквизиты = Новый Массив;
		Сч = 1;
		Для Каждого ТекСтрока Из тблИтогиПоВалютам Цикл
			Если Элементы.Найти("Всего" + Сч) = Неопределено Тогда
				мсвДобавитьРеквизиты.Добавить(Новый РеквизитФормы("Всего" + Сч, Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2))));
				мсвДобавитьРеквизиты.Добавить(Новый РеквизитФормы("ВалютаРасчетов" + Сч, Новый ОписаниеТипов("Строка")));
			КонецЕсли;
						
			Сч = Сч + 1;
		КонецЦикла;
		Колич = Сч-1;
		
		// удаляем лишние мультивалютные реквизиты
		мсвУдалитьРеквизиты = Новый Массив;
		Для Сч = Колич+1 По 1000 Цикл
			ТекПолеИтого = Элементы.Найти("Всего" + Сч);
			Если ТекПолеИтого = Неопределено Тогда
				Прервать;
			КонецЕсли;
			
			мсвУдалитьРеквизиты.Добавить("Всего" + Сч);
			мсвУдалитьРеквизиты.Добавить("ВалютаРасчетов" + Сч);
			
			Элементы.Удалить(Элементы["Всего" + Сч]);
			Элементы.Удалить(Элементы["ВалютаРасчетов" + Сч]);
		КонецЦикла;
				
		ИзменитьРеквизиты(мсвДобавитьРеквизиты, мсвУдалитьРеквизиты);
		
		Сч = 1;
		Для Каждого ТекСтрока Из тблИтогиПоВалютам Цикл
			ЭтотОбъект["Всего" + Сч] = ТекСтрока.Сумма;
			ТекСтрокаВалюта = "" + ТекСтрока.Валюта + " (" + Формат(ТекСтрока.Курс, "ЧДЦ=4; ЧРД=.; ЧН=0.0000; ЧГ=0") + ")";
			Если Сч < тблИтогиПоВалютам.Количество() Тогда
				ТекСтрокаВалюта = ТекСтрокаВалюта + ", ";
			КонецЕсли;
			ЭтотОбъект["ВалютаРасчетов" + Сч] = ТекСтрокаВалюта;
			
			Если Элементы.Найти("Всего" + Сч) = Неопределено Тогда
				НовыйЭлем = Элементы.Добавить("Всего" + Сч, Тип("ПолеФормы"), Элементы.ИтогоМноговалютный);
				НовыйЭлем.Вид = ВидПоляФормы.ПолеВвода;
				НовыйЭлем.ПутьКДанным = "Всего" + Сч;
				НовыйЭлем.Ширина = 10;
				НовыйЭлем.РастягиватьПоГоризонтали = Ложь;
				НовыйЭлем.ТолькоПросмотр = Истина;
				Если Сч = 1 Тогда
					НовыйЭлем.Заголовок = НСтр("en='Total';ru='Всего'");
				Иначе
					НовыйЭлем.ПоложениеЗаголовка=ПоложениеЗаголовкаЭлементаФормы.Нет;
				КонецЕсли;
			КонецЕсли;
			
			Если Элементы.Найти("ВалютаРасчетов" + Сч) = Неопределено Тогда
				НовыйЭлем = Элементы.Добавить("ВалютаРасчетов" + Сч, Тип("ПолеФормы"), Элементы.ИтогоМноговалютный);
				НовыйЭлем.Вид = ВидПоляФормы.ПолеНадписи;
				НовыйЭлем.ПутьКДанным = "ВалютаРасчетов" + Сч;
				НовыйЭлем.Ширина = 10;
				НовыйЭлем.РастягиватьПоГоризонтали = Ложь;
				НовыйЭлем.ПоложениеЗаголовка=ПоложениеЗаголовкаЭлементаФормы.Нет;
			КонецЕсли;
						
			Сч = Сч + 1;
		КонецЦикла;
				
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.Валюта.Доступность           = НЕ Объект.Мультивалютный;
	Элементы.Итого.Видимость              = НЕ Объект.Мультивалютный;
	Элементы.ИтогоМноговалютный.Видимость = Объект.Мультивалютный;
	Элементы.СоставГруппаВалюта.Видимость = Объект.Мультивалютный;
	Элементы.СоставЗаказНаТС.Видимость = ИспользуетсяПлатежныйКалендарь();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьАвтоотметкуНезаполненныхСтатей()
	флЕстьДоходы = Ложь;
	флЕстьРасходы = Ложь;
	Для Каждого ТекСтрока Из Объект.Состав Цикл
		ТекВидДоговора = уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ТекСтрока.Договор, "ВидДоговора");
		
		Если ТекСтрока.УвеличениеДолга > ТекСтрока.УменьшениеДолга Тогда // УвеличениеДолга
			Если ТекВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком") Тогда
				флЕстьРасходы = Истина;
			Иначе
				флЕстьДоходы = Истина;
			КонецЕсли;
		ИначеЕсли ТекСтрока.УвеличениеДолга < ТекСтрока.УменьшениеДолга Тогда // УменьшениеДолга
			Если ТекВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком") Тогда
				флЕстьДоходы = Истина;
			Иначе
				флЕстьРасходы = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СтатьяДоходов.АвтоОтметкаНезаполненного = флЕстьДоходы;
	Если НЕ флЕстьДоходы Тогда
		Элементы.СтатьяДоходов.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	Элементы.СтатьяРасходов.АвтоОтметкаНезаполненного = флЕстьРасходы;
	Если НЕ флЕстьРасходы Тогда
		Элементы.СтатьяРасходов.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборЗаказов()
	ТекСделка = Элементы.Состав.ТекущиеДанные.Сделка;
	мсвПараметрыВыбора = Новый Массив;
	мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ЗаказыПоСделке(ТекСделка)));
	Элементы.СоставЗаказНаТС.ПараметрыВыбора = Новый ФиксированныйМассив(мсвПараметрыВыбора);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаказыПоСделке(Сделка)
	Возврат Документы.уатКорректировкаДолга.ЗаказыПоСделке(Сделка);
КонецФункции

&НаСервереБезКонтекста
Функция ИспользуетсяПлатежныйКалендарь()
	Возврат ПолучитьФункциональнуюОпцию("уатИспользоватьПлатежныйКалендарь");
КонецФункции

#КонецОбласти
