#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Проверка на то, что все ТС и оборудование введены в эксплуатацию
	Если НЕ ТСВведеныВЭксплуатацию(Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатПеремещениеТСиОборудования.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение_проф.ОтразитьМестонахождениеТС(ДополнительныеСвойства, Движения, Отказ);
	уатПроведение.ОтразитьОстаткиГСМНаТС(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Документы.уатПеремещениеТСиОборудования.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	// Запись даты ввода в карточках ТС и оборудования
	ЗаписатьРеквизитыТСиОборудования(Отказ);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		// Заголовок для сообщений об ошибках проведения.
		Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
		
		СтруктураПолей = Новый Структура("Организация, Ответственный");
		уатОбщегоНазначенияТиповые.уатПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураПолей, Отказ, Заголовок);
		СтруктураПолей = Новый Структура("ТС, ДатаВвода");
		уатОбщегоНазначенияТиповые.уатПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ТС", СтруктураПолей, Отказ, Заголовок);
		
		// проверка на дубли строк
		тблТС = ТС.Выгрузить().Скопировать();
		тблТС.Свернуть("ТС");
		Если тблТС.Количество() < ТС.Количество() Тогда
			ТекстНСТР = НСтр("en='In the tabular section ""Vehicle"" there are duplicate lines (duplicate vehicle)!';ru='В табличной части ""ТС"" присутствуют повторяющиеся строки (дублирование ТС)!'");
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ, Заголовок);
		КонецЕсли;
		
		уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьТСиОборудованиеДоПеремещения(Отказ);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТС = Новый Массив();
	Для Каждого ТекСтрока Из ТС Цикл 
		Если МассивТС.Найти(ТекСтрока.ТС) = Неопределено Тогда 
			МассивТС.Добавить(ТекСтрока.ТС);
		КонецЕсли;
	КонецЦикла;
	
	СписокТС = "";
	Для Каждого ТекТС Из МассивТС Цикл 
		СписокТС = СписокТС + ?(СписокТС = "", "", ", ") + ТекТС.Наименование;
	КонецЦикла;
	
	// уатСогласованиеДокументов
	уатСогласованиеДокументовСервер.ПередЗаписью(ЭтотОбъект, РежимЗаписи, Отказ);
	// Конец уатСогласованиеДокументов
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьРеквизитыТСиОборудования(Отказ)
	Для Каждого СтрТС Из ТС Цикл
		МестонахождениеТС = уатОбщегоНазначения.МестонахождениеТС(СтрТС.ТС);
		мсвДокументов = уатОбщегоНазначения.ПолучитьРегистрационныеДокументы(СтрТС.ТС);
		
		Если СтрТС.ТС.Организация <> МестонахождениеТС.Организация
			ИЛИ СтрТС.ТС.Подразделение <> МестонахождениеТС.Подразделение
			ИЛИ СтрТС.ТС.Колонна <> МестонахождениеТС.Колонна Тогда
			
			ТСОбъект = СтрТС.ТС.ПолучитьОбъект();
			ТСОбъект.Организация   = МестонахождениеТС.Организация;
			ТСОбъект.Подразделение = МестонахождениеТС.Подразделение;
			ТСОбъект.Колонна       = МестонахождениеТС.Колонна;
			
			Попытка 
				ТСОбъект.Записать();
			Исключение
				ТекстНСТР = НСтр("en='Failed to record vehicle/equipment ""%1""!';ru='Не удалось записать ТС/оборудование ""%1""!'");
				ТекстНСТР = СтрШаблон(ТекстНСТР, Строка(ТСОбъект));
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
			КонецПопытки;
		КонецЕсли;
		
		Для Каждого ТекДокумент Из мсвДокументов Цикл
			Если ТекДокумент.Организация <> МестонахождениеТС.Организация
				ИЛИ ТекДокумент.Подразделение <> МестонахождениеТС.Подразделение Тогда
				
				ДокументОбъект = ТекДокумент.ПолучитьОбъект();
				ДокументОбъект.Организация   = МестонахождениеТС.Организация;
				ДокументОбъект.Подразделение = МестонахождениеТС.Подразделение;
			
				Попытка 
					ДокументОбъект.Записать();
				Исключение
					ТекстНСТР = НСтр("en='Failed to record document ""%1""';ru='Не удалось записать документ ""%1""'");
					ТекстНСТР = СтрШаблон(ТекстНСТР, Строка(ДокументОбъект));
					уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
					Прервать;
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ВосстановитьТСиОборудованиеДоПеремещения(Отказ)
	Для каждого СтрТС Из ТС Цикл
		ТСОбъект = СтрТС.ТС.ПолучитьОбъект();
		
		МестоТС = уатОбщегоНазначения.МестонахождениеТС(СтрТС.ТС, СтрТС.ДатаВвода - 1);
		
		ТСОбъект.Организация = МестоТС.Организация;
		ТСОбъект.Подразделение = МестоТС.Подразделение;
		ТСОбъект.Колонна = МестоТС.Колонна;
		
		Попытка 
			ТСОбъект.Записать();
		Исключение
			ТекстНСТР = НСтр("en='Failed to record vehicle/equipment ""%1""!';ru='Не удалось записать ТС/оборудование ""%1""!'");
			ТекстНСТР = СтрШаблон(ТекстНСТР, Строка(СтрТС.ТС));
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Функция ТСВведеныВЭксплуатацию(Отказ)
	Рез = Истина;
	
	Для Каждого ТекСтрока Из ТС Цикл
		Если ТекСтрока.ТС.ДатаВводаВЭксплуатацию = '00010101' ИЛИ ТекСтрока.ДатаВвода < ТекСтрока.ТС.ДатаВводаВЭксплуатацию Тогда
			ТекстНСТР = НСтр("en='Vehicle/equipment ""%1"" is not put into operation!';ru='ТС/оборудование ""%1"" не введено в эксплуатацию!'");
			ТекстНСТР = СтрШаблон(ТекстНСТР, Строка(ТекСтрока.ТС));
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
			Рез = Ложь;
		ИначеЕсли ТекСтрока.ТС.ДатаВыбытия <> '00010101' И ТекСтрока.ДатаВвода >= ТекСтрока.ТС.ДатаВыбытия Тогда
			ТекстНСТР = НСтр("en='Vehicle/equipment ""%1"" was decommissioning!';ru='ТС/оборудование ""%1"" выбыло из эксплуатации!'");
			ТекстНСТР = СтрШаблон(ТекстНСТР, Строка(ТекСтрока.ТС));
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
			Рез = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Рез;
КонецФункции

#КонецОбласти


#КонецЕсли