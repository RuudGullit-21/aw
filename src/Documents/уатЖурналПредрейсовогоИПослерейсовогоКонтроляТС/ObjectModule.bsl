
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
		Организация   = ДанныеЗаполнения.Организация;
		Подразделение = ДанныеЗаполнения.Подразделение;
		ПутевойЛист   = ДанныеЗаполнения.Ссылка;
		ВидКонтроля   = Перечисления.уатВидыКонтроляТранспортныхДокументов.Предрейсовый;
		Контролер     = ПутевойЛист.ВыпустилМеханик;
		ТС            = ПутевойЛист.ТранспортноеСредство;
		Для Каждого ТекПрицеп Из ПутевойЛист.Прицепы Цикл
			НоваяСтрока = Прицепы.Добавить();
			НоваяСтрока.ТС = ТекПрицеп.ТС;
		КонецЦикла;
	
		СозданныеДокументы = уатЖурналыТранспортныхДокументов.СозданныеДокументыКонтроляТСПоПЛ(ПутевойЛист);
		
		Если СозданныеДокументы.Предрейсовый <> Неопределено И СозданныеДокументы.Послерейсовый = Неопределено Тогда
			ВидКонтроля = Перечисления.уатВидыКонтроляТранспортныхДокументов.Послерейсовый;
			Контролер = ПутевойЛист.ПринялМеханик;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	// проверка на существование уже созданного документа
	Если уатЖурналыТранспортныхДокументов.УжеСозданДокументКонтроляТС(ПутевойЛист, ВидКонтроля, Ссылка) Тогда
		ТекстНСТР = СтрШаблон("Запись журнала %1 контроля ТС для ""%2"" уже создана!",
			?(ВидКонтроля = Перечисления.уатВидыКонтроляТранспортныхДокументов.Предрейсовый, "предрейсового", "послерейсового"),
			ПутевойЛист);
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ, Заголовок);
	КонецЕсли;
	
	// проверка на нарушение минимального интервала ПЛ
	Если НЕ Отказ И ЗначениеЗаполнено(Дата) Тогда
		МинИнтервалПЛ = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(Организация,
			ПланыВидовХарактеристик.уатПраваИНастройки.МинимальныйИнтервалМеждуПредрейсовымИПослерейсовымОсмотром);
			
		Если МинИнтервалПЛ > 0 Тогда
			ИнтервалПоЖурналу = Неопределено;
			
			СозданныеДокументы = уатЖурналыТранспортныхДокументов.СозданныеДокументыКонтроляТСПоПЛ(ПутевойЛист);
			
			Если ВидКонтроля = Перечисления.уатВидыКонтроляТранспортныхДокументов.Предрейсовый Тогда
				ДокументКонтрольДругой = СозданныеДокументы.Послерейсовый;
				Если ЗначениеЗаполнено(ДокументКонтрольДругой) Тогда
					ИнтервалПоЖурналу = ДокументКонтрольДругой.Дата - Дата;
					ДатаПредрейсовый  = Дата;
					ДатаПослерейсовый = ДокументКонтрольДругой.Дата;
				КонецЕсли;
			Иначе
				ДокументКонтрольДругой = СозданныеДокументы.Предрейсовый;
				Если ЗначениеЗаполнено(ДокументКонтрольДругой) Тогда
					ИнтервалПоЖурналу = Дата - ДокументКонтрольДругой.Дата;
					ДатаПредрейсовый  = ДокументКонтрольДругой.Дата;
					ДатаПослерейсовый = Дата;
				КонецЕсли;
			КонецЕсли;
			
			Если ИнтервалПоЖурналу <> Неопределено И ИнтервалПоЖурналу < МинИнтервалПЛ*60 Тогда
				Если НачалоДня(ДатаПредрейсовый) = НачалоДня(ДатаПослерейсовый) Тогда
					ДатаПредрейсовый  = Формат(ДатаПредрейсовый,  "ДФ=HH:mm");
					ДатаПослерейсовый = Формат(ДатаПослерейсовый, "ДФ=HH:mm");
				Иначе
					ДатаПредрейсовый  = Формат(ДатаПредрейсовый,  "ДФ='dd.MM.yyyy HH:mm'");
					ДатаПослерейсовый = Формат(ДатаПослерейсовый, "ДФ='dd.MM.yyyy HH:mm'");
				КонецЕсли;
				
				ТекстНСТР = СтрШаблон(
					"Интервал между предрейсовым и послерейсовым контролем состояния ТС не должен быть меньше %1 минут!
					|Время предрейсового осмотра: %2
					|Время послерейсового осмотра: %3",
					МинИнтервалПЛ, ДатаПредрейсовый, ДатаПослерейсовый);
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		уатЖурналыТранспортныхДокументов.ИзменитьСостоянияПодписейПЛ(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
	Если ЗначениеЗаполнено(ПутевойЛист) И ЗначениеЗаполнено(ТС)
		И ТС <> ПутевойЛист.ТранспортноеСредство И ПутевойЛист.Прицепы.Найти(ТС, "ТС") = Неопределено Тогда
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(
			СтрШаблон("ТС ""%1"" отсутствует в указанном Путевом листе!", ТС),
			Отказ, Заголовок);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
