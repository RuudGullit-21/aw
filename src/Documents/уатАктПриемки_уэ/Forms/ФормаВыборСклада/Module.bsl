
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("мсвСклады") Тогда 
		Возврат;
	КонецЕсли;
	
	мсвСклады = Параметры.мсвСклады;
	ТаблицаЗаказы = Параметры.ТаблицаЗаказы;
	Если Параметры.Свойство("ТаблицаВсеЗаказы") Тогда
		ТаблицаВсеЗаказы = Параметры.ТаблицаВсеЗаказы;
	КонецЕсли;
	ДокументОснование = Параметры.ДокументОснование;
	СоздаватьДокумент = Параметры.СоздаватьДокумент;
	
	Если Параметры.Свойство("ВозможнаЗаменаЭтапов") Тогда
		ВозможнаЗаменаЭтапов = Параметры.ВозможнаЗаменаЭтапов;
	КонецЕсли;
	
	ДобавляемыеРеквизиты = Новый  Массив;
	Сч = 1;
	Для Каждого ТекСклад из мсвСклады Цикл
		РеквизитСклад = Новый РеквизитФормы("Склад" + Сч, Новый ОписаниеТипов("Булево"),,,Ложь);
		ДобавляемыеРеквизиты.Добавить(РеквизитСклад);
		Сч = Сч + 1;
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Сч = 1;
	Для Каждого ТекСклад Из Параметры.мсвСклады Цикл 
		Элемент = Элементы.Добавить("Склад" + Сч, Тип("ПолеФормы"));
		Элемент.Заголовок = ТекСклад;
		Элемент.Вид = ВидПоляФормы.ПолеФлажка;
		Элемент.ПутьКДанным = "Склад" + Сч;
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
				
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Сч = 1;
	Для Каждого ТекСклад из мсвСклады Цикл
		ЭтотОбъект["Склад" + Сч] = Истина;
		Сч = Сч + 1;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Сч = 1;
	Для Каждого ТекСклад из мсвСклады Цикл
		ЭтотОбъект["Склад" + Сч] = Ложь;
		Сч = Сч + 1;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Сч = 1;
	Для Каждого ТекСклад Из мсвСклады Цикл
		Если ЭтотОбъект["Склад" + Сч] Тогда
			ТаблицаНомераЭтапов.Очистить();
			ТекСклад = мсвСклады[Сч-1].Значение;
			ТаблицаЗаказыПоСкладу = Новый Массив;
			Для Каждого ТекСтрока Из ТаблицаЗаказы Цикл
				Если ТекСтрока.Значение.Склад = ТекСклад Тогда
					
					// Также строим таблицу изменения этапов (если она нужна), если она будет не пустой - будет задан вопрос пользователю.
					Если ВозможнаЗаменаЭтапов 
						И ТекСтрока.Значение.НомерИсходногоЭтапа <> ТекСтрока.Значение.НомерНовогоЭтапа Тогда
						СтрокаТаблицы = Новый Структура;
						СтрокаТаблицы.Вставить("НомерИсходногоЭтапа", ТекСтрока.Значение.НомерИсходногоЭтапа);
						СтрокаТаблицы.Вставить("НомерНовогоЭтапа", ТекСтрока.Значение.НомерНовогоЭтапа);
						Если ТаблицаНомераЭтапов.НайтиПоЗначению(СтрокаТаблицы) = Неопределено Тогда
							ТаблицаНомераЭтапов.Добавить(СтрокаТаблицы);
						КонецЕсли;
					КонецЕсли;

					ТаблицаЗаказыПоСкладу.Добавить(ТекСтрока.Значение);
				КонецЕсли;
			КонецЦикла;
			
			ПараметрыФормы = Новый Структура("Основание, ТаблицаЗаказы, Склад",
				ДокументОснование, ТаблицаЗаказыПоСкладу, ТекСклад);
			Если ТаблицаВсеЗаказы.Количество() > 0 Тогда
				ПараметрыФормы.Вставить("ТаблицаВсеЗаказы", ТаблицаВсеЗаказы);
			КонецЕсли;
				
			// Список всех заказанных грузовых мест нужен для отображения строк с пустым количеством в акте отгрузки.
			// Удаляем из него те грузовые места, по которым отгрузка есть.
			Если ЗначениеЗаполнено(ТаблицаВсеЗаказы) Тогда
				ПараметрыФормы.Вставить("ТаблицаВсеЗаказы", ТаблицаВсеЗаказы);
			КонецЕсли;
			
			Если ВозможнаЗаменаЭтапов И ТаблицаНомераЭтапов.Количество() > 0 Тогда
				ПараметрыФормы.Вставить("ТаблицаНомераЭтапов", ТаблицаНомераЭтапов);
			КонецЕсли;
			
			Если СоздаватьДокумент = 0 Тогда	
				ОткрытьФорму("Документ.уатАктПриемки_уэ.ФормаОбъекта", ПараметрыФормы,, ПараметрыФормы.Склад);
			Иначе
				ОткрытьФорму("Документ.уатАктОтгрузки_уэ.ФормаОбъекта", ПараметрыФормы,, ПараметрыФормы.Склад);
			КонецЕсли;
		КонецЕсли;
		
		Сч = Сч + 1;
	КонецЦикла;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
