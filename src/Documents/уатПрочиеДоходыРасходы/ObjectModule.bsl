#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДата();
	БазовыйДокумент = Неопределено;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	Документы.уатПрочиеДоходыРасходы.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	Если ВидОперации = Перечисления.уатВидыОперацийПрочиеДоходыРасходы.Доходы Тогда
		уатПроведение.ОтразитьДоходы(ДополнительныеСвойства, Движения, Отказ);
	Иначе
		уатПроведение.ОтразитьРасходы(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Документы.уатПрочиеДоходыРасходы.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатОтчетСистемыПлатон") Тогда
		ВидОперации = Перечисления.уатВидыОперацийПрочиеДоходыРасходы.Расходы;
		ДокументОснование = ДанныеЗаполнения;
		Основание = ДанныеЗаполнения;
		Подразделение = ДанныеЗаполнения.Подразделение;
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		Организация = ДанныеЗаполнения.Организация;
		Комментарий = ДанныеЗаполнения.Комментарий;
		
		тзРасходы = ДанныеЗаполнения.Расходы.Выгрузить();
		тзРасходы.Свернуть("ТС", "СписаниеСРЗ");
		Для Каждого ТекСтрока Из тзРасходы Цикл
			НоваяСтрока = Расходы.Добавить();
			НоваяСтрока.Контрагент		 = ДанныеЗаполнения.Контрагент;
			НоваяСтрока.ТС				 = ТекСтрока.ТС;
			НоваяСтрока.Сумма			 = ТекСтрока.СписаниеСРЗ;
			НоваяСтрока.СтавкаНДС		 = Перечисления.СтавкиНДС.БезНДС;

		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатОтчетСистемыАвтодор") Тогда
		ВидОперации = Перечисления.уатВидыОперацийПрочиеДоходыРасходы.Расходы;
		ДокументОснование = ДанныеЗаполнения;
		Основание = ДанныеЗаполнения;
		Подразделение = ДанныеЗаполнения.Подразделение;
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		Организация = ДанныеЗаполнения.Организация;
		Комментарий = ДанныеЗаполнения.Комментарий;
		
		тзРасходы = ДанныеЗаполнения.Расходы.Выгрузить();
		тзРасходы.Свернуть("ТС", "Сумма");
		Для Каждого ТекСтрока Из тзРасходы Цикл
			НоваяСтрока = Расходы.Добавить();
			НоваяСтрока.Контрагент		 = ДанныеЗаполнения.Контрагент;
			НоваяСтрока.ТС				 = ТекСтрока.ТС;
			НоваяСтрока.Сумма			 = ТекСтрока.Сумма;
			НоваяСтрока.СтавкаНДС		 = Перечисления.СтавкиНДС.БезНДС;

		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатПарковка") Тогда
		ВидОперации        = Перечисления.уатВидыОперацийПрочиеДоходыРасходы.Расходы;
		ДокументОснование  = ДанныеЗаполнения;
		Основание          = ДанныеЗаполнения;
		Подразделение      = ДанныеЗаполнения.Подразделение;
		Организация        = ДанныеЗаполнения.Организация;
		Колонна            = ДанныеЗаполнения.Колонна;

		НоваяСтрока = Расходы.Добавить();
		НоваяСтрока.ДатаОтражения	 = ДанныеЗаполнения.Запущена;
		НоваяСтрока.СтатьяРасходов	 = Справочники.уатСтатьиРасходов.Парковки;
		НоваяСтрока.ТС				 = ДанныеЗаполнения.ТС;
		НоваяСтрока.Сумма			 = ДанныеЗаполнения.ИтоговаяСтоимость;

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатСписаниеТоваров") Тогда
		
		ВидОперации       = Перечисления.уатВидыОперацийПрочиеДоходыРасходы.Расходы;
		Подразделение      = ДанныеЗаполнения.Подразделение;
		Организация        = ДанныеЗаполнения.Организация;
		ДокументОснование  = ДанныеЗаполнения;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уатПартииТоваровНаСкладах.Стоимость КАК Стоимость
		|ИЗ
		|	РегистрНакопления.уатПартииТоваровНаСкладах КАК уатПартииТоваровНаСкладах
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уатСписаниеТоваров КАК уатСписаниеТоваров
		|		ПО уатПартииТоваровНаСкладах.Регистратор = уатСписаниеТоваров.Ссылка
		|ГДЕ
		|	уатСписаниеТоваров.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		
		Материалы = Запрос.Выполнить().Выгрузить();
		
		Для Каждого текСтрока Из Материалы Цикл
			НоваяСтрока = Расходы.Добавить();
			НоваяСтрока.ДатаОтражения = ДанныеЗаполнения.Дата;
			НоваяСтрока.СтатьяРасходов = Справочники.уатСтатьиРасходов.ОсновнаяСтатьяРасходов;
			НоваяСтрока.Сумма = текСтрока.Стоимость;
			
			НоваяСтрока.СтавкаНДС = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
												ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяСтавкаНДС");
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатРемонтныйЛист") Тогда
		
		ВидОперации        = Перечисления.уатВидыОперацийПрочиеДоходыРасходы.Расходы;
		Подразделение      = ДанныеЗаполнения.Подразделение;
		Организация        = ДанныеЗаполнения.Организация;
		ДокументОснование  = ДанныеЗаполнения;
		
		НоваяСтрока = Расходы.Добавить();
		НоваяСтрока.ДатаОтражения	 = ДанныеЗаполнения.ДатаОкончания;
		НоваяСтрока.СтатьяРасходов	 = Справочники.уатСтатьиРасходов.Ремонты;
		НоваяСтрока.ТС				 = ДанныеЗаполнения.ТС;
		НоваяСтрока.СтавкаНДС 		 = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
					ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяСтавкаНДС");
		
		Если ДанныеЗаполнения.СпособРемонта = Перечисления.уатСпособыРемонта.Автосервис Тогда
			НоваяСтрока.Сумма = ДанныеЗаполнения.СуммаДокумента;
		КонецЕсли;
		
	КонецЕсли;
	
	СуммаВключаетНДС = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(Организация,	"СуммаВключаетНДС");
	УчитыватьНДС = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(Организация,	"УчитыватьНДС");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
КонецПроцедуры

#КонецОбласти
