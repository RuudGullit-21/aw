
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Расходы") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Мультивалютный") Тогда
		Мультивалютный = Параметры.Мультивалютный;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Параметры.Расходы Цикл
		НоваяСтрока = Расходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.Всего = НоваяСтрока.Сумма
			+ ?(НоваяСтрока.УчитыватьНДС И НЕ НоваяСтрока.СуммаВключаетНДС, НоваяСтрока.СуммаНДС, 0);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("en='Immediate opening for this object is prohibited!';ru='Непосредственное открытие для данного объекта запрещено!'"), 10);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьВидимость();
	УстановитьФлажки(Неопределено);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого ТекКонтрагент из Расходы Цикл
		ТекКонтрагент.ФлагВыбора = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого ТекКонтрагент из Расходы Цикл
		ТекКонтрагент.ФлагВыбора = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = ОКСервер();
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОКСервер()

	ПараметрыОтбора = Новый Структура("ФлагВыбора", Истина);
	ВыбранныеСтроки = Расходы.НайтиСтроки(ПараметрыОтбора);
	тзРасходы = Расходы.Выгрузить(ВыбранныеСтроки);
	Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(тзРасходы);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.РасходыГруппаВалюта.Видимость = Мультивалютный;
КонецПроцедуры

#КонецОбласти
