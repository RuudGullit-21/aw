#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	// обновление служебного реквизита КраткийСостав
	КраткийСоставДокумента = "";
	мсвСотрудники = Новый Массив;
	Для Каждого ТекСтрока Из ДанныеОВремени Цикл
		Если ЗначениеЗаполнено(ТекСтрока.Сотрудник) И мсвСотрудники.Найти(ТекСтрока.Сотрудник) = Неопределено Тогда
			мсвСотрудники.Добавить(ТекСтрока.Сотрудник);
			КраткийСоставДокумента = КраткийСоставДокумента + ", " + уатОбщегоНазначенияТиповые.уатФамилияИнициалыФизЛица(ТекСтрока.Сотрудник);
		КонецЕсли;
	КонецЦикла;
	Если НЕ ПустаяСтрока(КраткийСоставДокумента) Тогда
		КраткийСоставДокумента = Сред(КраткийСоставДокумента, 3);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	Если НачалоМесяца(ДатаНачалаПериода) <> НачалоМесяца(ДатаОкончанияПериода)
		ИЛИ НачалоМесяца(ДатаНачалаПериода) <> НачалоМесяца(Дата) Тогда 
		СтрокаСообщения = НСтр("en='Timesheet period may be inside document`s date month!';ru='Период табеля должен входить в календарный месяц даты документа!'");
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(СтрокаСообщения, Отказ, Заголовок);
	КонецЕсли;
	
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти
