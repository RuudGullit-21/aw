#Область ОписаниеПеременных

Перем мВалютаРегламентированногоУчета Экспорт; // Переменная хранит значение валюты регламентированного учёта,
// полученное из константы.
Перем мВалютаУпрУчета Экспорт; // Переменная хранит значение валюты управленческого учёта, полученное из константы.

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатПоступлениеТоваровУслуг") ИЛИ
		ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатПоступлениеТоваровУслуг") Тогда
		
		Дата = ТекущаяДата();
		ДокументОснование		= ДанныеЗаполнения.Ссылка;
		Организация             = ДанныеЗаполнения.Организация;
		Комментарий         	= "Заполнен на основании " + ДанныеЗаполнения;
		СкладОтправитель		= ДанныеЗаполнения.Склад;
		
		Для Каждого ТекСтрока из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока				 	 = Товары.Добавить();
			НоваяСтрока.Номенклатура 	 = ТекСтрока.Номенклатура;
			НоваяСтрока.ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
			НоваяСтрока.Количество 	 	 = ТекСтрока.Количество;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатРемонтныйЛист")
		ИЛИ	ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатРемонтныйЛист")
		ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатВыдачаРасходныхМатериалов")
		ИЛИ	ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатВыдачаРасходныхМатериалов") Тогда
		
		Дата = ДанныеЗаполнения.Дата-1;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатРемонтныйЛист")
			ИЛИ	ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатРемонтныйЛист") Тогда
				
			Если ДанныеЗаполнения.СпособРемонта = Перечисления.уатСпособыРемонта.Автосервис Тогда
				СкладПолучатель = ДанныеЗаполнения.СкладСобственныхМатериалов;
				ТЧМатериалы = ДанныеЗаполнения.СобственныеМатериалы;
			Иначе
				СкладПолучатель = ДанныеЗаполнения.Контрагент;
				ТЧМатериалы = ДанныеЗаполнения.Материалы;
			КонецЕсли;
		Иначе
			СкладПолучатель = ДанныеЗаполнения.Склад;
			ТЧМатериалы = ДанныеЗаполнения.Материалы;
		КонецЕсли;
		
		Для Каждого ТекСтрока Из ТЧМатериалы Цикл
			НоваяСтрока				 	  = Товары.Добавить();
			НоваяСтрока.Номенклатура 	  = ТекСтрока.Номенклатура;
			НоваяСтрока.ЕдиницаИзмерения  = ТекСтрока.ЕдиницаИзмерения;
			НоваяСтрока.Количество 	 	  = ТекСтрока.Количество;
			Если СкладПолучатель.АдресноеХранение Тогда
				НоваяСтрока.ЯчейкаПолучатель = СкладПолучатель.ТранзитнаяЯчейка;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// проверка на ведение складского учета средствами УАТ
	Если НЕ уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(Организация, ПланыВидовХарактеристик.уатПраваИНастройки.ВестиСкладскойУчетУАТ) Тогда
		ТекстНСТР = СтрШаблон(НСтр("en='For company ""%1"" the possibility of inventory management with FMS documents is disabled!';ru='Для организации ""%1"" отключена возможность ведения складского учета документами УАТ!'"), Организация);
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ,, СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатПеремещениеТоваров.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение.ОтразитьПартииТоваровНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	Если ПолучитьФункциональнуюОпцию("уатАдресноеХранение") = ИСТИНА
		И (СкладОтправитель.АдресноеХранение ИЛИ СкладПолучатель.АдресноеХранение) Тогда
		уатПроведение_проф.ОтразитьТоварыВЯчейках(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	уатПроведение_проф.ОтразитьДатыДвиженияТоваровНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Документы.уатПеремещениеТоваров.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);

	уатУчетОригиналовПервичныхДокументов.СформироватьДвиженияРеестрДокументов(Ссылка);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.уатПеремещениеТоваров.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	уатОбщегоНазначенияТиповые.ПроверитьЧтоНетУслуг(ЭтотОбъект, "Товары", , Отказ, Заголовок);
	
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
	Если ПолучитьФункциональнуюОпцию("уатАдресноеХранение") = Истина Тогда
		Если СкладОтправитель.АдресноеХранение Тогда
			ПроверяемыеРеквизиты.Добавить("Товары.ЯчейкаОтправитель");
		КонецЕсли;
		Если СкладПолучатель.АдресноеХранение Тогда
			ПроверяемыеРеквизиты.Добавить("Товары.ЯчейкаПолучатель");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		уатЗащищенныеФункцииСервер_проф.ПолучитьСформироватьШтрихкодОбъекта(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область Инициализация

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
Если уатОбщегоНазначенияТиповые.уатЕстьКонстанта("ВалютаУправленческогоУчета") Тогда 
	мВалютаУпрУчета = Константы.ВалютаУправленческогоУчета.Получить();
Иначе
	мВалютаУпрУчета = мВалютаРегламентированногоУчета;
КонецЕсли;

#КонецОбласти