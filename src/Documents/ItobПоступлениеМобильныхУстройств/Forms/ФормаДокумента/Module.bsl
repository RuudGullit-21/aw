
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Статус         = Перечисления.мкСтатусыРегистрацииМобильныхУстройств.Новый;	
		Объект.ОписаниеОшибки = "";
		Объект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТерминалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытияФормы = Новый Структура("ВидТерминала, НаДату", Объект.ВидТерминала, Объект.Дата); 
	ОткрытьФорму("Справочник.ItobТерминалы.ФормаВыбора", ПараметрыОткрытияФормы, Элемент); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВидТерминалаПриИзменении(Элемент)
	
	Объект.Терминал = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура мкОтменаРегистрацииНаСВМУ(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("мкОтменаРегистрацииНаСВМУЗавершение", ЭтотОбъект), "Отменить запрос к СВМУ?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура мкОтменаРегистрацииНаСВМУЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.Статус = ПредопределенноеЗначение("Перечисление.мкСтатусыРегистрацииМобильныхУстройств.Отмена");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Если Не ПустаяСтрока(Объект.ОписаниеОшибки) Тогда
		Элементы.ОписаниеОшибки.Видимость = Истина;
	Иначе
		Элементы.ОписаниеОшибки.Видимость = Ложь;
	КонецЕсли;
	
	РедактированиеДанныхТерминала = Объект.Статус = Перечисления.мкСтатусыРегистрацииМобильныхУстройств.Новый;
	ТолькоПросмотр = НЕ РедактированиеДанныхТерминала;
	
КонецПроцедуры

#КонецОбласти