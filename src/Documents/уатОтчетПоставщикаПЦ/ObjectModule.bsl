
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатОтчетПоставщикаПЦ.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение.ОтразитьОборотыПоОтчетамПоставщиковПЦ(ДополнительныеСвойства, Движения, Отказ);

	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Документы.уатОтчетПоставщикаПЦ.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	Если АЗС.ЭтоАЗССклад Тогда
		ИндексКонтрагент = ПроверяемыеРеквизиты.Найти("Контрагент");
		ПроверяемыеРеквизиты.Удалить(ИндексКонтрагент);
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Заправки Цикл
		Если ТекСтрока.ПластиковаяКарта.ДатаОкончания <> Дата(1,1,1) Тогда
			Если ТекСтрока.ПластиковаяКарта.ДатаОкончания < ТекСтрока.Дата Тогда 
				ТекстНСТР = СтрШаблон(НСтр("en='In line №%1 fuel card is expired';ru='В строке №%1 топливная карта просрочена'"), ТекСтрока.НомерСтроки);
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
		Если ТекСтрока.Количество < 0 ИЛИ ТекСтрока.Сумма < 0 Тогда
			ТекстНСТР = СтрШаблон(НСтр("en='At line №%1 a negative value is specified of quantity or sum';ru='В строке №%1 указано отрицательное значение количества или суммы'"), ТекСтрока.НомерСтроки);
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР,, Заголовок);
		КонецЕсли;
	КонецЦикла;
	
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	МассивТС = Новый Массив();
	Для Каждого ТекСтрока Из Заправки Цикл 
		Если МассивТС.Найти(ТекСтрока.ТС) = Неопределено Тогда 
			МассивТС.Добавить(ТекСтрока.ТС);
		КонецЕсли;
	КонецЦикла;
	
	СписокТС = "";
	Для Каждого ТекТС Из МассивТС Цикл 
		СписокТС = СписокТС + ?(СписокТС = "", "", ", ") + ТекТС.Наименование;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти


#КонецЕсли