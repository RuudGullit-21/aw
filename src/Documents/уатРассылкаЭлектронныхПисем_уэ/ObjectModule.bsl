#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатТендер_уэ") Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатТендер_уэ") Тогда
		Дата          = ТекущаяДата();
		Основание     = ДанныеЗаполнения.Ссылка;
		Организация   = ДанныеЗаполнения.Организация;
		Подразделение = ДанныеЗаполнения.Подразделение;
		
		Для Каждого ТекСтрока Из ДанныеЗаполнения.Перевозчики Цикл 
			Если Не ТекСтрока.СпособЗапроса = Справочники.уатСпособыВзаимодействий_уэ.ЭлектронноеПисьмо Тогда 
				Продолжить;
			КонецЕсли;
			
			НовыйПолучатель = Получатели.Добавить();
			НовыйПолучатель.Контрагент     = ТекСтрока.Перевозчик;
			НовыйПолучатель.КонтактноеЛицо = ТекСтрока.Перевозчик.ОсновноеКонтактноеЛицо;
			
			СписокАдресов = Документы.уатРассылкаЭлектронныхПисем_уэ.СформироватьСписокАдресов(Новый Структура("Контрагент, КонтактноеЛицо",НовыйПолучатель.Контрагент,НовыйПолучатель.КонтактноеЛицо));
			Если СписокАдресов.Количество() > 0 Тогда
				стрАдрес = СписокАдресов[0].Значение;
				Если НЕ стрАдрес.Количество() = 0 Тогда
					НовыйПолучатель.Адрес               = стрАдрес.Представление;
					НовыйПолучатель.ПредставлениеАдреса = ?(стрАдрес.Объект = НовыйПолучатель.Контрагент,Строка(НовыйПолучатель.Контрагент),Строка(НовыйПолучатель.КонтактноеЛицо)) + " <" + стрАдрес.Представление + ">";
				Иначе
					НовыйПолучатель.Адрес               = "";
					НовыйПолучатель.ПредставлениеАдреса = "";
				КонецЕсли;
			Иначе
				НовыйПолучатель.Адрес               = "";
				НовыйПолучатель.ПредставлениеАдреса = "";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
КонецПроцедуры

#КонецОбласти

#КонецЕсли