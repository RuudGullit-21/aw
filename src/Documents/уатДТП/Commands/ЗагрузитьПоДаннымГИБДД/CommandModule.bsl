
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Обработчик = Новый ОписаниеОповещения("ЗагрузитьПоДаннымГИБДДЗавершение", ЭтотОбъект, ПараметрыВыполненияКоманды.Источник);
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("Заголовок", Нстр("ru = 'Выберите ТС'"));

	СтруктураОтбора = Новый Структура("ЛевоеЗначение,Использование,ВидСравнения,ПравоеЗначение");
	СтруктураОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("VIN");
	СтруктураОтбора.Использование  = Истина;
	СтруктураОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Заполнено;
	СтруктураОтбора.ПравоеЗначение = Неопределено;
	
	Отборы = Новый Массив();
	Отборы.Добавить(СтруктураОтбора);
	ПараметрыОткрытияФормы.Вставить("Отборы",       Отборы);
	ПараметрыОткрытияФормы.Вставить("КлючНастроек", "ГИБДД");

	ОткрытьФорму("Справочник.уатТС.Форма.ФормаЗаполнения", ПараметрыОткрытияФормы, ПараметрыВыполненияКоманды.Источник,,,,Обработчик);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьПоДаннымГИБДДЗавершение(МассивТС, ФормаВладелец) Экспорт
	
	Если МассивТС = Неопределено Тогда
		Возврат;
	КонецЕсли;
	

	ОписаниеОповещения = Новый ОписаниеОповещения("ПодтверждениеКапчи", ЭтотОбъект,
		Новый Структура("ФормаВладелец, МассивТС", ФормаВладелец, МассивТС));
	ПараметрыОткрытия  = Новый Структура();
	
	ОткрытьФорму("Документ.уатДТП.Форма.ФормаКапчи", ПараметрыОткрытия,ЭтотОбъект,,,,ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеКапчи(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Капча       = Новый Структура();
	Капча.Вставить("captchaToken", Результат.captchaToken);
	Капча.Вставить("captchaWord",  Результат.captchaWord);
	
	Загружено   = 0;
	ЗагрузитьПоДаннымГИБДДЗавершениеСервер(ДополнительныеПараметры.МассивТС, Капча, Загружено, ДополнительныеПараметры.ФормаВладелец);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПоДаннымГИБДДЗавершениеСервер(МассивТС, Капча, Загружено = 0, ФормаВладелец)
	
	ТекстОшибки = "";

	ДлительнаяОперация = ЗагрузитьПоДаннымГИБДДДлительнаяОперацияСервер(МассивТС, Капча, ТекстОшибки);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ФормаВладелец);
	НастройкиОжидания.ВыводитьОкноОжидания       = Истина;
	НастройкиОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	Обработчик = Новый ОписаниеОповещения("ЗагрузитьПоДаннымГИБДДЗавершениеДлительнаяОперацияЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПоДаннымГИБДДЗавершениеДлительнаяОперацияЗавершение(Операция, ДополнительныеПараметры) Экспорт
	
	Если Операция = Неопределено Тогда
		
	Иначе
		Если Операция.Статус = "Выполнено" Тогда
			Если ЭтоАдресВременногоХранилища(Операция.АдресРезультата) Тогда
				Данные = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);
				Если ТипЗнч(Данные) = Тип("Структура") Тогда
					Если Данные.Свойство("МассивТС") Тогда 
						Если ЗначениеЗаполнено(Данные.ТекстПредупреждения) Тогда
							ПоказатьПредупреждение(,Данные.ТекстПредупреждения);
						КонецЕсли;
						ОповеститьОбИзменении(Тип("ДокументСсылка.уатДТП")); 
						
						ФормаВладелец = ПолучитьФорму("Документ.уатДТП.Форма.ФормаСписка");
						ЗагрузитьПоДаннымГИБДДЗавершение(Данные.МассивТС, ФормаВладелец);
					ИначеЕсли Данные.Свойство("ТекстПредупреждения") Тогда
						Если ЗначениеЗаполнено(Данные.ТекстПредупреждения) Тогда
							ПоказатьПредупреждение(,Данные.ТекстПредупреждения);
						КонецЕсли;
						ОповеститьОбИзменении(Тип("ДокументСсылка.уатДТП"));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ВызватьИсключение Операция.КраткоеПредставлениеОшибки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьПоДаннымГИБДДДлительнаяОперацияСервер(МассивТС, Капча, ТекстОшибки)
	
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка данных из ГИБДД'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ПараметрыПроцедуры = Новый Структура("МассивТС, Капча", МассивТС, Капча);
	Возврат ДлительныеОперации.ВыполнитьВФоне(
	"уатИнтеграции_проф.ГИБДД_СписокДТПДлительнаяОперация",
	ПараметрыПроцедуры,
	ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти