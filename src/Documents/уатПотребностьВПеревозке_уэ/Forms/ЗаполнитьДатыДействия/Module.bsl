
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("АдресОтправления", АдресОтправления);
	Параметры.Свойство("АдресНазначения",  АдресНазначения);
	Параметры.Свойство("Грузоотправитель", Грузоотправитель);
	Параметры.Свойство("Грузополучатель",  Грузополучатель);
	Параметры.Свойство("Маршрут",          Маршрут);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НачалоПериода = НачалоМесяца(ТекущаяДата());
	КонецПериода = КонецМесяца(ТекущаяДата());
	Длительность = 1;
	
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДлительностьПриИзменении(Элемент)
	Если Длительность = 0 Тогда
		Длительность = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РежимЗаполненияПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Длительность = 0 Тогда
		Длительность = 1;
	КонецЕсли;
	
	Если НачалоПериода = '00010101' ИЛИ КонецПериода = '00010101' ИЛИ НачалоПериода > КонецПериода Тогда
		ТекстНСТР = НСтр("en='Period is not specified or specified incorrectly';ru='Период не указан или указан некорректно'");
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР);
		Возврат;
	КонецЕсли;
	
	РежимЗаполненияПоДнямНедели = 0;
	Если РежимЗаполнения = РежимЗаполненияПоДнямНедели Тогда
		мсвДат = Новый Массив;
		ТекДата = НачалоПериода;
		Пока ТекДата <= КонецПериода Цикл
			Если Пн И ДеньНедели(ТекДата) = 1
				ИЛИ Вт И ДеньНедели(ТекДата) = 2
				ИЛИ Ср И ДеньНедели(ТекДата) = 3
				ИЛИ Чт И ДеньНедели(ТекДата) = 4
				ИЛИ Пт И ДеньНедели(ТекДата) = 5
				ИЛИ Сб И ДеньНедели(ТекДата) = 6
				ИЛИ Вс И ДеньНедели(ТекДата) = 7 Тогда
				
				ДатаПриб = ТекДата + (24*3600*(Длительность-1));
				
				мсвДат.Добавить(Новый Структура("ДатаОтпр, ДатаПриб", ТекДата, ДатаПриб));
			КонецЕсли;
			
			ТекДата = ТекДата + 24*3600;
		КонецЦикла;
	Иначе
		мсвДат = ДанныеРасписанияСервер();
	КонецЕсли;
	
	Закрыть(мсвДат);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПериода(Команда)
	ДиалогПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогПериода.Период.ДатаНачала = НачалоПериода;
	ДиалогПериода.Период.ДатаОкончания = КонецПериода;
	Оповещение = Новый ОписаниеОповещения("НастройкаПериодаОбработкаВыбораПериода", ЭтотОбъект);
	ДиалогПериода.Показать(Оповещение);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастройкаПериодаОбработкаВыбораПериода(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		НачалоПериода = Период.ДатаНачала;
		КонецПериода = Период.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	РежимЗаполненияПоДнямНедели = 0;
	Если РежимЗаполнения = РежимЗаполненияПоДнямНедели Тогда
		Элементы.Длительность.Видимость = Истина;
		Элементы.ГруппаДниНедели.Видимость = Истина;
		Элементы.ДекорацияДней.Видимость = Истина;
	Иначе
		Элементы.Длительность.Видимость = Ложь;
		Элементы.ГруппаДниНедели.Видимость = Ложь;
		Элементы.ДекорацияДней.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДанныеРасписанияСервер()
	мсвДат = Новый Массив;
	СтруктураПараметров = Новый Структура("Маршрут, АдресОтправления, АдресНазначения, Грузоотправитель, Грузополучатель");
	СтруктураПараметров.Маршрут = Маршрут;
	СтруктураПараметров.АдресОтправления = АдресОтправления;
	СтруктураПараметров.АдресНазначения = АдресНазначения;
	СтруктураПараметров.Грузоотправитель = Грузоотправитель;
	СтруктураПараметров.Грузополучатель = Грузополучатель;
	ПодходящиеРейсы = уатОбщегоНазначения_уэ.ДанныеРасписания(НачалоПериода, КонецПериода, СтруктураПараметров);
	Для Каждого ТекСтрока Из ПодходящиеРейсы Цикл
		мсвДат.Добавить(Новый Структура("ДатаОтпр, ДатаПриб", ТекСтрока.ДатаОтправления, ТекСтрока.ДатаПрибытия));
	КонецЦикла;
	Возврат мсвДат;
КонецФункции

#КонецОбласти
