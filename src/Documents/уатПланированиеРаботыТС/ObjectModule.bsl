
#Область ПеременныеОбъекта

Перем мВалютаРегламентированногоУчета Экспорт;//Переменная хранит значение валюты регламентированного учёта,
                                              // полученное из констант

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатПланированиеРаботыТС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение_проф.ОтразитьПланРаботыТС(ДополнительныеСвойства, Движения, Отказ);

	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль наличия других документов с планами для тех же организации, подразделению и перевозчику
	// с пересекающимися периодами
	Документы.уатПланированиеРаботыТС.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	// Инициализация дополнительных свойств для проведения документа
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.уатПланированиеРаботыТС.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
КонецПроцедуры

Процедура ПередЗаписью(Отказ,РежимЗаписи , РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = уатОбщегоНазначенияТиповые.уатПолучитьСуммуДокументаСНДС(ЭтотОбъект, "СоставПлана");
	
	// уатСогласованиеДокументов
	уатСогласованиеДокументовСервер.ПередЗаписью(ЭтотОбъект, РежимЗаписи, Отказ);
	// Конец уатСогласованиеДокументов
	
КонецПроцедуры 

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	Если Сценарий.Раздел = Перечисления.уатРазделыПланирования.ПредоставленныеУслуги Тогда
		Если УчитыватьНДС Тогда
			ПроверяемыеРеквизиты.Добавить("СоставПлана.СтавкаНДС");
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из СоставПлана Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.Договор) И СтрокаТаблицы.Договор.ВалютаВзаиморасчетов <> ВалютаДокумента Тогда
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке("В строке №" + СтрокаТаблицы.НомерСтроки + " валюта договора не совпадает с валютой документа!", Отказ, Заголовок);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Сценарий.Раздел = Перечисления.уатРазделыПланирования.ОбъемыПеревозок
		И Сценарий.ПланированиеОбъемовВПроцентах Тогда
		
		ТаблицаСоставПлана = СоставПлана.Выгрузить(, "ВидПеревозки, Маршрут, ОбъектСтроительства, НаправлениеПеревозки, ДетализацияПланирования, Количество");
		ТаблицаСоставПлана.Свернуть("ВидПеревозки, Маршрут, ОбъектСтроительства, НаправлениеПеревозки, ДетализацияПланирования", "Количество");
		Для Каждого ТекСтрока ИЗ ТаблицаСоставПлана Цикл
			Если ТекСтрока.Количество > 100 Тогда
				ТекстНСтр = НСтр("ru = 'По аналитикам %1 запланировано %2%3 общего количества выработки, что превышает 100%3.'");
				МассивАналитик = Новый Массив;
				Если ЗначениеЗаполнено(ТекСтрока.Маршрут) Тогда
					МассивАналитик.Добавить(ТекСтрока.Маршрут);
				КонецЕсли;
				Если ЗначениеЗаполнено(ТекСтрока.ВидПеревозки) Тогда
					МассивАналитик.Добавить(ТекСтрока.ВидПеревозки);
				КонецЕсли;
				Если ЗначениеЗаполнено(ТекСтрока.НаправлениеПеревозки) Тогда
					МассивАналитик.Добавить(ТекСтрока.НаправлениеПеревозки);
				КонецЕсли;
				Если ЗначениеЗаполнено(ТекСтрока.ОбъектСтроительства) Тогда
					МассивАналитик.Добавить(ТекСтрока.ОбъектСтроительства);
				КонецЕсли;
				Если ЗначениеЗаполнено(ТекСтрока.ДетализацияПланирования) Тогда
					МассивАналитик.Добавить(ТекСтрока.ДетализацияПланирования);
				КонецЕсли;
				ТекстНСтр = СтрШаблон(ТекстНСтр, СтрСоединить(МассивАналитик, ", "), ТекСтрока.Количество, "%");
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСтр, Отказ, Заголовок);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Сценарий.Раздел = Перечисления.уатРазделыПланирования.ОбъемыПеревозок Тогда
		уатОбщегоНазначенияСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "СоставПлана.ДетализацияПланирования");
		
		Для Каждого СтрокаТаблицы Из СоставПлана Цикл
			Если Сценарий.Раздел = Перечисления.уатРазделыПланирования.ОбъемыПеревозок 
				И Не ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				ТекстНСтр = СтрШаблон(НСтр("ru = 'В строке %1 не заполнен перевозчик'"), СтрокаТаблицы.НомерСтроки);
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСтр, Отказ, Заголовок);
			КонецЕсли;
			
			Если Сценарий.Раздел = Перечисления.уатРазделыПланирования.ОбъемыПеревозок 
				И Не ЗначениеЗаполнено(СтрокаТаблицы.Договор) Тогда
				ТекстНСтр = СтрШаблон(НСтр("ru = 'В строке %1 не заполнен договор с перевозчиком'"), СтрокаТаблицы.НомерСтроки);
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСтр, Отказ, Заголовок);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти


#Область ИсполняемаяЧастьМодуля

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

#КонецОбласти
