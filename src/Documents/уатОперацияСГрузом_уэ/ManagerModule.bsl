#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Настройки.ПриПолученииСлужебныхРеквизитов = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Позволяет переопределить ограничение, указанное в модуле менеджера объекта метаданных.
//
// Параметры:
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВариантыОтчетов

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Отказ) Экспорт
	
	// Блокировка устанавливается позже (в модуле объекта, в процедуре ОбработкаПроведения)
	
	мЗапрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатОперацияСГрузом_уэ.Ссылка КАК Ссылка,
	|	уатОперацияСГрузом_уэ.Дата КАК Дата,
	|	уатОперацияСГрузом_уэ.ВидОперации КАК ВидОперации,
	|	уатОперацияСГрузом_уэ.Склад КАК Склад,
	|	уатОперацияСГрузом_уэ.Организация КАК Организация,
	|	уатОперацияСГрузом_уэ.СтатусВыполнения КАК СтатусВыполнения,
	|	уатОперацияСГрузом_уэ.ОкончаниеОперации КАК ОкончаниеОперации,
	|	уатОперацияСГрузом_уэ.Контейнер КАК Контейнер,
	|	уатОперацияСГрузом_уэ.ЗаказНаКонтейнер КАК ЗаказНаКонтейнер,
	|	уатОперацияСГрузом_уэ.ЗаказНаКонтейнерНовый КАК ЗаказНаКонтейнерНовый
	|ПОМЕСТИТЬ втОперация
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ КАК уатОперацияСГрузом_уэ
	|ГДЕ
	|	уатОперацияСГрузом_уэ.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА втОперация.СтатусВыполнения = ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Выполнен)
	|			ТОГДА втОперация.ОкончаниеОперации
	|		ИНАЧЕ втОперация.Дата
	|	КОНЕЦ КАК Период,
	|	ВЫБОР
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|				ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнере)
	|				ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнереВПункте)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Скомплектован)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Разукомплектован)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПереносОстатков)
	|				ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПерегрузкаКонтейнера)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.ВыданСоСклада)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.ПустаяСсылка)
	|	КОНЕЦ КАК Статус,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ КАК Заказ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыДоОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоГрузовымМестам)
	|			ТОГДА уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ГрузовоеМесто
	|ИЗ
	|	втОперация КАК втОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уатОперацияСГрузом_уэ.ГрузыДоОперации КАК уатОперацияСГрузом_уэГрузыДоОперации
	|		ПО втОперация.Ссылка = уатОперацияСГрузом_уэГрузыДоОперации.Ссылка
	|ГДЕ
	|	(втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнере)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнереВПункте)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПереносОстатков)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПерегрузкаКонтейнера))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА втОперация.СтатусВыполнения = ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Выполнен)
	|			ТОГДА втОперация.ОкончаниеОперации
	|		ИНАЧЕ втОперация.Дата
	|	КОНЕЦ,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыДоОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоГрузовымМестам)
	|			ТОГДА уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|				ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнере)
	|				ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнереВПункте)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Скомплектован)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Разукомплектован)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПереносОстатков)
	|				ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПерегрузкаКонтейнера)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.ВыданСоСклада)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОперация.ОкончаниеОперации,
	|	ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.ДоставленНаСклад),
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыПослеОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоГрузовымМестам)
	|			ТОГДА уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|ИЗ
	|	втОперация КАК втОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уатОперацияСГрузом_уэ.ГрузыПослеОперации КАК уатОперацияСГрузом_уэГрузыПослеОперации
	|		ПО втОперация.Ссылка = уатОперацияСГрузом_уэГрузыПослеОперации.Ссылка
	|ГДЕ
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ <> ЗНАЧЕНИЕ(Документ.уатЗаказГрузоотправителя.ПустаяСсылка)
	|	И (втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ИзвлечениеГрузаИзКонтейнера)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ИзвлечениеГрузаИзКонтейнераВПункте)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПереносОстатков)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПерегрузкаКонтейнера))
	|
	|СГРУППИРОВАТЬ ПО
	|	втОперация.ОкончаниеОперации,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыПослеОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоГрузовымМестам)
	|			ТОГДА уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОперация.ОкончаниеОперации,
	|	ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.РазмещениеВКонтейнере),
	|	втОперация.ЗаказНаКонтейнер,
	|	втОперация.Контейнер
	|ИЗ
	|	втОперация КАК втОперация
	|ГДЕ
	|	(втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнере)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнереВПункте))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОперация.ОкончаниеОперации,
	|	ЗНАЧЕНИЕ(Справочник.уатСтатусы_уэ.Разукомплектован),
	|	втОперация.ЗаказНаКонтейнер,
	|	втОперация.Контейнер
	|ИЗ
	|	втОперация КАК втОперация
	|ГДЕ
	|	(втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ИзвлечениеГрузаИзКонтейнера)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ИзвлечениеГрузаИзКонтейнераВПункте))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втОперация.ОкончаниеОперации КАК Период,
	|	втОперация.Склад КАК Склад,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ КАК ЗаказГрузоотправителя,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто КАК ГрузовоеМесто,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ВидУпаковки КАК ЕдиницаИзмерения,
	|	СУММА(уатОперацияСГрузом_уэГрузыПослеОперации.КоличествоМест) КАК Количество
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыПослеОперации КАК уатОперацияСГрузом_уэГрузыПослеОперации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОперация КАК втОперация
	|		ПО уатОперацияСГрузом_уэГрузыПослеОперации.Ссылка = втОперация.Ссылка
	|ГДЕ
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ <> ЗНАЧЕНИЕ(Документ.уатЗаказГрузоотправителя.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втОперация.ОкончаниеОперации,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ВидУпаковки,
	|	втОперация.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОперация.ОкончаниеОперации,
	|	втОперация.Склад,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыДоОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоЗаказам)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыДоОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоЗаказам)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ уатОперацияСГрузом_уэГрузыДоОперации.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	СУММА(уатОперацияСГрузом_уэГрузыДоОперации.Количество)
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыДоОперации КАК уатОперацияСГрузом_уэГрузыДоОперации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОперация КАК втОперация
	|		ПО уатОперацияСГрузом_уэГрузыДоОперации.Ссылка = втОперация.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	втОперация.ОкончаниеОперации,
	|	втОперация.Склад,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыДоОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоЗаказам)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА уатОперацияСГрузом_уэГрузыДоОперации.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоЗаказам)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ уатОперацияСГрузом_уэГрузыДоОперации.ЕдиницаИзмерения
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ КАК Заказ,
	|	уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто КАК ГрузовоеМесто,
	|	СУММА(-уатОперацияСГрузом_уэГрузыДоОперации.ВесБрутто) КАК Вес,
	|	СУММА(-уатОперацияСГрузом_уэГрузыДоОперации.Объем) КАК Объем,
	|	СУММА(-уатОперацияСГрузом_уэГрузыДоОперации.Количество) КАК Количество,
	|	втОперация.Склад КАК Склад,
	|	втОперация.ОкончаниеОперации КАК Период,
	|	ВЫБОР
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Комплектация)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Разукомплектация)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.ПустаяСсылка)
	|	КОНЕЦ КАК ВидОперации,
	|	СУММА(-уатОперацияСГрузом_уэГрузыДоОперации.КоличествоМест) КАК КоличествоМест
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыДоОперации КАК уатОперацияСГрузом_уэГрузыДоОперации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОперация КАК втОперация
	|		ПО уатОперацияСГрузом_уэГрузыДоОперации.Ссылка = втОперация.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Комплектация)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Разукомплектация)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.ПустаяСсылка)
	|	КОНЕЦ,
	|	уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ,
	|	втОперация.Склад,
	|	втОперация.ОкончаниеОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто,
	|	СУММА(уатОперацияСГрузом_уэГрузыПослеОперации.ВесБрутто),
	|	СУММА(уатОперацияСГрузом_уэГрузыПослеОперации.Объем),
	|	СУММА(уатОперацияСГрузом_уэГрузыПослеОперации.КоличествоМест),
	|	втОперация.Склад,
	|	втОперация.ОкончаниеОперации,
	|	ВЫБОР
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Комплектация)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Разукомплектация)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.ПустаяСсылка)
	|	КОНЕЦ,
	|	СУММА(уатОперацияСГрузом_уэГрузыПослеОперации.КоличествоМест)
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыПослеОперации КАК уатОперацияСГрузом_уэГрузыПослеОперации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОперация КАК втОперация
	|		ПО уатОперацияСГрузом_уэГрузыПослеОперации.Ссылка = втОперация.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто,
	|	втОперация.Склад,
	|	втОперация.ОкончаниеОперации,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ,
	|	ВЫБОР
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Комплектация)
	|		КОГДА втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.Разукомплектация)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.Разукомплектация)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.уатВидыСкладскихОпераций_уэ.ПустаяСсылка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(уатГрузыКПеревозке_уэОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокГрузыКПеревозке,
	|	ЕСТЬNULL(уатЗаказыГрузоотправителейОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыГрузоотправителей,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Количество КАК КоличествоДокумент,
	|	уатЗаказыГрузоотправителейОстатки.Контрагент КАК Контрагент,
	|	уатЗаказыГрузоотправителейОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	уатЗаказыГрузоотправителейОстатки.ЗаказГрузоотправителя КАК ЗаказГрузоотправителя,
	|	уатЗаказыГрузоотправителейОстатки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	уатЗаказыГрузоотправителейОстатки.Номенклатура КАК Номенклатура,
	|	уатЗаказыГрузоотправителейОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	втОперация.ОкончаниеОперации КАК ОкончаниеОперации
	|ПОМЕСТИТЬ втОстаткиГрузов
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыДоОперации КАК уатОперацияСГрузом_уэГрузыДоОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатГрузыКПеревозке_уэ.Остатки(&ОкончаниеОперации, ) КАК уатГрузыКПеревозке_уэОстатки
	|		ПО уатОперацияСГрузом_уэГрузыДоОперации.Заказ = уатГрузыКПеревозке_уэОстатки.ЗаказГрузоотправителя
	|			И (ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто) = ТИП(Справочник.уатГрузовыеМеста_уэ)
	|					ТОГДА уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто = уатГрузыКПеревозке_уэОстатки.ГрузовоеМесто
	|				ИНАЧЕ уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто = уатГрузыКПеревозке_уэОстатки.Номенклатура
	|			КОНЕЦ)
	|			И уатОперацияСГрузом_уэГрузыДоОперации.ЕдиницаИзмерения = уатГрузыКПеревозке_уэОстатки.ЕдиницаИзмерения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатЗаказыГрузоотправителей.Остатки(&ОкончаниеОперации, ) КАК уатЗаказыГрузоотправителейОстатки
	|		ПО уатОперацияСГрузом_уэГрузыДоОперации.Заказ = уатЗаказыГрузоотправителейОстатки.ЗаказГрузоотправителя
	|			И (ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто) = ТИП(Справочник.уатГрузовыеМеста_уэ)
	|					ТОГДА уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто = уатЗаказыГрузоотправителейОстатки.ГрузовоеМесто
	|				ИНАЧЕ уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто = уатЗаказыГрузоотправителейОстатки.Номенклатура
	|			КОНЕЦ)
	|			И уатОперацияСГрузом_уэГрузыДоОперации.ЕдиницаИзмерения = уатЗаказыГрузоотправителейОстатки.ЕдиницаИзмерения
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОперация КАК втОперация
	|		ПО уатОперацияСГрузом_уэГрузыДоОперации.Ссылка = втОперация.Ссылка
	|ГДЕ
	|	втОперация.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСгрузом_уэ.ПереносОстатков)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиГрузов.Контрагент КАК Контрагент,
	|	втОстаткиГрузов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	втОстаткиГрузов.ЗаказГрузоотправителя КАК ЗаказГрузоотправителя,
	|	втОстаткиГрузов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втОстаткиГрузов.Номенклатура КАК Номенклатура,
	|	втОстаткиГрузов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА втОстаткиГрузов.КоличествоДокумент > втОстаткиГрузов.КоличествоОстатокГрузыКПеревозке
	|			ТОГДА втОстаткиГрузов.КоличествоОстатокГрузыКПеревозке
	|		ИНАЧЕ втОстаткиГрузов.КоличествоДокумент
	|	КОНЕЦ КАК Количество,
	|	втОстаткиГрузов.ОкончаниеОперации КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|ИЗ
	|	втОстаткиГрузов КАК втОстаткиГрузов
	|ГДЕ
	|	втОстаткиГрузов.КоличествоОстатокГрузыКПеревозке = втОстаткиГрузов.КоличествоОстатокЗаказыГрузоотправителей
	|	И втОстаткиГрузов.КоличествоОстатокГрузыКПеревозке > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОперация.Контейнер КАК Контейнер,
	|	ВЫБОР
	|		КОГДА втОперация.ЗаказНаКонтейнер.Мультимодальный
	|				И втОперация.ЗаказНаКонтейнер.ЭтоЭтап
	|			ТОГДА втОперация.ЗаказНаКонтейнер.РодительскийЗаказ
	|		ИНАЧЕ втОперация.ЗаказНаКонтейнер
	|	КОНЕЦ КАК ЗаказНаКонтейнер,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Заказ КАК ЗаказГрузоотправителя,
	|	уатОперацияСГрузом_уэГрузыДоОперации.ГрузовоеМесто КАК ГрузовоеМесто,
	|	уатОперацияСГрузом_уэГрузыДоОперации.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	уатОперацияСГрузом_уэГрузыДоОперации.Количество КАК Количество,
	|	втОперация.ОкончаниеОперации КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
	|ИЗ
	|	втОперация КАК втОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уатОперацияСГрузом_уэ.ГрузыДоОперации КАК уатОперацияСГрузом_уэГрузыДоОперации
	|		ПО втОперация.Ссылка = уатОперацияСГрузом_уэГрузыДоОперации.Ссылка
	|ГДЕ
	|	(втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнере)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.РазмещениеГрузаВКонтейнереВПункте))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОперация.Контейнер,
	|	ВЫБОР
	|		КОГДА втОперация.ЗаказНаКонтейнер.Мультимодальный
	|				И втОперация.ЗаказНаКонтейнер.ЭтоЭтап
	|			ТОГДА втОперация.ЗаказНаКонтейнер.РодительскийЗаказ
	|		ИНАЧЕ втОперация.ЗаказНаКонтейнер
	|	КОНЕЦ,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.Заказ,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ГрузовоеМесто,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.ВидУпаковки,
	|	уатОперацияСГрузом_уэГрузыПослеОперации.КоличествоМест,
	|	втОперация.ОкончаниеОперации,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ИЗ
	|	втОперация КАК втОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уатОперацияСГрузом_уэ.ГрузыПослеОперации КАК уатОперацияСГрузом_уэГрузыПослеОперации
	|		ПО втОперация.Ссылка = уатОперацияСГрузом_уэГрузыПослеОперации.Ссылка
	|ГДЕ
	|	(втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ИзвлечениеГрузаИзКонтейнера)
	|			ИЛИ втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ИзвлечениеГрузаИзКонтейнераВПункте))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.Контейнер,
	|	ВЫБОР
	|		КОГДА втОперация.ЗаказНаКонтейнерНовый.Мультимодальный
	|				И втОперация.ЗаказНаКонтейнерНовый.ЭтоЭтап
	|			ТОГДА втОперация.ЗаказНаКонтейнерНовый.РодительскийЗаказ
	|		ИНАЧЕ втОперация.ЗаказНаКонтейнерНовый
	|	КОНЕЦ,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.ЗаказГрузоотправителя,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.ГрузовоеМесто,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.ЕдиницаИзмерения,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.КоличествоОстаток,
	|	втОперация.ОкончаниеОперации,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|ИЗ
	|	втОперация КАК втОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.уатОстаткиГрузовВКонтейнерах_уэ.Остатки(&ОкончаниеОперации, ) КАК уатОстаткиГрузовВКонтейнерах_уэОстатки
	|		ПО втОперация.Контейнер = уатОстаткиГрузовВКонтейнерах_уэОстатки.Контейнер
	|ГДЕ
	|	втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПерегрузкаКонтейнера)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.Контейнер,
	|	ВЫБОР
	|		КОГДА втОперация.ЗаказНаКонтейнер.Мультимодальный
	|				И втОперация.ЗаказНаКонтейнер.ЭтоЭтап
	|			ТОГДА втОперация.ЗаказНаКонтейнер.РодительскийЗаказ
	|		ИНАЧЕ втОперация.ЗаказНаКонтейнер
	|	КОНЕЦ,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.ЗаказГрузоотправителя,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.ГрузовоеМесто,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.ЕдиницаИзмерения,
	|	уатОстаткиГрузовВКонтейнерах_уэОстатки.КоличествоОстаток,
	|	втОперация.ОкончаниеОперации,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ИЗ
	|	втОперация КАК втОперация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.уатОстаткиГрузовВКонтейнерах_уэ.Остатки(&ОкончаниеОперации, ) КАК уатОстаткиГрузовВКонтейнерах_уэОстатки
	|		ПО втОперация.Контейнер = уатОстаткиГрузовВКонтейнерах_уэОстатки.Контейнер
	|ГДЕ
	|	втОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.уатВидыОперацийОперацияСГрузом_уэ.ПерегрузкаКонтейнера)");
	
	мЗапрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	мЗапрос.УстановитьПараметр("МоментВремени", ДокументСсылка.МоментВремени());
	МоментОкончанияОперации = Новый МоментВремени(ДокументСсылка.ОкончаниеОперации, ДокументСсылка);
	мЗапрос.УстановитьПараметр("ОкончаниеОперации", МоментОкончанияОперации);
	мЗапрос.УстановитьПараметр("Контейнер", ДокументСсылка.Контейнер);
	
	
	МассивРезультатов = мЗапрос.ВыполнитьПакет();
	
	ТаблицаСтатусыГрузов = МассивРезультатов[1].Выгрузить();
	ТаблицаОстаткиГрузов = МассивРезультатов[2].Выгрузить();
	ТаблицаСкладскоеХранениеГрузов = МассивРезультатов[3].Выгрузить();
	ТаблицаГрузовКПеревозке = МассивРезультатов[5].Выгрузить();
	ТаблицаЗаказовГрузоотправителя = ТаблицаГрузовКПеревозке;
	ТаблицаОстаткиГрузовВКонтейнерах = МассивРезультатов[6].Выгрузить();
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиГрузов", ТаблицаОстаткиГрузов);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСтатусыГрузов", ТаблицаСтатусыГрузов);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСкладскоеХранениеГрузов", ТаблицаСкладскоеХранениеГрузов);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаГрузовКПеревозке", ТаблицаГрузовКПеревозке);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказовГрузоотправителя", ТаблицаЗаказовГрузоотправителя);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиГрузовВКонтейнерах", ТаблицаОстаткиГрузовВКонтейнерах);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

Процедура ВыполнитьКонтроль(ДокументСсылка, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	мЗапрос = Новый Запрос;
	мЗапрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц; 
	мЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаДокумента.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ТаблицаДокумента.Заказ КАК Заказ,
	|	ТаблицаДокумента.Ссылка КАК Регистратор,
	|	ЕСТЬNULL(уатОстаткиГрузовНаСкладеКомплектации_уэ.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыДоОперации КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатОстаткиГрузовНаСкладеКомплектации_уэ.Остатки(&МоментВремени, Склад = &Склад) КАК уатОстаткиГрузовНаСкладеКомплектации_уэ
	|		ПО (уатОстаткиГрузовНаСкладеКомплектации_уэ.Склад = &Склад)
	|			И ТаблицаДокумента.Заказ = уатОстаткиГрузовНаСкладеКомплектации_уэ.ЗаказГрузоотправителя
	|			И (ТаблицаДокумента.ГрузовоеМесто = уатОстаткиГрузовНаСкладеКомплектации_уэ.ГрузовоеМесто
	|				ИЛИ ТаблицаДокумента.Заказ.ДетализацияЗакрытия = ЗНАЧЕНИЕ(Перечисление.уатДетализацияЗаказаГрузоотправителя_уэ.ПоЗаказам))
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ЕСТЬNULL(уатОстаткиГрузовНаСкладеКомплектации_уэ.КоличествоОстаток, 0) < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.ГрузовоеМесто,
	|	ТаблицаДокумента.Заказ,
	|	ТаблицаДокумента.Ссылка,
	|	ЕСТЬNULL(уатОстаткиГрузовНаСкладеКомплектации_уэ.КоличествоОстаток, 0)
	|ИЗ
	|	Документ.уатОперацияСГрузом_уэ.ГрузыПослеОперации КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатОстаткиГрузовНаСкладеКомплектации_уэ.Остатки(&МоментВремени, Склад = &Склад) КАК уатОстаткиГрузовНаСкладеКомплектации_уэ
	|		ПО (уатОстаткиГрузовНаСкладеКомплектации_уэ.Склад = &Склад)
	|			И ТаблицаДокумента.Заказ = уатОстаткиГрузовНаСкладеКомплектации_уэ.ЗаказГрузоотправителя
	|			И ТаблицаДокумента.ГрузовоеМесто = уатОстаткиГрузовНаСкладеКомплектации_уэ.ГрузовоеМесто
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ЕСТЬNULL(уатОстаткиГрузовНаСкладеКомплектации_уэ.КоличествоОстаток, 0) < 0";
	
	мЗапрос.УстановитьПараметр("Склад"        , ДокументСсылка.Склад);
	мЗапрос.УстановитьПараметр("Ссылка"       , ДокументСсылка);
	МоментКонтроля = Новый МоментВремени(ДокументСсылка.ОкончаниеОперации, ДокументСсылка);
	мГраницаКонтроля = Новый Граница(МоментКонтроля, ВидГраницы.Включая);
	мЗапрос.УстановитьПараметр("МоментВремени", мГраницаКонтроля);
	
	Выборка = мЗапрос.Выполнить().Выбрать();
	Если Выборка.Количество() тогда
		Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ДокументСсылка);
		Пока Выборка.Следующий() Цикл
			ТекстНСТР = НСтр("en='For the cargo ""%1"" (%2) negative balances were received in temporary storage area ""%3"": %4';ru='Для груза ""%1"" (%2) получены отрицательные остатки на складе комплектации ""%3"": %4'");
			ТекстНСТР = СтрШаблон(ТекстНСТР, Выборка.ГрузовоеМесто, Выборка.Заказ, мЗапрос.Параметры.Склад, Выборка.КоличествоОстаток);
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР,, Заголовок);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПараметрыОткрытияОтчетОстаткиГрузовВКонтейнере(Ссылка) Экспорт
	
	СхемаКомпоновкиДанных = Отчеты.уатОстаткиГрузовВКонтейнерах_уэ.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	НастройкиКомпоновки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Для Каждого ТекЭлем Из НастройкиКомпоновки.Отбор.Элементы Цикл
		Если Строка(ТекЭлем.ЛевоеЗначение) = "Контейнер" И ЗначениеЗаполнено(Ссылка.Контейнер) Тогда
			ТекЭлем.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ТекЭлем.ПравоеЗначение = Ссылка.Контейнер;
			ТекЭлем.Использование = Истина;
		КонецЕсли;
	КонецЦикла;
	ТекЭлем = НастройкиКомпоновки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ТекЭлем <> Неопределено И Ссылка.ОперацияВыполнена Тогда
		ТекЭлем.Значение = Ссылка.ОкончаниеОперации;
		ТекЭлем.Использование = Истина;
	КонецЕсли;
	
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(НастройкиКомпоновки);
	
	сткПараметры = Новый Структура("СформироватьПриОткрытии, ПользовательскиеНастройки", Истина, КомпоновщикНастроекКомпоновкиДанных.ПользовательскиеНастройки);
	
	Возврат сткПараметры;
	
КонецФункции
	
#КонецОбласти

#КонецЕсли