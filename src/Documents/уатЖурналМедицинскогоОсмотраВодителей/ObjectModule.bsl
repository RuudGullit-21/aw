
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
		Организация         = ДанныеЗаполнения.Организация;
		Подразделение       = ДанныеЗаполнения.Подразделение;
		Водитель            = ДанныеЗаполнения.Водитель1;
		МедицинскийРаботник = ДанныеЗаполнения.МедработникВыезд;
		ПутевойЛист         = ДанныеЗаполнения.Ссылка;
		
		СозданныеДокументы = уатЖурналыТранспортныхДокументов.СозданныеДокументыМедосмотраПоПЛ(ДанныеЗаполнения);
			
		Если ЗначениеЗаполнено(ДанныеЗаполнения.Водитель2) Тогда // в ПЛ два водителя
			Если СозданныеДокументы.Послерейсовый1 <> Неопределено
				И СозданныеДокументы.Послерейсовый2 = Неопределено Тогда // все документы по водителю 1 уже созданы
				Водитель            = ДанныеЗаполнения.Водитель2;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	// проверка на существование уже созданного документа
	Если уатЖурналыТранспортныхДокументов.УжеСозданДокументМедосмотраПоВодителю(ПутевойЛист, Водитель, ВидКонтроля, Ссылка) Тогда
		ТекстНСТР = СтрШаблон("Запись журнала %1 медосмотра водителя ""%2"" (""%3"") уже создана!",
			?(ВидКонтроля = Перечисления.уатВидыКонтроляТранспортныхДокументов.Предрейсовый, "предрейсового", "послерейсового"),
			Водитель, ПутевойЛист);
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ, Заголовок);
	КонецЕсли;
	
	// проверка на нарушение минимального интервала ПЛ
	Если НЕ Отказ И ЗначениеЗаполнено(Дата) Тогда
		МинИнтервалПЛ = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(Организация,
			ПланыВидовХарактеристик.уатПраваИНастройки.МинимальныйИнтервалМеждуПредрейсовымИПослерейсовымОсмотром);
			
		Если МинИнтервалПЛ > 0 Тогда
			ИнтервалПоЖурналу = Неопределено;
			
			СозданныеДокументы = уатЖурналыТранспортныхДокументов.СозданныеДокументыМедосмотраПоПЛ(ПутевойЛист);
			
			Если ВидКонтроля = Перечисления.уатВидыКонтроляТранспортныхДокументов.Предрейсовый Тогда
				Если Водитель = ПутевойЛист.Водитель1 Тогда
					ДокументМедосмотрДругой = СозданныеДокументы.Послерейсовый1;
				Иначе
					ДокументМедосмотрДругой = СозданныеДокументы.Послерейсовый2;
				КонецЕсли;
				Если ЗначениеЗаполнено(ДокументМедосмотрДругой) Тогда
					ИнтервалПоЖурналу = ДокументМедосмотрДругой.Дата - Дата;
					ДатаПредрейсовый  = Дата;
					ДатаПослерейсовый = ДокументМедосмотрДругой.Дата;
				КонецЕсли;
			Иначе
				Если Водитель = ПутевойЛист.Водитель1 Тогда
					ДокументМедосмотрДругой = СозданныеДокументы.Предрейсовый1;
				Иначе
					ДокументМедосмотрДругой = СозданныеДокументы.Предрейсовый2;
				КонецЕсли;
				Если ЗначениеЗаполнено(ДокументМедосмотрДругой) Тогда
					ИнтервалПоЖурналу = Дата - ДокументМедосмотрДругой.Дата;
					ДатаПредрейсовый  = ДокументМедосмотрДругой.Дата;
					ДатаПослерейсовый = Дата;
				КонецЕсли;
			КонецЕсли;
			
			Если ИнтервалПоЖурналу <> Неопределено И ИнтервалПоЖурналу < МинИнтервалПЛ*60 Тогда
				Если НачалоДня(ДатаПредрейсовый) = НачалоДня(ДатаПослерейсовый) Тогда
					ДатаПредрейсовый  = Формат(ДатаПредрейсовый,  "ДФ=HH:mm");
					ДатаПослерейсовый = Формат(ДатаПослерейсовый, "ДФ=HH:mm");
				Иначе
					ДатаПредрейсовый  = Формат(ДатаПредрейсовый,  "ДФ='dd.MM.yyyy HH:mm'");
					ДатаПослерейсовый = Формат(ДатаПослерейсовый, "ДФ='dd.MM.yyyy HH:mm'");
				КонецЕсли;
				
				ТекстНСТР = СтрШаблон(
					"Интервал между предрейсовым и послерейсовым медицинским осмотром водителя не должен быть меньше %1 минут!
					|Время предрейсового осмотра: %2
					|Время послерейсового осмотра: %3",
					МинИнтервалПЛ, ДатаПредрейсовый, ДатаПослерейсовый);
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		уатЖурналыТранспортныхДокументов.ИзменитьСостоянияПодписейПЛ(Ссылка);
	КонецЕсли;
				
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
	Если ЗначениеЗаполнено(ПутевойЛист) И ЗначениеЗаполнено(Водитель)
		И Водитель <> ПутевойЛист.Водитель1 И Водитель <> ПутевойЛист.Водитель2 Тогда
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(
			СтрШаблон("Водитель ""%1"" отсутствует в указанном Путевом листе!", Водитель),
			Отказ, Заголовок);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
