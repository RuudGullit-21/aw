
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		АвтоТест = Истина;
	КонецЕсли;
	
	ПутевойЛист = Параметры.ПутевойЛист;
	
	НоваяСтрока = Водители.Добавить();
	НоваяСтрока.Пометка  = Истина;
	НоваяСтрока.Водитель = ПутевойЛист.Водитель1;
	НоваяСтрока.Предрейсовый  = Параметры.СозданныеДокументы.Предрейсовый1;
	Если ЗначениеЗаполнено(НоваяСтрока.Предрейсовый) Тогда
		НоваяСтрока.ПредрейсовыйСтрока = "№" + НоваяСтрока.Предрейсовый.Номер + " от " + НоваяСтрока.Предрейсовый.Дата;
	Иначе
		НоваяСтрока.ПредрейсовыйСтрока = "Создать новый документ";
	КонецЕсли;
	НоваяСтрока.Послерейсовый = Параметры.СозданныеДокументы.Послерейсовый1;
	Если ЗначениеЗаполнено(НоваяСтрока.Послерейсовый) Тогда
		НоваяСтрока.ПослерейсовыйСтрока = "№" + НоваяСтрока.Послерейсовый.Номер + " от " + НоваяСтрока.Послерейсовый.Дата;
	ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.Предрейсовый) Тогда
		НоваяСтрока.ПослерейсовыйСтрока = "Создать новый документ";
	КонецЕсли;
	
	НоваяСтрока = Водители.Добавить();
	НоваяСтрока.Пометка  = Истина;
	НоваяСтрока.Водитель = ПутевойЛист.Водитель2;
	НоваяСтрока.Предрейсовый  = Параметры.СозданныеДокументы.Предрейсовый2;
	Если ЗначениеЗаполнено(НоваяСтрока.Предрейсовый) Тогда
		НоваяСтрока.ПредрейсовыйСтрока = "№" + НоваяСтрока.Предрейсовый.Номер + " от " + НоваяСтрока.Предрейсовый.Дата;
	Иначе
		НоваяСтрока.ПредрейсовыйСтрока = "Создать новый документ";
	КонецЕсли;
	НоваяСтрока.Послерейсовый = Параметры.СозданныеДокументы.Послерейсовый2;
	Если ЗначениеЗаполнено(НоваяСтрока.Послерейсовый) Тогда
		НоваяСтрока.ПослерейсовыйСтрока = "№" + НоваяСтрока.Послерейсовый.Номер + " от " + НоваяСтрока.Послерейсовый.Дата;
	ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.Предрейсовый) Тогда
		НоваяСтрока.ПослерейсовыйСтрока = "Создать новый документ";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВыбраныСтроки И ВладелецФормы <> Неопределено Тогда
		ВладелецФормы.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого ТекВодитель из Водители Цикл
		ТекВодитель.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого ТекВодитель из Водители Цикл
		ТекВодитель.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Сч = 0;
	Для Каждого ТекСтрока Из Водители Цикл
		Если НЕ ТекСтрока.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		Сч = Сч + 1;
		
		Если ЗначениеЗаполнено(ТекСтрока.Предрейсовый) Тогда
			ТекВидКонтроля = ПредопределенноеЗначение("Перечисление.уатВидыКонтроляТранспортныхДокументов.Послерейсовый");
		Иначе
			ТекВидКонтроля = ПредопределенноеЗначение("Перечисление.уатВидыКонтроляТранспортныхДокументов.Предрейсовый");
		КонецЕсли;
		
		ПутевойЛистРеквизиты = уатОбщегоНазначенияТиповые.ПолучитьЗначенияРеквизитов(ПутевойЛист, "Организация, Подразделение, МедработникВыезд");
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Водитель",    ТекСтрока.Водитель);
		ЗначенияЗаполнения.Вставить("ВидКонтроля", ТекВидКонтроля);
		ЗначенияЗаполнения.Вставить("ПутевойЛист", ПутевойЛист);
		ЗначенияЗаполнения.Вставить("Организация",   ПутевойЛистРеквизиты.Организация);
		ЗначенияЗаполнения.Вставить("Подразделение", ПутевойЛистРеквизиты.Подразделение);
		ЗначенияЗаполнения.Вставить("МедицинскийРаботник", ПутевойЛистРеквизиты.МедработникВыезд);
			
		Если Сч = 1 Тогда
			Оповестить("СозданиеДокументаИзФормыВыбораВодителя", ЗначенияЗаполнения);
		Иначе
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Документ.уатЖурналМедицинскогоОсмотраВодителей.ФормаОбъекта",
				ПараметрыФормы,, Новый УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
	ВыбраныСтроки = (Сч <> 0);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыВодители

&НаКлиенте
Процедура ВодителиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВодителиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВодителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекСтрока = Элементы.Водители.ТекущиеДанные;
	
	Если Поле.Имя = "ВодителиПредрейсовый" Тогда
		Если ЗначениеЗаполнено(ТекСтрока.Предрейсовый) Тогда
			ПоказатьЗначение(Неопределено, ТекСтрока.Предрейсовый);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ВодителиПослерейсовый" Тогда
		Если ЗначениеЗаполнено(ТекСтрока.Послерейсовый) Тогда
			ПоказатьЗначение(Неопределено, ТекСтрока.Послерейсовый);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
