
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Режим") Тогда
		Возврат;
	КонецЕсли;

	Если Параметры.Режим = "Грузоотправитель" Или Параметры.Режим = "Грузополучатель" Тогда 
		РежимВыбораПунктовКЛ = Ложь;
		
		Если Параметры.Режим = "Грузоотправитель" Тогда 
			Элементы.СписокВыбораСсылка.Заголовок = НСтр("en='Consignor';ru='Грузоотправитель'");
			Заголовок = НСтр("en='Select consignor';ru='Выбор грузоотправителя'");
		Иначе 
			Элементы.СписокВыбораСсылка.Заголовок = НСтр("en='Consignee';ru='Грузополучатель'");
			Заголовок = НСтр("en='Choose consignee';ru='Выбор грузополучателя'");
		КонецЕсли;
		
		Элементы.СписокВыбораПунктНазначения.Видимость = Ложь;
		Элементы.СписокВыбораКонтактноеЛицо.Видимость  = Ложь;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Параметры.Контрагент);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.Основной КАК Основной,
		|	Контрагенты.Контрагент КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты." + ?(Параметры.Режим = "Грузоотправитель", "Грузоотправители", "Грузополучатели") + " КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.НомерСтроки";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			НовСтрока = СписокВыбора.Добавить();
			НовСтрока.Ссылка   = Выборка.Контрагент;
			НовСтрока.Основной = Выборка.Основной;
		КонецЦикла;
		
		СтрокаФокус = Неопределено;
		Для Каждого ТекСтрока Из СписокВыбора Цикл 
			Если ТекСтрока.Ссылка = Параметры.НачальноеЗначениеВыбора Тогда 
				СтрокаФокус = ТекСтрока;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не СтрокаФокус = Неопределено Тогда 
			Элементы.СписокВыбора.ТекущаяСтрока = СтрокаФокус.ПолучитьИдентификатор();
		КонецЕсли;
		
	ИначеЕсли Параметры.Режим = "КонтактноеЛицоАдрес" Тогда
		Заголовок = НСтр("en='Select counterparty data';ru='Выбор данных контрагента'");
		РежимВыбораПунктовКЛ = Истина;
		
		Элементы.СписокВыбораСсылка.Видимость = Ложь;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Параметры.Контрагент);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтрагентыПунктыНазначения.ПунктНазначения КАК ПунктНазначения,
		|	КонтрагентыПунктыНазначения.КонтактноеЛицо КАК КонтактноеЛицо,
		|	КонтрагентыПунктыНазначения.Основной КАК Основной
		|ИЗ
		|	Справочник.Контрагенты.ПунктыНазначения КАК КонтрагентыПунктыНазначения
		|ГДЕ
		|	КонтрагентыПунктыНазначения.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	КонтрагентыПунктыНазначения.НомерСтроки";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			НовСтрока = СписокВыбора.Добавить();
			НовСтрока.ПунктНазначения = Выборка.ПунктНазначения;
			НовСтрока.КонтактноеЛицо  = Выборка.КонтактноеЛицо;
			НовСтрока.Основной        = Выборка.Основной;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_СписокВыбора

&НаКлиенте
Процедура СписокВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если РежимВыбораПунктовКЛ Тогда 
		Закрыть(Новый Структура("ПунктНазначения, КонтактноеЛицо", ТекущиеДанные.ПунктНазначения, ТекущиеДанные.КонтактноеЛицо));
	Иначе 
		Закрыть(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбораВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если РежимВыбораПунктовКЛ Тогда 
		Закрыть(Новый Структура("ПунктНазначения, КонтактноеЛицо", ТекущиеДанные.ПунктНазначения, ТекущиеДанные.КонтактноеЛицо));
	Иначе 
		Закрыть(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
