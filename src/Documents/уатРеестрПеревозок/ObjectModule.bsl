#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	уатОбщегоНазначенияТиповые.уатПроверитьЧтоНетТоваров(ЭтотОбъект, "Услуги", , Отказ, Заголовок);
	
	Если УчитыватьНДС Тогда
		ПроверяемыеРеквизиты.Добавить("Услуги.СтавкаНДС");
	КонецЕсли;
	
	Если ДоговорКонтрагента.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказамНаТС Тогда
		ПроверяемыеРеквизиты.Добавить("Услуги.Заказ");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("уатМноговалютныйУчет") = Истина Тогда
		ПроверяемыеРеквизиты.Добавить("ВалютаВзаиморасчетов");
	КонецЕсли;
	
	Если ТипЗнч(Перевозчик) <> Тип("СправочникСсылка.Контрагенты") Тогда
		уатОбщегоНазначенияСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "ДоговорКонтрагента");
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Услуги Цикл
		Если Не ЗначениеЗаполнено(ТекСтрока.Номенклатура) И Не ЗначениеЗаполнено(ТекСтрока.Рекламация) Тогда
			ТекстНСТР = НСтр("ru='В табличной части ""Услуги"" в строке №%1 не указана номенклатура.'");
			ТекстНСТР = СтрШаблон(ТекстНСТР, ТекСтрока.НомерСтроки);
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
		КонецЕсли;
	КонецЦикла;
	
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = уатОбщегоНазначенияТиповые.уатПолучитьСуммуДокументаСНДС(ЭтотОбъект, "Услуги");
	
	// уатСогласованиеДокументов
	уатСогласованиеДокументовСервер.ПередЗаписью(ЭтотОбъект, РежимЗаписи, Отказ);
	// Конец уатСогласованиеДокументов
КонецПроцедуры

#КонецОбласти

#КонецЕсли