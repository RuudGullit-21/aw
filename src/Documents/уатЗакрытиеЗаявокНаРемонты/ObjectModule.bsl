#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	Документы.уатЗакрытиеЗаявокНаРемонты.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	уатПроведение.ОтразитьЗаявкиНаРемонт(ДополнительныеСвойства, Движения, Отказ);
	
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Документы.уатЗакрытиеЗаявокНаРемонты.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);

	уатОбщегоНазначенияСервер.ВыполнитьУстановкуСтатусаЗаявкиНаРемонт(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль при отмене проведения не выполняем
	//Документы.уатЗакрытиеЗаявокНаРемонты.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	уатОбщегоНазначенияСервер.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументОбъект.уатЗаявкаНаРемонт") Или
		ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уатЗаявкаНаРемонт") Тогда
		
		Дата = ТекущаяДата();
		Организация = ДанныеЗаполнения.Организация;
		Подразделение = ДанныеЗаполнения.Подразделение;
		НоваяСтрока = Заявки.Добавить();
		НоваяСтрока.ЗаявкаНаРемонт = ДанныеЗаполнения.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
	
	// проверка дублей строк заявок
	тблЗаявки = Заявки.Выгрузить().Скопировать();
	тблЗаявки.Свернуть("ЗаявкаНаРемонт");
	Если Заявки.Количество() <> тблЗаявки.Количество() Тогда
		ТекстНСТР = НСтр("en='In the tabular section, there are duplication of orders!';ru='В табличной части обнаружено дублирование заявок!'");
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
	КонецЕсли;
	
	уатОбщегоНазначения.ПроверкаЗаполненияПодразделения(Организация, Подразделение, Отказ, Заголовок);
	
КонецПроцедуры

#КонецОбласти
