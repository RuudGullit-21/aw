
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	Документы.уатПакетСопроводительныхДокументов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	уатПроведение_проф.ОтразитьСтатусыСопроводительныхДокументов(ДополнительныеСвойства, Движения, Отказ);
	
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Документы.уатПакетСопроводительныхДокументов.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);

	уатУчетОригиналовПервичныхДокументов.СформироватьДвиженияРеестрДокументов(Ссылка);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Отправлен Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаОтправления");
	КонецЕсли;
	Если Получен Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаПолучения");
	КонецЕсли;
	
	// проверка наличия шаблонов в ТЧ
	Для Каждого ТекСтрока Из СопроводительныеДокументы Цикл
		Если ТекСтрока.СопроводительныйДокумент.Шаблон Тогда
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке("В табличной части ""Сопроводительные документы"" не должно быть шаблонов.", Отказ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Уведомления
	Если Не Отказ 
		И Не ПометкаУдаления 
		И ПолучитьФункциональнуюОпцию("уатИспользоватьУведомления_уэ") Тогда
		// Заполнение ТЧ "Получатели уведомлений"
		Если Модифицированность() Тогда
			тблПолучатели = уатОбщегоНазначения_уэ.СформироватьСписокПолучателейУведомлений(ЭтотОбъект);
			ПолучателиУведомлений.Загрузить(тблПолучатели);
		КонецЕсли;
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			// Отслеживаем событие "Изменение статуса отправления"
			Если Ссылка.СтатусОтправления <> СтатусОтправления Тогда
				ДополнительныеСвойства.Вставить("флИзменениеСтатусаОтправления");
			КонецЕсли;
			// Отслеживаем событие "Изменение статуса получения"
			Если Ссылка.СтатусПолучения <> СтатусПолучения Тогда
				ДополнительныеСвойства.Вставить("флИзменениеСтатусаПолучения");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("флИзменениеСтатусаОтправления") Тогда
		уатОбщегоНазначения_уэ.СформироватьУведомление(Ссылка, Перечисления.уатТипыСобытийДляУведомления_уэ.ИзменениеСтатусаОтправления);
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("флИзменениеСтатусаПолучения") Тогда
		уатОбщегоНазначения_уэ.СформироватьУведомление(Ссылка, Перечисления.уатТипыСобытийДляУведомления_уэ.ИзменениеСтатусаПолучения);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
