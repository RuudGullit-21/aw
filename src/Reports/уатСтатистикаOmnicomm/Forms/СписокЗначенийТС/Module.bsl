#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Список") Тогда
		Если ТипЗнч(Параметры.Список) = Тип("СправочникСсылка.уатТС") Тогда
			Список.Добавить(Параметры.Список);
		Иначе
			Список = Параметры.Список;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Элемент.ТекущийЭлемент.ВыбиратьТип = Ложь; 
	Элемент.ТекущийЭлемент.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.уатТС");
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	УдалитьДубли();
	Закрыть(Список);
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ЗначениеОтбора = Новый Структура("ИспользуемаяСистемаGPS", ПредопределенноеЗначение("Справочник.уатВнешниеСистемы.Omnicomm"));
	ПараметрыФормы = Новый Структура("РежимВыбора, МножественныйВыбор, ЗакрыватьПриВыборе, Отбор", Истина, Истина, Ложь, ЗначениеОтбора);
	ОткрытьФорму("Справочник.уатТС.Форма.ФормаВыбора", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УдалитьДубли()
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ТС");
	Для Каждого ТекСтрока Из Список Цикл 
		Если ТекСтрока.Значение <> Неопределено Тогда
			Строка = Таблица.Добавить();
			Строка.ТС = ТекСтрока.Значение;
		КонецЕсли;
	КонецЦикла;
	Таблица.Свернуть("ТС");
	Список.ЗагрузитьЗначения(Таблица.ВыгрузитьКолонку("ТС"));
КонецПроцедуры

#КонецОбласти
