
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаНач = НачалоГода(ТекущаяДата());
	ДатаКон = ТекущаяДата();
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяОрганизация");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПериод(Команда)
	ДиалогПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогПериода.Период.ДатаНачала = ДатаНач;
	ДиалогПериода.Период.ДатаОкончания = ДатаКон;
	ДиалогПериода.Показать(Новый ОписаниеОповещения("УстановитьПериодЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	ТекстСообщения = "";
	Если ДатаНач > ДатаКон И ДатаКон <> '00010101' Тогда
		ТекстСообщения = ТекстСообщения + " - " + НСтр("en='Period start date cannot be greater than the period end date';ru='Дата начала периода не может быть больше даты конца периода'") + Символы.ПС;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Организация) тогда
		ТекстСообщения = ТекстСообщения + " - " + НСтр("en='Organisation not specified';ru='Не указана организация'") + Символы.ПС;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекстСообщения) тогда
		ТекстНСТР = НСтр("en='It is impossible to generate report:';ru='Невозможно сформировать отчет:'") + " " + Символы.ПС + ТекстСообщения;
		ПоказатьПредупреждение(, ТекстНСТР, 10);
		Возврат;
	КонецЕсли;
	
	ТабДок = СформироватьОтчет();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
    
    Если Не Период = Неопределено Тогда
        ДатаНач = Период.ДатаНачала;
        ДатаКон = Период.ДатаОкончания;
    КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СформироватьОтчет()
	
	ДокументРезультат = Новый ТабличныйДокумент();
	
	Макет = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("Макет");
	
	ЗаголовокОтчета = Макет.ПолучитьОбласть("ШапкаОтчета");
	ЗаголовокОтчета.Параметры.ВладелецТС = Организация.НаименованиеПолное;
	
	ЗапросЮрАдреса = Новый Запрос();
	ЗапросЮрАдреса.Текст = "ВЫБРАТЬ
	|	ОрганизацииКонтактнаяИнформация.Представление КАК Представление
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|ГДЕ
	|	ОрганизацииКонтактнаяИнформация.Ссылка = &Ссылка
	|	И ОрганизацииКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации)";
	
	ЗапросЮрАдреса.УстановитьПараметр("Ссылка", Организация);
	РезультатЗапроса = ЗапросЮрАдреса.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЮрАдресВладельцаТС = Выборка.Представление;
	Иначе
		ЮрАдресВладельцаТС = "";
	КонецЕсли;
	
	ЗаголовокОтчета.Параметры.АдресВладельцаТС = ЮрАдресВладельцаТС;
	
	ДокументРезультат.Вывести(ЗаголовокОтчета);
	
	// Раздел 1 отчета
	ЗапросРаздел1 = Новый Запрос();
	ЗапросРаздел1.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатДТП.НомерСправкиГИБДД КАК НомерСправкиГИБДД,
	|	уатДТП.ДатаДТП КАК ДатаДТП,
	|	уатДТП.МестоДТП КАК МестоДТП,
	|	уатДТП.ВидДТП КАК ВидДТП,
	|	уатДТП.ТС КАК ТС,
	|	уатДТП.ТС.Модель.Наименование КАК Модель,
	|	уатДТП.ПричинаДТП КАК ПричинаДТП,
	|	уатДТП.ТС.ГосударственныйНомер КАК ГосНомер,
	|	уатДТП.Водитель.Наименование КАК ФИОВодителя,
	|	уатДТП.ОбстоятельстваДТП КАК ОбстоятельстваДТП,
	|	уатДТП.ПогиблоЧеловек КАК ПогиблоЧеловек,
	|	уатДТП.РаненоЧеловек КАК РаненоЧеловек
	|ИЗ
	|	Документ.уатДТП КАК уатДТП
	|ГДЕ
	|	НЕ уатДТП.ПометкаУдаления
	|	И уатДТП.ДатаДТП МЕЖДУ &ДатаНач И &ДатаКон
	|	И уатДТП.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДТП";
	
	ЗапросРаздел1.УстановитьПараметр("ДатаНач", ДатаНач);
	ЗапросРаздел1.УстановитьПараметр("ДатаКон", ДатаКон);
	ЗапросРаздел1.УстановитьПараметр("Организация", Организация);
	
	ЗаголовокОтчета = Макет.ПолучитьОбласть("ЗаголовокТаблицыРаздел1");
	ДокументРезультат.Вывести(ЗаголовокОтчета);
	
	РезультатЗапроса = ЗапросРаздел1.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекНомерСтроки = 1;
	Иначе
		ТекстНСтр = НСтр("en='No traffic accident documents found in selected period in organisation %1.';ru='В выбранном периоде отсутствуют ДТП в организации %1.'");
		ТекстНСтр = СтрШаблон(ТекстНСтр, Организация);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСтр);
	КонецЕсли;
	
	СтрокаОтчета = Макет.ПолучитьОбласть("СтрокаТаблицыРаздел1");
	
	ВыборкаРаздел1 = РезультатЗапроса.Выбрать();
	Пока ВыборкаРаздел1.Следующий() Цикл
		СтрокаОтчета.Параметры.Заполнить(ВыборкаРаздел1);
		СтрокаОтчета.Параметры.НомерСтроки = ТекНомерСтроки;
		ТекДатаДТП = Строка(Лев(ВыборкаРаздел1.ДатаДТП, 10));
		СтрокаОтчета.Параметры.ДатаДТП = ТекДатаДТП;
		ТекВремяДТП = Строка(Сред(ВыборкаРаздел1.ДатаДТП,12));
		Если СтрДлина(ТекВремяДТП) = 7 Тогда
			ТекВремяДТП = Лев(ТекВремяДТП, 4);
		Иначе
			ТекВремяДТП = Лев(ТекВремяДТП, 5);
		КонецЕсли;
		СтрокаОтчета.Параметры.ВремяДТП = ТекВремяДТП;
		СтрокаОтчета.Параметры.МодельНомерТС = ВыборкаРаздел1.Модель + Символы.ПС + ВыборкаРаздел1.ГосНомер;
		СтрокаОтчета.Параметры.ФИОВодителя = ВыборкаРаздел1.ФИОВодителя;
		ДокументРезультат.Вывести(СтрокаОтчета);
		ТекНомерСтроки = ТекНомерСтроки + 1; 
	КонецЦикла;
	
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	
	// Раздел 2 отчета
	ЗапросРаздел2 = Новый Запрос();
	ЗапросРаздел2.Текст = "ВЫБРАТЬ
	|	уатДТП.ДанныеОВодителе КАК ДанныеОВодителе,
	|	уатДТП.ДанныеОТранспортномСредстве КАК ДанныеОТранспортномСредстве,
	|	уатДТП.УсловияОрганизацииИОсуществленияПеревозок КАК УсловияОрганизацииИОсуществленияПеревозок,
	|	уатДТП.СведенияОПострадавших КАК СведенияОПострадавших,
	|	уатДТП.МатериальныйУщербОтПоврежденияТС КАК МатериальныйУщербОтПоврежденияТС,
	|	уатДТП.МатериальныйУщербОтПоврежденияГруза КАК МатериальныйУщербОтПоврежденияГруза,
	|	уатДТП.МатериальныйУщербОтПоврежденияДорогИСооружений КАК МатериальныйУщербОтПоврежденияДорогИСооружений,
	|	уатДТП.ПринятыеМеры КАК ПринятыеМеры
	|ИЗ
	|	Документ.уатДТП КАК уатДТП
	|ГДЕ
	|	НЕ уатДТП.ПометкаУдаления
	|	И уатДТП.ДатаДТП МЕЖДУ &ДатаНач И &ДатаКон
	|	И уатДТП.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	уатДТП.ДатаДТП";
	
	ЗапросРаздел2.УстановитьПараметр("ДатаНач", ДатаНач);
	ЗапросРаздел2.УстановитьПараметр("ДатаКон", ДатаКон);
	ЗапросРаздел2.УстановитьПараметр("Организация", Организация);
		
	ЗаголовокОтчета = Макет.ПолучитьОбласть("ЗаголовокТаблицыРаздел2");
	ДокументРезультат.Вывести(ЗаголовокОтчета);
	
	РезультатЗапроса = ЗапросРаздел2.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекНомерСтроки = 1;
	КонецЕсли;
	
	СтрокаОтчета = Макет.ПолучитьОбласть("СтрокаТаблицыРаздел2");
	
	ВыборкаРаздел2 = РезультатЗапроса.Выбрать();
	Пока ВыборкаРаздел2.Следующий() Цикл
		СтрокаОтчета.Параметры.Заполнить(ВыборкаРаздел2);
		СтрокаОтчета.Параметры.НомерСтроки = ТекНомерСтроки;
		ДокументРезультат.Вывести(СтрокаОтчета);
		ТекНомерСтроки = ТекНомерСтроки + 1; 
	КонецЦикла;
	
	// Ориентация страницы
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСлева = 10;
	ДокументРезультат.ПолеСправа = 0;
	
	// Присвоим имя для сохранения параметров печати табличного документа
	ДокументРезультат.ИмяПараметровПечати = "Форма учета ДТП владельцами ТС";
	
	Возврат ДокументРезультат;
	
КонецФункции

#КонецОбласти
