
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	ДобавитьОформлениеСтолбца("СуммаНачальныйОстаток", Настройки);
	ДобавитьОформлениеСтолбца("СуммаКонечныйОстаток", Настройки);
	ДобавитьОформлениеСтолбца("СуммаНДСКонечныйОстаток", Настройки);
	ДобавитьОформлениеСтолбца("СуммаНДСНачальныйОстаток", Настройки);
	ДобавитьОформлениеСтолбца("СуммаНДСРасход", Настройки);
	ДобавитьОформлениеСтолбца("СуммаНДСПриход", Настройки);
	ДобавитьОформлениеСтолбца("СуммаПриход", Настройки);
	ДобавитьОформлениеСтолбца("СуммаРасход", Настройки);
	ДобавитьОформлениеСтолбца("СуммаРеглКонечныйОстаток", Настройки);
	ДобавитьОформлениеСтолбца("СуммаРеглНачальныйОстаток", Настройки);
	ДобавитьОформлениеСтолбца("СуммаРеглПриход", Настройки);
	ДобавитьОформлениеСтолбца("СуммаРеглРасход", Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьОформлениеСтолбца(Колонка, Настройки)
	
	ЭлементУсловногоОформления = Настройки.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.ИспользоватьВОбщемИтоге               = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	ЭлементУсловногоОформления.ИспользоватьВГруппировке              = ИспользованиеУсловногоОформленияКомпоновкиДанных.Использовать;
	ЭлементУсловногоОформления.ИспользоватьВЗаголовке                = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	ЭлементУсловногоОформления.ИспользоватьВЗаголовкеПолей           = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	ЭлементУсловногоОформления.ИспользоватьВИерархическойГруппировке = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	ЭлементУсловногоОформления.ИспользоватьВОтборе                   = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	ЭлементУсловногоОформления.ИспользоватьВПараметрах               = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать;
	
	ГруппаЭлементовОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
 
	ЭлементОтбора = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПолучательУслуг");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Перечисления.уатПолучателиУслуг.НашаОрганизация;
	
	ЭлементОтбора = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Колонка);
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбора.ПравоеЗначение = 0;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ЧО=0; ЧДЦ=2;");
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
	
	ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Колонка);
	
КонецПроцедуры

#КонецОбласти
