
#Область ОбработчикиСобытийФормы

// Обработчик события "при создании на сервере" формы
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ВызватьИсключение НСтр("ru = 'Не поддерживается в мобильном клиенте.'")
	КонецЕсли;

	Если ItobОбщегоНазначения.ОтчетСПараметрамиПриСозданииНаСервере(ЭтаФорма) Тогда
	    СформироватьДиаграмму();
	КонецЕсли;
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ItobОбщегоНазначенияКлиент.ОтчетПриОткрытии(Отчет);	
	
	ЭтаФорма.ДиаграммаГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.Фиксированная;
	ЭтаФорма.ДиаграммаГанта.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.НеОтображать;
	ЭтаФорма.ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ЭтаФорма.ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Истина;
	ЭтаФорма.ДиаграммаГанта.АвтоУстановкаТекстаТочек = Истина;
	ЭтаФорма.ДиаграммаГанта.АвтоУстановкаТекстаСерий = Истина;
	ЭтаФорма.ДиаграммаГанта.РастягиваниеПоВертикали = РастягиваниеПоВертикалиДиаграммыГанта.НеРастягивать;
	ЭтаФорма.ДиаграммаГанта.ВертикальнаяПрокрутка = Истина;
			
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик команды "УстановитьИнтервал".
//
&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	ItobОбщегоНазначенияКлиент.УстановитьИнтервал(Отчет, Отчет.НачПериода, Отчет.КонПериода)
КонецПроцедуры // УстановитьИнтервал()

// Процедура - обработчик события "Регулирование" поля "НачПериода" таблицы.
//
&НаКлиенте
Процедура НачПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ItobОбщегоНазначенияКлиент.ИзменениеПериодаРегулирование(Отчет.НачПериода, Направление, СтандартнаяОбработка);
КонецПроцедуры // НачПериодаРегулирование()

// Процедура - обработчик события "Регулирование" поля "КонПериода" таблицы.
//
&НаКлиенте
Процедура КонПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ItobОбщегоНазначенияКлиент.ИзменениеПериодаРегулирование(Отчет.КонПериода, Направление, СтандартнаяОбработка);
КонецПроцедуры // КонПериодаРегулирование()

// Процедура - обработчик события "ОбработкаВыбора" поля "КонПериода" таблицы.
//
&НаКлиенте
Процедура КонПериодаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ItobОбщегоНазначенияКлиент.КонецПериодаОбработкаВыбора(Элемент, Отчет.КонПериода, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры // КонПериодаОбработкаВыбора()
	
#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
&НаКлиенте
Процедура ДиаграммаГантаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	Если ТипЗнч(Расшифровки) = Тип("Массив") Тогда
		
				
		Расшифровка = Расшифровки.Получить(1);
		Расшифровка.Вставить("Объект", Отчет.Объект);
		Оповестить("РежимМаршрута", Расшифровка);
	
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // ДиаграммаГантаОбработкаРасшифровки()
	
#КонецОбласти 

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "Сформировать".
//
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьДиаграмму();
КонецПроцедуры // Сформировать()
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
	
// Процедура формирует диаграмму.
//
&НаСервере
Процедура СформироватьДиаграмму()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");	
	Если ОтчетОбъект.ПроверитьЗаполнение() Тогда
		
		ДиаграммаГанта.Очистить();
		ОтчетОбъект.ВывестиДиаграммуГанта(ДиаграммаГанта, Отчет.НачПериода, Отчет.КонПериода, Отчет.Объект);
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ОтчетОбъект, "Отчет");
		
КонецПроцедуры // СформироватьДиаграмму()

#КонецОбласти
