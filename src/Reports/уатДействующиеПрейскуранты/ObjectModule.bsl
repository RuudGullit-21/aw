
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета         = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки           = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, ДанныеРасшифровки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки, Истина);
	ПроцессорВывода           = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	СтрокаЗаголовка = ДокументРезультат.НайтиТекст("%ЗаголовокОтчета%");
	Если Не СтрокаЗаголовка = Неопределено Тогда 
		ДатаОтчета = '00010101';
		Для Каждого ТекПараметр Из НастройкиКомпоновкиДанных.ПараметрыДанных.Элементы Цикл 
			Если ТекПараметр.Параметр = Новый ПараметрКомпоновкиДанных("Период") И ТекПараметр.Использование Тогда 
				ДатаОтчета = ТекПараметр.Значение.Дата;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ВыбраннаяОрганизация = Неопределено;
		Для Каждого ЭлементОтбора Из НастройкиКомпоновкиДанных.Отбор.Элементы Цикл 
			Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") И ЭлементОтбора.Использование 
					И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				ВыбраннаяОрганизация = ЭлементОтбора.ПравоеЗначение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		СтрокаЗаголовка.Текст = НСтр("en='Pricelist of tariffs on';ru='Прейскурант тарифов на'") + " " + Формат(ДатаОтчета, "ДФ=dd.MM.yyyy") + НСтр("en=' y. for rendering of transport services';ru=' г. на оказание транспортных услуг'") + ?(ВыбраннаяОрганизация=Неопределено, "", Символы.ПС + ВыбраннаяОрганизация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
