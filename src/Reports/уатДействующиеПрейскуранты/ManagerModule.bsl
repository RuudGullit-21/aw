#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Задает настройки размещения вариантов отчетов в панели отчетов.
// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       См. ""Реквизиты для изменения"" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Действующие прейскуранты");
	НастройкиВарианта.Описание = НСтр("en='Report provides information about the current price lists for transport services.';ru='Отчет предназначен для отображения информации о действующих прейскурантах на транспортные услуги.'");
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьПериодДействияПрейскуранта(ДатаНачала, ДатаОкончания) Экспорт
	
	Возврат НСтр("en='from';ru='с'") + " " + Формат(ДатаНачала, "ДФ=dd.MM.yy") + " " + НСтр("en='till';ru='по'") + " " + Формат(ДатаОкончания, "ДФ=dd.MM.yy");
	
КонецФункции // ПолучитьПериодДействияПрейскуранта()

Функция ПолучитьСтоимость(ТарифСсылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТарифСсылка) Тогда
		Возврат "";
	КонецЕсли;
	
	СтрокаСтоимости = "";
	ВалютаТарифа = ТарифСсылка.Владелец.Валюта;
	
	Если ТарифСсылка.ТарифнаяСетка Тогда 
		СтрокаСтоимости = НСтр("en='Determined by tariff scale';ru='Определяется по тарифной сетке'");
		
	ИначеЕсли ТарифСсылка.СложныйТариф Тогда 
		Для Каждого СтрокаТарифа Из ТарифСсылка.Тарифы Цикл 
			ПодстрокаСтоимости = "";
			Если ЗначениеЗаполнено(СтрокаТарифа.ЗнакГруппировки1) Тогда 
				ПодстрокаСтоимости = ПодстрокаСтоимости + ?(ПодстрокаСтоимости = "", "", " " + НСтр("en='and';ru='и'") + " ") + ПолучитьПредставлениеГруппировки(ТарифСсылка.ГруппировкаТарифа1) + СтрокаТарифа.ЗнакГруппировки1 + Формат(СтрокаТарифа.ЗначениеГруппировки1, "ЧН=0");
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТарифа.ЗнакГруппировки2) Тогда 
				ПодстрокаСтоимости = ПодстрокаСтоимости + ?(ПодстрокаСтоимости = "", "", " " + НСтр("en='and';ru='и'") + " ") + ПолучитьПредставлениеГруппировки(ТарифСсылка.ГруппировкаТарифа2) + СтрокаТарифа.ЗнакГруппировки2 + Формат(СтрокаТарифа.ЗначениеГруппировки2, "ЧН=0");
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТарифа.ЗнакГруппировки3) Тогда 
				ПодстрокаСтоимости = ПодстрокаСтоимости + ?(ПодстрокаСтоимости = "", "", " " + НСтр("en='and';ru='и'") + " ") + ПолучитьПредставлениеГруппировки(ТарифСсылка.ГруппировкаТарифа3) + СтрокаТарифа.ЗнакГруппировки3 + Формат(СтрокаТарифа.ЗначениеГруппировки3, "ЧН=0");
			КонецЕсли;
			
			СтрокаСтоимости = СтрокаСтоимости + ?(СтрокаСтоимости = "", "", Символы.ПС) + ПодстрокаСтоимости + " - " + Строка(СтрокаТарифа.Тариф) + " " + ВалютаТарифа + ";";
		КонецЦикла;
		
	Иначе 
		СтрокаСтоимости = "" + Строка(ТарифСсылка.Тариф) + " " + ВалютаТарифа;
	КонецЕсли;
	
	Возврат СтрокаСтоимости;
	
КонецФункции // ПолучитьСтоимость()

Функция ПолучитьМинимальнуюВыработку(ТарифСсылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТарифСсылка) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТарифСсылка.МинимальнаяВыработка) Тогда 
		Возврат Строка(ТарифСсылка.МинимальнаяВыработка) + " " + Строка(ТарифСсылка.ПараметрВыработки.ЕдиницаИзмерения);
	Иначе 
		Возврат "";
	КонецЕсли;
	
КонецФункции // ПолучитьМинимальнуюВыработку()

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатОтчетОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПредставлениеГруппировки(Группировка)
	
	Если Группировка = Перечисления.уатГруппировкиТарифов.ОбъемГруза Тогда 
		Возврат "V";
	ИначеЕсли Группировка = Перечисления.уатГруппировкиТарифов.ВесГруза Тогда 
		Возврат "W";
	ИначеЕсли Группировка = Перечисления.уатГруппировкиТарифов.КоличествоМест Тогда 
		Возврат "N";
	ИначеЕсли Группировка = Перечисления.уатГруппировкиТарифов.СтоимостьГруза Тогда 
		Возврат "P";
	ИначеЕсли Группировка = Перечисления.уатГруппировкиТарифов.Расстояние Тогда 
		Возврат "R";
	ИначеЕсли Группировка = Перечисления.уатГруппировкиТарифов.ВремяРаботы Тогда 
		Возврат "T";
	Иначе 
		Возврат "";
	КонецЕсли;
	
КонецФункции // ПолучитьПредставлениеГруппировки()

#КонецОбласти


#КонецЕсли