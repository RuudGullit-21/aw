
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	
	ТабДанных   = Новый ТаблицаЗначений;
	ТекстОшибки = "";
	
	Если Константы.уатИспользоватьСКАУТ.Получить() Тогда 
		
		УчетнаяЗапись = Неопределено;
		ПользовательскиеНастройки = ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
		Для Каждого ТекСтрока Из ПользовательскиеНастройки Цикл
			Если Строка(ТекСтрока.Параметр) = "УчетнаяЗапись" Тогда
				УчетнаяЗапись = ТекСтрока.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ТекстНСТР = НСтр("ru='Учетная запись не заполнена.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
			Возврат;
		КонецЕсли;
	
		СтруктураПараметровУчетнойЗаписи = уатМониторинг.ПолучитьСтруктуруПараметровДляРаботыССистемойМониторинга(УчетнаяЗапись);
		ТаблицаТС = уатМониторинг.ЗаполнитьТаблицуВсемиТС(СтруктураПараметровУчетнойЗаписи, ТекстОшибки);

		Если ТаблицаТС = Неопределено 
			ИЛИ ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстНСТР = НСтр("en='Failed to get data from the system ""SKAUT"".';ru='Не удалось получить данные от системы ""СКАУТ"".'") + Символы.ПС + ТекстОшибки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		Иначе 
			ТаблицаДанныхСКАУТ = Неопределено;
			уатИнтеграцияМониторингСКАУТ.СКАУТ_СформироватьСтруктуруТаблицыДанныхПоТС(ТаблицаДанныхСКАУТ);

			Для Каждого ТекСтрока Из ТаблицаТС Цикл
				НоваяСтрока = ТаблицаДанныхСКАУТ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				НоваяСтрока.Name 			 = ТекСтрока.Наименование;
				НоваяСтрока.UnitId			 = ТекСтрока.ИДвСистеме;
				НоваяСтрока.garageNumber	 = ТекСтрока.ГаражныйНомер;
				НоваяСтрока.StateNumber		 = ТекСтрока.ГосударственныйНомер;
				НоваяСтрока.VinNumber		 = ТекСтрока.VIN;
			КонецЦикла;

			ВнешниеНаборыДанных = Новый Структура;
			ВнешниеНаборыДанных.Вставить("ТабДанныхСКАУТ", ТаблицаДанныхСКАУТ);
			СхемаКомпоновкиДанных = ЭтотОбъект.СхемаКомпоновкиДанных;
			Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
			
			КомпоновщикМакета         = Новый КомпоновщикМакетаКомпоновкиДанных;
			МакетКомпоновки           = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
			ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
			ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
			ПроцессорВывода           = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
			ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
			ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
			ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
			ДокументРезультат.АвтоМасштаб = Истина;
			
		КонецЕсли;
		
	Иначе 
		ДокументРезультат = Новый ТабличныйДокумент;
		ТекстНСТР = НСтр("en='Using a ""SCOUT"" system is disabled.';ru='Использование системы ""СКАУТ"" отключено.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
