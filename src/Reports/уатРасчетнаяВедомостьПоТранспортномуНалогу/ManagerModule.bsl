
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// Задает настройки размещения вариантов отчетов в панели отчетов.
// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       См. ""Реквизиты для изменения"" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Расчетная ведомость по транспортному налогу");
	НастройкиВарианта.Описание = "Отчет предназначен для отображения суммы платежей по транспортному налогу";
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатОтчетОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Возвращает таблицу данных для формирования отчета по транспортному налогу
// Параметры:
//	- ГодОтчета - число - отчет всегда строится на период с начала по конец указанного года
//
Функция ПолучитьДанныеПоТранспортномуНалогу(ГодОтчета, Организация, Подразделение, Колонна, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = Новый Структура;
	КонецЕсли;
	
	Если ДопПараметры.Свойство("ПериодКвартал") И ДопПараметры.ПериодКвартал Тогда
		Если ЗначениеЗаполнено(ГодОтчета) Тогда
			мДатаНачалаПериодаОтчета = НачалоКвартала(ГодОтчета);
		Иначе
			мДатаНачалаПериодаОтчета = НачалоКвартала(ТекущаяДата());
		КонецЕсли;
		мДатаКонцаПериодаОтчета  = КонецКвартала(мДатаНачалаПериодаОтчета);
	Иначе
		Если ЗначениеЗаполнено(ГодОтчета) Тогда
			мДатаНачалаПериодаОтчета = НачалоГода(ГодОтчета);
		Иначе
			мДатаНачалаПериодаОтчета = НачалоГода(ТекущаяДата());
		КонецЕсли;
		мДатаКонцаПериодаОтчета = КонецГода(мДатаНачалаПериодаОтчета);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Колонна",       Колонна);
	Запрос.УстановитьПараметр("ДатаКон",       мДатаКонцаПериодаОтчета);
	
	ТекДата = мДатаНачалаПериодаОтчета;
	Пока ТекДата <= мДатаКонцаПериодаОтчета Цикл
		Если ТекДата <> мДатаНачалаПериодаОтчета Тогда
			Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	уатСведенияОТССрезПоследних.ТС КАК ТС,
		|	ВЫБОР
		|		КОГДА &ПериодМес МЕЖДУ НАЧАЛОПЕРИОДА(&ПериодМес, ГОД) И КОНЕЦПЕРИОДА(НАЧАЛОПЕРИОДА(&ПериодМес, ГОД), КВАРТАЛ)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевКв1,
		|	ВЫБОР
		|		КОГДА &ПериодМес МЕЖДУ ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(НАЧАЛОПЕРИОДА(&ПериодМес, ГОД), КВАРТАЛ), СЕКУНДА, 1) И КОНЕЦПЕРИОДА(НАЧАЛОПЕРИОДА(&ПериодМес, ГОД), ПОЛУГОДИЕ)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевКв2,
		|	ВЫБОР
		|		КОГДА &ПериодМес МЕЖДУ НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&ПериодМес, ГОД), ПОЛУГОДИЕ) И КОНЕЦПЕРИОДА(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&ПериодМес, ГОД), ПОЛУГОДИЕ), КВАРТАЛ)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевКв3,
		|	ВЫБОР
		|		КОГДА &ПериодМес МЕЖДУ НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&ПериодМес, ГОД), КВАРТАЛ) И КОНЕЦПЕРИОДА(&ПериодМес, ГОД)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевКв4
		|ПОМЕСТИТЬ ВТ_СписокТС
		|ИЗ
		|	РегистрСведений.уатСведенияОТС.СрезПоследних(&ПериодМес) КАК уатСведенияОТССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уатМестонахождениеТС.СрезПоследних(&ПериодМес) КАК РегистрацияТранспортныхСредствСрезПоследних
		|		ПО уатСведенияОТССрезПоследних.ТС = РегистрацияТранспортныхСредствСрезПоследних.ТС
		|ГДЕ
		|	(уатСведенияОТССрезПоследних.ТС.ДатаВыбытия = ДАТАВРЕМЯ(1,1,1) ИЛИ уатСведенияОТССрезПоследних.ТС.ДатаВыбытия > &ПериодМес)
		|	И уатСведенияОТССрезПоследних.НачислятьТранспортныйНалог
		|	И РегистрацияТранспортныхСредствСрезПоследних.Организация = &Организация
		|	И РегистрацияТранспортныхСредствСрезПоследних.Подразделение = &Подразделение
		|	И РегистрацияТранспортныхСредствСрезПоследних.Колонна = &Колонна";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПериодМес", "&ПериодМес" + Месяц(ТекДата));
		Если ТекДата <> мДатаНачалаПериодаОтчета Тогда 
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВТ_СписокТС", "");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ПериодМес" + Месяц(ТекДата), КонецДня(НачалоМесяца(ТекДата) + 14*24*3600));
				
		Запрос.Текст = Запрос.Текст + ТекстЗапроса;
		ТекДата = ДобавитьМесяц(ТекДата, 1);
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РегистрацияТранспортныхСредствСрезПоследних.Организация = &Организация", "");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Подразделение) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РегистрацияТранспортныхСредствСрезПоследних.Подразделение = &Подразделение", "");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Колонна) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РегистрацияТранспортныхСредствСрезПоследних.Колонна = &Колонна", "");
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|;" +
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(СписокТС.ТС КАК Справочник.уатТС) КАК ТС,
	|	СУММА(СписокТС.КоличествоМесяцевКв1) КАК КоличествоМесяцевКв1,
	|	СУММА(СписокТС.КоличествоМесяцевКв2) КАК КоличествоМесяцевКв2,
	|	СУММА(СписокТС.КоличествоМесяцевКв3) КАК КоличествоМесяцевКв3,
	|	СУММА(СписокТС.КоличествоМесяцевКв4) КАК КоличествоМесяцевКв4,
	|	СУММА(СписокТС.КоличествоМесяцевКв1) + СУММА(СписокТС.КоличествоМесяцевКв2) + СУММА(СписокТС.КоличествоМесяцевКв3) + СУММА(СписокТС.КоличествоМесяцевКв4) КАК КоличествоМесяцевГод
	|ПОМЕСТИТЬ ВТ_РазличныеЗаписи
	|ИЗ
	|	ВТ_СписокТС КАК СписокТС
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокТС.ТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокТС.ТС КАК ТС,
	|	СписокТС.КоличествоМесяцевКв1 КАК КоличествоМесяцевКв1,
	|	СписокТС.КоличествоМесяцевКв2 КАК КоличествоМесяцевКв2,
	|	СписокТС.КоличествоМесяцевКв3 КАК КоличествоМесяцевКв3,
	|	СписокТС.КоличествоМесяцевКв4 КАК КоличествоМесяцевКв4,
	|	СписокТС.КоличествоМесяцевГод КАК КоличествоМесяцевГод,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы = ""251""
	|				И уатСведенияОТС.НалоговаяБаза > 0
	|			ТОГДА уатСведенияОТС.НалоговаяБаза
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокТС.ТС.МощностьДвигателяЛС > 0
	|					ТОГДА СписокТС.ТС.МощностьДвигателяЛС
	|				ИНАЧЕ СписокТС.ТС.Модель.МощностьДвигателяЛС
	|			КОНЕЦ
	|	КОНЕЦ КАК МощностьДвигателя,
	|	уатСведенияОТС.СтавкаТранспортногоНалога.Ставка КАК СтавкаТранспортногоНалога,
	|	уатСведенияОТС.НалоговаяБаза КАК НалоговаяБаза,
	|	уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	уатСведенияОТС.ЭкологическийКласс КАК ЭкологическийКласс,
	|	уатСведенияОТС.ОбщаяСобственность КАК ОбщаяСобственность,
	|	уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ уатСведенияОТС.ПовышающийКоэффициент
	|	КОНЕЦ КАК ПовышающийКоэффициент,
	|	уатСведенияОТС.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	уатСведенияОТС.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	уатСведенияОТС.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	уатСведенияОТС.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	уатСведенияОТС.ПроцентУменьшения КАК ПроцентУменьшения,
	|	уатСведенияОТС.СуммаНалоговойЛьготы КАК СуммаНалоговойЛьготы,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы = ""251""
	|				И уатСведенияОТС.НалоговаяБаза > 0
	|			ТОГДА уатСведенияОТС.НалоговаяБаза
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокТС.ТС.МощностьДвигателяЛС > 0
	|					ТОГДА СписокТС.ТС.МощностьДвигателяЛС
	|				ИНАЧЕ СписокТС.ТС.Модель.МощностьДвигателяЛС
	|			КОНЕЦ
	|	КОНЕЦ * уатСведенияОТС.СтавкаТранспортногоНалога.Ставка * СписокТС.КоличествоМесяцевГод / 12 * ВЫБОР
	|		КОГДА уатСведенияОТС.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ уатСведенияОТС.ПовышающийКоэффициент
	|	КОНЕЦ * ВЫБОР
	|		КОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель > 0
	|				И уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|			ТОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель / уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПлатежаГод,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы = ""251""
	|				И уатСведенияОТС.НалоговаяБаза > 0
	|			ТОГДА уатСведенияОТС.НалоговаяБаза
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокТС.ТС.МощностьДвигателяЛС > 0
	|					ТОГДА СписокТС.ТС.МощностьДвигателяЛС
	|				ИНАЧЕ СписокТС.ТС.Модель.МощностьДвигателяЛС
	|			КОНЕЦ
	|	КОНЕЦ * уатСведенияОТС.СтавкаТранспортногоНалога.Ставка * СписокТС.КоличествоМесяцевКв1 / 12 * ВЫБОР
	|		КОГДА уатСведенияОТС.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ уатСведенияОТС.ПовышающийКоэффициент
	|	КОНЕЦ * ВЫБОР
	|		КОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель > 0
	|				И уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|			ТОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель / уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПлатежа1кв,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы = ""251""
	|				И уатСведенияОТС.НалоговаяБаза > 0
	|			ТОГДА уатСведенияОТС.НалоговаяБаза
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокТС.ТС.МощностьДвигателяЛС > 0
	|					ТОГДА СписокТС.ТС.МощностьДвигателяЛС
	|				ИНАЧЕ СписокТС.ТС.Модель.МощностьДвигателяЛС
	|			КОНЕЦ
	|	КОНЕЦ * уатСведенияОТС.СтавкаТранспортногоНалога.Ставка * СписокТС.КоличествоМесяцевКв2 / 12 * ВЫБОР
	|		КОГДА уатСведенияОТС.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ уатСведенияОТС.ПовышающийКоэффициент
	|	КОНЕЦ * ВЫБОР
	|		КОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель > 0
	|				И уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|			ТОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель / уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПлатежа2кв,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы = ""251""
	|				И уатСведенияОТС.НалоговаяБаза > 0
	|			ТОГДА уатСведенияОТС.НалоговаяБаза
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокТС.ТС.МощностьДвигателяЛС > 0
	|					ТОГДА СписокТС.ТС.МощностьДвигателяЛС
	|				ИНАЧЕ СписокТС.ТС.Модель.МощностьДвигателяЛС
	|			КОНЕЦ
	|	КОНЕЦ * уатСведенияОТС.СтавкаТранспортногоНалога.Ставка * СписокТС.КоличествоМесяцевКв3 / 12 * ВЫБОР
	|		КОГДА уатСведенияОТС.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ уатСведенияОТС.ПовышающийКоэффициент
	|	КОНЕЦ * ВЫБОР
	|		КОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель > 0
	|				И уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|			ТОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель / уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПлатежа3кв,
	|	ВЫБОР
	|		КОГДА уатСведенияОТС.ЕдиницаИзмеренияНалоговойБазы = ""251""
	|				И уатСведенияОТС.НалоговаяБаза > 0
	|			ТОГДА уатСведенияОТС.НалоговаяБаза
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписокТС.ТС.МощностьДвигателяЛС > 0
	|					ТОГДА СписокТС.ТС.МощностьДвигателяЛС
	|				ИНАЧЕ СписокТС.ТС.Модель.МощностьДвигателяЛС
	|			КОНЕЦ
	|	КОНЕЦ * уатСведенияОТС.СтавкаТранспортногоНалога.Ставка * СписокТС.КоличествоМесяцевКв4 / 12 * ВЫБОР
	|		КОГДА уатСведенияОТС.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ уатСведенияОТС.ПовышающийКоэффициент
	|	КОНЕЦ * ВЫБОР
	|		КОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель > 0
	|				И уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|			ТОГДА уатСведенияОТС.ДоляВПравеОбщейСобственностиЧислитель / уатСведенияОТС.ДоляВПравеОбщейСобственностиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаПлатежа4кв
	|ИЗ
	|	ВТ_РазличныеЗаписи КАК СписокТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уатСведенияОТС.СрезПоследних(&ДатаКон, ) КАК уатСведенияОТС
	|		ПО СписокТС.ТС = уатСведенияОТС.ТС";
	
	тблТС = Запрос.Выполнить().Выгрузить();
	
	// учет налоговых льгот
	Для Каждого ТекСтрока Из тблТС Цикл
		Если НЕ ЗначениеЗаполнено(ТекСтрока.НалоговаяЛьгота)
			ИЛИ ТекСтрока.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрока.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
			ТекСтрока.СуммаПлатежаГод = 0;
			ТекСтрока.СуммаПлатежа1кв = 0;
			ТекСтрока.СуммаПлатежа2кв = 0;
			ТекСтрока.СуммаПлатежа3кв = 0;
			ТекСтрока.СуммаПлатежа4кв = 0;
		ИначеЕсли ТекСтрока.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки
			И ЗначениеЗаполнено(ТекСтрока.ЛьготнаяСтавка)
			И ЗначениеЗаполнено(ТекСтрока.СтавкаТранспортногоНалога) Тогда
			ТекСтрока.СуммаПлатежаГод = ТекСтрока.СуммаПлатежаГод / ТекСтрока.СтавкаТранспортногоНалога * ТекСтрока.ЛьготнаяСтавка;
			ТекСтрока.СуммаПлатежа1кв = ТекСтрока.СуммаПлатежа1кв / ТекСтрока.СтавкаТранспортногоНалога * ТекСтрока.ЛьготнаяСтавка;
			ТекСтрока.СуммаПлатежа2кв = ТекСтрока.СуммаПлатежа2кв / ТекСтрока.СтавкаТранспортногоНалога * ТекСтрока.ЛьготнаяСтавка;
			ТекСтрока.СуммаПлатежа3кв = ТекСтрока.СуммаПлатежа3кв / ТекСтрока.СтавкаТранспортногоНалога * ТекСтрока.ЛьготнаяСтавка;
			ТекСтрока.СуммаПлатежа4кв = ТекСтрока.СуммаПлатежа4кв / ТекСтрока.СтавкаТранспортногоНалога * ТекСтрока.ЛьготнаяСтавка;
		ИначеЕсли ТекСтрока.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
			ТекСтрока.СуммаПлатежаГод = ТекСтрока.СуммаПлатежаГод * (1 - ТекСтрока.ПроцентУменьшения/100);
			ТекСтрока.СуммаПлатежа1кв = ТекСтрока.СуммаПлатежа1кв * (1 - ТекСтрока.ПроцентУменьшения/100);
			ТекСтрока.СуммаПлатежа2кв = ТекСтрока.СуммаПлатежа2кв * (1 - ТекСтрока.ПроцентУменьшения/100);
			ТекСтрока.СуммаПлатежа3кв = ТекСтрока.СуммаПлатежа3кв * (1 - ТекСтрока.ПроцентУменьшения/100);
			ТекСтрока.СуммаПлатежа4кв = ТекСтрока.СуммаПлатежа4кв * (1 - ТекСтрока.ПроцентУменьшения/100);
		ИначеЕсли ТекСтрока.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму
			И мДатаНачалаПериодаОтчета < '20120101' Тогда
			ТекСтрока.СуммаПлатежаГод = ТекСтрока.СуммаПлатежаГод - ТекСтрока.СуммаНалоговойЛьготы;
			ТекСтрока.СуммаПлатежа1кв = ТекСтрока.СуммаПлатежа1кв - ТекСтрока.СуммаНалоговойЛьготы/4;
			ТекСтрока.СуммаПлатежа2кв = ТекСтрока.СуммаПлатежа2кв - ТекСтрока.СуммаНалоговойЛьготы/4;
			ТекСтрока.СуммаПлатежа3кв = ТекСтрока.СуммаПлатежа3кв - ТекСтрока.СуммаНалоговойЛьготы/4;
			ТекСтрока.СуммаПлатежа4кв = ТекСтрока.СуммаПлатежа4кв - ТекСтрока.СуммаНалоговойЛьготы/4;
		КонецЕсли;
	КонецЦикла;
		
	Возврат тблТС;
КонецФункции

#КонецОбласти


#КонецЕсли


