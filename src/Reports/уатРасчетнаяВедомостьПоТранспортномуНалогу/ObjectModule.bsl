
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиКомпоновщикаНастроек = КомпоновщикНастроек.ПолучитьНастройки();
	
	ГодОтчета     = НастройкиКомпоновщикаНастроек.ПараметрыДанных.Элементы.Найти("Год").Значение;
	Организация   = НастройкиКомпоновщикаНастроек.ПараметрыДанных.Элементы.Найти("Организация").Значение;
	Подразделение = НастройкиКомпоновщикаНастроек.ПараметрыДанных.Элементы.Найти("Подразделение").Значение;
	Колонна       = НастройкиКомпоновщикаНастроек.ПараметрыДанных.Элементы.Найти("Колонна").Значение;
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТабДанных",
		Отчеты.уатРасчетнаяВедомостьПоТранспортномуНалогу.ПолучитьДанныеПоТранспортномуНалогу(ГодОтчета, Организация, Подразделение, Колонна));
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновщикаНастроек, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти
