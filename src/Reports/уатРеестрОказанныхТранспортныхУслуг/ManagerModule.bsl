#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// Задает настройки размещения вариантов отчетов в панели отчетов.
// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Параметры:
//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//       См. ""Реквизиты для изменения"" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Реестр оказанных транспортных услуг");
	НастройкиВарианта.Описание = НСтр("en='Report is to analyze provided transport services.';ru='Отчет служит для анализа оказанных транспортных услуг.'");
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Возвращает строковое представление периода отчета
//
// Параметры:
//  ДатаНач	 - Дата	 - Начало периода отчета
//  ДатаКон	 - Дата	 - Конец периода отчета
// 
// Возвращаемое значение:
//  Строковое - представление периода отчета - Строка
//
Функция ПредставлениеПериодаОтчета(ДатаНач, ДатаКон) Экспорт
	// Вывод заголовка, описателя периода и фильтров и заголовка
	Если ДатаНач = '00010101' И ДатаКон = '00010101' Тогда
		
		ОписаниеПериода     = НСтр("en='Period: without restriction.';ru='Период: без ограничения.'");
		
	Иначе
		
		Если ДатаНач = '00010101' ИЛИ ДатаКон = '00010101' Тогда
			ОписаниеПериода = НСтр("en='Period:';ru='Период:'") + " " + Формат(ДатаНач, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""") 
				+ " - "      + Формат(ДатаКон, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""");
		Иначе
			Если ДатаНач <= ДатаКон Тогда
				ОписаниеПериода = НСтр("en='Period:';ru='Период:'") + " " + ПредставлениеПериода(НачалоДня(ДатаНач), КонецДня(ДатаКон), "ФП = Истина");
			Иначе
				ОписаниеПериода = НСтр("en='Incorrect period!';ru='Неправильно задан период!'");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеПериода;
КонецФункции

// Возвращает количество по параметру выбора, отформатированное:
//  - если временный, то в виде 12,67 или 12:40 (в зависимости от настройки "Форматировать время в отчетах")
//  - если не временный, то в виде 12,431 км
//  Введена по причине того, что условное оформление не применяется для пользовательского макета
//
// Параметры:
//  ПараметрВыработкиВременный	 - 	 - 
//  Количество					 - 	 - 
//  ФорматироватьВремя			 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция КоличествоПараметраВыработки(ПараметрВыработкиВременный, Знач Количество, ФорматироватьВремя) Экспорт
	Если Количество = Null Тогда
		Количество = 0;
	КонецЕсли;
	
	Если ПараметрВыработкиВременный = ИСТИНА Тогда
		Если ФорматироватьВремя Тогда
			Рез = "" + Цел(Количество) + ":" + Прав(Формат(Окр((Количество - Цел(Количество))/10*6, 2), "ЧДЦ=2; ЧН=00"), 2);
		Иначе
			Рез = Формат(Количество, "ЧДЦ=2");
		КонецЕсли;
	Иначе
		Рез = Формат(Количество, "ЧДЦ=3");
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

// Возвращает представление итоговой надписи по ТС в отчете
//
// Параметры:
//  ТС	 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ПредставлениеИтогоПоТС(ТС) Экспорт
	Если ЗначениеЗаполнено(ТС) Тогда
		Возврат НСтр("en='Total on vehicle';ru='Итого по ТС'") + " """ + Строка(ТС) + """: ";
	Иначе
		Возврат НСтр("en='Total on orders or without allocation on vehicle:';ru='Итого по заказам или без распределения по ТС:'");
	КонецЕсли;
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатОтчетОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли