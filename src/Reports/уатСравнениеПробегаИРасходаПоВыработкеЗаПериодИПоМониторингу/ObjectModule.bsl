
#Область ОбработчикиСобытий

Функция СформироватьТаблицуДанных(Знач НастройкиКомпоновки, Знач ДатаС, Знач ДатаПо)
	
	мЗапрос = Новый Запрос;
	мЗапрос.УстановитьПараметр("ДатаС",  ДатаС);
	мЗапрос.УстановитьПараметр("ДатаПо", ДатаПо);
	
	СтрокаДопУсловий = "";
	СчПараметр = 1;
	Для Каждого ЭлементОтбора Из НастройкиКомпоновки.Отбор.Элементы Цикл 
		Если ЭлементОтбора.Использование Тогда 
			СтрокаУсловия = Строка(ЭлементОтбора.ЛевоеЗначение);
			
			Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда 
				СтрокаУсловия = СтрокаУсловия + " = &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно Тогда
				СтрокаУсловия = СтрокаУсловия + " <> &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше Тогда
				СтрокаУсловия = СтрокаУсловия + " < &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше Тогда
				СтрокаУсловия = СтрокаУсловия + " > &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
				СтрокаУсловия = СтрокаУсловия + " <= &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
				СтрокаУсловия = СтрокаУсловия + " >= &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
				СтрокаУсловия = СтрокаУсловия + " В(&Параметр_" + Формат(СчПараметр, "ЧГ=0") + ")";
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии Тогда
				СтрокаУсловия = СтрокаУсловия + " В ИЕРАРХИИ(&Параметр_" + Формат(СчПараметр, "ЧГ=0") + ")";
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит Тогда
				СтрокаУсловия = СтрокаУсловия + " ПОДОБНО &Параметр_" + Формат(СчПараметр, "ЧГ=0");
			Иначе 
				Продолжить;
			КонецЕсли;
			
			СтрокаДопУсловий = СтрокаДопУсловий + ?(СтрокаДопУсловий="",""," И ") + СтрокаУсловия;
			
			мЗапрос.УстановитьПараметр("Параметр_" + Формат(СчПараметр, "ЧГ=0"), ЭлементОтбора.ПравоеЗначение);
			
			СчПараметр = СчПараметр + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокаДопУсловий = "" Тогда 
		СтрокаДопУсловий = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса_ПЛ =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, ДЕНЬ) КАК ПериодДень,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, НЕДЕЛЯ) КАК ПериодНеделя,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, ДЕКАДА) КАК ПериодДекада,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, МЕСЯЦ) КАК ПериодМесяц,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, КВАРТАЛ) КАК ПериодКвартал,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, ПОЛУГОДИЕ) КАК ПериодПолугодие,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, ГОД) КАК ПериодГод,
	|	уатВыработкаТСОбороты.ТС КАК ТС,
	|	ВЫБОР
	|		КОГДА уатВыработкаТСОбороты.ТС.ВидМоделиТС = ЗНАЧЕНИЕ(Перечисление.уатВидыМоделейТС.Автотранспорт)
	|			ТОГДА уатВыработкаТСОбороты.КоличествоОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПробегПоПЛ,
	|	уатВыработкаТСОбороты.КоличествоОборот КАК ПробегПоПЛТС,
	|	0 КАК РасходПоПЛ,
	|	0 КАК ПробегПоGPS,
	|	0 КАК РасходПоGPS
	|ИЗ
	|	РегистрНакопления.уатВыработкаТС.Обороты(
	|			&ДатаС,
	|			&ДатаПо,
	|			Запись,
	|			ПараметрВыработки = ЗНАЧЕНИЕ(Справочник.уатПараметрыВыработки.ПробегОбщий)
	|				И %ДопУсловия%) КАК уатВыработкаТСОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ДЕНЬ),
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, НЕДЕЛЯ),
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ДЕКАДА),
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, КВАРТАЛ),
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ПОЛУГОДИЕ),
	|	НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ГОД),
	|	уатРасходГСМнаТСОбороты.ТС,
	|	0,
	|	0,
	|	уатРасходГСМнаТСОбороты.РасходПоФактуОборот,
	|	0,
	|	0
	|ИЗ
	|	РегистрНакопления.уатРасходГСМнаТС.Обороты(&ДатаС, &ДатаПо, Запись, %ДопУсловия%) КАК уатРасходГСМнаТСОбороты";
	
	ТекстЗапроса_ПЛ = СтрЗаменить(ТекстЗапроса_ПЛ, "%ДопУсловия%", СтрокаДопУсловий);
	
	мЗапрос.Текст = ТекстЗапроса_ПЛ;
	
	ТабДанных = мЗапрос.Выполнить().Выгрузить();
	
	ТекстЗапроса_GPS = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Период КАК ПериодДень,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, НЕДЕЛЯ) КАК ПериодНеделя,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕКАДА) КАК ПериодДекада,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ) КАК ПериодМесяц,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, КВАРТАЛ) КАК ПериодКвартал,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ПОЛУГОДИЕ) КАК ПериодПолугодие,
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ГОД) КАК ПериодГод,
	|	ВложенныйЗапрос.ТС КАК ТС,
	|	ВложенныйЗапрос.Пробег КАК ПробегПоGPS,
	|	ВложенныйЗапрос.РасходТоплива КАК РасходПоGPS
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(уатПробегРасходПоМониторингу.Период, ДЕНЬ) КАК Период,
	|		уатПробегРасходПоМониторингу.ТС КАК ТС,
	|		СУММА(уатПробегРасходПоМониторингу.Пробег) КАК Пробег,
	|		СУММА(уатПробегРасходПоМониторингу.РасходТоплива) КАК РасходТоплива
	|	ИЗ
	|		РегистрСведений.уатПробегРасходПоМониторингу КАК уатПробегРасходПоМониторингу
	|	ГДЕ
	|		уатПробегРасходПоМониторингу.Период МЕЖДУ &ДатаС И &ДатаПо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		НАЧАЛОПЕРИОДА(уатПробегРасходПоМониторингу.Период, ДЕНЬ),
	|		уатПробегРасходПоМониторингу.ТС) КАК ВложенныйЗапрос";
	
	ТекстЗапроса_GPS = СтрЗаменить(ТекстЗапроса_GPS, "%ДопУсловия%", СтрокаДопУсловий);
	
	мЗапрос.Текст = ТекстЗапроса_GPS;
	
	мЗапрос.УстановитьПараметр("ДатаС",  ДатаС);
	мЗапрос.УстановитьПараметр("ДатаПо", ДатаПо);
	
	мВыборка = мЗапрос.Выполнить().Выбрать();
	Пока мВыборка.Следующий() Цикл 
		НовСтрока = ТабДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, мВыборка);
	КонецЦикла;
	
	Возврат ТабДанных;
	
КонецФункции // СформироватьТаблицуДанных()

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	ВнешниеНаборыДанных = Новый Структура();
	ВнешниеНаборыДанных.Вставить("ТабДанныхДляСравнения", СформироватьТаблицуДанных(
		Настройки, 
		Настройки.ПараметрыДанных.Элементы.Найти("ПериодОтчета").Значение.ДатаНачала, 
		Настройки.ПараметрыДанных.Элементы.Найти("ПериодОтчета").Значение.ДатаОкончания));
	
	КомпоновщикМакета         = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки           = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	ПроцессорВывода           = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб = Истина;
	
КонецПроцедуры

#КонецОбласти
