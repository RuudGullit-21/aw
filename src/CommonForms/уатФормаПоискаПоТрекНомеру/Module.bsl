
#Область ОбработчикиСобытийФормы 

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		уатОбщегоНазначенияКлиент.ОбработатьСобытиеПодключаемогоОборудования(ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	// Конец ПодключаемоеОборудование
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ВыполнитьПоиск(Команда) 
	
	РезультатПоиска = ПоискОбъектовПоТрекНомеру(ВведеннаяСтрокаПоиска); 
	
	Если ТипЗнч(РезультатПоиска) = Тип("Строка") Тогда  
		ПоказатьПредупреждение(,РезультатПоиска);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(РезультатПоиска) = Тип("ДокументСсылка.уатЗаказГрузоотправителя") 
			Или ТипЗнч(РезультатПоиска) = Тип("ДокументСсылка.уатПотребностьВПеревозке_уэ") Тогда
			СсылкаНаДокумент = РезультатПоиска;
	КонецЕсли;
		
	
	ДействиеДляНайденногоОбъекта = ТекущееДействиеДляНайденногоОбъекта();  
	
	Если ДействиеДляНайденногоОбъекта = ПредопределенноеЗначение("Перечисление.уатДействиеПриСчитыванииКода_уэ.ОткрыватьФормуДокумента") Тогда 
		СтруктураПараметров = Новый Структура("Ключ", СсылкаНаДокумент);
		
		Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.уатПотребностьВПеревозке_уэ") Тогда
			ОткрытьФорму("Документ.уатПотребностьВПеревозке_уэ.ФормаОбъекта", СтруктураПараметров);
		ИначеЕсли ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.уатЗаказГрузоотправителя") Тогда
			ОткрытьФорму("Документ.уатЗаказГрузоотправителя.ФормаОбъекта", СтруктураПараметров);
		КонецЕсли;
		
	ИначеЕсли ДействиеДляНайденногоОбъекта = ПредопределенноеЗначение("Перечисление.уатДействиеПриСчитыванииКода_уэ.ОткрыватьОтчетИсторияЗаказа") Тогда
		
		Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.уатЗаказГрузоотправителя") Тогда
			МассивОбъектов = Новый Массив;
			МассивОбъектов.Добавить(СсылкаНаДокумент);
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.уатЗаказГрузоотправителя", "ЗаказНаТСИсторияЗаказа", МассивОбъектов, ЭтаФорма);  
		КонецЕсли;
		
	ИначеЕсли ДействиеДляНайденногоОбъекта = ПредопределенноеЗначение("Перечисление.уатДействиеПриСчитыванииКода_уэ.ОткрыватьСвязанныеДокументы") Тогда
		СтруктураПараметров = Новый Структура("ОбъектОтбора", СсылкаНаДокумент);
		ОткрытьФорму("ОбщаяФорма.СвязанныеДокументы", СтруктураПараметров, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции 

&НаСервереБезКонтекста
Функция ПоискОбъектовПоТрекНомеру(ТрекНомер)
	
	Запрос = Новый Запрос();
	// Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	//               |	уатШтрихкоды.Объект КАК Объект,
	//               |	уатШтрихкоды.ЗаказГрузоотправителя КАК ЗаказГрузоотправителя,
	//               |	уатШтрихкоды.Штрихкод КАК Штрихкод,
	//               |	ВЫБОР
	//               |		КОГДА ТИПЗНАЧЕНИЯ(уатШтрихкоды.ЗаказГрузоотправителя) = ТИП(Документ.уатЗаказГрузоотправителя)
	//               |			ТОГДА 1
	//               |		ИНАЧЕ 2
	//               |	КОНЕЦ КАК Приоритет
	//               |ИЗ
	//               |	РегистрСведений.уатШтрихкоды КАК уатШтрихкоды
	//               |ГДЕ
	//               |	уатШтрихкоды.Штрихкод ПОДОБНО ""%"" + &Штрихкод + ""%""
	//               |
	//               |УПОРЯДОЧИТЬ ПО
	//               |	Приоритет";
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	уатШтрихкоды.Объект КАК Объект,
	               |	уатШтрихкоды.ЗаказГрузоотправителя КАК ЗаказГрузоотправителя,
	               |	уатШтрихкоды.Штрихкод КАК Штрихкод,
	               |	ВЫБОР
	               |		КОГДА ТИПЗНАЧЕНИЯ(уатШтрихкоды.ЗаказГрузоотправителя) = ТИП(Документ.уатЗаказГрузоотправителя)
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Приоритет
	               |ИЗ
	               |	РегистрСведений.уатШтрихкоды КАК уатШтрихкоды
	               |ГДЕ
	               |	уатШтрихкоды.Штрихкод = &Штрихкод
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Приоритет";
	
	Запрос.УстановитьПараметр("Штрихкод", ТрекНомер); 
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если ЗначениеЗаполнено(Выборка.ЗаказГрузоотправителя) Тогда
			СсылкаНаДокумент = Выборка.ЗаказГрузоотправителя;
		Иначе
			СсылкаНаДокумент = Выборка.Объект;
		КонецЕсли;
		
		Возврат СсылкаНаДокумент;
		
	Иначе 	
		ТекстСообщения = НСтр("en='';ru='По данному трек-номеру объектов не обнаружено'");
		Возврат ТекстСообщения;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекущееДействиеДляНайденногоОбъекта()
	Возврат Константы.уатДействиеПриСчитыванииКода_уэ.Получить();
КонецФункции

#КонецОбласти
