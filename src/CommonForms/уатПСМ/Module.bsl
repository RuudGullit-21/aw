
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПроверитьПодключениеПСМ();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПодключениеПСМ()
	
	МассивПСМ = Новый Массив();
	уатРаботаСРасширениями.ПСМ_ЗаполнитьМассивПСМ(МассивПСМ);
	
	
	Счетчик = 0;
	Для Каждого ТекСтрока Из МассивПСМ Цикл
		ТекГруппа = Элементы.Добавить("ГруппаПСМ" + Счетчик, Тип("ГруппаФормы"), Элементы.ГруппаПСМ);
		ТекГруппа.Заголовок                  = ТекСтрока;
		ТекГруппа.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
		ТекГруппа.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		ТекГруппа.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
		ТекГруппа.Отображение                = ОтображениеОбычнойГруппы.СлабоеВыделение;
		ТекГруппа.ОтображатьЗаголовок        = Истина;
		
		ТекГруппаЛевая = Элементы.Добавить("ГруппаПСМЛевая" + Счетчик, Тип("ГруппаФормы"), ТекГруппа);
		ТекГруппаЛевая.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
		ТекГруппаЛевая.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		ТекГруппаЛевая.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
		ТекГруппаЛевая.Отображение                = ОтображениеОбычнойГруппы.Нет;
		ТекГруппаЛевая.Ширина                     = 30;
		ТекГруппаЛевая.ОтображатьЗаголовок        = Ложь;
		
		ТекГруппаПравая = Элементы.Добавить("ГруппаПСМПравая" + Счетчик, Тип("ГруппаФормы"), ТекГруппа);
		ТекГруппаПравая.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
		ТекГруппаПравая.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		ТекГруппаПравая.ШиринаПодчиненныхЭлементов = ШиринаПодчиненныхЭлементовФормы.Одинаковая;
		ТекГруппаПравая.Отображение                = ОтображениеОбычнойГруппы.Нет;
		ТекГруппаПравая.Ширина                     = 30;
		ТекГруппаПравая.ОтображатьЗаголовок        = Ложь;
		
		
		КнопкаНастрокиПСМ = Элементы.Добавить("НастройкиПСМ" + Счетчик, Тип("КнопкаФормы"), ТекГруппаПравая);
		КнопкаНастрокиПСМ.Заголовок   = Нстр("en = 'Settings'; ru = 'Настройки'") + " """ + ТекСтрока + """";
		КнопкаНастрокиПСМ.ИмяКоманды  = "НастройкиПСМ";
		КнопкаНастрокиПСМ.Вид         = ВидКнопкиФормы.Гиперссылка;
		
		КнопкаНастрокиПСМ = Элементы.Добавить("ЗагрузкаПСМ" + Счетчик, Тип("КнопкаФормы"), ТекГруппаЛевая);
		КнопкаНастрокиПСМ.Заголовок   = Нстр("en = 'Settings'; ru = 'Загрузка данных'") + " """ + ТекСтрока + """";
		КнопкаНастрокиПСМ.ИмяКоманды  = "ЗагрузкаПСМ";
		КнопкаНастрокиПСМ.Вид         = ВидКнопкиФормы.Гиперссылка;
		
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузкаПСМ(Команда)
	СистемаМониторинга = ТекущийЭлемент.Заголовок;
	СистемаМониторинга = СтрЗаменить(СистемаМониторинга, Нстр("en = 'Settings '; ru = 'Загрузка данных ""'"),"");
	СистемаМониторинга = Лев(СистемаМониторинга, СтрДлина(СистемаМониторинга) - 1);
	
	
	ИдентификаторДействия = "ОбщаяФорма.уатНастройкиМониторинга.ЗагрузкаПСМ";
	#Если Не ТолстыйКлиентОбычноеПриложение Тогда
	уатРаботаСРасширениямиКлиент.ПСМ_ВыполнитьДействияВФорме(ИдентификаторДействия, СистемаМониторинга);
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура НастройкиПСМ(Команда)
	СистемаМониторинга = ТекущийЭлемент.Заголовок;
	СистемаМониторинга = СтрЗаменить(СистемаМониторинга, Нстр("en = 'Settings '; ru = 'Настройки ""'"),"");
	СистемаМониторинга = Лев(СистемаМониторинга, СтрДлина(СистемаМониторинга) - 1);
	
	
	ИдентификаторДействия = "ОбщаяФорма.уатНастройкиМониторинга.НастройкиПСМ";
	#Если Не ТолстыйКлиентОбычноеПриложение Тогда
	уатРаботаСРасширениямиКлиент.ПСМ_ВыполнитьДействияВФорме(ИдентификаторДействия, СистемаМониторинга);	
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьРасширения(Команда)
	ОткрытьФорму("ОбщаяФорма.Расширения");
КонецПроцедуры

#КонецОбласти


