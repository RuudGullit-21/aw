 
#Область ОбработчикиСобытийЭлементовТаблицыФормыТабличноеПолеВарианты

&НаКлиенте
Процедура ТабличноеПолеВариантыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВладелецФормы <> Неопределено Тогда
		
		ТекущиеДанныеСтроки = ТабличноеПолеВарианты.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если ТекущиеДанныеСтроки = Неопределено Тогда
			Возврат;		
		КонецЕсли;
		
		СтруктураВозврата = Новый Структура("Адрес,Широта,Долгота");		
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, ТекущиеДанныеСтроки);
		ЭтаФорма.Закрыть(СтруктураВозврата);
	
	КонецЕсли;
	
КонецПроцедуры
 
#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы
	
&НаКлиенте
Процедура АдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ItobЦентрСпутниковогоМониторинга.ГеографическиеКарты.ItobСервисDaData") Тогда
		МодульDaData = ОбщегоНазначенияКлиент.ОбщийМодуль("ItobСервисDaDataКлиент");
		МодульDaData.ЭлементПолеАвтоПодбор(Элемент, Текст);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ItobЦентрСпутниковогоМониторинга.ГеографическиеКарты.ItobСервисDaData") Тогда
		МодульDaData = ОбщегоНазначенияКлиент.ОбщийМодуль("ItobСервисDaDataКлиент");
		МодульDaData.ЭлементПолеОбработкаВыбора(Элемент, Адрес, ВыбранноеЗначение, СтандартнаяОбработка);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПоиск(Команда)
	
	ТабличноеПолеВарианты.Очистить();
	
	Если ПустаяСтрока(Адрес) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Введите адрес!'"));
		Возврат;
	КонецЕсли;
	
	АдресПоиска = СокрЛП(Адрес);
	
	ТекстОшибки = "";
	ВыполнитьПоискАдреса(АдресПоиска, ТекстОшибки);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОшибки;
		Сообщение.Сообщить(); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьПоискАдреса(АдресПоиска, ТекстОшибки)

	ТекущиеВарианты = ItobГеографическиеСервисы.ПрямоеГеокодирование(АдресПоиска, ТекстОшибки);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат;	
	КонецЕсли; 
	
	Если ТекущиеВарианты = Неопределено Тогда
		Возврат;	
	КонецЕсли;
		
	Для каждого ЭлементВарианты Из ТекущиеВарианты Цикл
		ЗаполнитьЗначенияСвойств(ТабличноеПолеВарианты.Добавить(), ЭлементВарианты.Значение);	
	КонецЦикла;		

КонецПроцедуры

#КонецОбласти

