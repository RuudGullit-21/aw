
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.Объекты)
		ИЛИ НЕ ЗначениеЗаполнено(Параметры.МенеджерПечати)
		ИЛИ НЕ ЗначениеЗаполнено(Параметры.ИмяФормы) Тогда
		
		ВызватьИсключение НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
		
	КонецЕсли;
	
	Если Параметры.Свойство("Дерево") Тогда
		ЭтоДерево = Истина;
		Элементы.КомплектПечатныхФорм.Отображение = ОтображениеТаблицы.Дерево;
	КонецЕсли;
	
	Объекты.ЗагрузитьЗначения(Параметры.Объекты);
	ТипОбъекта = Параметры.МенеджерПечати;
	// Формирования состава печатных форм
	Идентификатор = "";
	Если ТипЗнч(Параметры.ИмяФормы) = Тип("Массив") Тогда
		Для Каждого ТекСтрока Из Параметры.ИмяФормы Цикл
			Идентификатор = ФормированиеСоставаПечатныхФорм(ТекСтрока.ИмяФормы, Идентификатор, ТекСтрока.Документ);
		КонецЦикла;
		ПараметрыИмяФормы = Параметры.Основание;
	Иначе
		Идентификатор     = ФормированиеСоставаПечатныхФорм(Параметры.ИмяФормы, Идентификатор);
		ПараметрыИмяФормы = Параметры.ИмяФормы;
	КонецЕсли;
	
	// Загрузка сохраненных настроек
	Если Идентификатор <> "" Тогда
		Идентификатор = Лев(Идентификатор, СтрДлина(Идентификатор) - 1);
	КонецЕсли;
	
	КлючНастроек = Параметры.МенеджерПечати + "-" + Идентификатор;
	Если ЭтоДерево Тогда
		КлючНастроек = Параметры.МенеджерПечати;
	КонецЕсли;
	
	Если Параметры.Свойство("НастройкаКомплектовДокументов") Тогда
		НастройкаКомплектовДокументов = Параметры.НастройкаКомплектовДокументов;
		СохраненныеНастройкиПечатныхФорм = НастройкаКомплектовДокументов.НастройкиПечатиХранилище.Получить();
		Если СохраненныеНастройкиПечатныхФорм = Неопределено Тогда
			СохраненныеНастройкиПечатныхФорм = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПечатныхФормКомплект", КлючНастроек, Новый Массив);
		КонецЕсли;
	Иначе
		НастройкаКомплектовДокументов = Неопределено;
		СохраненныеНастройкиПечатныхФорм = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПечатныхФормКомплект", КлючНастроек, Новый Массив);
	КонецЕсли;
		
	ВосстановитьНастройкиПечатныхФорм(СохраненныеНастройкиПечатныхФорм, Параметры.ИмяФормы);
	
	СразуНаПринтер = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОбщиеНастройкиПользователя", "ПечатьКомплектаСразуНаПринтер",Ложь);
	Если СразуНаПринтер <> Неопределено Тогда
		ПечатьСразуНаПринтер = СразуНаПринтер;
	Иначе
		ПечатьСразуНаПринтер = Ложь;
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ПечатьКомплектаСразуНаПринтер", Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЭтотОбъект.ТекущийЭлемент = Элементы.Печать;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)

	Если НЕ ЭтоДерево Тогда
		Если КомплектПечатныхФормСписок.Количество() > 0 Тогда
			ИменаМакетов = ПодготовитьНастройкиДляПечати();
			Если ПечатьСразуНаПринтер Тогда
				
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
				ТипОбъекта,
				ИменаМакетов,
				Объекты.ВыгрузитьЗначения(),
				Новый Структура("ФиксированныйКомплект", Истина));
				
			Иначе
				
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
				ТипОбъекта,
				ИменаМакетов,
				Объекты.ВыгрузитьЗначения(),
				Неопределено,
				Новый Структура("ФиксированныйКомплект", Истина));
				
			КонецЕсли;
		Иначе
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Выберите хотя бы одну печатную форму для печати.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КомплектПечатныхФорм");
		КонецЕсли;
	КонецЕсли;
	Если Не ПустаяСтрока(КлючНастроек) Тогда
		СохраняемыеНастройкиПечатныхФорм = Новый Массив;
		Для Каждого НастройкаПечати Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
			Для Каждого НастройкаПечатиДетально Из НастройкаПечати.ПолучитьЭлементы() Цикл
				СохраняемаяНастройка = Новый Структура("Имя, Копий, Печатать, МенеджерПечати");
				ЗаполнитьЗначенияСвойств(СохраняемаяНастройка, НастройкаПечатиДетально);
				СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
			КонецЦикла;
			СохраняемаяНастройка = Новый Структура("Имя, Копий, Печатать, МенеджерПечати");
			ЗаполнитьЗначенияСвойств(СохраняемаяНастройка, НастройкаПечати);
			СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
		КонецЦикла;
		СохранитьНастройкиПечатныхФорм("НастройкиПечатныхФормКомплект", КлючНастроек, СохраняемыеНастройкиПечатныхФорм, НастройкаКомплектовДокументов);
	КонецЕсли;
	
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсеСтроки(Команда)
	Для Каждого ТекСтрока Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
		ТекСтрока.Печатать = Истина;
		Для Каждого ТекСтрокаДетально Из ТекСтрока.ПолучитьЭлементы() Цикл
			Если ТекСтрокаДетально.Копий = 0 Тогда
				ТекСтрокаДетально.Копий = 1;
			КонецЕсли;
			ТекСтрокаДетально.Печатать = Истина;
			КомплектПечатныхФормСписок.Добавить(ТекСтрокаДетально.Представление);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкиВоВсехСтроках(Команда)
	Для Каждого ТекСтрока Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
		ТекСтрока.Печатать = Ложь;
		Для Каждого ТекСтрокаДетально Из ТекСтрока.ПолучитьЭлементы() Цикл
			Если ТекСтрокаДетально.Копий = 0 Тогда
				ТекСтрокаДетально.Копий = 1;
			КонецЕсли;
			ТекСтрокаДетально.Печатать = Ложь;
			КомплектПечатныхФормСписок.Добавить(ТекСтрокаДетально.Представление);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПечатьСразуНаПринтерПриИзменении(Элемент)

	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ПечатьКомплектаСразуНаПринтер", ПечатьСразуНаПринтер);

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыКомплектПечатныхФорм

&НаКлиенте
Процедура НастройкиПечатныхФормКоличествоРегулирование(Элемент, Направление, СтандартнаяОбработка)

	НастройкаПечатнойФормы = ТекущаяНастройкаПечатнойФормы();
	Если СтрНайти(НастройкаПечатнойФормы.Имя,"Документ.") <> 0 Тогда
		НастройкаПечатнойФормы.Копий = 0;
		Возврат;
	КонецЕсли;
	НастройкаПечатнойФормы.Печатать = НастройкаПечатнойФормы.Копий + Направление > 0;

КонецПроцедуры

&НаКлиенте
Процедура КомплектПечатныхФормКопийПриИзменении(Элемент)

	НастройкаПечатнойФормы = ТекущаяНастройкаПечатнойФормы();
	Если СтрНайти(НастройкаПечатнойФормы.Имя,"Документ.") <> 0 Тогда
		НастройкаПечатнойФормы.Копий = 0;
		Возврат;
	КонецЕсли;
	
	Если НастройкаПечатнойФормы.Копий = 0 Тогда
		НастройкаПечатнойФормы.Печатать = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиПечатныхФормПечататьПриИзменении(Элемент)

	НастройкаПечатнойФормы = ТекущаяНастройкаПечатнойФормы();
	Если СтрНайти(НастройкаПечатнойФормы.Имя,"Документ.") <> 0 Тогда
		Для Каждого ТекСтрока Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
			Если ТекСтрока.Имя = НастройкаПечатнойФормы.Имя Тогда
				Для Каждого ТекСтрокаДетально Из ТекСтрока.ПолучитьЭлементы() Цикл
					Если НастройкаПечатнойФормы.Печатать Тогда
						Если ТекСтрокаДетально.Копий = 0 Тогда
							ТекСтрокаДетально.Копий = 1;
						КонецЕсли;
						ТекСтрокаДетально.Печатать = Истина;
						КомплектПечатныхФормСписок.Добавить(НастройкаПечатнойФормы.Представление);
					Иначе
						ТекСтрокаДетально.Печатать = Ложь;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если НастройкаПечатнойФормы.Печатать И НастройкаПечатнойФормы.Копий = 0 Тогда
			НастройкаПечатнойФормы.Копий = 1;
			КомплектПечатныхФормСписок.Добавить(НастройкаПечатнойФормы.Представление);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПодготовитьНастройкиДляПечати()

	ИменаМакетов = "";
	СохраняемыеНастройкиПечатныхФорм = Новый Массив;
	Индекс = 0;
	Если ЭтоДерево Тогда
		Для Каждого ПечатнаяФормаРодитель Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
			Для Каждого ПечатнаяФорма Из ПечатнаяФормаРодитель.ПолучитьЭлементы() Цикл
				Если ПечатнаяФорма.Печатать Тогда
					Если ПечатнаяФорма.Копий = 0 Тогда
						ПечатнаяФорма.Копий = 1;
					КонецЕсли;
					Если ПечатнаяФорма.МенеджерПечати <> ТипОбъекта Тогда
						Если ПечатнаяФорма.МенеджерПечати = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
							ИмяМакета = "ВнешняяПечатнаяФорма." + ПечатнаяФорма.Имя;
						Иначе
							ИмяМакета = ПечатнаяФорма.МенеджерПечати + "." + ПечатнаяФорма.Имя;
						КонецЕсли;
					Иначе
						ИмяМакета = ПечатнаяФорма.Имя;
					КонецЕсли;
					СохраняемаяНастройка = Новый Структура;
					СохраняемаяНастройка.Вставить("ИмяМакета", ПечатнаяФорма.Имя);
					СохраняемаяНастройка.Вставить("Количество", ПечатнаяФорма.Копий);
					СохраняемаяНастройка.Вставить("ПозицияПоУмолчанию", Индекс);
					СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
					Индекс = Индекс + 1;
					
					// Для печати сразу на принтер строка с именами макетов
					// должна содержать столько имен макетов, сколько копий требуется напечатать.
					КоличествоМакетовДляПечати = ?(ПечатьСразуНаПринтер, ПечатнаяФорма.Копий, 1);
					Для Сч =1 по КоличествоМакетовДляПечати Цикл
						ИменаМакетов = ИменаМакетов +"," + ИмяМакета;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Иначе
		Для Каждого ПечатнаяФорма Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
			Если ПечатнаяФорма.Печатать Тогда
				Если ПечатнаяФорма.Копий = 0 Тогда
					ПечатнаяФорма.Копий = 1;
				КонецЕсли;
				Если ПечатнаяФорма.МенеджерПечати <> ТипОбъекта Тогда
					Если ПечатнаяФорма.МенеджерПечати = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
						ИмяМакета = "ВнешняяПечатнаяФорма." + ПечатнаяФорма.Имя;
					Иначе
						ИмяМакета = ПечатнаяФорма.МенеджерПечати + "." + ПечатнаяФорма.Имя;
					КонецЕсли;
				Иначе
					ИмяМакета = ПечатнаяФорма.Имя;
				КонецЕсли;
				СохраняемаяНастройка = Новый Структура;
				СохраняемаяНастройка.Вставить("ИмяМакета", ПечатнаяФорма.Имя);
				СохраняемаяНастройка.Вставить("Количество", ПечатнаяФорма.Копий);
				СохраняемаяНастройка.Вставить("ПозицияПоУмолчанию", Индекс);
				СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
				Индекс = Индекс + 1;
				
				// Для печати сразу на принтер строка с именами макетов
				// должна содержать столько имен макетов, сколько копий требуется напечатать.
				КоличествоМакетовДляПечати = ?(ПечатьСразуНаПринтер, ПечатнаяФорма.Копий, 1);
				Для Сч =1 по КоличествоМакетовДляПечати Цикл
					ИменаМакетов = ИменаМакетов +"," + ИмяМакета;
				КонецЦикла;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИменаМакетов = Сред(ИменаМакетов, 2);
	
	// Сохранение настроек
	КлючНастроекПечати = ТипОбъекта + "-" + ИменаМакетов;
	Если ЭтоДерево Тогда
		КлючНастроекПечати = ТипОбъекта;
	КонецЕсли;
	Если Не ПустаяСтрока(ИменаМакетов) Тогда
		СохранитьНастройкиПечатныхФорм("НастройкиПечатныхФорм",КлючНастроекПечати, СохраняемыеНастройкиПечатныхФорм, НастройкаКомплектовДокументов);
	КонецЕсли;
	
	Возврат ИменаМакетов;

КонецФункции

&НаСервере
Процедура ВосстановитьНастройкиПечатныхФорм(СохраненныеНастройкиПечатныхФорм, ИмяФормы)

	//Если Параметры.Основание = "ОбщаяФорма.уатФормаОформленияДокументов_уэ" Тогда
	//	
	//Иначе
		Если СохраненныеНастройкиПечатныхФорм = Неопределено
			ИЛИ (ТипЗнч(СохраненныеНастройкиПечатныхФорм) = Тип("Массив")
			И СохраненныеНастройкиПечатныхФорм.Количество() = 0) Тогда
			ВосстановитьНастройкиПечатныхФормПоУмолчанию(ИмяФормы);
		КонецЕсли;
		
		СохраненныеНастройки = Новый ТаблицаЗначений();
		СохраненныеНастройки.Колонки.Добавить("Имя");
		СохраненныеНастройки.Колонки.Добавить("Копий");
		СохраненныеНастройки.Колонки.Добавить("Печатать");
		СохраненныеНастройки.Колонки.Добавить("МенеджерПечати");
		Для Каждого СохраненнаяНастройка Из СохраненныеНастройкиПечатныхФорм Цикл
			НоваяСтрока = СохраненныеНастройки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СохраненнаяНастройка);
		КонецЦикла;
		
		Для Каждого ПечатнаяФорма Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
			НайденныеНастройки = СохраненныеНастройки.НайтиСтроки(Новый Структура("Имя, МенеджерПечати", ПечатнаяФорма.Имя, ПечатнаяФорма.МенеджерПечати));
			Если НайденныеНастройки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(ПечатнаяФорма, НайденныеНастройки[0]);
			КонецЕсли;
			Для Каждого ПечатнаяФормаДетально Из ПечатнаяФорма.ПолучитьЭлементы() Цикл
				НайденныеНастройки = СохраненныеНастройки.НайтиСтроки(Новый Структура("Имя, МенеджерПечати", ПечатнаяФормаДетально.Имя, ПечатнаяФормаДетально.МенеджерПечати));
				Если НайденныеНастройки.Количество() > 0 Тогда
					ЗаполнитьЗначенияСвойств(ПечатнаяФормаДетально, НайденныеНастройки[0]);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	//КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПечатныхФормПоУмолчанию(ИмяФормы)

	Для Каждого НастройкаПечатнойФормы Из КомплектПечатныхФорм.ПолучитьЭлементы() Цикл
		Если СтрНайти(НастройкаПечатнойФормы.Имя, "Документ.") Тогда
			НастройкаПечатнойФормы.Копий = 0;
		Иначе
			НастройкаПечатнойФормы.Копий = 1;
		КонецЕсли;
		НастройкаПечатнойФормы.Печатать = Истина;
		Для Каждого ТекСтрока Из НастройкаПечатнойФормы.ПолучитьЭлементы() Цикл
			Если СтрНайти(ТекСтрока.Имя, "Документ.") Тогда
				ТекСтрока.Копий = 0;
			Иначе
				ТекСтрока.Копий = 1;
			КонецЕсли;
			ТекСтрока.Печатать = Истина;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиПечатныхФорм(КлючОбъекта, КлючНастроек, СохраняемыеНастройкиПечатныхФорм, НастройкаКомплектовДокументов)
	
	Если ЗначениеЗаполнено(НастройкаКомплектовДокументов) Тогда
		НастройкаКомплектовДокументовОб = НастройкаКомплектовДокументов.ПолучитьОбъект();
		НастройкаКомплектовДокументовОб.НастройкиПечатиХранилище = Новый ХранилищеЗначения(СохраняемыеНастройкиПечатныхФорм, Новый СжатиеДанных(9));
		НастройкаКомплектовДокументовОб.Записать();
	Иначе
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, СохраняемыеНастройкиПечатныхФорм);		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ТекущаяНастройкаПечатнойФормы()

	Результат = Элементы.КомплектПечатныхФорм.ТекущиеДанные;
	//Если Результат = Неопределено И КомплектПечатныхФорм.Количество() > 0 Тогда
	//	Результат = КомплектПечатныхФорм[0];
	//КонецЕсли;
	//
	Возврат Результат;

КонецФункции

&НаСервере
Функция ФормированиеСоставаПечатныхФорм(ПараметрыИмяФормы, Идентификатор, Документ = "")

	КомандыПечати = УправлениеПечатью.КомандыПечатиФормы(ПараметрыИмяФормы);
	
	Если ЭтоДерево Тогда
		Если КомандыПечати.Количество() Тогда
			НоваяСтрокаРодитель = КомплектПечатныхФорм.ПолучитьЭлементы().Добавить();
			НоваяСтрокаРодитель.Представление = Документ;
			НоваяСтрокаРодитель.Имя           = ПараметрыИмяФормы;
		Иначе
			Возврат Идентификатор;
		КонецЕсли;
	Иначе
		НоваяСтрокаРодитель = КомплектПечатныхФорм;
	КонецЕсли;
	
	Для Каждого КомандаПечати Из КомандыПечати Цикл
		
		Если КомандаПечати.ДополнительныеПараметры.Свойство("НеВыводитьВКомплекте") Тогда
			Продолжить;
		КонецЕсли;
		
		Если КомандаПечати.СкрытаФункциональнымиОпциями Тогда
			Продолжить;
		КонецЕсли;
		
		Если КомандаПечати.МенеджерПечати = "Документ.уатТТД" И КомандаПечати.Идентификатор = "БланкиТТН" Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НоваяСтрокаРодитель.ПолучитьЭлементы().Добавить();
		Если КомандаПечати.ДополнительныеПараметры.Свойство("ИдентификаторВКомплекте") Тогда
			НоваяСтрока.Имя = КомандаПечати.ДополнительныеПараметры.ИдентификаторВКомплекте;
		Иначе
			НоваяСтрока.Имя = КомандаПечати.Идентификатор;
		КонецЕсли;
		НоваяСтрока.МенеджерПечати = КомандаПечати.МенеджерПечати;
		НоваяСтрока.Представление = КомандаПечати.Представление;
		НоваяСтрока.Порядок = КомандаПечати.Порядок;
		
		Если КомандаПечати.МенеджерПечати <> "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
			Идентификатор = Идентификатор + НоваяСтрока.Имя + ",";
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Идентификатор;
	
КонецФункции

#КонецОбласти
