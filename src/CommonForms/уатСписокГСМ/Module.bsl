
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ГруппаГСМ") И Параметры.Свойство("ТС") И Параметры.Свойство("Организация") Тогда
		Если Параметры.ГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Талон") Тогда
			Заголовок = НСтр("en='Fuels coupons for cars.';ru='Талоны ГСМ для авто.'") + " " + Строка(Параметры.ТС);
		ИначеЕсли Параметры.ГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.ПрисадкиИТехническиеЖидкости") Тогда
			Заголовок = НСтр("en='Additives and technical fluids';ru='Присадки и технические жидкости'");
		Иначе //топливо
			Если Параметры.БезАналогов Тогда
				Заголовок = НСтр("en='Main fuel for vehicle';ru='Основное топливо для ТС'") + " ";
			Иначе	
				Заголовок = НСтр("en='Fuel for vehicle';ru='Топливо для ТС'") + " ";
			КонецЕсли;
			Заголовок = Заголовок + Строка(Параметры.ТС);
		КонецЕсли;
		
		мсвГСМ = уатЗащищенныеФункцииСервер.СписокГСМдляТССервер(Параметры.Организация, Параметры.ТС, Параметры.ГруппаГСМ, 
			Параметры.БезАналогов);
		ТаблицаГСМ.Очистить();
		
		флЕстьТалоны = Ложь;
		
		Для Каждого ТекЭл Из мсвГСМ Цикл
			Если ТаблицаГСМ.НайтиСтроки(ТекЭл).Количество() > 0 Тогда //исключаем дубли строк
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаГСМ.Добавить();
			НоваяСтрока.ГСМ = ТекЭл.ГСМ;
			НоваяСтрока.ГСМТалона = ТекЭл.ГСМТалона;
			НоваяСтрока.Литровый = ТекЭл.Литровый;
			НоваяСтрока.Номинал = ТекЭл.Номинал;
			
			Если ЗначениеЗаполнено(ТекЭл.ГСМТалона) Тогда
				флЕстьТалоны = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если флЕстьТалоны Тогда
			Элементы.ТаблицаГСМГСМТалона.Видимость = Истина;
			Элементы.ТаблицаГСМЛитровый.Видимость = Истина;
			Элементы.ТаблицаГСМНоминал.Видимость = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.ТС) И Параметры.Свойство("УчитыватьТЖ") И Параметры.УчитыватьТЖ Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	уатНоменклатураГСМ.Номенклатура
			|ИЗ
			|	РегистрСведений.уатНоменклатураГСМ КАК уатНоменклатураГСМ
			|ГДЕ
			|	уатНоменклатураГСМ.ВестиУчетОстатковТЖ");
			тблТЖ = Запрос.Выполнить().Выгрузить();
			Для Каждого ТекСтрока Из тблТЖ Цикл
				НоваяСтрока = ТаблицаГСМ.Добавить();
				НоваяСтрока.ГСМ = ТекСтрока.Номенклатура;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаГСМ

&НаКлиенте
Процедура ТаблицаГСМВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ТекСтрока = Элементы.ТаблицаГСМ.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Закрыть();
	Иначе
		Закрыть(ТекСтрока.ГСМ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти