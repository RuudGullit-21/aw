#Область СлужебныйПрограммныйИнтерфейс

// Вызывается при перенаправлении задачи.
//
// Параметры
//   ЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя – перенаправляемая задача.
//   НоваяЗадачаСсылка  – ЗадачаСсылка.ЗадачаИсполнителя – задача для нового исполнителя.
//
Процедура ПриПеренаправленииЗадачи(ЗадачаСсылка, НоваяЗадачаСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	БизнесПроцессОбъект = ЗадачаСсылка.БизнесПроцесс.ПолучитьОбъект();
	БизнесПроцессОбъект.Заблокировать();
	
	Если ЗадачаСсылка.ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.уатСогласование.ТочкаМаршрута.Ознакомиться") Тогда
		НайденнаяСтрока = БизнесПроцессОбъект.РезультатыОзнакомлений.Найти(ЗадачаСсылка, "ЗадачаИсполнителя");
		Строка = БизнесПроцессОбъект.РезультатыОзнакомлений.Добавить();
		Строка.ЗадачаИсполнителя = НоваяЗадачаСсылка;
		Строка.НомерИтерации = НайденнаяСтрока.НомерИтерации;
	Иначе
		НайденнаяСтрока = БизнесПроцессОбъект.РезультатыСогласования.Найти(ЗадачаСсылка, "ЗадачаИсполнителя");
		НайденнаяСтрока.РезультатСогласования = Перечисления.уатРезультатыСогласования.Перенаправлено;
		Строка = БизнесПроцессОбъект.РезультатыСогласования.Добавить();
		Строка.ЗадачаИсполнителя = НоваяЗадачаСсылка;
		Строка.НомерИтерации = НайденнаяСтрока.НомерИтерации;
	КонецЕсли;
	
	БизнесПроцессОбъект.Записать();
	
КонецПроцедуры

// Получить структуру с описанием формы выполнения задачи.
// Вызывается при открытии формы выполнения задачи.
//
// Параметры
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - задача 
//   ТочкаМаршрутаБизнесПроцесса - точка маршрута 
//
// Возвращаемое значение:
//   Структура   - структуру с описанием формы выполнения задачи.
//                 Ключ "ПараметрыФормы" содержит параметры формы. 
//
Функция ФормаВыполненияЗадачи(ЗадачаСсылка, ТочкаМаршрутаБизнесПроцесса) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыФормы", Новый Структура("Ключ", ЗадачаСсылка));
	Результат.Вставить("ИмяФормы",       "БизнесПроцесс.уатСогласование.Форма.ФормаЗадачиОсновная");
	
	Возврат Результат;
	
КонецФункции

// Вызывается при выполнении задачи из формы списка.
//
// Параметры:
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - задача.
//   БизнесПроцессСсылка - БизнесПроцессСсылка - бизнес-процесс, по которому сформирована задача ЗадачаСсылка.
//   ТочкаМаршрутаБизнесПроцесса - точка маршрута.
//
Процедура ОбработкаВыполненияПоУмолчанию(ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса) Экспорт
	
	ОсобойОбработкиНеТребуется = Истина;
	
КонецПроцедуры

#КонецОбласти
