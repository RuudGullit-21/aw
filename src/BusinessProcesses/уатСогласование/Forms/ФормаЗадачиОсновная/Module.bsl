
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// номер итерации
	НайденнаяСтрока = Объект.БизнесПроцесс.РезультатыОзнакомлений.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	Если НайденнаяСтрока <> Неопределено Тогда 
		НомерИтерации = НайденнаяСтрока.НомерИтерации;
	КонецЕсли;
	
	Если Объект.Выполнена Тогда
		// Заполнение текста результата выполнения для выполненной задачи (согласование)
		НайденнаяСтрока = Объект.БизнесПроцесс.РезультатыСогласования.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
			ТекстРезультатаВыполнения = Строка(НайденнаяСтрока.РезультатСогласования);
			Если НайденнаяСтрока.РезультатСогласования = Перечисления.уатРезультатыСогласования.НеСогласовано Тогда
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = WebЦвета.Красный;
			Иначе
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = WebЦвета.Зеленый;
			КонецЕсли;
		КонецЕсли;
		
		// Заполнение текста результата выполнения для выполненной задачи (ознакомление)
		НайденнаяСтрока = Объект.БизнесПроцесс.РезультатыОзнакомлений.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
			Если НайденнаяСтрока.ОтправленоНаПовторноеСогласование Тогда
				ТекстРезультатаВыполнения = НСтр("en='Sent for reapproval';ru='Отправлено на повторное согласование'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = WebЦвета.Красный;
			Иначе	
				ТекстРезультатаВыполнения = НСтр("en='Familiarized';ru='Ознакомился'");
				Элементы.ТекстРезультатаВыполнения.ЦветТекста = WebЦвета.Зеленый;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НомерИтерации <= 1 Тогда 
		Элементы.НомерИтерации.Видимость = Ложь;
	КонецЕсли;	
	
	ЗаполнитьХодИсполнения();
	
	// результат согласования
	РезультатСогласования = Объект.БизнесПроцесс.РезультатСогласования;
	Если РезультатСогласования = Перечисления.уатРезультатыСогласования.НеСогласовано Тогда
		Элементы.РезультатСогласования.ЦветТекста = WebЦвета.Красный;
	Иначе
		Элементы.РезультатСогласования.ЦветТекста = WebЦвета.Зеленый;
	КонецЕсли;
	
	Если НЕ уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ПользователиКлиентСервер.АвторизованныйПользователь(), ПланыВидовХарактеристик.уатПраваИНастройки.РазрешитьИзменятьПараметрыСогласования) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХодИсполненияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	Попытка
		ПоказатьЗначение(,Элементы.ХодИсполнения.ТекущиеДанные.Ссылка);
	Исключение
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура БизнесПроцесс(Команда)
	Попытка
		ПоказатьЗначение(,Объект.БизнесПроцесс);
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура Согласовано(Команда)
	УстановитьДатуИсполненияПоУмолчанию();
	
	Если НЕ ПроверитьЗаполнениеЗадачи() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Записать();
	Исключение
		ТекстНСТР = НСтр("en='Task failed to record';ru='Задачу не удалось записать'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	СогласованоСервер();
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Оповестить("ЗадачаИзменена", Объект.Ссылка);
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ЕстьЗамечания(Команда)
	УстановитьДатуИсполненияПоУмолчанию();
	
	Если НЕ ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
		ОчиститьСообщения();
		ТекстНСТР = НСтр("en='""Comment"" field is not filled';ru='Поле ""Комментарий"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		Возврат;	
	КонецЕсли;	
	
	Если НЕ ПроверитьЗаполнениеЗадачи() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Записать();
	Исключение
		ТекстНСТР = НСтр("en='Task failed to record';ru='Задачу не удалось записать'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	ЕстьЗамечанияСервер();
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Оповестить("ЗадачаИзменена", Объект.Ссылка);
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура НеСогласовано(Команда)
	УстановитьДатуИсполненияПоУмолчанию();
	
	Если НЕ ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
		ОчиститьСообщения();
		ТекстНСТР = НСтр("en='""Comment"" field is not filled';ru='Поле ""Комментарий"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		Возврат;	
	КонецЕсли;	
	
	Если НЕ ПроверитьЗаполнениеЗадачи() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Записать();
	Исключение
		ТекстНСТР = НСтр("en='Task failed to record';ru='Задачу не удалось записать'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	НеСогласованоСервер();
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Оповестить("ЗадачаИзменена", Объект.Ссылка);
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	УстановитьДатуИсполненияПоУмолчанию();
	
	Если НЕ ПроверитьЗаполнениеЗадачи() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Записать();
	Исключение
		ТекстНСТР = НСтр("en='Task failed to record';ru='Задачу не удалось записать'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	ЗавершитьСогласованиеСервер();
	
	Оповестить("ЗадачаВыполнена", Объект.Ссылка);
	Оповестить("ЗадачаИзменена", Объект.Ссылка);
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Повторить(Команда)
	УстановитьДатуИсполненияПоУмолчанию();
	
	ПараметрыФормы = Новый Структура("Ключ", Объект.БизнесПроцесс);
	
	ОткрытьФорму("БизнесПроцесс.уатСогласование.Форма.ФормаИзменениеПараметров", ПараметрыФормы, ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ПовторитьЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьХодИсполнения()
	ХодИсполнения.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачаЗадачаИсполнителя.Ссылка,
	|	ЗадачаЗадачаИсполнителя.ПометкаУдаления,
	|	ЗадачаЗадачаИсполнителя.Номер,
	|	ЗадачаЗадачаИсполнителя.Дата,
	|	ЗадачаЗадачаИсполнителя.БизнесПроцесс,
	|	ЗадачаЗадачаИсполнителя.ТочкаМаршрута,
	|	ЗадачаЗадачаИсполнителя.Наименование,
	|	ЗадачаЗадачаИсполнителя.Выполнена,
	|	ЗадачаЗадачаИсполнителя.Важность,
	|	ЗадачаЗадачаИсполнителя.ДатаИсполнения,
	|	ЗадачаЗадачаИсполнителя.Автор,
	|	ЗадачаЗадачаИсполнителя.Описание,
	|	ЗадачаЗадачаИсполнителя.СрокИсполнения,
	|	ЗадачаЗадачаИсполнителя.ДатаНачала,
	|	ЗадачаЗадачаИсполнителя.РезультатВыполнения,
	|	ЗадачаЗадачаИсполнителя.Предмет,
	|	ЗадачаЗадачаИсполнителя.Исполнитель,
	|	РезультатыСогласования.РезультатСогласования,
	|	РезультатыСогласования.НомерИтерации
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК ЗадачаЗадачаИсполнителя
	|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.уатСогласование.РезультатыСогласования КАК РезультатыСогласования
	|		ПО ЗадачаЗадачаИсполнителя.Ссылка = РезультатыСогласования.ЗадачаИсполнителя
	|ГДЕ
	|	ЗадачаЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
	|	И РезультатыСогласования.НомерИтерации = &НомерИтерации
	|	И ЗадачаЗадачаИсполнителя.ТочкаМаршрута В(&ТочкиМаршрута)");
	
	списТочкиМаршрута = Новый СписокЗначений;
	списТочкиМаршрута.Добавить(БизнесПроцессы.уатСогласование.ТочкиМаршрута.СогласоватьПоследовательно);
	списТочкиМаршрута.Добавить(БизнесПроцессы.уатСогласование.ТочкиМаршрута.СогласоватьПараллельно);
	Запрос.УстановитьПараметр("ТочкиМаршрута", списТочкиМаршрута);
	Запрос.УстановитьПараметр("БизнесПроцесс", Объект.БизнесПроцесс);
	Запрос.УстановитьПараметр("НомерИтерации", НомерИтерации);
	
	тбл = Запрос.Выполнить().Выгрузить();
	Для Каждого ТекСтрока Из тбл Цикл
		НоваяСтрока = ХодИсполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	// настроить командную панель внизу
	Если Объект.Выполнена Тогда
		ТолькоПросмотр = Истина;
		Элементы.КомандыГруппаОзнакомиться.Видимость = Ложь;
		Элементы.КомандыГруппаСогласовать.Видимость = Ложь;
		
	Иначе
		Если (Объект.ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.уатСогласование.ТочкаМаршрута.СогласоватьПараллельно") ИЛИ
			Объект.ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.уатСогласование.ТочкаМаршрута.СогласоватьПоследовательно")) Тогда
			
			Элементы.КомандыГруппаОзнакомиться.Видимость = Истина;
			Элементы.КомандыГруппаСогласовать.Видимость = Ложь;
		
		ИначеЕсли Объект.ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.уатСогласование.ТочкаМаршрута.Ознакомиться") Тогда
			Элементы.КомандыГруппаОзнакомиться.Видимость = Ложь;
			Элементы.КомандыГруппаСогласовать.Видимость = Истина;
			
		КонецЕсли;
	КонецЕсли;
		
	Если Объект.ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.уатСогласование.ТочкаМаршрута.Ознакомиться") Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОзнакомление;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСогласование;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СогласованоСервер()
	УстановитьПривилегированныйРежим(Истина);
	СогласованиеОбъект = Объект.БизнесПроцесс.ПолучитьОбъект();
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СогласованиеОбъект.Ссылка);
	Исключение
		ТекстНСТР = НСтр("en='The agreement ""%1 from %2"" is blocked (the form is probably open)!"
"To continue the operation, it is necessary to unblock the agreement (close the form) and repeat the operation';ru='Согласование ""%1 от %2"" заблокировано (возможно открыта форма)!"
"Для продолжения операции следует разблокировать согласование (закрыть форму) и повторить операцию'");
		ТекстСообщения = СтрШаблон(ТекстНСТР, СогласованиеОбъект.Номер, Формат(СогласованиеОбъект.Дата, "ДФ='dd.MM.yyyy HH:mm'"));
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	НайденнаяСтрока = СогласованиеОбъект.РезультатыСогласования.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	НайденнаяСтрока.РезультатСогласования = Перечисления.уатРезультатыСогласования.Согласовано;
	СогласованиеОбъект.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		ЗадачаОбъект = Объект.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Прочитать();
		ЗадачаОбъект.Выполнена = Ложь;
		ЗадачаОбъект.ВыполнитьЗадачу();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЕстьЗамечанияСервер()
	УстановитьПривилегированныйРежим(Истина);
	СогласованиеОбъект = Объект.БизнесПроцесс.ПолучитьОбъект();
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СогласованиеОбъект.Ссылка);
	Исключение
		ТекстНСТР = НСтр("en='The agreement ""%1 from %2"" is blocked (the form is probably open)!"
"To continue the operation, it is necessary to unblock the agreement (close the form) and repeat the operation';ru='Согласование ""%1 от %2"" заблокировано (возможно открыта форма)!"
"Для продолжения операции следует разблокировать согласование (закрыть форму) и повторить операцию'");
		ТекстСообщения = СтрШаблон(ТекстНСТР, СогласованиеОбъект.Номер, Формат(СогласованиеОбъект.Дата, "ДФ='dd.MM.yyyy HH:mm'"));
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	НайденнаяСтрока = СогласованиеОбъект.РезультатыСогласования.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	НайденнаяСтрока.РезультатСогласования = Перечисления.уатРезультатыСогласования.СогласованоСЗамечаниями;
	СогласованиеОбъект.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		ЗадачаОбъект = Объект.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Прочитать();
		ЗадачаОбъект.Выполнена = Ложь;
		ЗадачаОбъект.ВыполнитьЗадачу();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура НеСогласованоСервер()
	УстановитьПривилегированныйРежим(Истина);
	СогласованиеОбъект = Объект.БизнесПроцесс.ПолучитьОбъект();
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СогласованиеОбъект.Ссылка);
	Исключение
		ТекстНСТР = НСтр("en='The agreement ""%1 from %2"" is blocked (the form is probably open)!"
"To continue the operation, it is necessary to unblock the agreement (close the form) and repeat the operation';ru='Согласование ""%1 от %2"" заблокировано (возможно открыта форма)!"
"Для продолжения операции следует разблокировать согласование (закрыть форму) и повторить операцию'");
		ТекстСообщения = СтрШаблон(ТекстНСТР, СогласованиеОбъект.Номер, Формат(СогласованиеОбъект.Дата, "ДФ='dd.MM.yyyy HH:mm'"));
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	НайденнаяСтрока = СогласованиеОбъект.РезультатыСогласования.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	НайденнаяСтрока.РезультатСогласования = Перечисления.уатРезультатыСогласования.НеСогласовано;
	СогласованиеОбъект.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		ЗадачаОбъект = Объект.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Прочитать();
		ЗадачаОбъект.Выполнена = Ложь;
		ЗадачаОбъект.ВыполнитьЗадачу();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗавершитьСогласованиеСервер()
	УстановитьПривилегированныйРежим(Истина);
	СогласованиеОбъект = Объект.БизнесПроцесс.ПолучитьОбъект();
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СогласованиеОбъект.Ссылка);
	Исключение
		ТекстНСТР = НСтр("en='The agreement ""%1 from %2"" is blocked (the form is probably open)!"
"To continue the operation, it is necessary to unblock the agreement (close the form) and repeat the operation';ru='Согласование ""%1 от %2"" заблокировано (возможно открыта форма)!"
"Для продолжения операции следует разблокировать согласование (закрыть форму) и повторить операцию'");
		ТекстСообщения = СтрШаблон(ТекстНСТР, СогласованиеОбъект.Номер, Формат(СогласованиеОбъект.Дата, "ДФ='dd.MM.yyyy HH:mm'"));
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	НайденнаяСтрока = СогласованиеОбъект.РезультатыОзнакомлений.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	НайденнаяСтрока.ОтправленоНаПовторноеСогласование = Ложь;
	СогласованиеОбъект.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		ЗадачаОбъект = Объект.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Прочитать();
		ЗадачаОбъект.Выполнена = Ложь;
		ЗадачаОбъект.ВыполнитьЗадачу();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ПовторитьСогласованиеСервер()
	УстановитьПривилегированныйРежим(Истина);
	СогласованиеОбъект = Объект.БизнесПроцесс.ПолучитьОбъект();
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СогласованиеОбъект.Ссылка);
	Исключение
		ТекстНСТР = НСтр("en='The agreement ""%1 from %2"" is blocked (the form is probably open)!"
"To continue the operation, it is necessary to unblock the agreement (close the form) and repeat the operation';ru='Согласование ""%1 от %2"" заблокировано (возможно открыта форма)!"
"Для продолжения операции следует разблокировать согласование (закрыть форму) и повторить операцию'");
		ТекстСообщения = СтрШаблон(ТекстНСТР, СогласованиеОбъект.Номер, Формат(СогласованиеОбъект.Дата, "ДФ='dd.MM.yyyy HH:mm'"));
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	СогласованиеОбъект.ПовторитьСогласование = Истина;
	НайденнаяСтрока = СогласованиеОбъект.РезультатыОзнакомлений.Найти(Объект.Ссылка, "ЗадачаИсполнителя");
	НайденнаяСтрока.ОтправленоНаПовторноеСогласование = Истина;
	СогласованиеОбъект.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		ЗадачаОбъект = Объект.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Прочитать();
		ЗадачаОбъект.Выполнена = Ложь;
		ЗадачаОбъект.ВыполнитьЗадачу();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеЗадачи()
	ЗадачаОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ЗадачаОбъект.ПроверитьЗаполнение();
КонецФункции

&НаКлиенте
Процедура УстановитьДатуИсполненияПоУмолчанию()
	ЗадачаБылаВыполнена = Ложь;
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ЗадачаБылаВыполнена = уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Объект.Ссылка, "Выполнена");
	КонецЕсли;
	
	Если НЕ ЗадачаБылаВыполнена Тогда
		// Если задача выполнена, то запишем в реквизит Исполнитель того
		// пользователя, который фактически выполнил задачу. Это нам потом
		// потребуется для отчетов. Такую запись делаем только в том
		// случае, если в базе было не выполнено, а в объекте стало выполнено
		Если Объект.Исполнитель.Пустая() Тогда
			Объект.Исполнитель = ПользователиКлиентСервер.АвторизованныйПользователь();
		КонецЕсли;
		Если Объект.ДатаИсполнения = Дата(1,1,1) Тогда
			Объект.ДатаИсполнения = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПовторитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда 
		Возврат;
	КонецЕсли;	
	
	Если НЕ ПроверитьЗаполнениеЗадачи() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Записать();
	Исключение
		ТекстНСТР = НСтр("en='Task failed to record';ru='Задачу не удалось записать'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	ПовторитьСогласованиеСервер();
	
	Закрыть();

КонецПроцедуры

#КонецОбласти
