
////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ ПРЕДНАЗНАЧЕН ДЛЯ ОБРАБОТКИ ВВОДА ТС В ПОЛЯ ВВОДА.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Процедура предназначена для открытия диалога выбора транспортного средства с учетом наложенных отборов.
//
// Параметры:
//  Элемент              - реквизит формы.
//  ТС                   - текущее значение реквизита.
//  ДанныеВыбора         - данные выбора.
//  СтандартнаяОбработка - признак стандартной обработки.
//  СтруктураОтбора      - дополнительные параметры отбора.
//
Процедура РеквизитТСНачалоВыбора(Элемент, ТС, ДанныеВыбора, СтандартнаяОбработка, СтруктураОтбора=Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПроверитьОтборыНаКорректность(СтруктураОтбора);
	
	ПараметрыВыбора = Новый Структура("Отбор, ТекущаяСтрока", СтруктураОтбора, ТС);
	ОткрытьФорму("Справочник.уатТС.ФормаВыбора", ПараметрыВыбора, Элемент);
	
КонецПроцедуры

// Процедура предназначена для формирования списка выбора транспортного средства по событию автоподбора.
//
// Параметры:
//  Элемент                  - реквизит формы.
//  Текст                    - введенный текст в поле реквизита.
//  ДанныеВыбора             - данные выбора значения.
//  ПараметрыПолученияДанных - параметры получения данных.
//  Ожидание                 - время ожидания перед вызовом события.
//  СтандартнаяОбработка     - признак стандартной обработки.
//  СтруктураОтбора          - дополнительные параметры отбора.
//
Процедура РеквизитТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка, СтруктураОтбора=Неопределено) Экспорт
	
	ПроверитьОтборыНаКорректность(СтруктураОтбора);
	
	ДанныеВыбора = уатЗащищенныеФункцииСервер.ПодобратьСписокТС(Текст, СтандартнаяОбработка, СтруктураОтбора);
	
КонецПроцедуры

// Процедура предназначена для формирования списка выбора транспортного средства по событию окончания ввода текста.
//
// Параметры:
//  Элемент                  - реквизит формы.
//  Текст                    - введенный текст в поле реквизита.
//  ДанныеВыбора             - данные выбора значения.
//  ПараметрыПолученияДанных - параметры получения данных.
//  СтандартнаяОбработка     - признак стандартной обработки.
//  СтруктураОтбора          - дополнительные параметры отбора.
//
Процедура РеквизитТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка, СтруктураОтбора=Неопределено) Экспорт
	
	Если Текст = "" Тогда 
		Возврат;
	КонецЕсли;
	
	ПроверитьОтборыНаКорректность(СтруктураОтбора);
	
	ДанныеВыбора = уатЗащищенныеФункцииСервер.ПодобратьСписокТС(Текст, СтандартнаяОбработка, СтруктураОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьОтборыНаКорректность(СтруктураОтбора)
	
	Если СтруктураОтбора = Неопределено Тогда
		СтруктураОтбора = Новый Структура();
		Возврат;
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("Прицеп") Тогда
		Если СтруктураОтбора.Свойство("ТС") Тогда
			
			Если ЗначениеЗаполнено(СтруктураОтбора.ТС) Тогда
				ПринадлежностьТС = уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтруктураОтбора.ТС,"ПринадлежностьТС");
				Если ПринадлежностьТС = ПредопределенноеЗначение("Перечисление.уатПринадлежностьТС.Собственное") Тогда
					Если СтруктураОтбора.Свойство("Организация") Тогда
						СтруктураОтбора.Организация = уатОбщегоНазначения.МестонахождениеТС(СтруктураОтбора.ТС,,Ложь).Организация;
					Иначе
						СтруктураОтбора.Вставить("Организация", уатОбщегоНазначения.МестонахождениеТС(СтруктураОтбора.ТС,,Ложь).Организация);
					Конецесли;
				ИначеЕсли ПринадлежностьТС = ПредопределенноеЗначение("Перечисление.уатПринадлежностьТС.Привлеченное") Тогда
					СтруктураОтбора.Вставить("ВладелецТС", уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтруктураОтбора.ТС, "ВладелецТС"));
				КонецЕсли;
			КонецЕсли;
			
			СтруктураОтбора.Удалить("Прицеп");
			СтруктураОтбора.Удалить("ТС");
		КонецЕсли;
	Иначе
		Если Не СтруктураОтбора.Свойство("ПринадлежностьТС") Тогда
			СтруктураОтбора.Вставить("ПринадлежностьТС", ПредопределенноеЗначение("Перечисление.уатПринадлежностьТС.Собственное"));
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("Организация") И Не ЗначениеЗаполнено(СтруктураОтбора.Организация) Тогда 
		СтруктураОтбора.Удалить("Организация");
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("ВладелецТС") И Не ЗначениеЗаполнено(СтруктураОтбора.ВладелецТС) Тогда 
		СтруктураОтбора.Удалить("ВладелецТС");
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("ПустаяОрганизация") Тогда 
		СтруктураОтбора.Удалить("ПустаяОрганизация");
		СтруктураОтбора.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

