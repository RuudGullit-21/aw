////////////////////////////////////////////////////////////////////////////////
// Команды для трекеров (клиент)
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОписанийОповещений
	
// Обрабатывает выбор команды из списка события "ПослеВыбораКомандыНаТрекер".
//
// Параметры:
//  Результат				 - ЭлементСпискаЗначений - Команда из списка.
//  ДополнительныеПараметры	 - Структура			 - Дополнительные параметры.
//
Процедура ПослеВыбораКомандыНаТрекер(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ItobКомандыДляТрекеровКлиентСервер.ВыполнитьКомандуНаСервере(ДополнительныеПараметры.Терминал, Результат.Значение, "",,Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

// Процедура - обработчик команды "ОтправитьКомандуНаТрекер".
//
// Параметры:
//  ЭтотОбъект	 - УправляемаяФорма	 - Форма обработки "ItobРабочееМестоДиспетчера".
//
Процедура ОтправитьКомандуНаТрекер(ЭтотОбъект) Экспорт
	
	ПараметрыОтчета = ItobОбщегоНазначенияКлиент.СобратьДанныеДляПостроенияОтчета(ЭтотОбъект);
	Если ПараметрыОтчета = Неопределено Тогда
	 	Возврат;		
	
	КонецЕсли;
			
	СписокКоманд = ItobКомандыДляТрекеровВызовСервера.ПолучитьКомандыДляТрекера(ПараметрыОтчета.Терминал);
	Если СписокКоманд.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Доступных команд не обнаружено!'"), 10);
		Возврат;
		
	КонецЕсли;
		
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораКомандыНаТрекер", ItobКомандыДляТрекеровКлиент, ПараметрыОтчета);
	СписокКоманд.ПоказатьВыборЭлемента(Оповещение, НСтр("ru = 'Выберите команду'"));
	
КонецПроцедуры

// Отправляет команду на трекер.
//
// Параметры:
//  Терминал				 - СправочникСсылка.ItobТерминалы	 - Указатель на терминал.
//  Команда					 - Строка							 - Команда.
//  ТекстОшибки				 - Строка							 - Текст ошибки.
//  ПисатьВЖурнал			 - Булево							 - Результат писать в журнал регистрации.
//  ПоказыватьПредупреждение - Булево							 - Флаг отображения предупреждения.
// 
// Возвращаемое значение:
//  Булево - Результат выполнения команды.
//
Функция ВыполнитьКомандуНаСервере(Терминал, Команда, ТекстОшибки, ПисатьВЖурнал = Ложь, ПоказыватьПредупреждение = Ложь) Экспорт
	Результат = ItobКомандыДляТрекеровВызовСервера.ВыполнитьКомандуНаСервере(Терминал, Команда, ТекстОшибки, ПисатьВЖурнал);
	
	Если ПоказыватьПредупреждение Тогда
		Если Результат Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'Команда успешно отправлена!'"), 10);
		Иначе
			ПоказатьПредупреждение(,НСтр("ru = 'Ошибка отправки команды:'")+" "+ТекстОшибки, 30);			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
