////////////////////////////////////////////////////////////////////////////////
// Подсистема "Учет рабочего времени".
//  
////////////////////////////////////////////////////////////////////////////////


#Область СлужебныйПрограммныйИнтерфейс

Функция СформироватьЗапросПоВидамВремени() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатВидыИспользованияРабочегоВремени.Ссылка КАК ВидВремени,
	|	уатВидыИспользованияРабочегоВремени.БуквенныйКод,
	|	уатВидыИспользованияРабочегоВремени.Наименование,
	|	уатВидыИспользованияРабочегоВремени.Ссылка КАК ОсновноеВремя
	|ИЗ
	|	Справочник.уатВидыИспользованияРабочегоВремени КАК уатВидыИспользованияРабочегоВремени";
	
	Возврат Запрос.Выполнить();
КонецФункции

Функция КодыВидовВремени() Экспорт  
	Выборка = СформироватьЗапросПоВидамВремени().Выбрать();
	
	КодыВидовВремени = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл 
		КодыВидовВремени.Вставить(Выборка.ВидВремени, Выборка.БуквенныйКод);
	КонецЦикла;	
	
	КодыВидовВремени.Вставить(Справочники.уатВидыИспользованияРабочегоВремени.ПустаяСсылка(), "");
	
	Возврат КодыВидовВремени;
КонецФункции

Функция ОписаниеВидовВремени(МассивВидовВремени) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивВидовВремени", МассивВидовВремени);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатВидыИспользованияРабочегоВремени.Ссылка КАК ВидВремени,
	|	уатВидыИспользованияРабочегоВремени.Наименование,
	|	уатВидыИспользованияРабочегоВремени.БуквенныйКод,
	|	уатВидыИспользованияРабочегоВремени.Ссылка КАК ОсновноеВремя
	|ИЗ
	|	Справочник.уатВидыИспользованияРабочегоВремени КАК уатВидыИспользованияРабочегоВремени
	|ГДЕ
	|	уатВидыИспользованияРабочегоВремени.Ссылка В(&МассивВидовВремени)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОписаниеВидовВремени = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		ОписаниеВидаВремени = Новый Структура("Наименование, БуквенныйКод, ВидВремени, ОсновноеВремя");
		ЗаполнитьЗначенияСвойств(ОписаниеВидаВремени, Выборка);
		ОписаниеВидовВремени.Вставить(Выборка.ВидВремени, ОписаниеВидаВремени);
	КонецЦикла;	

	Возврат ОписаниеВидовВремени;
	
КонецФункции	

#КонецОбласти