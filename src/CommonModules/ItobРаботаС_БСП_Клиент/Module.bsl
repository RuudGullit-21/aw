////////////////////////////////////////////////////////////////////////////////
// Работа с БСП (клиент)
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс
	
#Область ПодсистемаБазоваяФункциональность

// Обновляет интерфейс программы сохраняя текущее активное окно.
//
Процедура ОбновитьИнтерфейсПрограммы() Экспорт
	Если ВерсияБСП() >= 22500 Тогда
		// Подсистема СтандартныеПодсистемы.БазоваяФункциональность 
		Если ItobВызовСервераПовтИсп.ПодсистемаСуществует("СтандартныеПодсистемы.БазоваяФункциональность") Тогда
			ОбщийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбщегоНазначенияКлиент");
			ОбщийМодуль.ОбновитьИнтерфейсПрограммы();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ПодсистемаПечать 
	
// Выполняет интерактивное проведение документов перед печатью.
// Если есть непроведенные документы, предлагает выполнить проведение. Спрашивает
// пользователя о продолжении, если какие-то из документов не провелись и имеются проведенные.
//
// Параметры:
//  ОписаниеПроцедурыЗавершения - ОписаниеОповещения - процедура, в которую необходимо передать управление после
//                                                     выполнения.
//                                Параметры вызываемой процедуры:
//                                  СписокДокументов - Массив - проведенные документы;
//                                  ДополнительныеПараметры - значение, которое было указано при создании объекта
//                                                            оповещения.
//  СписокДокументов            - Массив            - ссылки на документы, которые требуется провести.
//  Форма                       - УправляемаяФорма  - форма, из которой было вызвана команда. Параметр требуется, когда
//                                                    процедура
//                                                    вызвана из формы объекта, для того, чтобы перечитать форму.
//
// Возвращаемое значение:
//  Булево 
// 	 
Функция ПроверитьДокументыПроведены(ОписаниеПроцедурыЗавершения, СписокДокументов, Форма = Неопределено) Экспорт 
	Результат = Истина;
	ВерсияБиблиотекиБСП = ВерсияБСП();
	
	Если ВерсияБиблиотекиБСП >= 23441 Тогда
		// Подсистема СтандартныеПодсистемы.Печать 
		Если ItobВызовСервераПовтИсп.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
			ОбщийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеПечатьюКлиент");
			Результат = ОбщийМодуль.ПроверитьПроведенностьДокументов(ОписаниеПроцедурыЗавершения, СписокДокументов, Форма);
		КонецЕсли;
	ИначеЕсли ВерсияБиблиотекиБСП >= 22500 Тогда
		// Подсистема СтандартныеПодсистемы.Печать 
		Если ItobВызовСервераПовтИсп.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
			ОбщийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеПечатьюКлиент");
			Результат = ОбщийМодуль.ПроверитьДокументыПроведены(ОписаниеПроцедурыЗавершения, СписокДокументов);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // УправлениеПечатьюКлиент.ПроверитьДокументыПроведены(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник)

// Формирует и выводит на экран печатные формы.
// 
// Параметры:
//  ИмяМенеджераПечати - Строка - менеджер печати для печатаемых объектов;
//  ИменаМакетов       - Строка - идентификаторы печатных форм;
//  МассивОбъектов     - Ссылка, Массив - объекты печати;
//  ВладелецФормы      - УправляемаяФорма - форма, из которой выполняется печать;
//  ПараметрыПечати    - Структура - произвольные параметры для передачи в менеджер печати.
//
Процедура ВыполнитьКомандуПечати(ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ВладелецФормы, ПараметрыПечати = Неопределено) Экспорт	
	Если ВерсияБСП() >= 22500 Тогда
		// Подсистема СтандартныеПодсистемы.Печать 
		Если ItobВызовСервераПовтИсп.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
			ОбщийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеПечатьюКлиент");
			ОбщийМодуль.ВыполнитьКомандуПечати(ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ВладелецФормы, ПараметрыПечати);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти 
	
#Область ПодсистемаКонтактнаяИнформация

// Обработчик события НачалоВыбора поля формы контактной информации.
// Вызывается из подключаемых действий при внедрении подсистемы "Контактная информация".
//
// Параметры:
//     Форма                - УправляемаяФорма - Форма владельца контактной информации.
//     Элемент              - ПолеФормы        - Элемент формы, содержащий представление контактной информации.
//     Модифицированность   - Булево           - Устанавливаемый флаг модифицированности формы.
//     СтандартнаяОбработка - Булево           - Устанавливаемый флаг стандартной обработки события формы.
//     ПараметрыОткрытия    - Структура        - Параметры открытия формы ввода контактной информации.
//
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Форма, Элемент, Модифицированность = Истина, СтандартнаяОбработка = Ложь, ПараметрыОткрытия = Неопределено) Экспорт 
	Если ВерсияБСП() >= 24283 Тогда
		Если ItobВызовСервераПовтИсп.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
			ОбщийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
			ОбщийМодуль.НачалоВыбора(Форма, Элемент, Модифицированность, СтандартнаяОбработка, ПараметрыОткрытия);
		КонецЕсли;
	ИначеЕсли ВерсияБСП() >= 22500 Тогда
		Если ItobВызовСервераПовтИсп.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
			ОбщийМодуль = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
			ОбщийМодуль.ПредставлениеНачалоВыбора(Форма, Элемент, Модифицированность, СтандартнаяОбработка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает установленную версию Библиотеки стандартных подсистем
// 
// Возвращаемое значение:
//  Число 
//
Функция ВерсияБСП()
	Возврат ItobРаботаС_БСП_ВызовСервераПовтИсп.ВерсияБСП();
КонецФункции

#КонецОбласти
