////////////////////////////////////////////////////////////////////////////////
// Управление автотранспортом.
// 
// Процедуры и функции расчета доходов и расходов по тарифам на транспортные услуги.
// 
// Содержит код, используемый в вариантах поставки ПРОФ и КОРП
////////////////////////////////////////////////////////////////////////////////


#Область СлужебныйПрограммныйИнтерфейс

// Процедура заполняет ТЧ доходов или расходов из структуры документа с данными по расчету услуг
// Используется при расчете доходов и расходов в документах логистики
//
Процедура ЗаполнитьДоходыРасходыИзСтруктуры(Объект, СтруктураДокумента, ИмяТЧ, ИмяДокумента, флПлан = Ложь, флСообщение = Ложь, IDпункта = Неопределено) Экспорт
	
	флТЧИзменена = Ложь;
	флЕстьПлановыеДанные = (ИмяДокумента = "уатЗаказГрузоотправителя" ИЛИ ИмяДокумента = "уатЗаказПеревозчику_уэ" ИЛИ ИмяДокумента = "уатМаршрутныйЛист" И ИмяТЧ = "Расходы");
	
	//Объект[ИмяТЧ].Очистить();
	
	мсвСтрокУдалить = Новый Массив;
	Для Каждого ТекСтрокаДок Из Объект[ИмяТЧ] Цикл
		Если ИмяДокумента = "уатПотребностьВПеревозке_уэ" И ТекСтрокаДок.ID <> IDпункта Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрокаДок.РучнойВвод Тогда
			Продолжить;
		КонецЕсли;
		
		флСтрокаНайдена = Ложь;
		НомСтр = -1;
		Для Каждого ТекСтрока Из СтруктураДокумента[ИмяТЧ] Цикл
			НомСтр = НомСтр + 1;
			Если ТекСтрокаДок.Тариф = ТекСтрока.Тариф И (ИмяДокумента <> "уатМаршрутныйЛист" ИЛИ ТекСтрокаДок.ЗаказНаТС = ТекСтрока.ЗаказНаТС) Тогда
				флСтрокаНайдена = Истина;
				Если флПлан Тогда
					//Если Окр(ТекСтрока.КоличествоПлан, 3) <> ТекСтрокаДок.КоличествоПлан ИЛИ Окр(ТекСтрока.ЦенаПлан, 2) <> ТекСтрокаДок.ЦенаПлан
					//	ИЛИ Окр(ТекСтрока.СуммаНДСПлан, 2) <> ТекСтрокаДок.СуммаНДСПлан Тогда
						СтрокаСвойстваИсключить = "Количество, Цена, Сумма, СуммаНДС";
						Если ИмяДокумента = "уатЗаказГрузоотправителя" И ИмяТЧ = "Услуги" Тогда
							СтрокаСвойстваИсключить = СтрокаСвойстваИсключить + ", СуммаСкидки";
						КонецЕсли;
						
						ЗаполнитьЗначенияСвойств(ТекСтрокаДок, ТекСтрока,, СтрокаСвойстваИсключить); //фактические данные оставляем, затираем только плановые
						
						флТЧИзменена = Истина;
					//КонецЕсли;
				Иначе
					//Если Окр(ТекСтрока.Количество, 3) <> ТекСтрокаДок.Количество ИЛИ Окр(ТекСтрока.Цена, 2) <> ТекСтрокаДок.Цена
					//	ИЛИ Окр(ТекСтрока.СуммаНДС, 2) <> ТекСтрокаДок.СуммаНДС Тогда
						СтрокаСвойстваИсключить = "КоличествоПлан, ЦенаПлан, СуммаПлан, СуммаНДСПлан";
						Если ИмяДокумента = "уатЗаказГрузоотправителя" И ИмяТЧ = "Услуги" Тогда
							СтрокаСвойстваИсключить = СтрокаСвойстваИсключить + ", СуммаСкидкиПлан";
						КонецЕсли;
						
						Если флЕстьПлановыеДанные Тогда
							ЗаполнитьЗначенияСвойств(ТекСтрокаДок, ТекСтрока,, СтрокаСвойстваИсключить); //плановые данные оставляем, затираем только фактические
						Иначе
							ЗаполнитьЗначенияСвойств(ТекСтрокаДок, ТекСтрока);
						КонецЕсли;
						
						флТЧИзменена = Истина;
					//КонецЕсли;
				КонецЕсли;
				СтруктураДокумента[ИмяТЧ].Удалить(НомСтр); //удаляем строки расчитанной таблицы тарифов, которые есть в ТЧ, для того чтобы затем добавить новые тарифы в ТЧ
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ флСтрокаНайдена Тогда
			Если флПлан Тогда
				ТекСтрокаДок.КоличествоПлан = 0;
				ТекСтрокаДок.ЦенаПлан = 0;
				ТекСтрокаДок.СуммаПлан = 0;
				ТекСтрокаДок.СуммаНДСПлан = 0;
				Если ИмяДокумента = "уатЗаказГрузоотправителя" И ИмяТЧ = "Услуги" Тогда
					ТекСтрокаДок.СуммаСкидкиПлан = 0;
				КонецЕсли;
			Иначе
				ТекСтрокаДок.Количество = 0;
				ТекСтрокаДок.Цена = 0;
				ТекСтрокаДок.Сумма = 0;
				ТекСтрокаДок.СуммаНДС = 0;
				Если ИмяДокумента = "уатЗаказГрузоотправителя" И ИмяТЧ = "Услуги" Тогда
					ТекСтрокаДок.СуммаСкидки = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ТекСтрокаДок.Сумма = 0 И (НЕ флЕстьПлановыеДанные ИЛИ ТекСтрокаДок.СуммаПлан = 0) Тогда
			мсвСтрокУдалить.Добавить(ТекСтрокаДок);
			флТЧИзменена = Истина;
		КонецЕсли;
	КонецЦикла;
	Для Каждого ТекСтрокаУдалить Из мсвСтрокУдалить Цикл //удаляем строки ТЧ документа, которых нет в расчитанной таблице тарифов
		Объект[ИмяТЧ].Удалить(ТекСтрокаУдалить);
	КонецЦикла;
	
	// добавляем новые расчитанные тарифы (которых раньше не было в ТЧ)
	флЕстьЗаказ = СтруктураДокумента[ИмяТЧ + "СтруктураСтроки"].Свойство("ЗаказНаТС"); //для МЛ
	флЕстьГруз = СтруктураДокумента[ИмяТЧ + "СтруктураСтроки"].Свойство("Груз"); //для Заказа перевозчику
	флЕстьIDСтроки = СтруктураДокумента[ИмяТЧ + "СтруктураСтроки"].Свойство("ID"); //для Потребности в перевозке
	Для Каждого ТекСтрока Из СтруктураДокумента[ИмяТЧ] Цикл
		Если флЕстьЗаказ Тогда
			мсвНовыеТарифы = Объект[ИмяТЧ].НайтиСтроки(Новый Структура("Тариф, ЗаказНаТС", ТекСтрока.Тариф, ТекСтрока.ЗаказНаТС));
		ИначеЕсли флЕстьГруз Тогда
			мсвНовыеТарифы = Объект[ИмяТЧ].НайтиСтроки(Новый Структура("Тариф, Груз", ТекСтрока.Тариф, ТекСтрока.Груз));
		ИначеЕсли флЕстьIDСтроки Тогда
			мсвНовыеТарифы = Объект[ИмяТЧ].НайтиСтроки(Новый Структура("Тариф, ID", ТекСтрока.Тариф, ТекСтрока.ID));
		Иначе
			мсвНовыеТарифы = Объект[ИмяТЧ].НайтиСтроки(Новый Структура("Тариф", ТекСтрока.Тариф));
		КонецЕсли;
		Если мсвНовыеТарифы.Количество() = 0 Тогда
			Если флЕстьЗаказ ИЛИ флЕстьГруз Тогда
				мсвПоискРучнойВвод = Объект[ИмяТЧ].НайтиСтроки(Новый Структура("Тариф, РучнойВвод", ТекСтрока.Тариф, Истина));
				Если мсвПоискРучнойВвод.Количество() = 0 Тогда
					НоваяСтрока = Объект[ИмяТЧ].Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
					флТЧИзменена = Истина;
				КонецЕсли;
			Иначе
				НоваяСтрока = Объект[ИмяТЧ].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				флТЧИзменена = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если флСообщение И флТЧИзменена Тогда
		Если ИмяТЧ = "Доходы" ИЛИ ИмяТЧ = "Услуги" Тогда
			ОписаниеНСТР = НСтр("en='Incomes was calculated automaticaly'; ru='Выполнен автоматический пересчет табличной части ""Доходы""'");
		Иначе
			ОписаниеНСТР = НСтр("en='Expenses was calculated automaticaly'; ru='Выполнен автоматический пересчет табличной части ""Расходы""'");
		КонецЕсли;
		
		#Если Клиент Тогда
			ЗаголовокНСТР = "";
			ПоказатьОповещениеПользователя(ЗаголовокНСТР,, ОписаниеНСТР);
		#Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеНСТР + "(" + Объект.Ссылка + ")"); 
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
