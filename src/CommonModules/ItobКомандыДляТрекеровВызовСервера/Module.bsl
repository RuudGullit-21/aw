////////////////////////////////////////////////////////////////////////////////
// Команды для трекеров (вызов сервера)
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает команды для трекера.
//
// Параметры:
//  Терминал - СправочникСсылка.ItobТерминалы	 - Указатель на терминал.
// 
// Возвращаемое значение:
//  СписокЗначений - Команды для трекера.
//
Функция ПолучитьКомандыДляТрекера(Терминал) Экспорт

	СписокКоманд = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Терминал", Терминал);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ItobПараметрыКомандДляТерминалов.Команда.Представление КАК КомандаПредставление,
	               |	ItobПараметрыКомандДляТерминалов.Содержание
	               |ИЗ
	               |	РегистрСведений.ItobПараметрыКомандДляТерминалов КАК ItobПараметрыКомандДляТерминалов
	               |ГДЕ
	               |	ItobПараметрыКомандДляТерминалов.МодельТерминала В
	               |			(ВЫБРАТЬ
	               |				ItobТерминалы.Модель
	               |			ИЗ
	               |				Справочник.ItobТерминалы КАК ItobТерминалы
	               |			ГДЕ
	               |				ItobТерминалы.Ссылка = &Терминал)
	               |	И (НЕ ItobПараметрыКомандДляТерминалов.Команда.ПометкаУдаления)
	               |	И ItobПараметрыКомандДляТерминалов.Содержание <> """"
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КомандаПредставление";	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокКоманд.Добавить(Выборка.Содержание, Выборка.КомандаПредставление);		
	
	КонецЦикла;
	
	Возврат СписокКоманд;	

КонецФункции // ПолучитьКомандыДляТрекера()

// Отправляет команду на трекер.
//
// Параметры:
//  Терминал		 - СправочникСсылка.ItobТерминалы	 - Указатель на терминал.
//  Команда			 - Строка							 - Команда.
//  ТекстОшибки		 - Строка							 - Текст ошибки.
//  ПисатьВЖурнал	 - Булево							 - Результат писать в журнал регистрации.
// 
// Возвращаемое значение:
//  Булево - Результат выполнения команды.
//
Функция ВыполнитьКомандуНаСервере(Терминал, Команда, ТекстОшибки, ПисатьВЖурнал = Ложь) Экспорт
	Возврат ItobКомандыДляТрекеров.ВыполнитьКомандуНаСервере(Терминал, Команда, ТекстОшибки, ПисатьВЖурнал);
КонецФункции

#КонецОбласти
