
#Область ПрограммныйИнтерфейс

// Используется регламентным заданием ItobЗаполнениеЗаправочныхВедомостей
//
Процедура ЗаписатьДанныеПоВыдачамТопливаРеглЗадание() Экспорт
	
	НачТекДня = НачалоДня(ТекущаяДата());
	
	ЗаписатьДанныеПоВыдачамТоплива(НачалоДня(НачТекДня - 1)); // Заполняем данные за предыдущий день
	ЗаписатьДанныеПоВыдачамТоплива(НачТекДня); // Заполняем данные за текущий день
	
КонецПроцедуры

// Получение данных из внешнего источника данных и запись в документ ItobВедомостьПоЗаправкам
//
// Параметры:
//  НаДату	 - Дата - За какой день формируем заправочную ведомость.
//
Процедура ЗаписатьДанныеПоВыдачамТоплива(НаДату) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ItobВедомостьПоВыдачамТоплива.Ссылка КАК Ссылка,
	|	ItobЦистерныТопливозаправщиковСрезПоследних.Топливозаправщик КАК Топливозаправщик,
	|	ItobЦистерныТопливозаправщиковСрезПоследних.Цистерна КАК Цистерна
	|ИЗ
	|	РегистрСведений.ItobЦистерныТопливозаправщиков.СрезПоследних(&НаДату, ) КАК ItobЦистерныТопливозаправщиковСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ItobВедомостьПоВыдачамТоплива КАК ItobВедомостьПоВыдачамТоплива
	|		ПО ItobЦистерныТопливозаправщиковСрезПоследних.Топливозаправщик = ItobВедомостьПоВыдачамТоплива.Топливозаправщик
	|			И ItobЦистерныТопливозаправщиковСрезПоследних.Цистерна = ItobВедомостьПоВыдачамТоплива.Цистерна
	|			И (НАЧАЛОПЕРИОДА(ItobВедомостьПоВыдачамТоплива.Дата, ДЕНЬ) = &НаДату)
	|			И (НЕ ItobВедомостьПоВыдачамТоплива.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Иначе
			ДокОбъект = Документы.ItobВедомостьПоВыдачамТоплива.СоздатьДокумент();
			ДокОбъект.Дата = НаДату;
			ДокОбъект.Топливозаправщик = Выборка.Топливозаправщик;
			ДокОбъект.Цистерна = Выборка.Цистерна;
		КонецЕсли;
		ДокОбъект.ЗаполнитьЗаправки();
		Если ДокОбъект.Заправки.Количество() Тогда
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
