#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений();
	
	Для Каждого ТекВид Из Метаданные.Перечисления.ВидыДоговоровКонтрагентов.ЗначенияПеречисления Цикл 
		Если ТекВид.Имя = "СКомитентомНаЗакупку" Или ТекВид.Имя = "СКомиссионеромНаЗакупку" Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не Параметры.СтрокаПоиска = Неопределено И ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда 
			ДлинаСтрокиПодбора = СтрДлина(Параметры.СтрокаПоиска);
			
			Если НРег(Лев(ТекВид.Синоним, ДлинаСтрокиПодбора)) = НРег(Параметры.СтрокаПоиска) Тогда 
				тПредставление = Новый ФорматированнаяСтрока(
					Новый ФорматированнаяСтрока(Лев(ТекВид.Синоним, ДлинаСтрокиПодбора),, ЦветаСтиля.уатЦветТекстаПриАвтоподборе), 
					Новый ФорматированнаяСтрока(Прав(ТекВид.Синоним, СтрДлина(ТекВид.Синоним)-ДлинаСтрокиПодбора)));
				ДанныеВыбора.Добавить(Перечисления.ВидыДоговоровКонтрагентов[ТекВид.Имя], тПредставление);
			КонецЕсли;
			
		Иначе 
			ДанныеВыбора.Добавить(Перечисления.ВидыДоговоровКонтрагентов[ТекВид.Имя]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли