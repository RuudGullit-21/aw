
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстОшибки = "";
	СоздатьШаблонПоУмолчанию(ТекстОшибки);
	
	Если ТекстОшибки = "" Тогда 
		ПоказатьПредупреждение(, "Шаблон создан успешно.");
		
		Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") 
				И ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Справочник.ШаблоныСообщений.Форма.ФормаСписка"
				И Не ПараметрыВыполненияКоманды.Источник.Элементы.Найти("Список") = Неопределено Тогда 
			ПараметрыВыполненияКоманды.Источник.Элементы.Список.Обновить();
		КонецЕсли;
	Иначе 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьШаблонПоУмолчанию(ТекстОшибки)
	
	ШаблонДляТендера = Справочники.ШаблоныСообщений.СоздатьЭлемент();
	ШаблонДляТендера.Наименование                           = "Шаблон для тендера";
	ШаблонДляТендера.Назначение                             = "Тендер";
	ШаблонДляТендера.ПолноеИмяТипаПараметраВводаНаОсновании = "Документ.уатТендер_уэ";
	ШаблонДляТендера.ПредназначенДляВводаНаОсновании        = Истина;
	ШаблонДляТендера.ПредназначенДляЭлектронныхПисем        = Истина;
	ШаблонДляТендера.ТемаПисьма                             = "Тендер № [уатТендер_уэ.Номер]";
	ШаблонДляТендера.ТипТекстаПисьма                        = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML;
	
	ШаблонДляТендера.ТекстШаблонаПисьма = 
	"Добрый день, [уатТендер_уэ.ПолучательРассылки]!
	|
	|Приглашаем принять участие в тендере № [уатТендер_уэ.Номер] от [уатТендер_уэ.Дата].
	|
	|С уважением, [уатТендер_уэ.Организация.Наименование]";
	
	ШаблонДляТендера.ТекстШаблонаПисьмаHTML =
	"<html>
	|<head>
	|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
	|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
	|<meta name=""format-detection"" content=""telephone=no"" />
	|<style type=""text/css"">
	|body{margin:0;padding:8px;}
	|p{line-height:1.15;margin:0;white-space:pre-wrap;}
	|ol,ul{margin-top:0;margin-bottom:0;}
	|img{border:none;}
	|li>p{display:inline;}
	|</style>
	|</head>
	|<body>
	|<p>Добрый день, [уатТендер_уэ.ПолучательРассылки]!</p>
	|<p><br></p>
	|<p>Приглашаем принять участие в тендере № [уатТендер_уэ.Номер] от [уатТендер_уэ.Дата].</p>
	|<p><br></p>
	|<p>С уважением, [уатТендер_уэ.Организация.Наименование]</p>
	|</body>
	|</html>";
	
	Попытка
		ШаблонДляТендера.Записать();
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
