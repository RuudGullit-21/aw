
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстОшибки = "";
	СоздатьШаблонПоУмолчанию(ТекстОшибки);
	
	Если ТекстОшибки = "" Тогда 
		ТекстНстр = Нстр("ru = 'Шаблон создан успешно'");
		ПоказатьПредупреждение(, ТекстНстр);
		
		Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") 
				И ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Справочник.ШаблоныСообщений.Форма.ФормаСписка"
				И Не ПараметрыВыполненияКоманды.Источник.Элементы.Найти("Список") = Неопределено Тогда 
			ПараметрыВыполненияКоманды.Источник.Элементы.Список.Обновить();
		КонецЕсли;
	Иначе 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьШаблонПоУмолчанию(ТекстОшибки)
	
	ШаблонДляСтраховогоСертификата = Справочники.ШаблоныСообщений.СоздатьЭлемент();
	ШаблонДляСтраховогоСертификата.Наименование                           = Нстр("ru = 'Шаблон для страхового сертификата'");
	ШаблонДляСтраховогоСертификата.Назначение                             = Нстр("ru = 'Страховой сертификат'; en = 'Insurance certificate'");
	ШаблонДляСтраховогоСертификата.ПолноеИмяТипаПараметраВводаНаОсновании = "Документ.уатСтраховойСертификат_уэ";
	ШаблонДляСтраховогоСертификата.ПредназначенДляВводаНаОсновании        = Истина;
	ШаблонДляСтраховогоСертификата.ПредназначенДляЭлектронныхПисем        = Истина;
	ШаблонДляСтраховогоСертификата.ТемаПисьма                             = "Страхового сертификат № [уатСтраховойСертификат_уэ.Номер]";
	ШаблонДляСтраховогоСертификата.ТипТекстаПисьма                        = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML;
	
	ШаблонДляСтраховогоСертификата.ТекстШаблонаПисьма = 
	"Добрый день, [уатСтраховойСертификат_уэ.Контрагент.ОсновноеКонтактноеЛицо]!
	|
	|Прошу застраховать груз, заявка [уатСтраховойСертификат_уэ.Номер] от [уатСтраховойСертификат_уэ.Дата] в прикрепленном файле.
	|
	|С уважением, [уатСтраховойСертификат_уэ.Организация.Наименование]";
	
	ШаблонДляСтраховогоСертификата.ТекстШаблонаПисьмаHTML =
	"<html>
	|<head>
	|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
	|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
	|<meta name=""format-detection"" content=""telephone=no"" />
	|<style type=""text/css"">
	|body{margin:0;padding:8px;}
	|p{line-height:1.15;margin:0;white-space:pre-wrap;}
	|ol,ul{margin-top:0;margin-bottom:0;}
	|img{border:none;}
	|li>p{display:inline;}
	|</style>
	|</head>
	|<body>
	|<p>Добрый день, [уатСтраховойСертификат_уэ.Контрагент.ОсновноеКонтактноеЛицо]!</p>
	|<p><br></p>
	|<p>Прошу застраховать груз, заявка [уатСтраховойСертификат_уэ.Номер] от [уатСтраховойСертификат_уэ.Дата] в прикрепленном файле.</p>
	|<p><br></p>
	|<p>С уважением, [уатСтраховойСертификат_уэ.Организация.Наименование]</p>
	|</body>
	|</html>";
	
	Попытка
		ШаблонДляСтраховогоСертификата.Записать();
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
