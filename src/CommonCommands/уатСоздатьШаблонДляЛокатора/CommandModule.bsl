
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстОшибки = "";
	СоздатьШаблонПоУмолчанию(ТекстОшибки);
	
	Если ТекстОшибки = "" Тогда 
		ПоказатьПредупреждение(, "Шаблон создан успешно.");
		
		Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") 
				И ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Справочник.ШаблоныСообщений.Форма.ФормаСписка"
				И Не ПараметрыВыполненияКоманды.Источник.Элементы.Найти("Список") = Неопределено Тогда 
			ПараметрыВыполненияКоманды.Источник.Элементы.Список.Обновить();
		КонецЕсли;
	Иначе 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьШаблонПоУмолчанию(ТекстОшибки)
	
	ШаблонПисьма = Справочники.ШаблоныСообщений.СоздатьЭлемент();
	ШаблонПисьма.Наименование                           = "Ссылка на локатор Wialon";
	ШаблонПисьма.Назначение                             = "Заказ на ТС";
	ШаблонПисьма.ПолноеИмяТипаПараметраВводаНаОсновании = "Документ.уатЗаказГрузоотправителя";
	ШаблонПисьма.ПредназначенДляВводаНаОсновании        = Истина;
	ШаблонПисьма.ПредназначенДляЭлектронныхПисем        = Истина;
	ШаблонПисьма.ТемаПисьма                             = "";
	ШаблонПисьма.ТипТекстаПисьма                        = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML;
	
	ШаблонПисьма.ТекстШаблонаПисьма = 
	"Отслеживание ТС по ссылке [уатЗаказГрузоотправителя.ДанныеМаршрутногоЛиста.СсылкаНаЛокатор]";
	
	ШаблонПисьма.ТекстШаблонаПисьмаHTML =
	"<html>
	|<head>
	|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
	|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
	|<meta name=""format-detection"" content=""telephone=no"" />
	|<style type=""text/css"">
	|body{margin:0;padding:8px;}
	|p{line-height:1.15;margin:0;white-space:pre-wrap;}
	|ol,ul{margin-top:0;margin-bottom:0;}
	|img{border:none;}
	|li>p{display:inline;}
	|</style>
	|</head>
	|<body>
	|<p>Отслеживание ТС по ссылке <a href=""[уатЗаказГрузоотправителя.ДанныеМаршрутногоЛиста.СсылкаНаЛокатор]"">[уатЗаказГрузоотправителя.ДанныеМаршрутногоЛиста.СсылкаНаЛокатор]</a></p>
	|</body>
	|</html>";
	
	Попытка
		ШаблонПисьма.Записать();
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
