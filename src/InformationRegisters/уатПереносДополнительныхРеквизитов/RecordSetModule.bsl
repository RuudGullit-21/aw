
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого ТекущаяЗапись Из ЭтотОбъект Цикл
		
		Если Не ЗначениеЗаполнено(ТекущаяЗапись.ДокументПриемник)
			Или Не ЗначениеЗаполнено(ТекущаяЗапись.ДокументИсточник) Тогда
			// Обязательные измерения регистра не заполнены. 
			// Отказ и сообщение о том, что они не заполнены, будут выполнены средствами платформы.
			Возврат;
		КонецЕсли;
		
		// Получаем имена метаданных из имен предопределенных элементов справочника
		МетаданныеИсточника = уатОбщегоНазначенияСервер.МетаданныеДокументаПоНаборуДопРеквизитов(
			ТекущаяЗапись.ДокументИсточник);
		МетаданныеПриемника = уатОбщегоНазначенияСервер.МетаданныеДокументаПоНаборуДопРеквизитов(
			ТекущаяЗапись.ДокументПриемник);
		Если МетаданныеИсточника = Неопределено Тогда
			ТекстНСтр = СтрШаблон(
				НСтр("ru = 'Ошибка при определении типа документа, связанного с ""%1"". Выберите другой документ-источник'"),
				ТекущаяЗапись.ДокументИсточник);
			ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,,, Отказ);
			Возврат;
		ИначеЕсли МетаданныеПриемника = Неопределено Тогда
			ТекстНСтр = СтрШаблон(
				НСтр("ru = 'Ошибка при определении типа документа, связанного с ""%1"". Выберите другой документ-приемник'"),
				ТекущаяЗапись.ДокументПриемник);
			ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,,, Отказ);
			Возврат;
		КонецЕсли;
		
		Если Не МетаданныеПриемника.ВводитсяНаОсновании.Содержит(МетаданныеИсточника) ТОгда
			ТекстНСтр = СтрШаблон(
				НСтр("ru = 'Невозможен ввод документа-приемника ""%1"" на основании документа-источника ""%2""'"),
				МетаданныеПриемника.Представление(),
				МетаданныеИсточника.Представление());
			ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,,, Отказ);
		КонецЕсли;
		
		// Проверяем, что "Реквизит приемника" действительно является
		// реквизитом или дополнительным реквизитом для документа-приемника
		Если ЗначениеЗаполнено(ТекущаяЗапись.РеквизитПриемника)
			И ТипЗнч(ТекущаяЗапись.РеквизитПриемника) = Тип("Строка") 
			И МетаданныеПриемника.Реквизиты.Найти(ТекущаяЗапись.РеквизитПриемника) = Неопределено Тогда
			ТекстНСтр = СтрШаблон(
				НСтр("ru = 'У документа-приемнике ""%1"" нет реквизита ""%2""'"),
				МетаданныеПриемника.Представление(),
				ТекущаяЗапись.РеквизитПриемника);
			ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,,, Отказ);
		ИначеЕсли ЗначениеЗаполнено(ТекущаяЗапись.РеквизитПриемника)
			И ТипЗнч(ТекущаяЗапись.РеквизитПриемника) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения")
			И Не ЕстьДополнительныйРеквизит(ТекущаяЗапись.ДокументПриемник, ТекущаяЗапись.РеквизитПриемника) Тогда
			ТекстНСтр = СтрШаблон(
				НСтр("ru = 'У документа-приемника ""%1"" нет дополнительного реквизита ""%2""'"),
				МетаданныеПриемника.Представление(),
				ТекущаяЗапись.РеквизитПриемника);
			ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,,, Отказ);
		КонецЕсли;
		
		// Проверяем, что "Дополнительный реквизит источника" действительно является
		// дополнительным реквизитом для документа-источника
		Если ЗначениеЗаполнено(ТекущаяЗапись.ДокументИсточник)
			И ЗначениеЗаполнено(ТекущаяЗапись.ДополнительныйРеквизитИсточника)
			И Не ЕстьДополнительныйРеквизит(ТекущаяЗапись.ДокументИсточник, ТекущаяЗапись.ДополнительныйРеквизитИсточника) Тогда
			ТекстНСтр = СтрШаблон(
				НСтр("ru = 'У документа ""%1"" нет дополнительного реквизита ""%2""'"),
				МетаданныеИсточника.Представление(),
				ТекущаяЗапись.ДополнительныйРеквизитИсточника);
			ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,,, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьДополнительныйРеквизит(НаборДополнительныхРеквизитов, Свойство)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка КАК Ссылка,
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК Свойство
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Ссылка
	|	И НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство = &Свойство");
	Запрос.УстановитьПараметр("Ссылка", НаборДополнительныхРеквизитов);
	Запрос.УстановитьПараметр("Свойство", Свойство);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли