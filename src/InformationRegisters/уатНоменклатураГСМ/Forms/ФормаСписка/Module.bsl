
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатРегистрФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка,ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.уатНоменклатураГСМ) Тогда 
		Элементы.Список.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ВыборГСМ") И Параметры.ВыборГСМ = Истина Тогда
		РежимВыбораГСМ = Истина;
		
		Если Параметры.Свойство("ГруппаГСМ") И ЗначениеЗаполнено(Параметры.ГруппаГСМ) Тогда
			Если Параметры.Свойство("УчитыватьТЖ") И Параметры.УчитыватьТЖ Тогда
				Если ТипЗнч(Параметры.ГруппаГСМ) = Тип("СписокЗначений") Тогда
					списГруппыГСМ = Параметры.ГруппаГСМ;
				Иначе
					списГруппыГСМ = Новый СписокЗначений;
					списГруппыГСМ.Добавить(Параметры.ГруппаГСМ);
				КонецЕсли;
				ГруппаИЛИ = Список.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
				Элемент1 = ГруппаИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				Элемент1.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
				Элемент1.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ГруппаГСМ");
				Элемент1.ПравоеЗначение = списГруппыГСМ;
				ГруппаИ = ГруппаИЛИ.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
				Элемент2 = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				Элемент2.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				Элемент2.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ГруппаГСМ");
				Элемент2.ПравоеЗначение = Перечисления.уатГруппыГСМ.ПрисадкиИТехническиеЖидкости;
				Элемент2 = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				Элемент2.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				Элемент2.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВестиУчетОстатковТЖ");
				Элемент2.ПравоеЗначение = Истина;
			Иначе
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГруппаГСМ", Параметры.ГруппаГСМ,
					ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
			КонецЕсли;
		КонецЕсли;
		Элементы.ВыбратьГСМ.Видимость = Истина;
		Если ЗначениеЗаполнено(Параметры.НачальноеЗначениеВыбора) Тогда
			ТекГСМ = Параметры.НачальноеЗначениеВыбора;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ТекГСМ) Тогда
		Элементы.Список.ТекущаяСтрока = КлючЗаписиРегистра();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если РежимВыбораГСМ Тогда
		СтандартнаяОбработка = Ложь;
		ТекСтрока = Элементы.Список.ТекущиеДанные;
		Закрыть(ТекСтрока.Номенклатура);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьГСМ(Команда)
	ТекСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Закрыть(ТекСтрока.Номенклатура)
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция КлючЗаписиРегистра()
	КлючЗаписи = РегистрыСведений.уатНоменклатураГСМ.СоздатьКлючЗаписи(Новый Структура("Номенклатура", ТекГСМ));
	Возврат КлючЗаписи;
КонецФункции

#КонецОбласти