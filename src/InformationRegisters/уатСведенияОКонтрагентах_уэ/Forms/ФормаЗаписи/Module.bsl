
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Запись.РедактированиеРасходов) Тогда
		Запись.РедактированиеРасходов = Перечисления.уатУровеньДоступаКУслугам.РедактированиеЗапрещено;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Запись.РедактированиеПоступленийУслуг) Тогда
		Запись.РедактированиеПоступленийУслуг = Перечисления.уатУровеньДоступаКУслугам.РедактированиеЗапрещено;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Запись.РедактированиеСчетовНаОплату) Тогда
		Запись.РедактированиеСчетовНаОплату = Перечисления.уатУровеньДоступаКУслугам.РедактированиеЗапрещено;
	КонецЕсли;
	
	ИспользоватьСогласования = ПолучитьФункциональнуюОпцию("уатИспользоватьСогласованиеДокументов");
	ВариантПоставкиКОРП = уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не Запись.СогласованиеРекламацийСПеревозчиком Тогда
		Запись.ИсполнительДляСогласования = ПредопределенноеЗначение("Справочник.ВнешниеПользователи.ПустаяСсылка");
	КонецЕсли;
	
	Если Не Запись.Перевозчик Тогда
		Запись.РедактированиеРасходов = ПредопределенноеЗначение("Перечисление.уатУровеньДоступаКУслугам.ПустаяСсылка");
		Запись.СозданиеПроизвольныхПоступленийУслуг = Ложь;
		Запись.РедактированиеПоступленийУслуг = ПредопределенноеЗначение("Перечисление.уатУровеньДоступаКУслугам.ПустаяСсылка");
		Запись.РедактированиеСчетовНаОплату = ПредопределенноеЗначение("Перечисление.уатУровеньДоступаКУслугам.ПустаяСсылка");
		Запись.СогласованиеРекламацийСПеревозчиком = Ложь;
		Запись.ИсполнительДляСогласования = ПредопределенноеЗначение("Справочник.ВнешниеПользователи.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Не ЗначениеЗаполнено(Запись.ИсполнительДляСогласования)
		И ИспользоватьСогласования
		И Запись.СогласованиеРекламацийСПеревозчиком
		И Запись.Перевозчик Тогда
		ТекстНСтр = НСтр("ru = 'Поле ""Исполнитель для согласования"" не заполнено'");
		ОбщегоНазначения.СообщитьПользователю(ТекстНСтр,,"ИсполнительДляСогласования" , "Запись" , Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПеревозчикПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПроизвольныхПоступленийУслугПриИзменении(Элемент)
	УстановитьВидимость();
	Если Запись.СозданиеПроизвольныхПоступленийУслуг Тогда
		Запись.РедактированиеПоступленийУслуг
			= ПредопределенноеЗначение("Перечисление.уатУровеньДоступаКУслугам.РедактированиеУслугРазрешено");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СогласованиеРекламацийСПеревозчикомПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.ГруппаДоступВнешнегоПользователя.Видимость = Запись.Перевозчик;
	Элементы.РедактированиеПоступленийУслуг.Доступность = Не Запись.СозданиеПроизвольныхПоступленийУслуг;
	Элементы.ГруппаСогласованиеРекламаций.Видимость = ИспользоватьСогласования И ВариантПоставкиКОРП;
	Элементы.ИсполнительДляСогласования.Видимость = Запись.СогласованиеРекламацийСПеревозчиком;
КонецПроцедуры

#КонецОбласти