#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда		
		Возврат;	
	КонецЕсли;
	
	РадиусФиксацииПоУмолчанию = ItobВызовСервераПовтИсп.ПолучитьРадиусЗоныПриходаНаТочку();
	Если РадиусФиксацииПоУмолчанию = 0 Тогда
		РадиусФиксацииПоУмолчанию = 200;	
	КонецЕсли;
	
	// Константы, используемые для вычисления смещения и расстояния
	Pi=3.1415926535897932;
	D2R = Pi/180; // Константа для преобразования градусов в радианы
	Для каждого Запись Из ЭтотОбъект Цикл	
		Если НЕ Запись.ГеометрияФиксацииПосещения = Перечисления.ItobВидыГеометрииФиксацииПосещения.Полигон Тогда	
			Если Запись.Широта=0 ИЛИ Запись.Долгота=0 Тогда
				Запись.ГеографическаяРамкаШирота1=0;
				Запись.ГеографическаяРамкаШирота2=0;
				Запись.ГеографическаяРамкаДолгота1=0;
				Запись.ГеографическаяРамкаДолгота2=0;
				Продолжить;
			КонецЕсли;
			
			РадиусФиксации = Запись.РадиусФиксацииПосещения;
			Если РадиусФиксации=0 Тогда
				РадиусФиксации = РадиусФиксацииПоУмолчанию;	
			КонецЕсли;
			
			ДельтаШироты  = РадиусФиксации/111111.111;
			РадиусЗемлиШ = Cos(Запись.Широта * D2R)*40000000/360;
			ДельтаДолготы = ?(РадиусЗемлиШ=0,0,РадиусФиксации/РадиусЗемлиШ);
			
			Запись.ГеографическаяРамкаШирота1  = Запись.Широта-ДельтаШироты;
			Запись.ГеографическаяРамкаШирота2  = Запись.Широта+ДельтаШироты;
			Запись.ГеографическаяРамкаДолгота1 = Запись.Долгота-ДельтаДолготы;
			Запись.ГеографическаяРамкаДолгота2 = Запись.Долгота+ДельтаДолготы;   
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
