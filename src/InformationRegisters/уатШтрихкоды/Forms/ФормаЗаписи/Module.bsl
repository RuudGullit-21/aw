
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатРегистрФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка,ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Метаданные.Обработки.Найти("ПечатьШтрихкода") = Неопределено Тогда
		Элементы.ПечатьШтрихкода.Видимость = Ложь;
	КонецЕсли;
	
	//ПодключаемоеОборудование
	уатОбщегоНазначения_проф.НастроитьПодключаемоеОборудование(ЭтотОбъект);
	//Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатРегистрФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	//ПодключаемоеОборудование
	уатОбщегоНазначенияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(ЭтотОбъект);
	//Конец ПодключаемоеОборудование
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Оповещ = Новый ОписаниеОповещения("СчитатьПодставитьШтрихкод", ЭтотОбъект);
		уатОбщегоНазначенияКлиент.ОбработатьСобытиеПодключаемогоОборудования(ИмяСобытия, Параметр, Источник,
			Новый Структура("Оповещение", Оповещ));
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	// ПодключаемоеОборудование
	уатОбщегоНазначенияКлиент.ОбработкаВнешнегоСобытия(Источник, Событие, Данные);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьСлучайныйШК(Команда)
	Запись.Штрихкод = уатЗащищенныеФункцииСервер_проф.СформироватьШтрихкодОбъекта(Запись.Объект,
		Запись.ЗаказГрузоотправителя, Запись.НомерСтрокиЗаказа, Запись.НомерПаллеты, Запись.ТипШтрихкода);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьШтрихкода(Команда)
	Если НЕ ЗначениеЗаполнено(Запись.Штрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Штрихкод, СформироватьНаСервере", Запись.Штрихкод);
	ОткрытьФорму("Обработка.ПечатьШтрихкода.Форма", ПараметрыФормы,, "ПечатьШтрихкода");
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	Элементы.СформироватьСлучайныйШК.Доступность = ЗначениеЗаполнено(Запись.Объект);
КонецПроцедуры

&НаКлиенте
Процедура СчитатьПодставитьШтрихкод(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.Штрихкод) Тогда
		Запись.Штрихкод = Результат.Штрихкод;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
