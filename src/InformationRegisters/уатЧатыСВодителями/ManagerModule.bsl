#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СоздатьСообщениеМобильногоПриложения(Получатель, ТекстСообщения,
	Чат = Неопределено, Отправитель = Неопределено,
	ВидСообщения = Неопределено, Дата = Неопределено,
	Отправлено = Истина, Доставлено = Ложь, ДатаДоставки = Неопределено,
	Прочитано = Ложь, ДатаПрочтения = Неопределено, Идентификатор = "", ТекстОшибки = "") Экспорт
	
	Если ТипЗнч(Получатель) = Тип("СправочникСсылка.Пользователи")
		ИЛИ ТипЗнч(Получатель) = Тип("СправочникСсылка.уатЧаты")
		ИЛИ ТипЗнч(Получатель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		мсвПолучателей = Новый Массив();
		мсвПолучателей.Добавить(Получатель);
	Иначе
		мсвПолучателей = Получатель;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидСообщения) Тогда
		ВидСообщения = Перечисления.уатВидыСообщенийМобильногоПриложения.Исходящее;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Чат) Тогда
		Чат = Справочники.уатЧаты.ПолучитьТекущийЧатВодителя(Отправитель);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Отправитель) Тогда
		Отправитель = Чат;
	КонецЕсли;

	Если Доставлено
		И НЕ ЗначениеЗаполнено(ДатаДоставки) Тогда
		ДатаДоставки = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Прочитано
		И НЕ ЗначениеЗаполнено(ДатаПрочтения) Тогда
		ДатаПрочтения = ТекущаяДатаСеанса();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Идентификатор) Тогда
		Идентификатор = Строка(Новый УникальныйИдентификатор());
	КонецЕсли;

	НачатьТранзакцию();
	Попытка
		
		НовоеСообщение = РегистрыСведений.уатЧатыСВодителями.СоздатьМенеджерЗаписи();
		НовоеСообщение.ВидСообщения   = ВидСообщения;
		НовоеСообщение.Отправитель    = Отправитель;
		НовоеСообщение.Идентификатор  = Идентификатор;
		НовоеСообщение.ТекстСообщения = ТекстСообщения;
		НовоеСообщение.Дата           = Дата;
		НовоеСообщение.Чат            = Чат;
		НовоеСообщение.Отправлено     = Отправлено;
		
		Попытка
			НовоеСообщение.Записать(Истина);
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ОписаниеОшибки();
			Возврат Ложь;
		КонецПопытки;
		
		Для Каждого ТекПолучатель Из мсвПолучателей Цикл
			
			НовоеСообщение = РегистрыСведений.уатСтатусыСообщенийМобильногоПриложения.СоздатьМенеджерЗаписи();
			НовоеСообщение.Получатель     = ТекПолучатель;
			НовоеСообщение.Идентификатор  = Идентификатор;
			НовоеСообщение.Доставлено     = Доставлено;
			НовоеСообщение.ДатаДоставки   = ДатаДоставки;
			НовоеСообщение.Прочитано      = Прочитано;
			НовоеСообщение.ДатаПрочтения  = ДатаПрочтения;
			
			Попытка
				НовоеСообщение.Записать(Истина);
			Исключение
				ОтменитьТранзакцию();
				ТекстОшибки = ОписаниеОшибки();
				Возврат Ложь;
			КонецПопытки;
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ТекстОшибки = ТекстОшибки + " " + ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли