
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если флПарольМодифицирован Тогда 
		ТекущийОбъект.Пароль = Пароль;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Пароль = "заглушка";
	
	ОбновитьСписокТССервер();
	
	стрНастройкиМобильногоПриложения = уатМобильноеПриложениеВодителяСерверПовтИсп.ПолучитьНастройкиМобильногоПриложения();
	Элементы.ИспользоватьПутевыеЛисты.Доступность    = стрНастройкиМобильногоПриложения.useWaybills;
	Элементы.ИспользоватьМаршрутныеЛисты.Доступность = стрНастройкиМобильногоПриложения.useRouteLists;
	Элементы.ИспользоватьЗаявкиНаРемонт.Доступность  = стрНастройкиМобильногоПриложения.useOrdersForRepair;
	Элементы.ИспользоватьЧат.Доступность             = стрНастройкиМобильногоПриложения.useChat;
	
	Если Запись.Логин = "" Тогда
		Если Элементы.ИспользоватьПутевыеЛисты.Доступность Тогда
			Запись.ИспользоватьПутевыеЛисты = стрНастройкиМобильногоПриложения.useWaybills;
		КонецЕсли;
		Если Элементы.ИспользоватьМаршрутныеЛисты.Доступность Тогда
			Запись.ИспользоватьМаршрутныеЛисты = стрНастройкиМобильногоПриложения.useRouteLists;
		КонецЕсли;
		Если Элементы.ИспользоватьЗаявкиНаРемонт.Доступность Тогда
			Запись.ИспользоватьЗаявкиНаРемонт = стрНастройкиМобильногоПриложения.useOrdersForRepair;
		КонецЕсли;
		Если Элементы.ИспользоватьЧат.Доступность Тогда
			Запись.ИспользоватьЧат = стрНастройкиМобильногоПриложения.useChat;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьПользователиМобильногоПриложения_ТС(ТекущийОбъект.Пользователь);
	уатМобильноеПриложениеВодителяСервер.ОбновлениеПользователейМобильногоПриложения();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	НомерСтроки = 1;
	Для Каждого ТекСтрока Из РазрешенныеТС Цикл
		Если НЕ ЗначениеЗаполнено(ТекСтрока.ТС) Тогда
			ТекстНСТР = НСтр("ru='Не заполнена колонка ""Транспортное средство"" в строке %1 списка ""Разрешенные ТС""'");
			ТекстСообщения = СтрШаблон(ТекстНСТР, НомерСтроки);
			уатОбщегоНазначенияСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения,"РазрешенныеТС",НомерСтроки, "ТС", Отказ);
			НомерСтроки = НомерСтроки + 1;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьПутевыеЛистыПриИзменении(Элемент)
	Если (Запись.ИспользоватьПутевыеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьПутевыеЛисты.Доступность)
		И (Запись.ИспользоватьМаршрутныеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьМаршрутныеЛисты.Доступность)
		И (Запись.ИспользоватьЗаявкиНаРемонт = Ложь ИЛИ НЕ Элементы.ИспользоватьЗаявкиНаРемонт.Доступность)
		И (Запись.ИспользоватьЧат = Ложь ИЛИ НЕ Элементы.ИспользоватьЧат.Доступность) Тогда
		Запись.ИспользоватьПутевыеЛисты = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЗаявкиНаРемонтПриИзменении(Элемент)
	Если (Запись.ИспользоватьПутевыеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьПутевыеЛисты.Доступность)
		И (Запись.ИспользоватьМаршрутныеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьМаршрутныеЛисты.Доступность)
		И (Запись.ИспользоватьЗаявкиНаРемонт = Ложь ИЛИ НЕ Элементы.ИспользоватьЗаявкиНаРемонт.Доступность)
		И (Запись.ИспользоватьЧат = Ложь ИЛИ НЕ Элементы.ИспользоватьЧат.Доступность) Тогда
		Запись.ИспользоватьЗаявкиНаРемонт = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЧатПриИзменении(Элемент)
	Если (Запись.ИспользоватьПутевыеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьПутевыеЛисты.Доступность)
		И (Запись.ИспользоватьМаршрутныеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьМаршрутныеЛисты.Доступность)
		И (Запись.ИспользоватьЗаявкиНаРемонт = Ложь ИЛИ НЕ Элементы.ИспользоватьЗаявкиНаРемонт.Доступность)
		И (Запись.ИспользоватьЧат = Ложь ИЛИ НЕ Элементы.ИспользоватьЧат.Доступность) Тогда
		Запись.ИспользоватьЧат = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьМаршрутныеЛистыПриИзменении(Элемент)
	Если (Запись.ИспользоватьПутевыеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьПутевыеЛисты.Доступность)
		И (Запись.ИспользоватьМаршрутныеЛисты = Ложь ИЛИ НЕ Элементы.ИспользоватьМаршрутныеЛисты.Доступность)
		И (Запись.ИспользоватьЗаявкиНаРемонт = Ложь ИЛИ НЕ Элементы.ИспользоватьЗаявкиНаРемонт.Доступность)
		И (Запись.ИспользоватьЧат = Ложь ИЛИ НЕ Элементы.ИспользоватьЧат.Доступность) Тогда
		Запись.ИспользоватьМаршрутныеЛисты = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	флПарольМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	ОбновитьСписокТССервер();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписокТС(Команда)
	ОбновитьСписокТССервер();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокТССервер()
	
	РазрешенныеТС.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Пользователь", Запись.Пользователь);
	Запрос.Текст = "ВЫБРАТЬ
	               |	уатПользователиМобильногоПриложения_ТС.ТС КАК ТС
	               |ИЗ
	               |	РегистрСведений.уатПользователиМобильногоПриложения_ТС КАК уатПользователиМобильногоПриложения_ТС
	               |ГДЕ
	               |	уатПользователиМобильногоПриложения_ТС.Пользователь = &Пользователь";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = РазрешенныеТС.Добавить();
		НоваяСтрока.ТС = Выборка.ТС;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПользователиМобильногоПриложения_ТС(Пользователь)
	
	ПользователиМобильногоПриложения = РегистрыСведений.уатПользователиМобильногоПриложения_ТС.СоздатьНаборЗаписей();
	ПользователиМобильногоПриложения.Отбор.Пользователь.Установить(Пользователь);
	ПользователиМобильногоПриложения.Прочитать();

	Если ПользователиМобильногоПриложения.Количество() <> 0 Тогда
		ПользователиМобильногоПриложения.Очистить();
	КонецЕсли;

	МассивТС = Новый Массив();
	Для Каждого ТекСтрока Из РазрешенныеТС Цикл
		Если МассивТС.Найти(ТекСтрока.ТС) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = ПользователиМобильногоПриложения.Добавить();
		НоваяЗапись.Пользователь = Пользователь;
		НоваяЗапись.ТС			 = ТекСтрока.ТС;
		
		МассивТС.Добавить(ТекСтрока.ТС);
	КонецЦикла;
	
	Попытка
		ПользователиМобильногоПриложения.Записать();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

#КонецОбласти
