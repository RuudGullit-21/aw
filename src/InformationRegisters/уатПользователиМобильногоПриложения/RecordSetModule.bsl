#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	ОбновитьПредставлениеВодителейМобУстройств();
	ВключитьВодителяВЧат();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПредставлениеВодителейМобУстройств()
	
	ОбновляемыеВодители = Новый Массив();
	Если ЗначениеЗаполнено(Отбор.Пользователь.Значение) Тогда 
		ОбновляемыеВодители.Добавить(Отбор.Пользователь.Значение);
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл 
		Если ЗначениеЗаполнено(Запись.Пользователь) Тогда 
			ОбновляемыеВодители.Добавить(Запись.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ОбновляемыеВодители", ОбновляемыеВодители);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	уатМобильныеУстройстваВодители.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.уатМобильныеУстройства.УдалитьВодители КАК уатМобильныеУстройстваВодители
	|ГДЕ
	|	уатМобильныеУстройстваВодители.ФизическоеЛицо В(&ОбновляемыеВодители)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		СпрОб = Выборка.Ссылка.ПолучитьОбъект();
		СпрОб.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура ВключитьВодителяВЧат()
	
	Пользователь    = Неопределено;
	Если ЗначениеЗаполнено(Отбор.Пользователь.Значение) Тогда 
		Пользователь = Отбор.Пользователь.Значение;
	Иначе
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	уатЧатыВодители.Водитель КАК Водитель,
	               |	уатЧатыВодители.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.уатЧаты.Водители КАК уатЧатыВодители
	               |ГДЕ
	               |	НЕ уатЧатыВодители.Ссылка.ПометкаУдаления
	               |	И уатЧатыВодители.Водитель = &Пользователь";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда 
		СпрОб = Справочники.уатЧаты.ОбщийЧат.ПолучитьОбъект();
		НовыйВодитель = СпрОб.Водители.Добавить();
		НовыйВодитель.Водитель = Пользователь;
		СпрОб.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли