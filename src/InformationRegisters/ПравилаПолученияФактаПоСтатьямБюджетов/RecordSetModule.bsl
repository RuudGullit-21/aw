#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
 
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("ИсточникСуммыОперации");
	МассивНепроверяемыхРеквизитов.Добавить("ТипИтога");
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПравил.РазделИсточникаДанных,
	|	ТаблицаПравил.ИсточникСуммыОперации,
	|	ТаблицаПравил.ТипИтога
	|ПОМЕСТИТЬ ТаблицаПравил
	|ИЗ
	|	&ТаблицаПравил КАК ТаблицаПравил
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ТаблицаПравил.РазделИсточникаДанных = ЗНАЧЕНИЕ(Перечисление.РазделыИсточниковДанныхБюджетирования.ОперативныйУчет)
	|				И ТаблицаПравил.ИсточникСуммыОперации = ЗНАЧЕНИЕ(Перечисление.ПоказателиАналитическихРегистров.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаполненИсточникСуммыОперации,
	|	ВЫБОР
	|		КОГДА (ТаблицаПравил.РазделИсточникаДанных = ЗНАЧЕНИЕ(Перечисление.РазделыИсточниковДанныхБюджетирования.РегламентированныйУчет)
	|				ИЛИ ТаблицаПравил.РазделИсточникаДанных = ЗНАЧЕНИЕ(Перечисление.РазделыИсточниковДанныхБюджетирования.МеждународныйУчет))
	|				И ТаблицаПравил.ТипИтога = ЗНАЧЕНИЕ(Перечисление.ТипыИтогов.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаполненТипИтога
	|ИЗ
	|	ТаблицаПравил КАК ТаблицаПравил";
	Запрос.УстановитьПараметр("ТаблицаПравил", ЭтотОбъект.Выгрузить(,"РазделИсточникаДанных, ИсточникСуммыОперации, ТипИтога"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.НеЗаполненИсточникСуммыОперации Тогда
			ТекстНСТР = НСтр("en='Поле Источник суммы не заполнено';ru='Поле Источник суммы не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР, ,"ИсточникСуммыОперации", "Запись", Отказ);
		КонецЕсли;
		Если Выборка.НеЗаполненТипИтога Тогда
			ТекстНСТР = НСтр("en='Поле Тип итога не заполнено';ru='Поле Тип итога не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР, ,"ТипИтога", "Запись", Отказ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
