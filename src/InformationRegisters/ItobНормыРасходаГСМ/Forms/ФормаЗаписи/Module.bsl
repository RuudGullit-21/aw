
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МодельТСПриИзменении(Элемент)
	
	УстановитьВидимость();
		
	ОсновноеТопливо = ПолучитьСвойстваМодели().ОсновноеТопливо;	
	Если ЗначениеЗаполнено(ОсновноеТопливо) Тогда
		Запись.ГСМ = ОсновноеТопливо;
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Необходимо в свойствах модели ТС указать основное топливо";
		Сообщение.Сообщить(); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ООВыборГСМИзСписка = Новый ОписаниеОповещения("ОбработатьВыборГСМ", ЭтотОбъект);
	ПоказатьВыборИзСписка(ООВыборГСМИзСписка, СписокВыбораГСМ());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборГСМ(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		Запись.ГСМ = ВыбранныйЭлемент.Значение;	
	КонецЕсли;
	
КонецПроцедуры
 
// Возвращает свойства модели ТС.
//
&НаСервере
Функция ПолучитьСвойстваМодели()
	
	ПустаяНоменклатура = Справочники[ItobВызовСервераПовтИсп.ПолучитьИмяОбъекта("Номенклатура")].ПустаяСсылка();	
	
	Результат = Новый Структура("НаличиеСпидометра, ОсновноеТопливо, ДополнительноеТопливо", Ложь, ПустаяНоменклатура, ПустаяНоменклатура);
	Если ОбщегоНазначения.ПодсистемаСуществует("ItobЦентрСпутниковогоМониторинга.УчетГСМ") Тогда
		МодульГСМ = ОбщегоНазначения.ОбщийМодуль("ItobУчетГСМ_ВызовСервера");
		Результат = МодульГСМ.ПолучитьСвойстваМоделиТранспортногоСредства(Запись.МодельТС);	
	Иначе
		Результат.НаличиеСпидометра =  Запись.МодельТС.НаличиеСпидометра;
		Результат.ОсновноеТопливо   =  Запись.МодельТС.ОсновноеТопливо;
		Результат.ДополнительноеТопливо   =  Запись.МодельТС.ДополнительноеТопливо;
	КонецЕсли; 
		
	Возврат Результат;

КонецФункции
 
&НаКлиенте
Процедура УстановитьВидимость()
	
	НаличиеСпидометра = ПолучитьСвойстваМодели().НаличиеСпидометра;
	Элементы.НормаНаРаботуСпецОборудованияПоПробегу.Видимость = НаличиеСпидометра;
	Элементы.НормаНаРаботуСпецОборудованияПоМЧ.Видимость = Не НаличиеСпидометра;	
	
КонецПроцедуры   

// Заполнение списка выбора у поля "ГСМ".
//
&НаКлиенте
Функция СписокВыбораГСМ()
	
	ДопустимоеТопливо = Новый СписокЗначений;
	
	ОсновноеТопливо = ПолучитьСвойстваМодели().ОсновноеТопливо;
	ДополнительноеТопливо = ПолучитьСвойстваМодели().ДополнительноеТопливо;	
	Если ЗначениеЗаполнено(ОсновноеТопливо) Тогда
		ДопустимоеТопливо.Добавить(ОсновноеТопливо);	
	КонецЕсли;	
	Если ЗначениеЗаполнено(ДополнительноеТопливо) Тогда
		ДопустимоеТопливо.Добавить(ДополнительноеТопливо);
	КонецЕсли;
	
	
	Возврат ДопустимоеТопливо;
	
КонецФункции
 
#КонецОбласти
