
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Попытка
		Регистратор = ЭтотОбъект.Отбор.Регистратор.Значение;
	Исключение
		Возврат;
	КонецПопытки;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не Запись.ТерминалУстановлен Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ItobПривязкиТрекеровСрезПоследних.Регистратор КАК Регистратор,
		|	ItobПривязкиТрекеровСрезПоследних.Терминал КАК Терминал
		|ИЗ
		|	РегистрСведений.ItobПривязкиТрекеров.СрезПоследних(&Период, Объект = &Объект) КАК ItobПривязкиТрекеровСрезПоследних
		|ГДЕ
		|	ItobПривязкиТрекеровСрезПоследних.ТерминалУстановлен
		|	И НЕ ItobПривязкиТрекеровСрезПоследних.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ItobПривязкиТрекеровСрезПоследних.Регистратор КАК Регистратор,
		|	ItobПривязкиТрекеровСрезПоследних.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.ItobПривязкиТрекеров.СрезПоследних(&Период, Терминал = &Терминал) КАК ItobПривязкиТрекеровСрезПоследних
		|ГДЕ
		|	ItobПривязкиТрекеровСрезПоследних.ТерминалУстановлен
		|	И НЕ ItobПривязкиТрекеровСрезПоследних.Регистратор = &Регистратор
		|	И НЕ &ЭтоПеремещение";
		
		Запрос.УстановитьПараметр("Объект", Запись.Объект);
		Запрос.УстановитьПараметр("Терминал", Запись.Терминал);
		Запрос.УстановитьПараметр("Регистратор", Регистратор);
		Запрос.УстановитьПараметр("Период", Запись.Период);
		// Для перемещения сначала происходит отвязка терминала, поэтому проверка на привязку не нужна.
		Запрос.УстановитьПараметр("ЭтоПеремещение", ТипЗнч(Регистратор) = Тип("ДокументСсылка.ItobПеремещениеМобильногоУстройства"));
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		// Запрещаем делать привязку если к объекту уже привязан терминал.
		Выборка = РезультатЗапроса[0].Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщения = "К объекту " + Запись.Объект + " уже привязан терминал " + Выборка.Терминал + Символы.ПС
				+ "Документ регистрации: " + Выборка.Регистратор;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
		КонецЦикла;
		
		// Запрещаем делать привязку если терминал уже привязан.
		Выборка = РезультатЗапроса[1].Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщения = "Терминал " + Запись.Терминал + " уже привязан к объекту " + Выборка.Объект + Символы.ПС
				+ "Документ регистрации: " + Выборка.Регистратор;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти