// {УАТ}
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;
 	
	СтарыйНабор = РегистрыСведений.РеестрЭПД.СоздатьНаборЗаписей();
	Для Каждого ЭлементОтбора Из Отбор Цикл
		Если ЭлементОтбора.Использование Тогда
			СтарыйНабор.Отбор[ЭлементОтбора.Имя].Установить(ЭлементОтбора.Значение);
		КонецЕсли;
	КонецЦикла;
	СтарыйНабор.Прочитать();
	
	СоответвиеСостояний = Новый Соответствие();
	Для Каждого СтрокаНабора Из СтарыйНабор Цикл  
		Если ТипЗнч(СтрокаНабора.Ссылка) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда 
			СоответвиеСостояний.Вставить(СтрокаНабора.Ссылка, Новый Структура("Выполнен, Состояние", 
				СтрокаНабора.ТекущийШагВыполнен, СтрокаНабора.ТекущийШаг));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл  
		Если ТипЗнч(СтрокаНабора.Ссылка) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
			СоответвиеСостояний.Вставить(СтрокаНабора.Ссылка, Новый Структура("Выполнен, Состояние", 
				СтрокаНабора.ТекущийШагВыполнен, СтрокаНабора.ТекущийШаг));
		КонецЕсли;
	КонецЦикла;  
	
	ДополнительныеСвойства.Вставить("СоответвиеСостояний", СоответвиеСостояний);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат; 
	КонецЕсли;
	
	Для Каждого ТекДокумент Из ДополнительныеСвойства.СоответвиеСостояний Цикл
		ЭПЛ       = ТекДокумент.Ключ;
		Состояние = ТекДокумент.Значение.Состояние;
		Выполнен  = ТекДокумент.Значение.Выполнен;
		
		Для Каждого ТекПЛ Из ЭПЛ.ДокументыОснования Цикл
			Если Выполнен Тогда
				СостояниеПодписей = Перечисления.уатСостоянияТранспортныхДокументов.Подписан;
			Иначе
				СостояниеПодписей = Перечисления.уатСостоянияТранспортныхДокументов.Создан;
			КонецЕсли;

			ВидПодписи = Неопределено;	
			Если Состояние = "Оформление" Тогда  
				ВидПодписи = "ДиспетчерПредрейсовый";
			ИначеЕсли Состояние = "Контроль транспорта" Тогда 
				ВидПодписи = "КонтролерПредрейсовый";
			ИначеЕсли Состояние = "Медосмотр" Тогда
				ВидПодписи = "МедработникПредрейсовый";
			ИначеЕсли Состояние = "Показания (выезд)" Тогда
				ВидПодписи = "УполномоченныйНаПроставлениеОдометраПредрейсовый";
			ИначеЕсли Состояние = "Показания (заезд)" Тогда
				ВидПодписи = "УполномоченныйНаПроставлениеОдометраПослерейсовый";
			ИначеЕсли Состояние = "Медосмотр (после рейса)" Тогда
				ВидПодписи = "МедработникПослерейсовый";
			КонецЕсли;
			
			уатЗащищенныеФункцииСервер_проф.ОбновитьСостояниеПодписейЖурналы(ТекПЛ.ДокументОснование, ВидПодписи, СостояниеПодписей);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
	
#КонецЕсли
// {/УАТ}