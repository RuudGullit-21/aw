
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	уатЗащищенныеФункцииСервер.уатРегистрФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка,ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатРегистрФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка	 = Ложь;
	СписокГруппДляОтбора	 = Новый СписокЗначений();
	СписокГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Топливо"));
	СписокГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.ПрисадкиИТехническиеЖидкости"));
	
	ДополнительныеПараметры	 = Новый Структура("ЗначениеГСМДоИзменения, КлючУникальности", Запись.ГСМ, Новый УникальныйИдентификатор);
	ОписаниеОповещенияЗакр	 = Новый ОписаниеОповещения("ОписаниеОповещенияВыбораГСМ", ЭтотОбъект, ДополнительныеПараметры);
	уатЗащищенныеФункцииКлиент.ВыбратьГСМ(СписокГруппДляОтбора,ДополнительныеПараметры, ОписаниеОповещенияЗакр);
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвГруппДляОтбора = Новый Массив;
	мсвГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Топливо"));
	мсвГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.ПрисадкиИТехническиеЖидкости"));

	ДанныеВыбора = уатГСМ.ПолучитьСписокАвтоподбораПоляГСМ(Текст, мсвГруппДляОтбора);
КонецПроцедуры

&НаКлиенте
Процедура ГСМОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ПараметрыПолученияДанных.СтрокаПоиска = "";
КонецПроцедуры

&НаКлиенте
Процедура АналогНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекСтруктураГСМ = ПолучитьЗначениеГСМ(Запись.ГСМ);
	
	ДополнительныеПараметры	 = Новый Структура("ЗначениеГСМДоИзменения, КлючУникальности", Запись.Аналог, Новый УникальныйИдентификатор);
	ОписаниеОповещенияЗакр	 = Новый ОписаниеОповещения("ОписаниеОповещенияВыборАналогаГСМ", ЭтотОбъект, ДополнительныеПараметры);
	Если ЗначениеЗаполнено(ТекСтруктураГСМ.ГруппаГСМ) Тогда
		уатЗащищенныеФункцииКлиент.ВыбратьГСМ(ТекСтруктураГСМ.ГруппаГСМ, ДополнительныеПараметры, ОписаниеОповещенияЗакр);
	Иначе
		мГруппаГСМ   = Новый СписокЗначений();
		мГруппаГСМ.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Топливо"));
		мГруппаГСМ.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.ПрисадкиИТехническиеЖидкости"));
		уатЗащищенныеФункцииКлиент.ВыбратьГСМ(мГруппаГСМ, ДополнительныеПараметры, ОписаниеОповещенияЗакр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АналогАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвГруппДляОтбора = Новый Массив;
	мсвГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Топливо"));
	мсвГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.ПрисадкиИТехническиеЖидкости"));

	ДанныеВыбора = уатГСМ.ПолучитьСписокАвтоподбораПоляГСМ(Текст, мсвГруппДляОтбора);
КонецПроцедуры

&НаКлиенте
Процедура АналогОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ПараметрыПолученияДанных.СтрокаПоиска = "";
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Подключаемый динамически обработчик оповещения
&НаКлиенте
Процедура ОписаниеОповещенияВыборАналогаГСМ(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Запись.Аналог = Результат;
		Если Запись.Аналог <> ДопПараметры.ЗначениеГСМДоИзменения Тогда 
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Подключаемый динамически обработчик оповещения
&НаКлиенте
Процедура ОписаниеОповещенияВыбораГСМ(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Запись.ГСМ = Результат;
		Если Запись.ГСМ <> ДопПараметры.ЗначениеГСМДоИзменения Тогда 
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеГСМ(ТекГСМ)
	СтруктураГСМ = РегистрыСведений.уатНоменклатураГСМ.Получить(Новый Структура("Номенклатура", ТекГСМ));
	Возврат СтруктураГСМ;
КонецФункции

#КонецОбласти
