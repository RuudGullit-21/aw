#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура формирования движений по регистру "Обороты бюджетов".
//
Процедура ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ) Экспорт

	ТаблицаОборотыБюджетов = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОборотыБюджетов;
	
	Если Отказ ИЛИ ТаблицаОборотыБюджетов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияЛимитыРасходаДенежныхСредств = Движения.ОборотыБюджетов;
	ДвиженияЛимитыРасходаДенежныхСредств.Записывать = Истина;
	ДвиженияЛимитыРасходаДенежныхСредств.Загрузить(ТаблицаОборотыБюджетов);
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// Обработчик обновления УП 2.0.4.6
//
Процедура ЗаполнитьВалюту() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭкземплярБюджета.Сценарий.Валюта КАК Валюта,
		|	ЭкземплярБюджета.Ссылка КАК Регистратор
		|ИЗ
		|	Документ.ЭкземплярБюджета КАК ЭкземплярБюджета";

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		Набор = РегистрыНакопления.ОборотыБюджетов.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Набор.Прочитать();
		Для Каждого СтрокаНабора из Набор Цикл
			СтрокаНабора.Валюта = Выборка.Валюта;
			СтрокаНабора.СуммаВВалюте = СтрокаНабора.СуммаСценария;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
		
		Объект = Выборка.Регистратор.ПолучитьОбъект();
		Для Каждого СтрокаАналитики из Объект.АналитикаСтатейБюджетов Цикл
			СтрокаАналитики.Валюта = Выборка.Валюта;
		КонецЦикла;
		Для Каждого СтрокаОборотов из Объект.ОборотыПоСтатьямБюджетов Цикл
			СтрокаОборотов.СуммаВВалюте = СтрокаОборотов.Сумма;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли