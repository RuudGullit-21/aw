
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВыбиратьИсполнителя = Параметры.ВыбиратьИсполнителя;
	ВыбиратьШагБюджетногоПроцесса = Параметры.ВыбиратьШагБюджетногоПроцесса;
	МодельБюджетирования = Параметры.МодельБюджетирования;
	
	Если Параметры.ФормированиеНесколькихЗадач Тогда
		ЗаголовокФормы = НСтр("en='Формирование связанных задач';ru='Формирование связанных задач'");
		ЗаголовокСформироватьЗадачу = НСтр("en='Сформировать задачи';ru='Сформировать задачи'");
		ЗаголовокНеФормироватьЗадачу = НСтр("en='Не формировать задачи';ru='Не формировать задачи'");
		ЗаголовокГруппаВыборШагаПроцесса = НСтр("en='Создать задачи в рамках';ru='Создать задачи в рамках'")
	Иначе
		ЗаголовокФормы = НСтр("en='Формирование связанной задачи';ru='Формирование связанной задачи'");
		ЗаголовокСформироватьЗадачу = НСтр("en='Сформировать задачу';ru='Сформировать задачу'");
		ЗаголовокНеФормироватьЗадачу = НСтр("en='Не формировать задачу';ru='Не формировать задачу'");
		ЗаголовокГруппаВыборШагаПроцесса = НСтр("en='Создать задачу в рамках';ru='Создать задачу в рамках'")
	КонецЕсли;
	
	ЭтотОбъект.Заголовок = ?(Не ПустаяСтрока(Параметры.ЗаголовокФормы), Параметры.ЗаголовокФормы, ЗаголовокФормы);
	Элементы.СформироватьЗадачу.Заголовок = ЗаголовокСформироватьЗадачу;
	Элементы.НеФормироватьЗадачу.Заголовок = ЗаголовокНеФормироватьЗадачу;
	Элементы.ГруппаВыборШагаПроцесса.Заголовок = ЗаголовокГруппаВыборШагаПроцесса;
	
	Элементы.ГруппаВыборШагаПроцесса.Видимость = ВыбиратьШагБюджетногоПроцесса;
	Элементы.Исполнитель.Видимость = ВыбиратьИсполнителя;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(СрокИсполнения) И НачалоДня(СрокИсполнения) < НачалоДня(ТекущаяДата()) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("en='Срок исполнения задачи не может быть меньше текущей даты';ru='Срок исполнения задачи не может быть меньше текущей даты'"), , , "СрокИсполнения", Отказ);
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВариантВыбораШагаПроцесса = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ШагБюджетногоПроцесса");
	КонецЕсли;
	
	Если Не ВыбиратьИсполнителя Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Исполнитель");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШагаПроцессаТекущийШагПриИзменении(Элемент)
	
	ШагБюджетногоПроцесса = Неопределено;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ШагПроцессаВыбранныйШагПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьЗадачу(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура("Наименование, СрокИсполнения, ОписаниеЗадачи");
	Если ВыбиратьИсполнителя Тогда
		Результат.Вставить("Исполнитель");
	КонецЕсли;
	Если ВыбиратьШагБюджетногоПроцесса Тогда
		Результат.Вставить("ШагБюджетногоПроцесса");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура НеФормироватьЗадачу(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ШагБюджетногоПроцесса.Доступность = (ВариантВыбораШагаПроцесса = 1);
	Элементы.ШагБюджетногоПроцесса.АвтоОтметкаНезаполненного = (ВариантВыбораШагаПроцесса = 1);
	
КонецПроцедуры

#КонецОбласти