
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.уатСерииНоменклатуры) Тогда 
		Элементы.СписокАгрегатовИзменитьВыделенные.Видимость = Ложь;
		Элементы.СписокАгрегатовКонтекстноеМенюИзменитьВыделенные.Видимость = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОстатков, "Дата", ТекущаяДата());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОстатков, "Агрегат", Справочники.уатСерииНоменклатуры.ПустаяСсылка());
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора = Истина Тогда
		Элементы.СписокАгрегатов.РежимВыбора = Истина;
	КонецЕсли;
	
	ОтборПоТипуАгрегатов = "";
	Если Параметры.Свойство("ТипАгрегата") Тогда
		АвтоЗаголовок = Ложь;
		Если Параметры.ТипАгрегата = "Шина" Тогда
			ОтборПоТипуАгрегатов = "Шина";
		ИначеЕсли Параметры.ТипАгрегата = "Аккумулятор" Тогда
			ОтборПоТипуАгрегатов = "Аккумулятор";
		Иначе
			ОтборПоТипуАгрегатов = "ПрочиеАгрегаты";
		КонецЕсли;
	ИначеЕсли Параметры.Отбор.Свойство("ТипАгрегата") И ЗначениеЗаполнено(Параметры.Отбор.ТипАгрегата) Тогда
		АвтоЗаголовок = Ложь;
		Если Параметры.Отбор.ТипАгрегата = Справочники.уатТипыАгрегатов.Шина Тогда
			ОтборПоТипуАгрегатов = "Шина";
		ИначеЕсли Параметры.Отбор.ТипАгрегата = Справочники.уатТипыАгрегатов.Аккумулятор Тогда
			ОтборПоТипуАгрегатов = "Аккумулятор";
		Иначе
			ОтборПоТипуАгрегатов = "ПрочиеАгрегаты";
		КонецЕсли;
	КонецЕсли;
	
	Если ОтборПоТипуАгрегатов = "Шина" Тогда
		Заголовок = НСтр("en='Tires';ru='Шины'");
		ТипАгрегатов = Справочники.уатТипыАгрегатов.Шина;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокАгрегатов, "ТипАгрегата", Справочники.уатТипыАгрегатов.Шина, ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокМоделей, "ТипАгрегата", Справочники.уатТипыАгрегатов.Шина, ВидСравненияКомпоновкиДанных.Равно);
		
	ИначеЕсли ОтборПоТипуАгрегатов = "Аккумулятор" Тогда
		Заголовок = НСтр("en='Batteries';ru='Аккумуляторы'");
		ТипАгрегатов = Справочники.уатТипыАгрегатов.Аккумулятор;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокАгрегатов, "ТипАгрегата", Справочники.уатТипыАгрегатов.Аккумулятор, ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокМоделей, "ТипАгрегата", Справочники.уатТипыАгрегатов.Аккумулятор, ВидСравненияКомпоновкиДанных.Равно);
		
	ИначеЕсли ОтборПоТипуАгрегатов = "ПрочиеАгрегаты" Тогда
		Заголовок = НСтр("en='Other car parts';ru='Прочие агрегаты'");
		СписокТиповАгрегата = Новый Массив;
		СписокТиповАгрегата.Добавить(Справочники.уатТипыАгрегатов.Аккумулятор);
		СписокТиповАгрегата.Добавить(Справочники.уатТипыАгрегатов.Шина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокАгрегатов, "ТипАгрегата", СписокТиповАгрегата, ВидСравненияКомпоновкиДанных.НеВСписке);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокМоделей, "ТипАгрегата", СписокТиповАгрегата, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокМоделей

&НаКлиенте
Процедура СписокМоделейПриАктивизацииСтроки(Элемент)
	ТекСтрока = Элементы.СписокМоделей.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		уатОбщегоНазначенияКлиент.УстановитьОтборУСписка(СписокАгрегатов.Отбор, Новый ПолеКомпоновкиДанных("Модель"), 
			Неопределено,, Ложь);
	Иначе
		уатОбщегоНазначенияКлиент.УстановитьОтборУСписка(СписокАгрегатов.Отбор, Новый ПолеКомпоновкиДанных("Модель"), 
			ТекСтрока.Ссылка, ВидСравненияКомпоновкиДанных.ВИерархии);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокАгрегатов

&НаКлиенте
Процедура СписокАгрегатовПриАктивизацииСтроки(Элемент)
	ТекСтрока = Элементы.СписокАгрегатов.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено ИЛИ НЕ ТекСтрока.Свойство("Ссылка") Тогда
		АгрегатСсылка = ПредопределенноеЗначение("Справочник.уатСерииНоменклатуры.ПустаяСсылка");
	Иначе
		АгрегатСсылка = ТекСтрока.Ссылка;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОстатков, "Агрегат", АгрегатСсылка);
КонецПроцедуры

&НаКлиенте
Процедура СписокАгрегатовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") И ВыбранноеЗначение.Количество() > 0 Тогда
		Элементы.СписокАгрегатов.Обновить();
		Элементы.СписокАгрегатов.ТекущаяСтрока = ВыбранноеЗначение[0].Значение;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПакетныйВводАгрегатов(Команда)
	ПараметрыОткрытия = Новый Структура;
	Если ЗначениеЗаполнено(ТипАгрегатов) Тогда
		ПараметрыОткрытия.Вставить("Отбор", Новый Структура("ТипАгрегата", ТипАгрегатов));
	КонецЕсли;
	
	ОткрытьФорму("Обработка.уатПакетныйВводАгрегатов.Форма", ПараметрыОткрытия, Элементы.СписокАгрегатов);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.СписокАгрегатов, СписокАгрегатов);
	
КонецПроцедуры

#КонецОбласти
