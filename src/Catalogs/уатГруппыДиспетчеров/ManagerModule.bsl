#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Функция выполняет создание служебного обсуждения
//
// Параметры:
//  ГруппаДиспетчеров	 - СпраочникСсылка - Группа диспетчеров
// 
// Возвращаемое значение:
//   - Строка - Идентификатор обсуждения
//
Функция СоздатьСлужебноеОбсуждение(ГруппаДиспетчеров) Экспорт
	
	ИдентификаторОбсуждения = ""; 
	Если НЕ Обсуждения.ОбсужденияДоступны() Тогда
		Возврат ИдентификаторОбсуждения;
	Конецесли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
		Обсуждение.Заголовок    = СтрШаблон(Нстр("ru = 'Новые сообщения группы диспетчеров ""%1""'"), ГруппаДиспетчеров.Наименование);
		Обсуждение.Групповое    = Истина;
		Обсуждение.Отображаемое = Истина;
		
		КонтекстОбсуждения = Новый КонтекстОбсужденияСистемыВзаимодействия(ПолучитьНавигационнуюСсылку(ГруппаДиспетчеров));
		Обсуждение.КонтекстОбсуждения  = КонтекстОбсуждения;
		Обсуждение.Записать();
		
		ИдентификаторОбсуждения = Обсуждение.Идентификатор;	
	Исключение
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	Возврат ИдентификаторОбсуждения;
КонецФункции

// Процедура выполняет отправку служебного сообщения
//
// Параметры:
//  ГруппаДиспетчеров	 - 	СправочникСсылка - Группа диспетчеров
//  СодержаниеСообщения	 - 	Строка - Текст сообщения
//  Автор				 - 	СправочникСсылка - Пользователь-автор сообщения
//
Процедура ОтправитьСообщениеВСлужебноеОбсуждение(ГруппаДиспетчеров, СодержаниеСообщения = "", Автор = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ГруппаДиспетчеров.ИдентификаторОбсуждения) Тогда
		СправочникОбъект = ГруппаДиспетчеров.ПолучитьОбъект();
		СправочникОбъект.ИдентификаторОбсуждения = Справочники.уатГруппыДиспетчеров.СоздатьСлужебноеОбсуждение(ГруппаДиспетчеров);
		Попытка
			СправочникОбъект.Записать();
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторОбсуждения = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ГруппаДиспетчеров.ИдентификаторОбсуждения);
	
	Сообщение = СистемаВзаимодействия.СоздатьСообщение(ИдентификаторОбсуждения);
	Если ЗначениеЗаполнено(Автор) Тогда
		ИдентификаторПользователя = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(Автор.ИдентификаторПользователяИБ);
		Сообщение.Автор = ИдентификаторПользователя;
	КонецЕсли;
	Сообщение.Текст = СодержаниеСообщения;
	
	Для Каждого ТекДиспетчер Из ГруппаДиспетчеров.Диспетчеры Цикл
		Диспетчер = ТекДиспетчер.Диспетчер;
		Если НЕ ЗначениеЗаполнено(Диспетчер) Тогда
			Продолжить;
		КонецЕсли;
		ИдентификаторПользователя = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(Диспетчер.ИдентификаторПользователяИБ);
		Сообщение.Получатели.Добавить(ИдентификаторПользователя); 
	КонецЦикла;
	Сообщение.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли