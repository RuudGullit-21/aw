
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Обработчик = Новый ОписаниеОповещения("ЗагрузитьКартыЗавершение", ЭтотОбъект);
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("Заголовок", Нстр("ru = 'Выберите АЗС'"));
	ПараметрыОткрытияФормы.Вставить("Отбор", Новый Структура("ВидЗагрузкиДанныхОтПЦ",
		ПредопределенноеЗначение("Перечисление.уатВидыЗагрузкиДанныхОтПЦ.АвтоматическаяЗагрузка")));
		
	ОткрытьФорму("Справочник.уатАЗС.ФормаВыбора", ПараметрыОткрытияФормы, ПараметрыВыполненияКоманды.Источник,,,,Обработчик);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьКартыЗавершение(АЗС, ДополнительныеПараметры) Экспорт
	
	Если АЗС = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Загружено			 = 0;
	ТекстОшибки			 = "";
	
	УчетнаяЗаписьПЦ		 = ЗначениеРеквизитаОбъекта(АЗС, "УчетнаяЗаписьПЦ");
	СтруктураПараметровУчетнойЗаписи = уатЗагрузкаПЦ.ПолучитьСтруктуруПараметровДляРаботыСПЦ(УчетнаяЗаписьПЦ);
	СтруктураПараметровУчетнойЗаписи.Вставить("АЗС", АЗС);

	Если СтруктураПараметровУчетнойЗаписи.ВнешняяСистема = ПредопределенноеЗначение("Справочник.уатВнешниеСистемы.ППР") Тогда
		ТекстНСТР = НСтр("ru='Загрузка топливных карт осуществляется для внешних систем: Газпромнефть; Лукойл, Роснефть'");
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР);
		Возврат;
	КонецЕсли;
		
	уатЗагрузкаПЦ.ЗагрузитьТопливныеКарты(СтруктураПараметровУчетнойЗаписи, Загружено, ТекстОшибки);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Если Загружено = 0 Тогда
		ТекстСообщения = НСтр("ru='Загрузка топливных карт завершена.';en='Download is complete.'");
	Иначе
		ТекстСообщения = НСтр("ru='Загрузка топливных карт выполнена успешно.'");
	КонецЕсли;
	
	Если Загружено > 0 Тогда
		ТекстСообщения = ТекстСообщения + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Загружено новых: %1.';en='Imported new: %1.'"), Загружено);
	Иначе
		ТекстСообщения = ТекстСообщения + Символы.ПС + НСтр("ru='Изменений нет.';en='There are no changes.'");
	КонецЕсли;

	ПоказатьОповещениеПользователя(НСтр("ru='Загрузка топливных карт.'"),,ТекстСообщения);
	ОповеститьОбИзменении(Тип("СправочникСсылка.уатПластиковыеКарты"));

КонецПроцедуры

&НаСервере
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
КонецФункции

#КонецОбласти