
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АдресИконки = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Иконка");
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(АдресИконки) Тогда
		ТекущийОбъект.Иконка = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресИконки));
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресИконкиНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	НачатьВыборКартинки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	НачатьВыборКартинки();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьВыборКартинки()
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор = Ложь;
	ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла для иконки'");
	ВыборФайла.Фильтр = НСтр("ru = 'Формат PNG'") + " (*.png)|*.png";
	
	ОписаниеОповещенияНачалаПомещения = Новый ОписаниеОповещения("ОбработчикОкончанияПомещения", ЭтотОбъект);
	НачатьПомещениеФайла(ОписаниеОповещенияНачалаПомещения, ,ВыборФайла, Истина, ЭтотОбъект.УникальныйИдентификатор);										
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОкончанияПомещения(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
        АдресИконки = Адрес;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
