
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("Сотрудник") Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудник = Параметры.Сотрудник;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Сотрудник",      Параметры.Сотрудник);
	Запрос.УстановитьПараметр("ФизическоеЛицо", Параметры.Сотрудник.ФизическоеЛицо);
	Запрос.УстановитьПараметр("ТекущаяДата",    ТекущаяДата());
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Сотрудники.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ втСотрудники
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |ГДЕ
	               |	Сотрудники.ФизическоеЛицо = &ФизическоеЛицо
	               |	И Сотрудники.Ссылка <> &Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	уатРегистрационныеДокументы.Ссылка КАК Ссылка,
	               |	уатРегистрационныеДокументы.Наименование КАК Наименование,
	               |	уатРегистрационныеДокументы.ВидДокумента КАК ВидДокумента,
	               |	уатРегистрационныеДокументы.ДатаВыдачи КАК ДатаВыдачи,
	               |	уатРегистрационныеДокументы.ДатаОкончания КАК ДатаОкончания,
	               |	уатРегистрационныеДокументы.КемВыдан КАК КемВыдан,
	               |	уатРегистрационныеДокументы.Номер КАК Номер,
	               |	уатРегистрационныеДокументы.Серия КАК Серия
	               |ИЗ
	               |	Справочник.уатРегистрационныеДокументы КАК уатРегистрационныеДокументы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСотрудники КАК втСотрудники
	               |		ПО уатРегистрационныеДокументы.ВладелецДокументов = втСотрудники.Ссылка
	               |ГДЕ
	               |	НЕ уатРегистрационныеДокументы.ПометкаУдаления
	               |	И (уатРегистрационныеДокументы.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ИЛИ уатРегистрационныеДокументы.ДатаОкончания > &ТекущаяДата)";  
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = РегДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
	Иначе
		Отказ = Истина;
		ТекстНСТР = НСтр("ru='Регистрационные документы не обнаружены'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РегДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) 
	ТекущиеДанные = Элементы.РегДокументы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, ТекущиеДанные.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого ТекПодпись Из РегДокументы Цикл
		ТекПодпись.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого ТекПодпись Из РегДокументы Цикл
		ТекПодпись.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокументы(Команда)
	ЗаписатьДокументыСервер();
	Оповестить("РегистрационныеДокументы_Запись");
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьДокументыСервер()
	Для Каждого ТекСтрока Из РегДокументы Цикл
		Если НЕ ТекСтрока.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		СведенияВодитель = уатОбщегоНазначения.уатПрочитатьРеквизитыВодителя(Сотрудник,
			ТекущаяДатаСеанса());

		НовыйОбъект = ТекСтрока.Ссылка.Скопировать();
		НовыйОбъект.Статус             = Перечисления.уатСтатусыДействия.Действует;
		НовыйОбъект.ВладелецДокументов = Сотрудник; 
		НовыйОбъект.Организация   = СведенияВодитель.Организация;
		НовыйОбъект.Подразделение = СведенияВодитель.ПодразделениеОрганизации; 
		НовыйОбъект.Наименование  = уатНастройкиШаблонов.СформироватьНаименованиеПоШаблону("Справочник_уатРегистрационныеДокументы", НовыйОбъект);
		НовыйОбъект.Записать();
		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
