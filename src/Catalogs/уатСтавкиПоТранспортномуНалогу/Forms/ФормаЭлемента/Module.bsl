
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораРегиона(Элементы.ОКТМО.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьНаименование(Команда)
	
	СформироватьНаименованиеСервер();
	Модифицированность  = Истина;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораРегиона(СписокРегионов)
	
	СписокРегионов.Очистить();
	
	Макет = Справочники.уатСтавкиПоТранспортномуНалогу.ПолучитьМакет("КодыОКТМОСубъектовРФ");
	КодыОКТМОСубъектовРФ = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	
	ТаблицаРегионов = АдресныйКлассификатор.СубъектыРФ();
	
	ШаблонПредставления = НСтр("ru='%1 %2'"); //например: "Москва г"
	Для Каждого Регион Из ТаблицаРегионов Цикл
		
		СтрокаСоответствия = КодыОКТМОСубъектовРФ.Найти(Строка(Регион.КодСубъектаРФ), "КодСубъектаРФ");
		
		Если СтрокаСоответствия <> Неопределено Тогда
		
			Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
				Регион.Наименование,
				Регион.Сокращение);
			СписокРегионов.Добавить(СтрокаСоответствия.КодОКТМО, Представление);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Сортируем по наименованию региона
	СписокРегионов.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервере
Функция СформироватьНаименованиеСервер()

	Объект.Наименование = Справочники.уатСтавкиПоТранспортномуНалогу.НаименованиеПоУмолчанию(
		Объект.Период, Объект.ОКТМО, Объект.Вид,
		Объект.ЛСилОт, Объект.ЛСилДо,
		Объект.МинимальноеКоличествоЛетСГодаВыпускаТС, Объект.МаксимальноеКоличествоЛетСГодаВыпускаТС);
	
КонецФункции

#КонецОбласти
