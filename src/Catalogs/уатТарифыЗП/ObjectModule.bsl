
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если НЕ ЭтоГруппа 
		И ВидНачисления.СпособРасчетаОплатыТруда = Перечисления.уатСпособыРасчетаОплатыТруда.СдельныйЗаработок Тогда
		ПроверяемыеРеквизиты.Добавить("ПараметрВыработки");
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если ЗначениеЗаполнено(ГруппировкаТарифа1)
			И ТипЗнч(ГруппировкаТарифа1) = Тип("СправочникСсылка.уатПараметрыВыработки")
			И Не ГруппировкаТарифа1.ДействуетНаСотрудников Тогда
			ТекстНСТР = СтрШаблон(НСтр("en='Output parameter %1 is not valid for employees.';ru='Параметр выработки %1 не действует на сотрудников. Расчет группировок по нему выполняться не будет'"), ГруппировкаТарифа1);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		КонецЕсли;
		Если ЗначениеЗаполнено(ГруппировкаТарифа2)
			И ТипЗнч(ГруппировкаТарифа2) = Тип("СправочникСсылка.уатПараметрыВыработки")
			И Не ГруппировкаТарифа2.ДействуетНаСотрудников Тогда
			ТекстНСТР = СтрШаблон(НСтр("en='Output parameter %1 is not valid for employees.';ru='Параметр выработки %1 не действует на сотрудников. Расчет группировок по нему выполняться не будет'"), ГруппировкаТарифа2);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		КонецЕсли;
		Если ЗначениеЗаполнено(ГруппировкаТарифа3)
			И ТипЗнч(ГруппировкаТарифа3) = Тип("СправочникСсылка.уатПараметрыВыработки")
			И Не ГруппировкаТарифа3.ДействуетНаСотрудников Тогда
			ТекстНСТР = СтрШаблон(НСтр("en='Output parameter %1 is not valid for employees.';ru='Параметр выработки %1 не действует на сотрудников. Расчет группировок по нему выполняться не будет'"), ГруппировкаТарифа3);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		КонецЕсли;
		
		// Удаление строки области действия, если вид области не входит в список доступных видов
		// например, Контрагент не будет доступен, если снят флаг "Построчный расчет"
		флУдаленыНедоступныеОбласти = Ложь;
		СписокДоступныхВидовОбластиДействия
			= Справочники.уатТарифыЗП.СписокДоступныхВидовОбластиДействия(ВидНачисления, РасчетПоСтрокам, Применение);
		СчОбласть = ОбластиДействия.Количество() - 1;
		Пока СчОбласть >= 0 Цикл
			ТекОбласть = ОбластиДействия[СчОбласть];
			Если СписокДоступныхВидовОбластиДействия.НайтиПоЗначению(ТекОбласть.ВидОбластиДействия) = Неопределено Тогда
				ОбластиДействия.Удалить(СчОбласть);
				флУдаленыНедоступныеОбласти = Истина;
			КонецЕсли;
			СчОбласть = СчОбласть - 1;
		КонецЦикла;
		Если флУдаленыНедоступныеОбласти Тогда
			ТекстСообщения = "В табличной части ""Область действия"" были удалены строки с недоступным видом области";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти