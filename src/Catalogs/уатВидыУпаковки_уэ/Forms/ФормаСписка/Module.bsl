
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	МожноРедактироватьОсновной = РольДоступна(Метаданные.Роли.ПолныеПрава.Имя); 
	
	Элементы.ФормаИспользоватьОсновным.Видимость = МожноРедактироватьОсновной;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Элементы.ФормаИспользоватьОсновным.Пометка Тогда
		Если ТекущиеДанные.Коэффициент = 1 Тогда 
			ИспользоватьКакОсновной(ТекущиеДанные.Ссылка);
			Элементы.Список.Обновить();
			УправлениеФормойКлиент();
			
		Иначе 
			ТекстНСТР = НСтр("en='It is not recommended to change basic form of package after you start working with system. Do you want to continue editing?';ru='Изменять основной вид упаковки после начала работы с системой не рекомендуется. Продолжить редактирование?'");
			Оповещение = Новый ОписаниеОповещения("ИспользоватьОсновнымОтветНаВопрос", ЭтотОбъект, ТекущиеДанные.Ссылка);
			ПоказатьВопрос(Оповещение, ТекстНСТР, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИспользоватьОсновнымОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ИспользоватьКакОсновной(ДополнительныеПараметры);
		ТекстНСТР = НСтр("en='For the current basic packaging type, set coefficient equal 1.';ru='Для текущего основного вида упаковки установлен коэффициент равный 1.'");
		ПоказатьПредупреждение(, ТекстНСТР);
	КонецЕсли;
	
	Элементы.Список.Обновить();
	УправлениеФормойКлиент();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИспользоватьКакОсновной(Знач ВидУпаковки)
	
	// 1. Снимем пометку "ОсновнойВидУпаковки" у всех элементов, отличных от текущего.
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", ВидУпаковки);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатВидыУпаковки_уэ.Ссылка
	|ИЗ
	|	Справочник.уатВидыУпаковки_уэ КАК уатВидыУпаковки_уэ
	|ГДЕ
	|	НЕ уатВидыУпаковки_уэ.ЭтоГруппа
	|	И уатВидыУпаковки_уэ.Ссылка <> &Ссылка
	|	И уатВидыУпаковки_уэ.ОсновнойВидУпаковки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		СпрОб = Выборка.Ссылка.ПолучитьОбъект();
		СпрОб.ОсновнойВидУпаковки = Ложь;
		СпрОб.Записать();
	КонецЦикла;
	
	// 2. Установим пометку "ОсновнойВидУпаковки" у текущего элемента.
	СпрОб = ВидУпаковки.ПолучитьОбъект();
	СпрОб.Коэффициент         = 1;
	СпрОб.ОсновнойВидУпаковки = Истина;
	СпрОб.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ЭтоГруппа Тогда
		Элементы.ФормаИспользоватьОсновным.Пометка     = Ложь;
		Элементы.ФормаИспользоватьОсновным.Доступность = Ложь;
	Иначе
		Элементы.ФормаИспользоватьОсновным.Пометка     = ТекущиеДанные.ОсновнойВидУпаковки;
		Элементы.ФормаИспользоватьОсновным.Доступность = МожноРедактироватьОсновной;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
