
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуТиповКузовов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокВалют

&НаКлиенте
Процедура СписокТиповКузововВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВСпискеТиповКузовов(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВыполнить()
	
	ОбработатьВыборВСпискеТиповКузовов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуТиповКузовов()
	
	// Заполняет список типов кузовов из макета.
	МакетДанных = Справочники.уатТипыКузовов.ПолучитьМакет("ДанныеДляЗаполнения");
	КолСтрок = МакетДанных.ВысотаТаблицы;
	Для сч = 1 По КолСтрок Цикл
		НоваяСтрока = ТипыКузовов.Добавить();
		НоваяСтрока.Группа = МакетДанных.Область(сч,1,сч,1).Текст;
		НоваяСтрока.Наименование = МакетДанных.Область(сч,2,сч,2).Текст;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки)
	
	ТекущаяСсылка = Неопределено;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ТипыКузовов[НомерСтроки];
		
		тГруппа = ТекущиеДанные.Группа;
		тНаименование = ТекущиеДанные.Наименование;
		
		СтрокаВБазе = Справочники.уатТипыКузовов.НайтиПоНаименованию(СокрЛП(тНаименование), Истина);
		// Либо такой элемент в справочнике уже есть
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.СписокТиповКузовов.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		// Либо этот элемент нужно создать
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	уатТипыКузовов.Ссылка
		|ИЗ
		|	Справочник.уатТипыКузовов КАК уатТипыКузовов
		|ГДЕ
		|	уатТипыКузовов.Наименование = &Наименование
		|	И уатТипыКузовов.ЭтоГруппа");
		Запрос.УстановитьПараметр("Наименование", СокрЛП(тГруппа));
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			мГруппа = Выборка.Ссылка;
		Иначе
			мГруппа = Справочники.уатТипыКузовов.СоздатьГруппу();
			мГруппа.Наименование = СокрЛП(тГруппа);
			мГруппа.Записать();
		КонецЕсли;
		
		Реквизиты = Новый Структура();
		Реквизиты.Вставить("Родитель", мГруппа.Ссылка);
		Реквизиты.Вставить("Наименование", тНаименование);
		
		ТипКузова = Справочники.уатТипыКузовов.СоздатьЭлемент();
	    ЗаполнитьЗначенияСвойств(ТипКузова, Реквизиты);
		ТипКузова.Записать();
	
		Если НомерСтроки = Элементы.СписокТиповКузовов.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = ТипКузова.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТекущаяСсылка;

КонецФункции

&НаКлиенте
Процедура ОбработатьВыборВСпискеТиповКузовов(СтандартнаяОбработка = Неопределено)
		
	// Добавление элемента справочника и вывод результата пользователю.
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСсылка = СохранитьВыбранныеСтроки(Элементы.СписокТиповКузовов.ВыделенныеСтроки);
	
	ОповеститьОВыборе(ТекущаяСсылка);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Типы кузовов добавлены.'"), , , БиблиотекаКартинок.Информация32);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
