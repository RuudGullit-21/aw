
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда 
		АвторизованныйПользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
		
		Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда 
			Объект.Ответственный = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
				АвторизованныйПользователь,
				"ОсновнойОтветственный"
			);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда 
			Объект.Ответственный = АвторизованныйПользователь;
		КонецЕсли;
		
		Если Параметры.Свойство("СтруктураНастроек") Тогда 
			Элементы.ФормаРедактироватьНастройки.Доступность = Ложь;
			НастройкиСтруктура = Параметры.СтруктураНастроек;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда 
			НастройкиСтруктура = Параметры.ЗначениеКопирования.ПолучитьОбъект().НастройкиХранилище.Получить();
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастройкиСтруктура = ТекущийОбъект.НастройкиХранилище.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.НастройкиХранилище = Новый ХранилищеЗначения(НастройкиСтруктура, Новый СжатиеДанных(9));
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда 
		Закрыть(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьНастройки(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда 
		Записать();
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("РедактироватьНастройкиЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.уатАРМЛогиста_уэ.Форма.ФормаНастроек", Новый Структура("ВариантНастройки", Параметры.Ключ), ЭтотОбъект, Параметры.Ключ,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РедактироватьНастройкиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НастройкиСтруктура = РезультатЗакрытия.СтруктураНастроек;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
