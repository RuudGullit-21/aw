
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ДополнительныеПоля") Тогда
		Возврат;
	КонецЕсли;

	СоответствиеРеквизитов = Новый Соответствие;
	ТипЗначения = Параметры.ТипЗначения;
	ВыбранныеПоля.Загрузить(ПолучитьИзВременногоХранилища(Параметры.ДополнительныеПоля));
	
	Для Каждого Тип из ТипЗначения.Типы() Цикл
		МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
		Для Каждого Реквизит из МетаданныеТипа.Реквизиты Цикл
			СоответствиеРеквизитов.Вставить(Реквизит.Имя, Реквизит.Синоним);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого КлючИЗначение из СоответствиеРеквизитов Цикл
		Если ВыбранныеПоля.НайтиСтроки(Новый Структура("Реквизит", КлючИЗначение.Ключ)).Количество() Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ДоступныеПоля.Добавить();
		НоваяСтрока.Реквизит = КлючИЗначение.Ключ;
		НоваяСтрока.Наименование = КлючИЗначение.Значение;
	КонецЦикла;
	
	ДоступныеПоля.Сортировать("Наименование");
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ДоступныеПоляВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Право(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПоляВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент.Имя = "ДополнительныеПоляЗаголовок" Тогда
		Возврат;
	КонецЕсли;
	
	Лево(Неопределено);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(ПоместитьРезультатВХранилище());
	
КонецПроцедуры

&НаКлиенте
Процедура Право(Команда)
	
	ТекущиеДанные = Элементы.ДоступныеПоля.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ВыбранныеПоля.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
	Элементы.ВыбранныеПоля.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
	ДоступныеПоля.Удалить(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура Лево(Команда)
	
	ТекущиеДанные = Элементы.ВыбранныеПоля.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ДоступныеПоля.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
	Элементы.ДоступныеПоля.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	ДоступныеПоля.Сортировать("Наименование");
	
	ВыбранныеПоля.Удалить(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьРезультатВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(ВыбранныеПоля.Выгрузить());
	
КонецФункции

#КонецОбласти

