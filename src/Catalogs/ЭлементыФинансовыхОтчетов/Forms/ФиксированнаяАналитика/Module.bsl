
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ВидЭлемента") Тогда
		Возврат;
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтотОбъект);
	Заголовок = Параметры.ВидЭлемента;

	Справочники.ЭлементыФинансовыхОтчетов.ЗаполнитьПредставлениеДополнительныхПолей(Объект, Элементы.ВыборДополнительныхПолей);
	
	//СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАналитикаПрочее Тогда
		Префикс = "";
		Если РазрешитьРедактирование Тогда
			Префикс = НСтр("en='добавляемые и ';ru='добавляемые и '");
		КонецЕсли;
		Объект.НаименованиеДляПечати = НСтр("en='<%1прочие %2>';ru='<%1прочие %2>'");
		Объект.НаименованиеДляПечати = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Объект.НаименованиеДляПечати, Префикс, НРег(ВидАналитики));
	Иначе
		Объект.НаименованиеДляПечати = Строка(Объект.ЗначениеАналитики);
	КонецЕсли;
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Перем Ошибки;
	
	Если Не ЭтоАналитикаПрочее Тогда
		Если Не ЗначениеЗаполнено(Объект.ЗначениеАналитики) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"ЗначениеАналитики",
				НСтр("en='Не заполнено значение аналитики';ru='Не заполнено значение аналитики'"), "");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭтоАналитикаПрочееПриИзменении(Элемент)
	
	Если ЭтоАналитикаПрочее Тогда
		Объект.ЗначениеАналитики = Неопределено;
	КонецЕсли;
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	//СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	Заглушка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДополнительныхПолей(Команда)
	
	ПараметрыФормы = БюджетнаяОтчетностьВызовСервера.ПараметрыНастройкиДополнительныхПолей(Объект, ВидАналитики);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновлениеДополнительныхПолей", ЭтотОбъект);
	ОткрытьФорму("Справочник.ЭлементыФинансовыхОтчетов.Форма.НастройкаДополнительныхПолей", 
					ПараметрыФормы,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ЗначениеАналитики.Доступность = Не ЭтоАналитикаПрочее;
	Элементы.ЗначениеАналитики.АвтоОтметкаНезаполненного = Не ЭтоАналитикаПрочее;
	Элементы.ЭтоАналитикаПрочее.Доступность = Не РазрешитьРедактирование;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеПриИзменении(Элемент)
	
	Если РазрешитьРедактирование Тогда
		ЭтоАналитикаПрочее = Истина;
		Объект.ЗначениеАналитики = Неопределено;
	КонецЕсли;
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьДополнительныеПоля(Результат)
	
	Объект.ДополнительныеПоля.Загрузить(ПолучитьИзВременногоХранилища(Результат));
	Справочники.ЭлементыФинансовыхОтчетов.ЗаполнитьПредставлениеДополнительныхПолей(Объект, Элементы.ВыборДополнительныхПолей);
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеДополнительныхПолей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьДополнительныеПоля(Результат);
	
КонецПроцедуры

#КонецОбласти