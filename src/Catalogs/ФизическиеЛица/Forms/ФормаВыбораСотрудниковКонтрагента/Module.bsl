
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	Если Параметры.Свойство("Отборы") Тогда
		Отборы = Параметры.Отборы;
		Если Отборы.Свойство("Контрагент") Тогда
			ОтборКонтрагент = Отборы.Контрагент;
		КонецЕсли;
		Если Отборы.Свойство("Организация") Тогда
			ОтборОрганизация = Отборы.Организация;
		КонецЕсли;
	Иначе
		Элементы.ОтборКонтрагент.Видимость  = Ложь;
		Элементы.ОтборОрганизация.Видимость = Ложь;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ВключенОтбор", Ложь);
	Список.Параметры.УстановитьЗначениеПараметра("МассивФизЛиц", Новый Массив);
	
	АвторизованВнешнийПользователь = Ложь;
	АвторизованныйКонтрагент = уатЗащищенныеФункцииСервер_проф.АвторизованныйКонтрагент(АвторизованВнешнийПользователь);
	Если АвторизованВнешнийПользователь Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Контрагент", АвторизованныйКонтрагент,,,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборы();
	
КонецПроцедуры

#КонецОбласти  

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)   
	УстановитьОтборы();
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент) 
	УстановитьОтборы();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура УстановитьОтборы()  
	ВключенОтбор  = Ложь;  
	МассивФизЛиц = Новый Массив(); 
		
	Если ЗначениеЗаполнено(ОтборКонтрагент)
		ИЛИ ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ВключенОтбор = Истина;
		
		МассивФизЛиц = ПолучитьФизЛицПоОтборам(ОтборКонтрагент, ОтборОрганизация);
	КонецЕсли;
	
	Если ВключенОтбор Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ВключенОтбор", Истина);
		Список.Параметры.УстановитьЗначениеПараметра("МассивФизЛиц", МассивФизЛиц);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ВключенОтбор", Ложь);
		Список.Параметры.УстановитьЗначениеПараметра("МассивФизЛиц", Новый Массив);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьФизЛицПоОтборам(ОтборКонтрагент, ОтборОрганизация)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ФизическиеЛица.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уатСотрудникиКонтрагентов КАК уатСотрудникиКонтрагентов
	               |		ПО ФизическиеЛица.Ссылка = уатСотрудникиКонтрагентов.ФизЛицо
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	               |		ПО (Сотрудники.ФизическоеЛицо = ФизическиеЛица.Ссылка)
	               |ГДЕ
	               |	НЕ ФизическиеЛица.ПометкаУдаления"; 
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда 
		Запрос.УстановитьПараметр("ОтборКонтрагент", ОтборКонтрагент);
		Запрос.Текст = Запрос.Текст + "
		|	И уатСотрудникиКонтрагентов.Контрагент = &ОтборКонтрагент";
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда 
		Запрос.УстановитьПараметр("ОтборОрганизация", ОтборОрганизация);
		Запрос.Текст = Запрос.Текст + "
		|	И Сотрудники.Организация = &ОтборОрганизация";
	КонецЕсли;
		
	Выборка = Запрос.Выполнить().Выбрать();
	МассивФизЛиц = Новый Массив();
	Пока Выборка.Следующий() Цикл
		МассивФизЛиц.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат МассивФизЛиц;
	
КонецФункции


#КонецОбласти 
