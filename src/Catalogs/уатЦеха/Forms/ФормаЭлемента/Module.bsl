
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = Объект.Родитель.Организация;
			Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
					ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяОрганизация");
			КонецЕсли;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.ГрафикРаботы) Тогда
			Объект.ГрафикРаботы = Объект.Родитель.ГрафикРаботы;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.Склад) Тогда
			Объект.Склад = Объект.Родитель.Склад;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.Мастер) Тогда
			Объект.Мастер = Объект.Родитель.Мастер;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьГрафикДляПодчиненных(Команда)
	ТекстНСТР = НСтр("en='Set work schedule %1 for all subordinate shops?';ru='Установить график работы %1 для всех подчиненных цехов?'");
	ТекстНСТР = СтрШаблон(ТекстНСТР, СокрЛП(Объект.ГрафикРаботы));
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьГрафикДляПодчиненныхЗавершение", ЭтотОбъект),
		ТекстНСТР, РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГрафикДляПодчиненныхЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    УстановитьГрафикДляПодчиненныхСервер();

КонецПроцедуры

&НаСервере
Процедура УстановитьГрафикДляПодчиненныхСервер()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Цеха.Ссылка КАК Цех
	|ИЗ
	|	Справочник.уатЦеха КАК Цеха
	|ГДЕ
	|	Цеха.Ссылка В ИЕРАРХИИ (&Ссылка)";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Цех = Объект.Ссылка Тогда
			Продолжить;
		КонецЕсли; 
		Цех = Выборка.Цех.ПолучитьОбъект();
		Цех.ГрафикРаботы = Объект.ГрафикРаботы;
		Попытка
			Цех.Записать();
		Исключение
		КонецПопытки; 
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
