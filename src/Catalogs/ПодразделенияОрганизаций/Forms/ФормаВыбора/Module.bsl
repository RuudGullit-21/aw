
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") И ТипЗнч(Параметры.Отбор) = Тип("Структура")
		И Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		Организация = Параметры.Отбор.Владелец;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	//Если Отказ Тогда
	//	Возврат;
	//КонецЕсли;
	//
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ПолучитьОрганизацию();
	КонецЕсли;
	
	УстановитьОтбор();
	ОбновитьЗаголовок();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьОтбор();
	ОбновитьЗаголовок();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьОрганизацию()
	Организация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ПользователиКлиентСервер.ТекущийПользователь(), 
		"ОсновнаяОрганизация");	
	Возврат Организация;
КонецФункции

&НаКлиенте
Процедура УстановитьОтбор()
	флЭлементНайден = Ложь;
	Для Каждого ТекЭл Из Список.Отбор.Элементы Цикл
		Если ТекЭл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец") Тогда
			флЭлементНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НЕ флЭлементНайден Тогда
		ТекЭл = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ТекЭл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
	КонецЕсли;
	ТекЭл.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ТекЭл.ПравоеЗначение = Организация;
	ТекЭл.Использование = Истина;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовок()
	ТекстНСТР = НСтр("en='List of company departments of ""%1""';ru='Список подразделений организации ""%1""'");
	Заголовок = СтрШаблон(ТекстНСТР, уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Организация, "Наименование"));
	
	ЭтоЮрЛицо = (уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Организация, "ЮридическоеФизическоеЛицо") = 
		ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	Если НЕ ЭтоЮрЛицо Тогда
		Элементы.КодПоОКАТО.Видимость = Ложь;
		Элементы.КПП.Видимость        = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
