
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимость();
	РаскраситьСписок();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписанЭлементСтатусДокумента" Тогда
		РаскраситьСписок();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РаскраситьСписок()
	
	СписокУдаляемыхЭлементов = Новый СписокЗначений;
	Для Каждого ЭлементУсловногоОформления Из Список.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" Тогда
			СписокУдаляемыхЭлементов.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из СписокУдаляемыхЭлементов Цикл
		Список.УсловноеОформление.Элементы.Удалить(Элемент.Значение);
	КонецЦикла;
	
	ВыборкаСтатусы = Справочники.уатСтатусы_уэ.Выбрать();
	Пока ВыборкаСтатусы.Следующий() Цикл
		ЦветТекста = ?(ЗначениеЗаполнено(ВыборкаСтатусы.ЦветТекстаПоУмолчанию), ВыборкаСтатусы.ЦветТекстаПоУмолчанию.Получить(), Неопределено);
		ЦветФона   = ?(ЗначениеЗаполнено(ВыборкаСтатусы.ЦветФонаПоУмолчанию),   ВыборкаСтатусы.ЦветФонаПоУмолчанию.Получить(),   Неопределено);
		
		Если ТипЗнч(ЦветТекста) <> Тип("Цвет") И ТипЗнч(ЦветФона) <> Тип("Цвет") Тогда
			Продолжить;
		КонецЕсли; 
		
		ЭлементУсловногоОформления = Список.УсловноеОформление.Элементы.Добавить();
	
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ВыборкаСтатусы.Ссылка;
		
		Если ТипЗнч(ЦветТекста) = Тип("Цвет") Тогда
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТекста);
		КонецЕсли;
		Если ТипЗнч(ЦветФона) = Тип("Цвет") И ЦветФона <> Новый Цвет(0,0,0) Тогда
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФона);
		КонецЕсли;
		
		ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
		ТекстНСТР = НСтр("en='By document status %1';ru='По статусу документа %1'");
		ТекстНСТР = СтрШаблон(ТекстНСТР, ВыборкаСтатусы.Наименование);
		ЭлементУсловногоОформления.Представление = ТекстНСТР;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Если уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП() Тогда
		Элементы.ДействуетНаАктыОтгрузки.Видимость = Истина;
		Элементы.ДействуетНаАктыПриемки.Видимость = Истина;
		Элементы.ДействуетНаОперацииСГрузами.Видимость = Истина;
	Иначе
		Элементы.ДействуетНаАктыОтгрузки.Видимость = Ложь;
		Элементы.ДействуетНаАктыПриемки.Видимость = Ложь;
		Элементы.ДействуетНаОперацииСГрузами.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
