
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	РаскраситьСписок();
	
	Если Параметры.Свойство("ПоказатьВводДаты") Тогда 
		РежимВводаДаты = Истина;
		Элементы.ГруппаДатаУстановкиСтатуса.Видимость = Истина;
	Иначе 
		РежимВводаДаты = Ложь;
		Элементы.ГруппаДатаУстановкиСтатуса.Видимость = Ложь;
	КонецЕсли;
	
	ДатаУстановкиСтатуса = ТекущаяДата();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписанЭлементСтатусДокумента" Тогда
		РаскраситьСписок();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		ЗакрытьФормуСВыбором(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗакрытьФормуСВыбором(ТекДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РаскраситьСписок()
	
	СписокУдаляемыхЭлементов = Новый СписокЗначений;
	Для Каждого ЭлементУсловногоОформления Из Список.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" Тогда
			СписокУдаляемыхЭлементов.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из СписокУдаляемыхЭлементов Цикл
		Список.УсловноеОформление.Элементы.Удалить(Элемент.Значение);
	КонецЦикла;
	
	ВыборкаСтатусы = Справочники.уатСтатусы_уэ.Выбрать();
	Пока ВыборкаСтатусы.Следующий() Цикл
		ЦветТекста = ?(ЗначениеЗаполнено(ВыборкаСтатусы.ЦветТекстаПоУмолчанию), ВыборкаСтатусы.ЦветТекстаПоУмолчанию.Получить(), Неопределено);
		ЦветФона   = ?(ЗначениеЗаполнено(ВыборкаСтатусы.ЦветФонаПоУмолчанию),   ВыборкаСтатусы.ЦветФонаПоУмолчанию.Получить(),   Неопределено);
		
		Если ТипЗнч(ЦветТекста) <> Тип("Цвет") И ТипЗнч(ЦветФона) <> Тип("Цвет") Тогда
			Продолжить;
		КонецЕсли; 
		
		ЭлементУсловногоОформления = Список.УсловноеОформление.Элементы.Добавить();
	
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ВыборкаСтатусы.Ссылка;
		
		Если ТипЗнч(ЦветТекста) = Тип("Цвет") Тогда
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТекста);
		КонецЕсли;
		Если ТипЗнч(ЦветФона) = Тип("Цвет") И ЦветФона <> Новый Цвет(0,0,0) Тогда
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФона);
		КонецЕсли;
		
		ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
		ТекстНСТР = НСтр("en='By document status %1';ru='По статусу документа %1'");
		ТекстНСТР = СтрШаблон(ТекстНСТР,ВыборкаСтатусы.Наименование);
		ЭлементУсловногоОформления.Представление = ТекстНСТР;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуСВыбором(СтатусСсылка)
	
	Если РежимВводаДаты Тогда 
		ПараметрЗакрытия = Новый Структура("Статус, ДатаУстановки", СтатусСсылка, ДатаУстановкиСтатуса);
	Иначе 
		ПараметрЗакрытия = СтатусСсылка;
	КонецЕсли;
	
	ОповеститьОВыборе(ПараметрЗакрытия);
	
КонецПроцедуры

#КонецОбласти
