
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(Неопределено, НСтр("en='Immediate opening for this object is prohibited!';ru='Непосредственное открытие для данного объекта запрещено!'"));
		Возврат;
	КонецЕсли;
	
	СтруктураНастроек = СтруктураНастроекПоУмолчанию();
	Если ВладелецФормы.ВыбиратьРезультатГеокодированияИзСписка Тогда
		СтруктураНастроек.ВыбиратьРезультатГеокодированияИзСписка = 1;
	Иначе
		СтруктураНастроек.ВыбиратьРезультатГеокодированияИзСписка = 0;
	КонецЕсли;
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрыЗакрытия = ПолучитьСтруктуруНастроек();
	ТекстОшибки = "";
	Если ПроверитьНастройки(ПараметрыЗакрытия, ТекстОшибки) Тогда
		Закрыть(ПараметрыЗакрытия);
	Иначе
		ПоказатьВопрос(Новый ОписаниеОповещения("ОКОтветНаВопрос", ЭтаФорма, ПараметрыЗакрытия), ТекстОшибки, РежимДиалогаВопрос.ОКОтмена);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СтруктураНастроекПоУмолчанию() 
	Возврат Справочники.уатПунктыНазначения.СтруктураНастроекГеокодированияПоУмолчанию();
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруНастроек() 
	СтруктураНастроек = СтруктураНастроекПоУмолчанию();
	
	Если ВыбиратьРезультатГеокодированияИзСписка = 1 Тогда
		СтруктураНастроек.ВыбиратьРезультатГеокодированияИзСписка = Истина;
	Иначе
		СтруктураНастроек.ВыбиратьРезультатГеокодированияИзСписка = Ложь;
	КонецЕсли;
	Возврат СтруктураНастроек;
КонецФункции

&НаСервере
Функция ПроверитьНастройки(СтруктураНастроек, ТекстОшибки = "")
	
	Результат = Истина;
	
	Если СтруктураНастроек.ВыбиратьРезультатГеокодированияИзСписка Тогда
		ТипСервиса = "ПодсказкиАдресов";
	Иначе
		ТипСервиса = "Геокодирование";
	КонецЕсли;
	ТипПоставщика = уатЭлектронныеКартыСервер.ПолучитьТипИспользуемыхЭлектронныхКарт(ТипСервиса);
	
	Если Не ЗначениеЗаполнено(ТипПоставщика) Тогда
		ТекстОшибки = НСтр("ru = 'Необходимо включить использование выбранного сервиса 
			|в панели ""Администрирование"", раздел ""Настройки функциональности"", ""Электронные карты"". Продолжить?'");
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&наКлиенте
Процедура ОКОтветНаВопрос(Результат, ПараметрыЗакрытия) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти