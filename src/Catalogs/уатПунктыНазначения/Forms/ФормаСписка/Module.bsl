
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.уатПунктыНазначения) Тогда 
		Элементы.ФормаИзменитьВыделенные.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = Ложь;
	КонецЕсли;
		
	Элементы.ФормаГруппаГеозоны.Видимость = уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП()
		И ПравоДоступа("Чтение", Метаданные.Справочники.уатГеозоны_уэ);
	Элементы.ФормаГруппаГеозоны1.Видимость = Элементы.ФормаГруппаГеозоны.Видимость;

	// уатУправлениеАвтотранспортом.МодификацияКонфигурации
	уатМодификацияКонфигурацииПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец уатУправлениеАвтотранспортом.МодификацияКонфигурации
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// уатУправлениеАвтотранспортом.МодификацияКонфигурации
&НаКлиенте
Процедура Подключаемый_уатВыполнитьКоманду(Команда)
	
	уатМодификацияКонфигурацииКлиентПереопределяемый.ВыполнитьПодключаемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры
// Конец уатУправлениеАвтотранспортом.МодификацияКонфигурации

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список, Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьГеозону(Команда)
	Оповещ = Новый ОписаниеОповещения("ОпределитьГеозонуВопрос", ЭтотОбъект);
	ПоказатьВопрос(Оповещ, "Введенные значения геозон будут очищены! Продолжить?", РежимДиалогаВопрос.ОКОтмена);
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьГеозонуВопрос(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		мсвПункты = ВыделенныеПункты();
		уатОбщегоНазначения_уэ.уатПривязкаПунктовНазначенияКГеозонам(мсвПункты);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГеозону(Команда)
	Оповещ = Новый ОписаниеОповещения("УстановитьГеозонуВопрос", ЭтотОбъект);
	ПоказатьВопрос(Оповещ, "Введенные значения геозон будут очищены! Продолжить?", РежимДиалогаВопрос.ОКОтмена);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГеозонуВопрос(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Оповещ = Новый ОписаниеОповещения("УстановитьГеозонуВыборЗначения", ЭтотОбъект);
		ОткрытьФорму("Справочник.уатГеозоны_уэ.ФормаВыбора",,,,,, Оповещ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГеозонуВыборЗначения(Результат, ДопПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		мсвПункты = ВыделенныеПункты();
		уатОбщегоНазначения_уэ.уатПривязкаПунктовНазначенияКГеозонам(мсвПункты, Результат);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВыделенныеПункты()
	мсвПункты = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Продолжить;
		КонецЕсли;
		
		ТекСтрока = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если ТекСтрока.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		мсвПункты.Добавить(ТекСтрока.Ссылка);
	КонецЦикла;
	
	Возврат мсвПункты;
КонецФункции
	
#КонецОбласти
