
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("мсвКоординат") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Заполнение таблицы
	Для Каждого ТекСтрока Из Параметры.мсвКоординат Цикл
		НоваяСтрока = ТаблицаРезультатов.Добавить();
		НоваяСтрока.Адрес = ТекСтрока.display_name;
		НоваяСтрока.Лат = ТекСтрока.lat;
		НоваяСтрока.Лон = ТекСтрока.lon;
	КонецЦикла;
	
	Параметры.Свойство("Пункт", Пункт);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("en='Immediate opening for this object is prohibited!';ru='Непосредственное открытие для данного объекта запрещено!'"), 10);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_ТаблицаРезультатов

&НаКлиенте
Процедура ТаблицаРезультатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ТаблицаРезультатов.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбраннаяШирота = текСтрока.Лат;
	ВыбраннаяДолгота = текСтрока.Лон;
	КоординатыПунктаИзменены = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если КоординатыПунктаИзменены Тогда
		// Выполняется при закрытии формы, чтобы диалоговые окна не привязались к закрываемой форме.
		Координаты = Новый Структура("Лат, Лон", ВыбраннаяШирота, ВыбраннаяДолгота);
		Оповестить("ВыборКоординатПункта", Координаты, Пункт);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
