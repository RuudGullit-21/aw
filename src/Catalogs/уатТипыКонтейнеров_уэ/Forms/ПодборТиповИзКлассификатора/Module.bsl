
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуТиповКонтейнеров();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокВалют

&НаКлиенте
Процедура СписокТиповКонтейнеровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВСпискеТиповКонтейнеров(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВыполнить()
	
	ОбработатьВыборВСпискеТиповКонтейнеров();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуТиповКонтейнеров()
	
	// Заполняет список типов контейнеров из макета.
	МакетДанных = Справочники.уатТипыКонтейнеров_уэ.ПолучитьМакет("ДанныеДляЗаполнения");
	КолСтрок = МакетДанных.ВысотаТаблицы;
	Для сч = 1 По КолСтрок Цикл
		НоваяСтрока = ТипыКонтейнеров.Добавить();
		НоваяСтрока.Группа       = МакетДанных.Область(сч,1,сч,1).Текст;
		НоваяСтрока.Наименование = МакетДанных.Область(сч,2,сч,2).Текст;
		НоваяСтрока.Длина        = МакетДанных.Область(сч,3,сч,3).Текст;
		НоваяСтрока.Ширина       = МакетДанных.Область(сч,4,сч,4).Текст;
		НоваяСтрока.Высота       = МакетДанных.Область(сч,5,сч,5).Текст;
		НоваяСтрока.Вместимость      = МакетДанных.Область(сч,6,сч,6).Текст;
		НоваяСтрока.Грузоподъемность = МакетДанных.Область(сч,7,сч,7).Текст;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьВыбранныеСтроки(Знач ВыбранныеСтроки)
	
	ТекущаяСсылка = Неопределено;
	
	Для каждого НомерСтроки Из ВыбранныеСтроки Цикл
		ТекущиеДанные = ТипыКонтейнеров[НомерСтроки];
		
		тГруппа       = ТекущиеДанные.Группа;
		тНаименование = ТекущиеДанные.Наименование;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	уатТипыКонтейнеров.Ссылка
		|ИЗ
		|	Справочник.уатТипыКонтейнеров_уэ КАК уатТипыКонтейнеров
		|ГДЕ
		|	уатТипыКонтейнеров.Наименование = &Наименование
		|	И уатТипыКонтейнеров.ЭтоГруппа");
		Запрос.УстановитьПараметр("Наименование", СокрЛП(тГруппа));
		Выборка = Запрос.Выполнить().Выбрать();
		
		СтрокаВБазе = "";
		Если Выборка.Следующий() Тогда
			мГруппа = Выборка.Ссылка;
			
			СтрокаВБазе = Справочники.уатТипыКонтейнеров_уэ.НайтиПоНаименованию(СокрЛП(тНаименование), Истина, мГруппа);
		Иначе
			мГруппа = Справочники.уатТипыКонтейнеров_уэ.СоздатьГруппу();
			мГруппа.Наименование = СокрЛП(тГруппа);
			мГруппа.Записать();
		КонецЕсли;

		// Либо такой элемент в справочнике уже есть
		Если ЗначениеЗаполнено(СтрокаВБазе) Тогда
			Если НомерСтроки = Элементы.СписокТиповКонтейнеров.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
				ТекущаяСсылка = СтрокаВБазе;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Реквизиты = Новый Структура();
		Реквизиты.Вставить("Родитель", мГруппа.Ссылка);
		Реквизиты.Вставить("Наименование", тНаименование);
		
		ТипКонтейнеров = Справочники.уатТипыКонтейнеров_уэ.СоздатьЭлемент();
	    ЗаполнитьЗначенияСвойств(ТипКонтейнеров, Реквизиты);
		ЗаполнитьЗначенияСвойств(ТипКонтейнеров, ТекущиеДанные);
		ТипКонтейнеров.Записать();
	
		Если НомерСтроки = Элементы.СписокТиповКонтейнеров.ТекущаяСтрока Или ТекущаяСсылка = Неопределено Тогда
			ТекущаяСсылка = ТипКонтейнеров.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТекущаяСсылка;

КонецФункции

&НаКлиенте
Процедура ОбработатьВыборВСпискеТиповКонтейнеров(СтандартнаяОбработка = Неопределено)
		
	// Добавление элемента справочника и вывод результата пользователю.
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСсылка = СохранитьВыбранныеСтроки(Элементы.СписокТиповКонтейнеров.ВыделенныеСтроки);
	
	ОповеститьОВыборе(ТекущаяСсылка);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Типы контейнеров добавлены.'"), , , БиблиотекаКартинок.Информация32);
	ОповеститьОбИзменении(Тип("СправочникСсылка.уатТипыКонтейнеров_уэ"));
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
