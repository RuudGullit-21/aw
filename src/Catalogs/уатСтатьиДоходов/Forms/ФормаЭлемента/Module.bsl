
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УстановитьВидимость();
	УстановитьОтборПараметровВыработкиМеждуТС();
	УстановитьОтборПараметровВыработкиМеждуАналитиками();
	
КонецПроцедуры    

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособРаспределенияМеждуТСПриИзменении(Элемент)
	Если Объект.СпособРаспределенияМеждуТС <> ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуТС.ПоОбъемуВыработки")
		И Объект.СпособРаспределенияМеждуТС <> ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуТС.ПоОбъемуВыработкиМЛ") Тогда
		Объект.ПараметрВыработкиРаспределенияМеждуТС = Неопределено;
	КонецЕсли;
	
	УстановитьВидимость();
	УстановитьОтборПараметровВыработкиМеждуТС()
КонецПроцедуры

&НаКлиенте
Процедура СпособРаспределенияЗатратМеждуАналитикамиПриИзменении(Элемент)
	УстановитьВидимость();
	УстановитьОтборПараметровВыработкиМеждуАналитиками();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеДоходовПоСцепкамПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ПараметрВыработкиРаспределенияМеждуАналитикамиПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.ПараметрВыработкиРаспределенияМеждуТС.Видимость = (Объект.СпособРаспределенияМеждуТС = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуТС.ПоОбъемуВыработки")
		ИЛИ Объект.СпособРаспределенияМеждуТС = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуТС.ПоОбъемуВыработкиМЛ"));
	Элементы.ПараметрВыработкиРаспределенияМеждуАналитиками.Видимость = (Объект.СпособРаспределенияМеждуАналитиками = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуАналитиками.РаспределятьПоВыработке")
		ИЛИ Объект.СпособРаспределенияМеждуАналитиками = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуАналитиками.РаспределятьПоЗаказам"));
	Элементы.СпособРаспределениеНепрямыхЗатратПоЗаказам.Видимость = Элементы.ПараметрВыработкиРаспределенияМеждуАналитиками.Видимость;
	Элементы.СпособРаспределенияВнутриСцепки.Видимость = Объект.РаспределениеДоходовПоСцепкам;
	Элементы.ГруппаНадписьВЦелом.Видимость = (Объект.СпособРаспределенияМеждуАналитиками = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуАналитиками.РаспределятьПоЗаказам")
		И уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Объект.ПараметрВыработкиРаспределенияМеждуАналитиками, "СпособВводаЗначений") = ПредопределенноеЗначение("Перечисление.уатСпособыВводаЗначенийВыработкивМЛ_уэ.ВЦеломПоДокументу"));
	Элементы.ПараметрВыработкиРаспределенияМеждуЗаказами.Видимость = уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП();
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПараметровВыработкиМеждуТС()
	мсвПараметрыВыбора = Новый Массив;
	Если Объект.СпособРаспределенияМеждуТС = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуТС.ПоОбъемуВыработкиМЛ") Тогда
		мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Предопределенный", Истина));
		мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ДействуетНаТСМЛ", Истина));
	КонецЕсли;
	Элементы.ПараметрВыработкиРаспределенияМеждуТС.ПараметрыВыбора = Новый ФиксированныйМассив(мсвПараметрыВыбора);
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПараметровВыработкиМеждуАналитиками()
	мсвПараметрыВыбора = Новый Массив;
	Если Объект.СпособРаспределенияМеждуАналитиками = ПредопределенноеЗначение("Перечисление.уатСпособыРаспределенияДоходовРасходовМеждуАналитиками.РаспределятьПоЗаказам") Тогда
		мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Справочники.уатПараметрыВыработки.ПараметрыВыработкиАвтоРасчетУслуг()));
		мсвПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ИспользоватьДляМаршрутныхЛистов", Истина));
	КонецЕсли;
	Элементы.ПараметрВыработкиРаспределенияМеждуАналитиками.ПараметрыВыбора = Новый ФиксированныйМассив(мсвПараметрыВыбора);
КонецПроцедуры

#КонецОбласти
