#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Настройки.ПриПолученииСлужебныхРеквизитов = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	// необходима для подключения внешних ПФ
	Заглушка = Истина;
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Запрещает/разрешает загрузку данных в этот справочник из подсистемы "ЗагрузкаДанныхИзФайла".
// 
// Возвращаемое значение:
//  Булево - флаг использования
//
Функция ИспользоватьЗагрузкуДанныхИзФайла() Экспорт
	Возврат Истина;
КонецФункции

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		ДанныеВыбора = Новый СписокЗначений();
		Возврат;
	КонецЕсли; 
	СтандартнаяОбработка = Ложь;
	
	СтрокаПоиска = Параметры.СтрокаПоиска;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц();
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("ТекстАвтоподбора", "%" + Параметры.СтрокаПоиска + "%");
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Ссылка,
	|	Контрагенты.Наименование КАК ПредставлениеОсн,
	|	Контрагенты.Код КАК ПредставлениеДоп
	|ПОМЕСТИТЬ ВТ_РезультатПодбора
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Наименование ПОДОБНО &ТекстАвтоподбора
	|	И &ДополнительныеОтборы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	Контрагенты.Код,
	|	Контрагенты.Наименование
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Код ПОДОБНО &ТекстАвтоподбора
	|	И &ДополнительныеОтборы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	Контрагенты.ИНН,
	|	Контрагенты.Наименование
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН ПОДОБНО &ТекстАвтоподбора
	|	И &ДополнительныеОтборы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
	|	ВТ_РезультатПодбора.Ссылка,
	|	ВТ_РезультатПодбора.ПредставлениеОсн,
	|	ВТ_РезультатПодбора.ПредставлениеДоп
	|ИЗ
	|	ВТ_РезультатПодбора КАК ВТ_РезультатПодбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_РезультатПодбора";
	
	ДополнительныеОтборы = "";
	
	Для Каждого ТекОтбор Из Параметры.Отбор Цикл 
		Если Метаданные.Справочники.Контрагенты.Реквизиты.Найти(ТекОтбор.Ключ) = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ДополнительныеОтборы = ДополнительныеОтборы + ?(ДополнительныеОтборы="", "", " И ") + "Контрагенты." + ТекОтбор.Ключ + " = &ДополнительныйОтбор" + ТекОтбор.Ключ;
		Запрос.УстановитьПараметр("ДополнительныйОтбор" + ТекОтбор.Ключ, ТекОтбор.Значение);
	КонецЦикла;
	
	Если Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы Тогда 
		ДополнительныеОтборы = ДополнительныеОтборы + ?(ДополнительныеОтборы="", "", " И ") + "Контрагенты.ЭтоГруппа";
	ИначеЕсли Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы Тогда 
		ДополнительныеОтборы = ДополнительныеОтборы + ?(ДополнительныеОтборы="", "", " И ") + "НЕ Контрагенты.ЭтоГруппа";
	КонецЕсли;
	
	Если ДополнительныеОтборы = "" Тогда 
		ДополнительныеОтборы = "ИСТИНА";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеОтборы", ДополнительныеОтборы);
	
	ДанныеВыбора = Новый СписокЗначений();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаДляЗамены = СтрокаПоиска;
		
		Позиция = Найти(НРег(Выборка.ПредставлениеОсн), НРег(СтрокаПоиска));
		
		Если Позиция > 0 Тогда 
			ТекстДо = Лев(Выборка.ПредставлениеОсн, Позиция-1);
			ТекстЦентр = Сред(Выборка.ПредставлениеОсн, Позиция, СтрДлина(СтрокаДляЗамены));
			ТекстПосле = Сред(Выборка.ПредставлениеОсн, Позиция+СтрДлина(СтрокаДляЗамены));
			ВыделенныйТекст = Новый ФорматированнаяСтрока(ТекстЦентр,, ЦветаСтиля.уатЦветТекстаПриАвтоподборе);
			Представление = Новый ФорматированнаяСтрока(ТекстДо, ВыделенныйТекст, ТекстПосле);	
		КонецЕсли;
		
		Представление = Новый ФорматированнаяСтрока(Представление," (",Выборка.ПредставлениеДоп,")");
		ДанныеВыбора.Добавить(Выборка.Ссылка, Представление);
		
	КонецЦикла;
	
	МенеджерВТ.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли