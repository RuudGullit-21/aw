#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Функция возвращает результат запроса по справочнику контрагентов с заданным головным контрагентом
//
// Параметры:
//  ГоловнойКонтрагент	 - СправочникСсылка.Контрагенты	 - заданный головной контрагент
// 
// Возвращаемое значение:
//  Результат - результат работы запроса
//
Функция ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту(ГоловнойКонтрагент) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
	|	И Контрагенты.ГоловнойКонтрагент <> Контрагенты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент";
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции // ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту()

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда
		ОсновнойБанковскийСчет     = Неопределено;
		ОсновноеКонтактноеЛицо     = Неопределено;
		ГоловнойКонтрагент         = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ГоловнойКонтрагент) Тогда
	
		Если ЭтоНовый() Тогда
			НоваяСсылка = Справочники.Контрагенты.ПолучитьСсылку();
			УстановитьСсылкуНового(НоваяСсылка);
			ГоловнойКонтрагент = ПолучитьСсылкуНового();
		Иначе
			ГоловнойКонтрагент = Ссылка;
		КонецЕсли;
	
	КонецЕсли;
	
	Если НЕ ЭтоНовый() Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактныеЛица.Ссылка КАК КонтЛицо
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|
		|ГДЕ
		|	КонтактныеЛица.ОбъектВладелец = &Ссылка
		|		И КонтактныеЛица.ПометкаУдаления <> &ПометкаУдаления";
		ВыборкаКонтЛиц = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаКонтЛиц.Следующий() Цикл
			
			КонтЛицо = ВыборкаКонтЛиц.КонтЛицо.ПолучитьОбъект();
			КонтЛицо.УстановитьПометкуУдаления(ПометкаУдаления);
			
		КонецЦикла;
	КонецЕсли;
	
	// Создание основного контактного лица контрагента
	Если ДополнительныеСвойства.Свойство("ДанныеКонтактногоЛица")
		И ТипЗнч(ДополнительныеСвойства.ДанныеКонтактногоЛица) = Тип("Структура") Тогда
		
		КонтактноеЛицо = Справочники.КонтактныеЛица.СоздатьЭлемент();
		
		КонтактноеЛицо.ОбъектВладелец = СсылкаКонтрагента();
		КонтактноеЛицо.Заполнить(ДополнительныеСвойства.ДанныеКонтактногоЛица);
		
		КонтактноеЛицо.Наименование = КонтактноеЛицо.Фамилия 
			+ ?(ЗначениеЗаполнено(КонтактноеЛицо.Имя), " " + КонтактноеЛицо.Имя, "")
			+ ?(ЗначениеЗаполнено(КонтактноеЛицо.Отчество), " " + КонтактноеЛицо.Отчество, "")
			+ ?(ЗначениеЗаполнено(КонтактноеЛицо.Должность), ", " + КонтактноеЛицо.Должность, "");
		
		КонтактноеЛицо.Записать();
		
		ОсновноеКонтактноеЛицо = КонтактноеЛицо.Ссылка;
		
	КонецЕсли;
	
	// Проверка ТЧ ПунктыНазначения. В ней должна быть только одна строка
	// с пометкой основной.
	флЕстьОсновной = Ложь;
	Для Каждого ТекСтрока Из ПунктыНазначения Цикл 
		Если ТекСтрока.Основной И Не флЕстьОсновной Тогда 
			флЕстьОсновной = Истина;
		ИначеЕсли ТекСтрока.Основной И флЕстьОсновной Тогда 
			ТекСтрока.Основной = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка ТЧ Отправители. В ней должна быть только одна строка
	// с пометкой основной.
	флЕстьОсновной = Ложь;
	Для Каждого ТекСтрока Из Грузоотправители Цикл 
		Если ТекСтрока.Основной И Не флЕстьОсновной Тогда 
			флЕстьОсновной = Истина;
		ИначеЕсли ТекСтрока.Основной И флЕстьОсновной Тогда 
			ТекСтрока.Основной = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка ТЧ Получатели. В ней должна быть только одна строка
	// с пометкой основной.
	флЕстьОсновной = Ложь;
	Для Каждого ТекСтрока Из Грузополучатели Цикл 
		Если ТекСтрока.Основной И Не флЕстьОсновной Тогда 
			флЕстьОсновной = Истина;
		ИначеЕсли ТекСтрока.Основной И флЕстьОсновной Тогда 
			ТекСтрока.Основной = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Организации") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Наименование,ЮридическоеФизическоеЛицо,ИНН,КПП,КодПоОКПО");
		
		Если ДанныеЗаполнения.ВариантНаименованияДляПечатныхФорм = 
				Перечисления.ВариантыНаименованияДляПечатныхФорм.ПолноеНаименование Тогда
			НаименованиеПолное = ДанныеЗаполнения.НаименованиеПолное;
		Иначе
			НаименованиеПолное = ДанныеЗаполнения.НаименованиеСокращенное;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ГоловнойКонтрагент) И ГоловнойКонтрагент <> Ссылка Тогда

		Если ЗначениеЗаполнено(ГоловнойКонтрагент.ГоловнойКонтрагент) 
				И ГоловнойКонтрагент.ГоловнойКонтрагент <> ГоловнойКонтрагент Тогда
				
			Сообщение = Новый СообщениеПользователю;
			ТекстНСТР = НСтр("en='Counterparty %1 cannot be selected as main,"
"since it has already been appointed main counterparty %2!';ru='Контрагент %1 не может быть выбран головным,"
"так как для него уже был назначен головной контрагент %2!'");
			Сообщение.Текст = СтрШаблон(ТекстНСТР, СокрЛП(ГоловнойКонтрагент), СокрЛП(ГоловнойКонтрагент.ГоловнойКонтрагент));
			Сообщение.Поле = "ГоловнойКонтрагент";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
				
			Отказ = Истина;
		Иначе

			// Надо проверить, что если указываем головного контрагента, то этот элемент уже не был установлен
			// в качестве головного у другого контрагента.
			ВыборкаПоГоловномуКонтрагенту = ПолучитьКонтрагентовПоЗаданномуГоловномуКонтрагенту(Ссылка).Выбрать();
			Если ВыборкаПоГоловномуКонтрагенту.Количество() <> 0 Тогда

				ТекстНСТР = НСтр("en='Counterparty %1 cannot have main counterparty!"
"This counterparty is already installed as main for:';ru='Контрагент %1 не может иметь головного контрагента!"
"Этот контрагент уже установлен головным для:'") + " ";
				СообщениеОНевозможностиЗаписи = СтрШаблон(ТекстНСТР,СокрЛП(ЭтотОбъект));
				Пока ВыборкаПоГоловномуКонтрагенту.Следующий() Цикл
					СообщениеОНевозможностиЗаписи = СообщениеОНевозможностиЗаписи + Символы.ПС 
						+ СокрЛП(ВыборкаПоГоловномуКонтрагенту.Контрагент);
				КонецЦикла;

				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СообщениеОНевозможностиЗаписи;
				Сообщение.Поле = "ГоловнойКонтрагент";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();
					
				Отказ = Истина;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СсылкаКонтрагента()
	
	Если ЭтоНовый() Тогда
		СсылкаКонтрагента = ПолучитьСсылкуНового();
		Если НЕ ЗначениеЗаполнено(СсылкаКонтрагента) Тогда
			СсылкаКонтрагента = Справочники.Контрагенты.ПолучитьСсылку();
		КонецЕсли;
		УстановитьСсылкуНового(СсылкаКонтрагента);
	Иначе
		СсылкаКонтрагента = Ссылка;
	КонецЕсли;
	
	Возврат СсылкаКонтрагента;
	
КонецФункции 

#КонецОбласти

#КонецЕсли