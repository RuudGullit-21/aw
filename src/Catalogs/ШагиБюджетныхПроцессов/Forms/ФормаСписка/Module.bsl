
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Владелец") Тогда
			МодельБюджетирования = Параметры.Отбор.Владелец;
			СсылкаНаМодель = ПолучитьНавигационнуюСсылку(МодельБюджетирования);
			Элементы.НадписьМодельБюджетирования.Заголовок = 
				Новый ФорматированнаяСтрока(
					НСтр("en='Модель бюджетирования: ';ru='Модель бюджетирования: '"),
					Новый ФорматированнаяСтрока(Строка(МодельБюджетирования), , , , СсылкаНаМодель),
					" ( ",
					Новый ФорматированнаяСтрока(НСтр("en='Обзор бюджетного процесса';ru='Обзор бюджетного процесса'"), , , , "Обзор"),
					" )");
			
		КонецЕсли;
	КонецЕсли;
	
	МодельОпределенаОтбором = ЗначениеЗаполнено(МодельБюджетирования);
	Элементы.СведенияОМоделиБюджетирования.Видимость = МодельОпределенаОтбором;
	Элементы.МодельБюджетирования.Видимость = Не МодельОпределенаОтбором;
	
	Если Не ЗначениеЗаполнено(МодельБюджетирования) Тогда
		МодельБюджетирования = Справочники.МоделиБюджетирования.МодельБюджетированияПоУмолчанию();
	КонецЕсли;
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Владелец", МодельБюджетирования, ЗначениеЗаполнено(МодельБюджетирования));
	
	//СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МодельБюджетированияПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Владелец", МодельБюджетирования, ЗначениеЗаполнено(МодельБюджетирования));
КонецПроцедуры

&НаКлиенте
Процедура НадписьМодельБюджетированияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "Обзор" Тогда
		ОткрытьФорму("Обработка.УправлениеБюджетнымПроцессом.Форма", 
				Новый Структура("МодельБюджетирования, СформироватьПриОткрытии", МодельБюджетирования, Истина), 
				ЭтотОбъект, 
				МодельБюджетирования);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СместитьЭлемент(Ссылка, Направление)
	
	Справочники.ШагиБюджетныхПроцессов.СдвинутьЭлемент(Ссылка, Направление);
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВыше(Команда)
	
	СместитьЭлемент(Элементы.Список.ТекущаяСтрока, -1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьНиже(Команда)
	
	СместитьЭлемент(Элементы.Список.ТекущаяСтрока, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если МодельБюджетирования.Пустая() Тогда
		ТекстНСТР = НСтр("en='Не указана модель бюджетирования';ru='Не указана модель бюджетирования'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	//СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	Заглушка = Истина;
	
КонецПроцедуры

#КонецОбласти

