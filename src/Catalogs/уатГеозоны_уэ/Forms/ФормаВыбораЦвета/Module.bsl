
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуЦветов();
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("Красный") И Параметры.Свойство("Зеленый") И Параметры.Свойство("Синий") Тогда 
		ВыбранныйЦвет = Новый Цвет(Параметры.Красный, Параметры.Зеленый, Параметры.Синий);
		
		НайдСтроки = ЦветаЗон.НайтиСтроки(Новый Структура("ЦветЗоны", ВыбранныйЦвет));
		
		Если НайдСтроки.Количество() Тогда 
			Элементы.ЦветаЗон.ТекущаяСтрока = НайдСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЦветаЗон

&НаКлиенте
Процедура ЦветаЗонВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Закрыть(Новый Структура("Красный, Зеленый, Синий", Элемент.ТекущиеДанные.ЦветЗоны.Красный, Элемент.ТекущиеДанные.ЦветЗоны.Зеленый, Элемент.ТекущиеДанные.ЦветЗоны.Синий));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьЦвет(Команда)
	
	ТекСтрока = Элементы.ЦветаЗон.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Закрыть(Новый Структура("Красный, Зеленый, Синий", ТекСтрока.ЦветЗоны.Красный, ТекСтрока.ЦветЗоны.Зеленый, ТекСтрока.ЦветЗоны.Синий));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуЦветов()
	
	Макет = Справочники.уатГеозоны_уэ.ПолучитьМакет("ВебЦветаВRGB");
	
	Для ИндексСтроки = 1 По 146 Цикл
		Красный = Число(Макет.Область("R"+Строка(ИндексСтроки)+"C1").Текст);
		Зеленый = Число(Макет.Область("R"+Строка(ИндексСтроки)+"C2").Текст);
		Синий   = Число(Макет.Область("R"+Строка(ИндексСтроки)+"C3").Текст);
		
		НовСтрокаЦвет = ЦветаЗон.Добавить();
		НовСтрокаЦвет.ЦветЗоны = Новый Цвет(Красный, Зеленый, Синий);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Для Каждого ТекЦвет Из ЦветаЗон Цикл
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЦветаЗон.ЦветЗоны");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ТекЦвет.ЦветЗоны;
		
		ОформлПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформлПоле.Поле = Новый ПолеКомпоновкиДанных("ЦветаЗонЦветЗоны");
		ОформлПоле.Использование = Истина;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ТекЦвет.ЦветЗоны);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ТекЦвет.ЦветЗоны);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", "");
		ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
