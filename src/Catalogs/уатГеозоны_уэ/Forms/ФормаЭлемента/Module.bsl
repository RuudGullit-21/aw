
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если НЕ Объект.ЭтоГруппа Тогда
		ПрочитатьЦветЗоны(Объект.Ссылка);
	КонецЕсли;
		
	ЭтоКОРП = уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП();
	Элементы.Страницы.Видимость      = ЭтоКОРП;
	Элементы.ГруппаЗаливка.Видимость = ЭтоКОРП;
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбновлятьПривязкуПунктовПриЗаписи = Истина;
	КонецЕсли;
	ПрочитатьПунктыГеозоны();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьЦветЗоны(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьПривязкуПунктовПриЗаписи(ТекущийОбъект);
	ПрочитатьПунктыГеозоны();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьЭлементаЗоны",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЦветЗоныНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КрасныйЦвет = ?(ЦветЗоны.Красный=-1, 255, ЦветЗоны.Красный);
	ЗеленыйЦвет = ?(ЦветЗоны.Зеленый=-1, 255, ЦветЗоны.Зеленый);
	СинийЦвет   = ?(ЦветЗоны.Синий=-1,   255, ЦветЗоны.Синий);
	
	ПараметрыФормы = Новый Структура("Красный, Зеленый, Синий", КрасныйЦвет, ЗеленыйЦвет, СинийЦвет);
	
	ОповеститьОВыбореЦвета = Новый ОписаниеОповещения("ЦветЗоныПослеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.уатГеозоны_уэ.Форма.ФормаВыбораЦвета", ПараметрыФормы, Элемент,,,, ОповеститьОВыбореЦвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветЗоныПослеВыбора(Результат, ДопПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда 
		ЦветЗоны = Новый Цвет(Результат.Красный, Результат.Зеленый, Результат.Синий);
		ЭтотОбъект.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоординатыПриИзменении(Элемент)
	ОбновлятьПривязкуПунктовПриЗаписи = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяПриИзменении(Элемент) 
	Если ЕстьОсновнаяГеозона(Объект.Ссылка) Тогда  
		ТекстНСТР = НСтр("ru='Сменить основную геозону?'");
		Оповещение = Новый ОписаниеОповещения("ИспользоватьОсновнойОтветНаВопрос", ЭтотОбъект, Объект.Ссылка);
		ПоказатьВопрос(Оповещение, ТекстНСТР, РежимДиалогаВопрос.ДаНет);
	Иначе
		ИспользоватьКакОсновнаяГеозона(Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОпределитьПункты(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, "Перед автоопределением пунктов геозоны необходимо записать элемент");
		Возврат;
	КонецЕсли;
	
	Оповещ = Новый ОписаниеОповещения("ОпределитьПунктыВопрос", ЭтотОбъект);
	ПоказатьВопрос(Оповещ,
		"Существующая привязка пунктов к данной геозоне будет удалена!
		|Операция может занять длительное время!
		|Продолжить?", РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПунктыВопрос(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		уатОбщегоНазначения_уэ.уатПривязкаПунктовНазначенияКГеозонам(, Объект.Ссылка);
		ПрочитатьПунктыГеозоны();
		ОбновлятьПривязкуПунктовПриЗаписи = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьЦветЗоны(Знач Геозона)
	
	Цвет = Геозона.ЦветЗоныХранилище.Получить();
	Если Цвет = Неопределено Тогда 
		ЦветЗоны = Новый Цвет();
	Иначе 
		ЦветЗоны = Цвет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЦветЗоны(ТекущийОбъект)
	
	Если НЕ ТекущийОбъект.Ссылка.ЭтоГруппа Тогда
		ТекущийОбъект.ЦветЗоныХранилище = Новый ХранилищеЗначения(ЦветЗоны, Новый СжатиеДанных(9));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьПунктыГеозоны()
	
	КоличествоПунктовНазначения = 0;
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(уатПривязкаПунктовНазначенияКГеозонам.ПунктНазначения) КАК Количество
	|ИЗ
	|	РегистрСведений.уатПривязкаПунктовНазначенияКГеозонам_уэ КАК уатПривязкаПунктовНазначенияКГеозонам
	|ГДЕ
	|	уатПривязкаПунктовНазначенияКГеозонам.Геозона = &Геозона");
	Запрос.УстановитьПараметр("Геозона", Объект.Ссылка);
	
	ВыборкаПункты = Запрос.Выполнить().Выбрать();
	Если ВыборкаПункты.Количество() > 0 Тогда
		ВыборкаПункты.Следующий();
		КоличествоПунктовНазначения = ВыборкаПункты.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПривязкуПунктовПриЗаписи(ТекущийОбъект)
	
	Если ОбновлятьПривязкуПунктовПриЗаписи Тогда
		флКоординатыЗаданы = Истина;
		Если ТекущийОбъект.Координаты.Количество() < 3 Тогда
			флКоординатыЗаданы = Ложь;
		Иначе
			Для Каждого ТекСтрока Из ТекущийОбъект.Координаты Цикл
				Если ТекСтрока.Лат = 0 ИЛИ ТекСтрока.Лон = 0 Тогда
					флКоординатыЗаданы = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если флКоординатыЗаданы Тогда
			уатОбщегоНазначения_уэ.уатПривязкаПунктовНазначенияКГеозонам(, Объект.Ссылка);
			ОбновлятьПривязкуПунктовПриЗаписи = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИспользоватьКакОсновнаяГеозона(Знач Геозона)
	
	Справочники.уатГеозоны_уэ.ИспользоватьКакОсновнаяГеозона(Геозона, Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьОсновнаяГеозона(Знач Геозона)
	
	Возврат Справочники.уатГеозоны_уэ.ЕстьОсновнаяГеозона(Геозона);
	
КонецФункции

&НаКлиенте
Процедура ИспользоватьОсновнойОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ИспользоватьКакОсновнаяГеозона(Объект.Ссылка);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
