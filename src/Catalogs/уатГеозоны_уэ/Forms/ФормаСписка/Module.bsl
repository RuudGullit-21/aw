
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаскраситьСписокЗон();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьЭлементаЗоны" Тогда 
		РаскраситьСписокЗон();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") Тогда
		Если ИсточникВыбора.ИмяФормы = "Справочник.уатГеозоны_уэ.Форма.ПодборИзКлассификатора" Тогда
			Элементы.Список.Обновить();
			Элементы.Список.ТекущаяСтрока = ВыбранноеЗначение;
			РаскраситьСписокЗон();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

 #Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИспользоватьОсновной(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Элементы.ФормаИспользоватьОсновной.Пометка Тогда 
		Если ЕстьОсновнаяГеозона(ТекущиеДанные.Ссылка) Тогда  
			ТекстНСТР = НСтр("ru='Сменить основную геозону?'");
			Оповещение = Новый ОписаниеОповещения("ИспользоватьОсновнойОтветНаВопрос", ЭтотОбъект, ТекущиеДанные.Ссылка);
			ПоказатьВопрос(Оповещение, ТекстНСТР, РежимДиалогаВопрос.ДаНет);
		Иначе
			ИспользоватьКакОсновнаяГеозона(ТекущиеДанные.Ссылка);
			Элементы.Список.Обновить();
			УправлениеФормойКлиент(); 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборИзКлассификатора(Команда)
	
	ИмяФормыПодбора = "Справочник.уатГеозоны_уэ.Форма.ПодборИзКлассификатора";
	ОткрытьФорму(ИмяФормыПодбора, , ЭтотОбъект);

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ИспользоватьКакОсновнаяГеозона(Знач Геозона)
	
	Справочники.уатГеозоны_уэ.ИспользоватьКакОсновнаяГеозона(Геозона);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьОсновнаяГеозона(Знач Геозона)
	
	Возврат Справочники.уатГеозоны_уэ.ЕстьОсновнаяГеозона(Геозона);
	
КонецФункции

&НаКлиенте
Процедура ИспользоватьОсновнойОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ИспользоватьКакОсновнаяГеозона(ДополнительныеПараметры);
		Элементы.Список.Обновить();
		УправлениеФормойКлиент();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ЭтоГруппа Тогда
		Элементы.ФормаИспользоватьОсновной.Пометка     = Ложь;
	Иначе
		Элементы.ФормаИспользоватьОсновной.Пометка     = ТекущиеДанные.Основная;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РаскраситьСписокЗон()
	
	СписокУдаляемыхЭлементов = Новый СписокЗначений;
	Для Каждого ЭлементУсловногоОформления Из Список.УсловноеОформление.Элементы Цикл
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" Тогда
			СписокУдаляемыхЭлементов.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из СписокУдаляемыхЭлементов Цикл
		Список.УсловноеОформление.Элементы.Удалить(Элемент.Значение);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатГеозоны_уэ.Ссылка,
	|	уатГеозоны_уэ.Наименование,
	|	уатГеозоны_уэ.ЦветЗоныХранилище
	|ИЗ
	|	Справочник.уатГеозоны_уэ КАК уатГеозоны_уэ
	|ГДЕ
	|	уатГеозоны_уэ.ЭтоГруппа = ЛОЖЬ";
	
	ВыборкаЦвета = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЦвета.Следующий() Цикл 
		хзЦветФона = ВыборкаЦвета.ЦветЗоныХранилище;
		Если хзЦветФона = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		ЦветФона = хзЦветФона.Получить();
		Если ЦветФона = Неопределено Или Не ТипЗнч(ЦветФона) = Тип("Цвет") Тогда 
			Продолжить;
		КонецЕсли;
		
		ЭлементУсловногоОформления = Список.УсловноеОформление.Элементы.Добавить();
		
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ВыборкаЦвета.Ссылка;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФона);
		ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
		ЭлементУсловногоОформления.Представление = НСтр("en='By zone color';ru='По цвету зоны'") + " " + ВыборкаЦвета.Наименование;
		
		ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ЦветЗоны");
		ПолеОформления.Использование = Истина;
	КонецЦикла;
	
КонецПроцедуры // РаскраситьСписокЗон()

#КонецОбласти
