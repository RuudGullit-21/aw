
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет заполнение предопределенных элементов справочника
//
Процедура ЗаполнитьПредопределенныеДанные() Экспорт
	Макет = Справочники.уатВидыИспользованияРабочегоВремени.ПолучитьМакет("НастройкиПредопределенныхЭлементов");
	ОбластьНастроек = Макет.ПолучитьОбласть("Элементы");
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		ИмяПредопрЭлемента = СтрЗаменить(СокрЛП(ОбластьНастроек.Область(НомерСтроки, 1, НомерСтроки, 1).Текст), "ИмяЭлемента_", "");
		Попытка
			ТекЭлемент = Справочники.уатВидыИспользованияРабочегоВремени[ИмяПредопрЭлемента];
		Исключение
			Продолжить;
		КонецПопытки;
		СтрНаименование			 = СокрЛП(Макет.Область(НомерСтроки, 2).Текст);
		СтрКод					 = СокрЛП(Макет.Область(НомерСтроки, 3).Текст);
		СтрБуквКод				 = СтрЗаменить(СокрЛП(Макет.Область(НомерСтроки, 4).Текст), "БКод_", "");
		СтрПолноеНаименование	 = СокрЛП(Макет.Область(НомерСтроки, 5).Текст);
		СтрРабочееВремя			 = НРег(СокрЛП(Макет.Область(НомерСтроки, 6).Текст)) = "1";
		СтрЦелодневныйПростой	 = НРег(СокрЛП(Макет.Область(НомерСтроки, 7).Текст)) = "1";
		
		Если (ТекЭлемент.Наименование <> СтрНаименование)
			ИЛИ (ТекЭлемент.БуквенныйКод <> СтрБуквКод)
			ИЛИ (ТекЭлемент.ЦифровойКод <> СтрКод)
			ИЛИ (ТекЭлемент.ПолноеНаименование <> СтрПолноеНаименование) 
			ИЛИ (ТекЭлемент.ИспользоватьКакРабочееВремя <> СтрРабочееВремя) 
			ИЛИ (ТекЭлемент.ИспользоватьКакЦелодневныйПростой <> СтрЦелодневныйПростой) Тогда
			
			СпрОбъект = ТекЭлемент.ПолучитьОбъект();
			ОписатьКодИспользованияРабочегоВремени(СпрОбъект,СтрБуквКод, СтрКод, СтрНаименование, СтрПолноеНаименование, СтрРабочееВремя, СтрЦелодневныйПростой);
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет поля элемента справочника "уатВидыИспользованияРабочегоВремени"
//
Процедура ОписатьКодИспользованияРабочегоВремени(КодИспользованияРабочегоВремени, Знач БуквенныйКод, Знач  ЦифровойКод, Знач  Наименование = Неопределено, Знач  ПолноеНаименование, Знач  СтрРабочееВремя, Знач СтрЦелодневныйПростой)
	
	КодИспользованияРабочегоВремени.БуквенныйКод = СокрЛП(БуквенныйКод);
	КодИспользованияРабочегоВремени.ЦифровойКод  = СокрЛП(ЦифровойКод);
	КодИспользованияРабочегоВремени.ПолноеНаименование = СокрЛП(ПолноеНаименование);
	КодИспользованияРабочегоВремени.Наименование = СокрЛП(?(Наименование = Неопределено,ПолноеНаименование,Наименование));
	КодИспользованияРабочегоВремени.ИспользоватьКакРабочееВремя = СтрРабочееВремя;
	КодИспользованияРабочегоВремени.ИспользоватьКакЦелодневныйПростой = СтрЦелодневныйПростой;
	КодИспользованияРабочегоВремени.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли