#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
 
	Если ЭтотОбъект.ЭтоГруппа Тогда

		НайденыДубли = ПроверитьДубликатГруппыПоИндексу();
		Если НайденыДубли Тогда
			Сообщение   	= Новый СообщениеПользователю();
			Сообщение.Текст = "Группа справочника с индексом " + ЭтотОбъект.Индекс + " уже существует!";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		// Изменение индекса у подчиненных элементов группы
		Если НЕ ЭтотОбъект.Ссылка.Пустая() Тогда
			ИндексДоЗаписи    = ЭтотОбъект.Ссылка.Индекс;
			ИндексПослеЗаписи = ЭтотОбъект.Индекс;
			Если ИндексДоЗаписи <> ИндексПослеЗаписи Тогда
				ТекВыборка = Справочники.ItobВидыГруппировок.ВыбратьИерархически(ЭтотОбъект.Ссылка);  
				Пока ТекВыборка.Следующий() Цикл
					ЭлементОбъект 			= ТекВыборка.Ссылка.ПолучитьОбъект();
					ЭлементОбъект.Индекс 	= ИндексПослеЗаписи;
					ЭлементОбъект.Записать();
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
          
КонецПроцедуры
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
	
Функция ПроверитьДубликатГруппыПоИндексу()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ItobВидыГруппировок.Ссылка,
	               |	ItobВидыГруппировок.ЭтоГруппа,
	               |	ItobВидыГруппировок.Индекс
	               |ИЗ
	               |	Справочник.ItobВидыГруппировок КАК ItobВидыГруппировок
	               |ГДЕ
	               |	ItobВидыГруппировок.Индекс = &ТекИндекс
	               |	И ItobВидыГруппировок.ЭтоГруппа = ИСТИНА
	               |	И ItobВидыГруппировок.Ссылка <> &ТекСсылка";
	
	Запрос.УстановитьПараметр("ТекИндекс", ЭтотОбъект.Индекс);
	Запрос.УстановитьПараметр("ТекСсылка", ЭтотОбъект.Ссылка);
	ТабРезультат = Запрос.Выполнить().Выгрузить();
	Если ТабРезультат.Количество() > 0 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
