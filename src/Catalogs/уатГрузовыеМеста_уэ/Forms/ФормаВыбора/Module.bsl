
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Контейнер = Ложь;
	Если Параметры.Свойство("Контейнер") Тогда
		АвтоЗаголовок = Ложь;
		Элементы.ТипГрузовогоМеста.Видимость = Ложь;
		Если Параметры.Контейнер Тогда
			Заголовок = НСтр("ru='Контейнеры'");
			Элементы.ТипКонтейнера.Видимость = Истина;
			Элементы.ВидУпаковки.Видимость   = Ложь;
			Контейнер = Истина;
		Иначе
			Заголовок = НСтр("ru='Грузовые места'");
			Элементы.ТипКонтейнера.Видимость = Ложь;
			Элементы.ВидУпаковки.Видимость   = Истина;
		КонецЕсли;
	Иначе
		ТипГрузовогоМеста = "Грузовое место";
		Заголовок = НСтр("ru='Грузовые места'");
	КонецЕсли;
	
	ПредставлениеЕдиницыИзмеренияВеса = уатОбщегоНазначенияПовтИсп.ПолучитьПредставлениеОсновнойЕдиницыИзмеренияВеса();
	Если ЗначениеЗаполнено(ПредставлениеЕдиницыИзмеренияВеса) Тогда
		Элементы.ВесБрутто.Заголовок = НСтр("en='Weight';ru='Вес'") + ", " + ПредставлениеЕдиницыИзмеренияВеса;
	КонецЕсли;
	
	ПредставлениеЕдиницыИзмеренияОбъема = уатОбщегоНазначенияПовтИсп.ПолучитьПредставлениеОсновнойЕдиницыИзмеренияОбъема();
	Если ЗначениеЗаполнено(ПредставлениеЕдиницыИзмеренияОбъема) Тогда
		Элементы.Объем.Заголовок = НСтр("en='Volume';ru='Объем'") + ", " + ПредставлениеЕдиницыИзмеренияОбъема;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокГрузовыхМест") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка",
			Параметры.СписокГрузовыхМест,	ВидСравненияКомпоновкиДанных.ВСписке, "ВыбиратьИзРодительскогоЗаказа", Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный, "Выбирать только грузовые места из родительского заказа"
		);
		Элементы.ГрузовыеМестаРодительскогоЗаказа.Видимость = Истина;
		ГрузовыеМестаРодительскогоЗаказа = Истина;
	Иначе
		Элементы.ГрузовыеМестаРодительскогоЗаказа.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокРазмещенныхГрузовыхМест")
		И Параметры.СписокРазмещенныхГрузовыхМест.Количество() <> 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка",
			Параметры.СписокРазмещенныхГрузовыхМест,	ВидСравненияКомпоновкиДанных.НеВСписке,, Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	УстановитьОтборГрузовогоМеста();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГрузовыеМестаРодительскогоЗаказаПриИзменении(Элемент)
	Для Каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		Если ЭлементОтбора.Представление = "ВыбиратьИзРодительскогоЗаказа" Тогда
			ЭлементОтбора.Использование = ГрузовыеМестаРодительскогоЗаказа;
			Элементы.Список.Обновить();
			Возврат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ТипГрузовогоМестаПриИзменении(Элемент)
	УстановитьОтборГрузовогоМеста();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборГрузовогоМеста()
	Если ЗначениеЗаполнено(ТипГрузовогоМеста) Тогда
		Если ТипГрузовогоМеста = "Грузовое место" Тогда
			Контейнер = Ложь;
			Заголовок = НСтр("ru='Грузовые места'");
		Иначе
			Контейнер = Истина;
			Заголовок = НСтр("ru='Контейнеры'");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Контейнер", Контейнер,,, Истина);
КонецПроцедуры

#КонецОбласти

