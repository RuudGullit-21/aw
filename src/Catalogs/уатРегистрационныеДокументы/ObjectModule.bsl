#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьОткрытыеКатегории() Экспорт
	ОткрытыеКатегории = "";
	Если ВидДокумента.УчетКатегорий Тогда
		списКатегории = Новый СписокЗначений;
		Для Каждого ТекКатегория Из Категории Цикл
			Если ТекКатегория.Открыта Тогда
				списКатегории.Добавить(СокрЛП(ТекКатегория.Категория));
			КонецЕсли;
		КонецЦикла;
		списКатегории.СортироватьПоЗначению();
		
		Для Каждого ТекЭлем Из списКатегории Цикл
			ОткрытыеКатегории = ОткрытыеКатегории + ТекЭлем.Значение + ", ";
		КонецЦикла;
		Если НЕ ПустаяСтрока(ОткрытыеКатегории) Тогда
			ОткрытыеКатегории = Лев(ОткрытыеКатегории, СтрДлина(ОткрытыеКатегории)-2);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если НЕ ЗначениеЗаполнено(Наименование) Тогда 
			Наименование = уатНастройкиШаблонов.СформироватьНаименованиеПоШаблону("Справочник_уатРегистрационныеДокументы", ЭтотОбъект);
		КонецЕсли;
	
		ЗаполнитьОткрытыеКатегории();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли