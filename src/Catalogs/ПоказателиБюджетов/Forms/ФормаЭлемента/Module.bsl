
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	//СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	УправлениеФормой(ЭтотОбъект);

	//МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ПоказателиБюджетов", , Объект.Ссылка);

	//МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	//МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	Заглушка = Истина;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	//МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	ЕдиницаИзмеренияОпределяетсяАналитикой = Объект.ЕдиницаИзмеренияОпределяетсяАналитикой;
	ВалютаОпределяетсяАналитикой = Объект.ВалютаОпределяетсяАналитикой;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидАналитикиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьСвязанныеПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ОбщегоНазначенияУТКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УчетПоКоличествуПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УчетПоВалютеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределятьЕдиницуИзмеренияПриИзменении(Элемент)
	
	Если ЕдиницаИзмеренияОпределяетсяАналитикой Тогда
		Объект.ЕдиницаИзмерения = Неопределено;
	Иначе
		Объект.АналитикаЕдиницыИзмерения = Неопределено;
	КонецЕсли;
	
	Объект.ЕдиницаИзмеренияОпределяетсяАналитикой = ЕдиницаИзмеренияОпределяетсяАналитикой;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаОпределяетсяАналитикойПриИзменении(Элемент)
	
	Если Не ВалютаОпределяетсяАналитикой Тогда
		Объект.АналитикаВалюты = Неопределено;
	КонецЕсли;
	
	Объект.ВалютаОпределяетсяАналитикой = ВалютаОпределяетсяАналитикой;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	//СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	Заглушка = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ЭлементыФормы = Форма.Элементы;
	
	Если НЕ ЭлементыФормы.УчитыватьПоКоличеству.ТолькоПросмотр Тогда
		ДоступностьКоличества = Форма.Объект.УчитыватьПоКоличеству;
		ЭлементыФормы.ЕдиницаИзмеренияОпределяетсяАналитикой.Доступность = ДоступностьКоличества;
		ОпределятьЕдиницуИзмерения = Форма.ЕдиницаИзмеренияОпределяетсяАналитикой;
		
		ЭлементыФормы.ЕдиницаИзмерения.Доступность = ДоступностьКоличества И НЕ ОпределятьЕдиницуИзмерения;
		ЭлементыФормы.АналитикаЕдиницыИзмерения.Доступность = ДоступностьКоличества И ОпределятьЕдиницуИзмерения;
	Иначе
		ЭлементыФормы.ЕдиницаИзмеренияОпределяетсяАналитикой.Доступность = Ложь;
		ЭлементыФормы.ЕдиницаИзмерения.Доступность = Ложь;
		ЭлементыФормы.АналитикаЕдиницыИзмерения.Доступность = Ложь;
	КонецЕсли;
	
	Если НЕ ЭлементыФормы.УчитыватьПоВалюте.ТолькоПросмотр Тогда
		ДоступностьВалюты = Форма.Объект.УчитыватьПоВалюте;
		ЭлементыФормы.ВалютаОпределяетсяАналитикой.Доступность = ДоступностьВалюты;
		ВалютаОпределяетсяАналитикой = Форма.ВалютаОпределяетсяАналитикой;
		ЭлементыФормы.АналитикаВалюты.Доступность = ДоступностьВалюты И ВалютаОпределяетсяАналитикой;
	Иначе
		ЭлементыФормы.ВалютаОпределяетсяАналитикой.Доступность = Ложь;
		ЭлементыФормы.АналитикаВалюты.Доступность = Ложь;
	КонецЕсли;
	
	Массив = Новый Массив;
	Для Сч = 1 по 6 Цикл
		Массив.Добавить(Форма.Объект["ВидАналитики" + Сч]);
	КонецЦикла;
	
	ПараметрыВыбора = Форма.Элементы.АналитикаЕдиницыИзмерения.ПараметрыВыбора;
	МассивВыбора = Новый Массив(ПараметрыВыбора);
	МассивВыбора[1] = 
		Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(Массив));
	Форма.Элементы.АналитикаЕдиницыИзмерения.ПараметрыВыбора = Новый ФиксированныйМассив(МассивВыбора);
	
	ПараметрыВыбора = Форма.Элементы.АналитикаВалюты.ПараметрыВыбора;
	МассивВыбора = Новый Массив(ПараметрыВыбора);
	МассивВыбора[1] = 
		Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(Массив));
	Форма.Элементы.АналитикаВалюты.ПараметрыВыбора = Новый ФиксированныйМассив(МассивВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСвязанныеПараметры()
	
	Массив = Новый Массив;
	Для Сч = 1 по 6 Цикл
		Массив.Добавить(Объект["ВидАналитики" + Сч]);
	КонецЦикла;
	
	Если Массив.Найти(Объект.АналитикаВалюты) = Неопределено Тогда
		Объект.АналитикаВалюты = Неопределено;
	КонецЕсли;
	
	Если Массив.Найти(Объект.АналитикаЕдиницыИзмерения) = Неопределено Тогда
		Объект.АналитикаЕдиницыИзмерения = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
