#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	УстановитьОтборВСписке(Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьТерминал(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	Если Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент")  Тогда	
		ЗначенияЗаполнения.Вставить("Модель", Модель);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ItobТерминалы.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборВСписке(Параметры)
	
	Если ПолучитьФункциональнуюОпцию("ItobИспользоватьНесколькоСерверовСбораДанных") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
																			    "СерверСбораДанных",, 
																			    ВидСравненияКомпоновкиДанных.Равно,, 
																				Ложь, 
																				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);	
	КонецЕсли; 
	
	НаДату = ТекущаяДата();
	Если Параметры.Свойство("НаДату") Тогда
		НаДату = Параметры.НаДату;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("НаДату", НаДату);
	
	Если Параметры.Свойство("ВидТерминала") Тогда
		Если Параметры.ВидТерминала = 0 Тогда
			Модель = Справочники.ItobМоделиТерминалов.МобильныйКлиент;
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
																			"Модель",
																			Справочники.ItobМоделиТерминалов.МобильныйКлиент, 
																			ВидСравненияКомпоновкиДанных.Равно,,, 
																			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
																			"Модель",
																			Справочники.ItobМоделиТерминалов.МобильныйКлиент, 
																			ВидСравненияКомпоновкиДанных.НеРавно,,, 
																			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);																
		КонецЕсли;
	КонецЕсли;
																			
КонецПроцедуры
 
#КонецОбласти 
