
#Область ПеременныеМодуля

&НаКлиенте
Перем мФормироватьНаименованиеАвтоматически;  // надо ли формировать автоматически Наименование, или нет
	
#КонецОбласти

#Область ОбработчикиСобытийФормы

// Процедура - При создании на сервере
//
// Параметры:
//  Отказ				 - Булево - Флаг отказа.
//  СтандартнаяОбработка - Булево  - Флаг стандартной обработки.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДоступностьНаСервере();
	ОтображениеПредупрежденияПриРедактированииКодТерминала();
	
КонецПроцедуры

// Процедура - обработчик события "ПередЗаписью" формы.
//
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = СформироватьАвтоНаименование();
	КонецЕсли;
	
	Если Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент") Тогда
		Если ПустаяСтрока(Объект.НомерSimКарты) Тогда
			ПоказатьПредупреждение(, "Не указан номер сим карты");
			Отказ = Истина;
		ИначеЕсли НЕ Лев(Объект.НомерSimКарты,1) = "+" Тогда
			ПоказатьПредупреждение(,"Номер сим карты должен быть указан в международном формате");
			Отказ = Истина;
		Иначе
			// Проверка номера
			СтрокаПроверки = Сред(Объект.НомерSimКарты, 2);
			
			Для Счетчик = 1 По СтрДлина(СтрокаПроверки) Цикл
				
				Если Найти("0123456789", Сред(СтрокаПроверки, Счетчик, 1)) = 0 Тогда
					ПоказатьПредупреждение(, "Номер сим карты должен содержать только знак + и цифры");
					Отказ = Истина;
					Возврат;
				КонецЕсли;	
				
			КонецЦикла; 	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события "ПриИзменении" поля "Код".
//
&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	Объект.Наименование = СформироватьАвтоНаименование();
	мФормироватьНаименованиеАвтоматически = Истина;
		
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Наименование".
//
&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	УстановитьФлагФормироватьНаименованиеАвтоматически();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Модель".
//
&НаКлиенте
Процедура МодельПриИзменении(Элемент)
	
	Объект.Наименование = СформироватьАвтоНаименование();
	мФормироватьНаименованиеАвтоматически = Истина;
	УстановитьВидимость();
	УстановитьДоступностьНаСервере();
	Если Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент") Тогда
		Объект.Код = 0;
		Объект.СерверСбораДанных = ПредопределенноеЗначение("Справочник.ItobСерверыСбораДанных.СВМУ");
	Иначе
		Объект.СерверСбораДанных = ПредопределенноеЗначение("Справочник.ItobСерверыСбораДанных.Основной");
	КонецЕсли;
	ОтображениеПредупрежденияПриРедактированииКодТерминала();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерSimКартыПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Модель) Тогда
		Если Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент") Тогда
			Объект.IMEI = СформироватьIMEIПоНомерSimКарты();
		КонецЕсли;
		Объект.Наименование = СформироватьАвтоНаименование();
		мФормироватьНаименованиеАвтоматически = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МодельНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент")
		 И ТерминалПривязан(Объект.Ссылка) Тогда
		 
		СтандартнаяОбработка = Ложь;
		ПараметрыОткрытияФормыВыбора = Новый Структура;
		ПараметрыОткрытияФормыВыбора.Вставить("ИсключитьМобильныйКлиент", Истина);
		ОткрытьФорму("Справочник.ItobМоделиТерминалов.ФормаВыбора", ПараметрыОткрытияФормыВыбора, Элементы.Модель);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДатчикиПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура УсреднятьЗначениеПоТопливуПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьКоличествоДатчиковТоплива()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	Результат = 0;	
	
	Для каждого ТекущаяСтрока Из ТекОбъект.Датчики Цикл
		Если ТекущаяСтрока.Назначение.ДатчикТоплива Тогда
		    Результат = Результат + 1;
		КонецЕсли;		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // ПолучитьКоличествоДатчиковТоплива()

// Функция формирует наименование терминала.
//
&НаКлиенте
Функция СформироватьАвтоНаименование()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	Если Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент") Тогда
		КодУстройства = Объект.IMEI;
	Иначе
		КодУстройства = Объект.Код;
	КонецЕсли;
	
	СтрокаНаименования = ?(Объект.Модель.Пустая(), Формат(КодУстройства, "ЧГ=0"),
	              Строка(Объект.Модель)+" ("+Формат(КодУстройства, "ЧГ=0")+")");
				  
	СтрокаНаименования = Лев(СтрокаНаименования, 50);
	
	Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Если НЕ Объект.Модель.Пустая() Тогда
		
		СтрокаНаименования2 = Формат(КодУстройства, "ЧГ=0")+ " ("+Строка(Объект.Модель)+")";
		СтрокаНаименования2 = Лев(СтрокаНаименования2, 50);
	
		Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования2);		
	
	КонецЕсли;
			
	Возврат СтрокаНаименования;

КонецФункции // СформироватьАвтоНаименование()

// Процедура проверяет, совпадало ли ранее наименование с кодом,
// и присваивает соответствующее значение переменной мФормироватьНаименованиеАвтоматически.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура УстановитьФлагФормироватьНаименованиеАвтоматически()

	Если ПустаяСтрока(Объект.Наименование) 
	 ИЛИ Объект.Наименование = Формат(Объект.Код, "ЧГ=0")
	 ИЛИ Объект.Наименование = СформироватьАвтоНаименование() Тогда
		мФормироватьНаименованиеАвтоматически = Истина;
	Иначе
		мФормироватьНаименованиеАвтоматически = Ложь;
	КонецЕсли;

КонецПроцедуры // УстановитьФлагФормироватьНаименованиеАвтоматически()

// Процедура устанавливает видимость элементов формы
//
&НаКлиенте
Процедура УстановитьВидимость()
	
	КоличествоДатчиковТоплива = ПолучитьКоличествоДатчиковТоплива();
	
	Если КоличествоДатчиковТоплива > 1 Тогда
		Элементы.УсреднятьЗначениеПоТопливу.Видимость = Истина;
		Элементы.НадписьУсреднение.Видимость = Истина;
		Если Объект.УсреднятьЗначениеПоТопливу Тогда
			Элементы.НадписьУсреднение.Заголовок = НСтр("ru = 'Данные по всем датчикам топлива будут усреднены 
												   |(датчики топлива установлены в одном баке)'");
		Иначе	
			Элементы.НадписьУсреднение.Заголовок = НСтр("ru = 'Данные по всем датчикам топлива будут суммироваться
												   |(датчики топлива установлены в разных баках)'");
		КонецЕсли; 
	Иначе
		Элементы.УсреднятьЗначениеПоТопливу.Видимость = Ложь;
		Элементы.НадписьУсреднение.Видимость = Ложь;
	КонецЕсли; 	
	
	Элементы.IMEI.Видимость = Не Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент");
	Элементы.МаксимальнаяСкоростьВыдачиТоплива.Видимость = Не Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент");
	
КонецПроцедуры // УстановитьВидимость()

// Процедура - Установить доступность
//
&НаСервере
Процедура УстановитьДоступностьНаСервере()
	
	ЭтоПривязанныйТерминал = ТерминалПривязан(Объект.Ссылка);
	
	Элементы.ПредупреждениеДляМодели.Видимость = Ложь;
	Если Объект.Модель = Справочники.ItobМоделиТерминалов.МобильныйКлиент Тогда
		Элементы.НадписьПояснениеККоду.Заголовок = "Заполняется автоматически после регистрации
												   |(также доступен ручной ввод кода)";
		
		Если ЭтоПривязанныйТерминал Тогда	
			Элементы.Модель.ТолькоПросмотр = Истина;
			Элементы.ПредупреждениеДляМодели.Видимость = Истина;
			Элементы.Наименование.ТолькоПросмотр = Истина;
			Элементы.IMEI.ТолькоПросмотр = Истина;
			Элементы.НомерSimКарты.ТолькоПросмотр = Истина;
			Элементы.СерверСбораДанных.ТолькоПросмотр = Истина;		
		КонецЕсли;  
	Иначе
		Элементы.НадписьПояснениеККоду.Заголовок = "Вводится вручную, должен соответствовать коду терминала на сервере IMCS";
		
		Если ЭтоПривязанныйТерминал Тогда	 
			Элементы.ПредупреждениеДляМодели.Подсказка = "Для терминала, не являющегося мобильным клиентом, и имеющего действующую привязку, 
														 |изменение модели доступно на любое значение, кроме предопределенной модели ""Мобильный клиент"""; 
		    Элементы.ПредупреждениеДляМодели.Видимость = Истина;
		КонецЕсли;  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьIMEIПоНомерSimКарты()
	
	IMEI = СокрЛП(СтрЗаменить(Объект.НомерSimКарты, "+", ""));
	Если Лев(IMEI,1) = "8" Тогда
		IMEI = "7" + Сред(IMEI, 2);
	КонецЕсли;	
	
	Возврат IMEI;	
	
КонецФункции

&НаСервере
Процедура ОтображениеПредупрежденияПриРедактированииКодТерминала()
	
	Если Объект.Модель = ПредопределенноеЗначение("Справочник.ItobМоделиТерминалов.МобильныйКлиент") Тогда
		Элементы.Код.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе 
		Элементы.Код.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТерминалПривязан(СсылкаНаТерминал)
	
	Если СсылкаНаТерминал.Пустая() Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Терминал", СсылкаНаТерминал);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ItobПривязкиТрекеров.Период КАК Период
	|ИЗ
	|	РегистрСведений.ItobПривязкиТрекеров КАК ItobПривязкиТрекеров
	|ГДЕ
	|	ItobПривязкиТрекеров.Терминал = &Терминал";
	
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции
  
#КонецОбласти
