
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//об = РеквизитФормыВЗначение("Объект");
	//уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Об, Отказ, СтандартнаяОбработка,ДопПараметрыОткрытие);
	//ЗначениеВРеквизитФормы(Об,"Объект");
	//Если Отказ тогда
	//	Возврат;
	//КонецЕсли;
	
	Объект.ВалютаЦены = Константы.ВалютаУправленческогоУчета.Получить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	//Если Отказ Тогда
	//	Возврат;
	//КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда 
		Объект.ПорядокОкругления = ПредопределенноеЗначение("Перечисление.ПорядкиОкругления.Окр0_01");
	КонецЕсли;
	
	// Заполнить список выбора номенклатуры
	Элементы.ВидТипаЦен.СписокВыбора.Добавить("Базовый");
	Элементы.ВидТипаЦен.СписокВыбора.Добавить("Динамический");
	Элементы.ВидТипаЦен.СписокВыбора.Добавить("Расчетный");

	ЗаполнитьВидТипаЦенПоПризнакам();

	УстановитьПодсказки();
	
	УстановитьДоступность();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если (ЭтаФорма.ВидТипаЦен = "Расчетный" Или ЭтаФорма.ВидТипаЦен = "Динамический")
	   И Не ЗначениеЗаполнено(Объект.БазовыйТипЦен) Тогда
		ТекстНСТР = НСтр("en='Not filled base type of prices!';ru='Не заполнен базовый тип цен!'");
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидТипаЦенПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ЭтаФорма.ВидТипаЦен) Тогда
		ЭтаФорма.ВидТипаЦен = "Базовый";
	КонецЕсли;

	ЗаполнитьПризнакиПоВидуТипаЦен();

	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Процедура заполняет по реквизиту формы ВидТипаЦен требуемые реквизиты справочника
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура ЗаполнитьПризнакиПоВидуТипаЦен()

	Если ЭтаФорма.ВидТипаЦен = "Базовый" Тогда
		Элементы.БазовыйТипЦен.АвтоОтметкаНезаполненного = Ложь;
		
		Объект.БазовыйТипЦен        = Неопределено;
		Объект.ПроцентСкидкиНаценки = 0;
		Объект.Рассчитывается       = Ложь;

	ИначеЕсли ЭтаФорма.ВидТипаЦен = "Динамический" Тогда
		Элементы.БазовыйТипЦен.АвтоОтметкаНезаполненного = Истина;
		
		Объект.Рассчитывается = Истина;
		
	ИначеЕсли ЭтаФорма.ВидТипаЦен = "Расчетный" Тогда
		Элементы.БазовыйТипЦен.АвтоОтметкаНезаполненного = Истина;
		
		Объект.Рассчитывается = Ложь;
	КонецЕсли;

	УстановитьПодсказки();

КонецПроцедуры // ЗаполнитьПризнакиПоВидуТипаЦен()

// Процедура заполняет на форме реквизит ВидТипаЦен по реквизитам элемента
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура ЗаполнитьВидТипаЦенПоПризнакам()

	Если Объект.Рассчитывается Тогда
		ЭтаФорма.ВидТипаЦен = "Динамический";
	Иначе
		Если НЕ ЗначениеЗаполнено(Объект.БазовыйТипЦен) Тогда
			ЭтаФорма.ВидТипаЦен = "Базовый";
		Иначе
			ЭтаФорма.ВидТипаЦен = "Расчетный";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ЗаполнитьВидТипаЦенПоПризнакам()

// Процедура устанавливает подсказки на форме.
//
&НаКлиенте
Процедура УстановитьПодсказки()

	Если ЭтаФорма.ВидТипаЦен = "Базовый" Тогда
		Элементы.ВидТипаЦен.Подсказка = НСтр("en='Prices are assigned and stored for each item of the products and services."
"Based on the prices of this type, prices of other types can be calculated';ru='Цены назначаются и хранятся для каждой позиции номенклатуры."
"На основании цен этого типа могут рассчитываться цены других типов'");
	ИначеЕсли ЭтаФорма.ВидТипаЦен = "Динамический" Тогда
		Элементы.ВидТипаЦен.Подсказка = НСтр("en='Prices are calculated automatically from the prices of the base type';ru='Цены рассчитываются автоматически от цен базового типа'");
		
	ИначеЕсли ЭтаФорма.ВидТипаЦен = "Расчетный" Тогда
		Элементы.ВидТипаЦен.Подсказка = НСтр("en='Prices are assigned and stored for each products and services position';ru='Цены назначаются и хранятся для каждой позиции номенклатуры'");
	КонецЕсли;

КонецПроцедуры // УстановитьНадписи()

// Процедура устанавливает доступность реквизитов формы.
//
&НаКлиенте
Процедура УстановитьДоступность()

	Если ЭтаФорма.ВидТипаЦен = "Базовый" Тогда
		Элементы.БазовыйТипЦен.Доступность               = Ложь;
		Элементы.БазовыйТипЦен.АвтоОтметкаНезаполненного = Ложь;
		Элементы.БазовыйТипЦен.ОтметкаНезаполненного     = Ложь;
		Элементы.ПроцентСкидкиНаценки.Доступность        = Ложь;
		
	ИначеЕсли ЭтаФорма.ВидТипаЦен = "Расчетный" Или ЭтаФорма.ВидТипаЦен = "Динамический" Тогда
		Элементы.БазовыйТипЦен.Доступность               = Истина;
		Элементы.БазовыйТипЦен.АвтоОтметкаНезаполненного = Истина;
		Элементы.ПроцентСкидкиНаценки.Доступность        = Истина;
	КонецЕсли;

КонецПроцедуры // УстановитьДоступность()

#КонецОбласти
