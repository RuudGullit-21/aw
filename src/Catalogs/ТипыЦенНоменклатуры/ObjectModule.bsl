
#Область ОбработчикиСобытий

// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БазовыйТипЦен) Тогда
		// Если цена расчетная и введена на основании расчетной - это неправильно - записывать нельзя
		Если БазовыйТипЦен.Рассчитывается Тогда
			ТекстНСТР = НСтр("en='Base type of prices can not be dynamic!';ru='Базовый тип цен не может быть динамическим!'");
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если Не ЭтоГруппа Тогда
		Если Рассчитывается Тогда
			// Если цена расчетная и на основании её введена уже расчетная - это неправильно - записывать нельзя
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ТекущийТипЦен", ЭтотОбъект.Ссылка);

			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТипЦен.Рассчитывается КАК Рассчитывается,
			|	ТипЦен.БазовыйТипЦен КАК БазовыйТипЦен
			|ИЗ
			|	Справочник.ТипыЦенНоменклатуры КАК ТипЦен
			|ГДЕ
			|	ТипЦен.БазовыйТипЦен = &ТекущийТипЦен
			|	И ТипЦен.Рассчитывается = ИСТИНА";

			ВыборкаЦен = Запрос.Выполнить().Выбрать();
			Если ВыборкаЦен.Следующий() Тогда
				ТекстНСТР = НСтр("en='This type of price is already used as a base, it can not be dynamic anymore!';ru='Этот тип цен уже используется как базовый, он уже не может быть динамическим!'");
				уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, 
					Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
