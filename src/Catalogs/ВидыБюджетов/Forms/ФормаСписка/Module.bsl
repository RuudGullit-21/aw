#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ПараметрОтборПоВладельцу;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Владелец", ПараметрОтборПоВладельцу) Тогда
			МодельБюджетирования = ПараметрОтборПоВладельцу;
			Элементы.МодельБюджетирования.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МодельБюджетирования) Тогда
		МодельБюджетирования = Справочники.МоделиБюджетирования.МодельБюджетированияПоУмолчанию();
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список, "Владелец", МодельБюджетирования, ЗначениеЗаполнено(МодельБюджетирования));

	//СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПросмотретьВидОтчета(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		ТекстНСТР = НСтр("en='Команда не может быть выполнена для указанного объекта';ru='Команда не может быть выполнена для указанного объекта'");
		ПоказатьПредупреждение(, ТекстНСТР);
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ТекущаяСтрока, "Владелец, Ссылка, ЭтоГруппа");
	Если ДанныеСтроки.ЭтоГруппа = Истина Тогда
		ТекстНСТР = НСтр("en='Просмотр отчета по группе элемента невозможен';ru='Просмотр отчета по группе элемента невозможен'");
		ПоказатьПредупреждение(, ТекстНСТР);
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("МодельБюджетирования", ДанныеСтроки.Владелец);
	ПараметрыОтчета.Вставить("ВидБюджета", ДанныеСтроки.Ссылка);
	ПараметрыОтчета.Вставить("СформироватьБюджетныйОтчетПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.БюджетныйОтчет.Форма.ФормаОтчета",
		ПараметрыОтчета,
		ЭтотОбъект,
		ДанныеСтроки.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура МодельБюджетированияПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список, "Владелец", МодельБюджетирования, ЗначениеЗаполнено(МодельБюджетирования));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭкземплярБюджета(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если Не ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		ТекстНСТР = НСтр("en='Команда не может быть выполнена для указанного объекта';ru='Команда не может быть выполнена для указанного объекта'");
		ПоказатьПредупреждение(, ТекстНСТР);
		Возврат;
	КонецЕсли;
	
	Если Не Элементы.Список.ТекущиеДанные.ИспользоватьДляВводаПлана Тогда
		ТекстНСТР = НСтр("en='Вид бюджета не используется для ввода плановых данных';ru='Вид бюджета не используется для ввода плановых данных'");
		ПоказатьПредупреждение(, ТекстНСТР);
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьВидБюджетаНаСервере(ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура("ВидБюджета", ТекущаяСтрока);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения);
	ОткрытьФорму("Документ.ЭкземплярБюджета.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	//СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	Заглушка = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьВидБюджетаНаСервере(ТекущаяСтрока)
	
	Возврат Справочники.ВидыБюджетов.ВидБюджетаЗаполненПравильно(ТекущаяСтрока);
	
КонецФункции

#КонецОбласти

