
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
	Если Объект.Ссылка.Пустая() Тогда
		ЗначениеКопирования = Параметры.ЗначениеКопирования;
		Если НЕ ЗначениеКопирования.Пустая() Тогда
			
			ВосстановитьНастройкиОформленияСервер(ЗначениеКопирования.НастройкиОформления);
			ВосстановитьПользовательскиеНастройкиОтборов(ЗначениеКопирования.ХранилищеПользовательскихНастроекОтборов);
			
		Иначе
			ВосстановитьПользовательскиеНастройкиОтборов();
			Объект.ВидФигуры = "КругКрасный";
			ЦветНадписи        = Новый Цвет(255,0,0);
		
		КонецЕсли;		 		
				
	Иначе 
		ВосстановитьНастройкиОформленияСервер();
		ВосстановитьПользовательскиеНастройкиОтборов();
		
	КонецЕсли;	
	
	Элементы.ВидФигуры.СписокВыбора.Очистить();
	
	Элементы.ВидФигуры.СписокВыбора.Добавить("КругКрасный"    , НСтр("ru = 'Круг красный'"),,БиблиотекаКартинок.ItobИконкаКругКрасный);
	Элементы.ВидФигуры.СписокВыбора.Добавить("КругЖелтый"     , НСтр("ru = 'Круг желтый'"),,БиблиотекаКартинок.ItobИконкаКругЖелтый);
	Элементы.ВидФигуры.СписокВыбора.Добавить("КругЗеленый"    , НСтр("ru = 'Круг зеленый'"),,БиблиотекаКартинок.ItobИконкаКругЗеленый);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СохранитьНастройкиОформленияСервер(ТекущийОбъект);
	
	СохранитьПользовательскиеНастройкиОтборовСервер(ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура СохранитьПользовательскиеНастройкиОтборовСервер(парОбъект)
	
	парОбъект.ХранилищеПользовательскихНастроекОтборов = Новый ХранилищеЗначения(КомпоновщикНастроек.ПользовательскиеНастройки);	
	
КонецПроцедуры

&НаСервере 
Процедура СохранитьНастройкиОформленияСервер(парОбъект)
	
	СтруктураНастроекОформления = Новый Структура;
	
	НаборЦветов = Новый Соответствие;
	НаборЦветов.Вставить("ЦветНадписи", Новый ХранилищеЗначения(ЦветНадписи));
		
	СтруктураНастроекОформления.Вставить("Цвета", НаборЦветов);
		
	парОбъект.НастройкиОформления = Новый ХранилищеЗначения(СтруктураНастроекОформления);	
	
КонецПроцедуры

&НаСервере 
Процедура ВосстановитьПользовательскиеНастройкиОтборов(ХранилищеПользовательскихНастроекОтборов = Неопределено)
	
	Схема_СКД = РеквизитФормыВЗначение("Объект").ПолучитьМакет("СКД");
		
	Если НЕ ХранилищеПользовательскихНастроекОтборов = Неопределено Тогда 
		СохраненныеПользовательскиеНастройки = ХранилищеПользовательскихНастроекОтборов.Получить();
	Иначе
		СохраненныеПользовательскиеНастройки = РеквизитФормыВЗначение("Объект").ХранилищеПользовательскихНастроекОтборов.Получить();
	КонецЕсли;
	
	АдресСКД = ПоместитьВоВременноеХранилище(Схема_СКД, Новый УникальныйИдентификатор());
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД);
		
	КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(Схема_СКД.НастройкиПоУмолчанию);
	Если НЕ СохраненныеПользовательскиеНастройки = Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(СохраненныеПользовательскиеНастройки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура ВосстановитьНастройкиОформленияСервер(ХранилищеНастроекОформления = Неопределено)
	
	НастройкаОтображенияОбъект = РеквизитФормыВЗначение("Объект");
	Если НЕ ХранилищеНастроекОформления = Неопределено Тогда
		НастройкиОформления = ХранилищеНастроекОформления.Получить();
	Иначе 
		// Восстановим цвета
		НастройкиОформления = НастройкаОтображенияОбъект.НастройкиОформления.Получить();
	
	КонецЕсли;
	
	Попытка
		Цвета = НастройкиОформления.Цвета;
			
		ЦветНадписи        = Цвета["ЦветНадписи"].Получить();
						
	Исключение
		// !! На самом деле нужно заполнять по умолчанию
	 	ItobРаботаС_БСП_КлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо заполнить цвета!'"));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

