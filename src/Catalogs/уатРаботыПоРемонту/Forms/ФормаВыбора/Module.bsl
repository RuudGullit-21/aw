
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") и Параметры.Отбор.Свойство("МодельТС") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НормочасыРабот.Работа КАК ВидРаботы
		|ИЗ
		|	РегистрСведений.уатНормочасыРаботПоРемонту КАК НормочасыРабот
		|ГДЕ
		|	НормочасыРабот.МодельТС = &МодельТС";
		Запрос.УстановитьПараметр("МодельТС", Параметры.Отбор.МодельТС);
		СписокАвторабот = Новый СписокЗначений;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокАвторабот.Добавить(Выборка.ВидРаботы);
		КонецЦикла; 
		
		ЭлементОтбораДанных = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбораДанных.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбораДанных.ПравоеЗначение = СписокАвторабот;
		ЭлементОтбораДанных.Использование  = Истина;      
		
		Параметры.Отбор.Удалить("МодельТС");
	КонецЕсли; 
	
	Если Параметры.Свойство("МодельТСИЗДокумента") Тогда
		МодельТСИзДокумента = Параметры.МодельТСИЗДокумента;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
    ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	УстановитьОтборНормочасовПоМоделиТСИзДокумента(МодельТСИзДокумента);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПоМоделиТСИзДокументаПриИзменении(Элемент)
	УстановитьОтборНормочасовПоМоделиТСИзДокумента(МодельТСИзДокумента);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	Если Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
		уатОбщегоНазначенияКлиент.УстановитьОтборУСписка(Автоработы.Отбор, Новый ПолеКомпоновкиДанных("Работа"), 
			Элементы.Список.ТекущаяСтрока, ВидСравненияКомпоновкиДанных.Равно);
		уатОбщегоНазначенияКлиент.УстановитьОтборУСписка(Нормочасы.Отбор , Новый ПолеКомпоновкиДанных("Работа"), 
			Элементы.Список.ТекущаяСтрока, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции     

&НаКлиенте
Процедура УстановитьОтборНормочасовПоМоделиТСИзДокумента(МодельТС) 
		
	Если ОтборПоМоделиТСИзДокумента Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Нормочасы, "МодельТС", МодельТС, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Нормочасы, "МодельТС", МодельТС, ВидСравненияКомпоновкиДанных.ВСписке,, Ложь);
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти