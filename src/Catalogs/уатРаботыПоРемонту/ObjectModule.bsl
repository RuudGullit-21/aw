#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("Номенклатура",1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

Процедура ПередЗаписью(Отказ)   
	
	Если Не ЗначениеЗаполнено(Номенклатура) И Не ЭтоГруппа Тогда 
		Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
		ОсновнаяОрганизация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнаяОрганизация");
		РежимЗаполненияНоменклатуры = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ОсновнаяОрганизация, ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ЗаполнениеНоменклатурыПриСозданииРаботПоРемонтуТС"));
		
		Если РежимЗаполненияНоменклатуры = Перечисления.уатЗаполнениеНоменклатурыПриСозданииРаботПоРемонту.Создавать Тогда
			ГруппаНоменклатурыДляРаботПоРемонтуТС = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ОсновнаяОрганизация, ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ГруппаНоменклатурыДляРаботПоРемонтуТС"));
			
			НовыйЭлемент 					= Справочники.Номенклатура.СоздатьЭлемент();
			НовыйЭлемент.Наименование 		= Наименование;
			НовыйЭлемент.НаименованиеПолное = НаименованиеПолное;
			НовыйЭлемент.Родитель 			= ГруппаНоменклатурыДляРаботПоРемонтуТС;
			НовыйЭлемент.ВидНоменклатуры	= Справочники.ВидыНоменклатуры.Услуга;
			
			Попытка
				НовыйЭлемент.Записать();
			Исключение 
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = ОписаниеОшибки();
				Сообщение.Сообщить();
			КонецПопытки; 
			
			Номенклатура = НовыйЭлемент.Ссылка;
		ИначеЕсли РежимЗаполненияНоменклатуры = Перечисления.уатЗаполнениеНоменклатурыПриСозданииРаботПоРемонту.ЗаполнятьОбщую Тогда	
			ОбщаяНоменклатура = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ОсновнаяОрганизация, ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОбщаяНоменклатураДляЗаполненияРаботПоРемонтуТС"));
			Номенклатура = ОбщаяНоменклатура;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
