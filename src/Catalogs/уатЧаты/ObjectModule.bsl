
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	мсвВодителей = Новый Массив();
	Для Каждого ТекВодитель Из Водители Цикл
		Если НЕ ЗначениеЗаполнено(ТекВодитель.Водитель) Тогда
			Продолжить;
		КонецЕсли;
		
		мсвВодителей.Добавить(ТекВодитель.Водитель);
	КонецЦикла;
	
	Если мсвВодителей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка на дубли 
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("мсвВодителей", мсвВодителей);

	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	уатЧатыВодители.Водитель КАК Водитель,
	               |	уатЧатыВодители.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.уатЧаты.Водители КАК уатЧатыВодители
	               |ГДЕ
	               |	НЕ уатЧатыВодители.Ссылка.ПометкаУдаления
	               |	И уатЧатыВодители.Ссылка <> &Ссылка
	               |	И уатЧатыВодители.Водитель В(&мсвВодителей)";

	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстНСТР = НСтр("ru='Водитель ""%1"" уже включен в чат ""%2""'");
		ТекстНСТР = СтрШаблон(ТекстНСТР, Выборка.Водитель, Выборка.Ссылка);
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти
