
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьИспользоватьЧат();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Чаты_Запись", Объект.Ссылка, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВодителиПриИзменении(Элемент)
	ЗаполнитьИспользоватьЧат();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьИспользоватьЧатСервер()
	
	мсвВодители = Новый Массив();
	Для Каждого ТекСтрока Из Объект.Водители Цикл
		мсвВодители.Добавить(ТекСтрока.Водитель);
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Водители", мсвВодители);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	уатПользователиМобильногоПриложения.ИспользоватьЧат
	|		И уатПользователиМобильногоПриложения.РаботаРазрешена КАК ИспользоватьЧат,
	|	уатПользователиМобильногоПриложения.Пользователь КАК Пользователь
	|ИЗ
	|	РегистрСведений.уатПользователиМобильногоПриложения КАК уатПользователиМобильногоПриложения
	|ГДЕ
	|	уатПользователиМобильногоПриложения.Пользователь В(&Водители)";

	Выборка = Запрос.Выполнить().Выбрать();
	
	мсвВодители = Новый Массив();
	Пока Выборка.Следующий() Цикл
		мсвВодители.Добавить(Новый Структура("Водитель, ИспользоватьЧат",
			Выборка.Пользователь, Выборка.ИспользоватьЧат));
	КонецЦикла;
	
	Возврат мсвВодители;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьИспользоватьЧат()
	
	мсвВодители = ЗаполнитьИспользоватьЧатСервер();
	Для Каждого ТекСтрока Из мсвВодители Цикл
		СтрокиВодителей = Объект.Водители.НайтиСтроки(Новый Структура("Водитель",  ТекСтрока.Водитель));
		Для Каждого ТекВодитель Из СтрокиВодителей Цикл
			ТекВодитель.ИспользоватьЧат = ТекСтрока.ИспользоватьЧат;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти