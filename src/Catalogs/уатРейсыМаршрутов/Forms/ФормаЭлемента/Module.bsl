
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ИзменениеНомераРейса") Тогда
		Элементы.Владелец.ТолькоПросмотр = Параметры.ИзменениеНомераРейса;
		Элементы.ПунктОтправления.ТолькоПросмотр = Параметры.ИзменениеНомераРейса;
	КонецЕсли;
	
	// Запрет на рейсы по пунктам в версии ПРОФ
	Если Не уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП()
		И Объект.Ссылка.Пустая() Тогда
		
		Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.уатПунктыНазначения") Тогда
			ТекстНСтр = НСтр("ru = 'Создание рейсов для пунктов разрешено только в версии КОРП'; 
				|en = 'Creating trips for destination points is only allowed for TMS CORP'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСтр,,,, Отказ);
		КонецЕсли;
		
		Объект.Владелец = Справочники.уатМаршруты.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимость()
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.уатПунктыНазначения") Тогда
		Элементы.ПунктОтправления.Видимость = Истина;
		Элементы.Владелец.Заголовок = НСтр("ru = 'Пункт прибытия'; en = 'Arival point'");
	Иначе
		Элементы.ПунктОтправления.Видимость = Ложь;
		Элементы.Владелец.Заголовок = НСтр("ru = 'Маршрут'; en = 'Route'");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВладелецНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Не уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП() Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ТекущаяСтрока", Объект.Владелец);
		
		ОткрытьФорму("Справочник.уатМаршруты.ФормаВыбора", ПараметрыОткрытия, Элемент);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти