
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Объект, ЭтотОбъект, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Организация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяОрганизация");
		Объект.Подразделение = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновноеПодразделениеОрганизации");
	КонецЕсли; 
	Элементы.ШаблонРезультатовОсмотра.Видимость = 
		НЕ Объект.ВидЖурнала = Перечисления.уатВидыЖурналовТранспортныхДокументов.ЖурналУчетаПоказанийОдометра;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидЖурналаПриИзменении(Элемент)
	Элементы.ШаблонРезультатовОсмотра.Видимость 
		= НЕ Объект.ВидЖурнала = ПредопределенноеЗначение("Перечисление.уатВидыЖурналовТранспортныхДокументов.ЖурналУчетаПоказанийОдометра");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура НастройкаПериода(Команда)
	ДиалогПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогПериода.Период.ДатаНачала = Объект.ДатаНачала;
	ДиалогПериода.Период.ДатаОкончания = Объект.ДатаОкончания;
	ДиалогПериода.Показать(Новый ОписаниеОповещения("НастройкаПериодаЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПериодаЗавершение(Период, ДополнительныеПараметры) Экспорт
	    
	Если Не Период = Неопределено Тогда
		Объект.ДатаНачала    = Период.ДатаНачала;
		Объект.ДатаОкончания = Период.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЖурнала(Команда)
	
	Если Объект.Ссылка.Пустая() ИЛИ Модифицированность Тогда
		Оповещ = Новый ОписаниеОповещения("ПечатьЖурналаЗаписатьЭлемент", ЭтотОбъект);
		ТекстСообщ = "Перед печатью элемент будет записан. Продолжить?";
		ПоказатьВопрос(Оповещ, ТекстСообщ, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ПечатьЖурналаЗавершение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЖурналаЗаписатьЭлемент(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		флОК = Записать();
		Если флОК Тогда
			ПечатьЖурналаЗавершение();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЖурналаЗавершение()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Журнал", Объект.Ссылка);
	ПараметрыФормы.Вставить("ФормироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("ТолькоПодписанные", Истина);
	
	Если Объект.ВидЖурнала = ПредопределенноеЗначение("Перечисление.уатВидыЖурналовТранспортныхДокументов.ЖурналРегистрацииРезультатовПредрейсовогоИПослерейсовогоКонтроля") Тогда
		ОткрытьФорму("Отчет.уатЖурналПредрейсовогоИПослерейсовогоКонтроляТС.Форма.ФормаОтчета", ПараметрыФормы);
	ИначеЕсли Объект.ВидЖурнала = ПредопределенноеЗначение("Перечисление.уатВидыЖурналовТранспортныхДокументов.ЖурналПредрейсовогоМедицинскогоОсмотраВодителей") Тогда
		ПараметрыФормы.Вставить("ВидКонтроля", ПредопределенноеЗначение("Перечисление.уатВидыКонтроляТранспортныхДокументов.Предрейсовый"));
		ОткрытьФорму("Отчет.уатЖурналМедицинскогоОсмотраВодителей.Форма.ФормаОтчета", ПараметрыФормы);
	ИначеЕсли Объект.ВидЖурнала = ПредопределенноеЗначение("Перечисление.уатВидыЖурналовТранспортныхДокументов.ЖурналПослерейсовогоМедицинскогоОсмотраВодителей") Тогда
		ПараметрыФормы.Вставить("ВидКонтроля", ПредопределенноеЗначение("Перечисление.уатВидыКонтроляТранспортныхДокументов.Послерейсовый"));
		ОткрытьФорму("Отчет.уатЖурналМедицинскогоОсмотраВодителей.Форма.ФормаОтчета", ПараметрыФормы); 
	ИначеЕсли Объект.ВидЖурнала = ПредопределенноеЗначение("Перечисление.уатВидыЖурналовТранспортныхДокументов.ЖурналУчетаПоказанийОдометра") Тогда
		ОткрытьФорму("Отчет.уатЖурналУчетаПоказанийОдометра.Форма.ФормаОтчета", ПараметрыФормы);
	Иначе
		ОткрытьФорму("Отчет.уатЖурналПутЛистов.Форма.ФормаОтчета", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
