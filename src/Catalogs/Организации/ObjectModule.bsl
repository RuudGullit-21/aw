#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
			И Не ЗначениеЗаполнено(ИндивидуальныйПредприниматель) Тогда
		УстановитьИндивидуальногоПредпринимателя();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ФамилияИП");
		МассивНепроверяемыхРеквизитов.Добавить("ИмяИП");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИНН = "";
	КПП = "";
	ФайлЛоготип            = Неопределено;
	ФайлФаксимильнаяПечать = Неопределено;

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьИндивидуальногоПредпринимателя()
	
	Если Не ЗначениеЗаполнено(ФамилияИП) И Не ЗначениеЗаполнено(ИмяИП) Тогда
		// Если не заполнены фамилия и имя, то и создавать физическое лицо не требуется.
		Возврат;
	КонецЕсли;
	
	// Возможно, физическое лицо, которое соответствует индивидуальному предпринимателю уже есть в ИБ
	// Поищем его сначала по ИНН, а потом по ФИО
	
	ФИО = СокрЛП(ФамилияИП)+" "+СокрЛП(ИмяИП)+" "+СокрЛП(ОтчествоИП);
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("ИНН", ИНН);
	Запрос.Параметры.Вставить("ФИО", ФИО);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФизическиеЛица.Ссылка,
	|	1 КАК Приоритет
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.ИНН = &ИНН
	|	И ФизическиеЛица.ИНН <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка,
	|	2
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.ФИО = &ФИО
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ИндивидуальныйПредприниматель = Выборка.Ссылка;
		
	Иначе
		ФИО = Новый Структура("Фамилия, Имя, Отчество", ФамилияИП, ИмяИП, ОтчествоИП);
		ДанныеФизическогоЛица = Новый Структура("ИНН", ИНН);
		ФизлицоОбъект = Справочники.ФизическиеЛица.НовоеФизическоеЛицо(ФИО, ДанныеФизическогоЛица);
		ИндивидуальныйПредприниматель = ФизлицоОбъект.Ссылка;
		
		Для Каждого СтрокаКонтактнойИнформации Из КонтактнаяИнформация Цикл
			Если СтрокаКонтактнойИнформации.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации Тогда
				СтрокаКонтактнойИнформацииФизлица = ФизлицоОбъект.КонтактнаяИнформация.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаКонтактнойИнформацииФизлица, СтрокаКонтактнойИнформации);
				СтрокаКонтактнойИнформацииФизлица.Вид = Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица;
			ИначеЕсли СтрокаКонтактнойИнформации.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации Тогда
				СтрокаКонтактнойИнформацииФизлица = ФизлицоОбъект.КонтактнаяИнформация.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаКонтактнойИнформацииФизлица, СтрокаКонтактнойИнформации);
				СтрокаКонтактнойИнформацииФизлица.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица;
			КонецЕсли;
		КонецЦикла;
		
		Если ФизлицоОбъект.КонтактнаяИнформация.Количество() > 0 Тогда
			ФизлицоОбъект.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли