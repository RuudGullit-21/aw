
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если ВидМоделиТС = Перечисления.уатВидыМоделейТС.АвиационныйТранспорт
			Или ВидМоделиТС = Перечисления.уатВидыМоделейТС.МорскойТранспорт
			Или ВидМоделиТС = Перечисления.уатВидыМоделейТС.ЖДТранспорт Тогда 
		Если ПринадлежностьТС = Перечисления.уатПринадлежностьТС.Собственное Тогда 
			Организация = ВладелецТС;
		Иначе 
			Организация = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда 
		Наименование = уатНастройкиШаблонов.СформироватьНаименованиеПоШаблону("Справочник_уатТС", ЭтотОбъект);
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		ИДвСистемеНавигации = СокрЛП(ИДвСистемеНавигации);
		Если НЕ ЗначениеЗаполнено(ПринадлежностьТС) Тогда
			ПринадлежностьТС = Перечисления.уатПринадлежностьТС.Собственное;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ИспользуемаяСистемаGPS) = Тип("СправочникСсылка.уатВнешниеСистемы")
		И ИспользуемаяСистемаGPS.НаименованиеПС = "Спутник-Авто" Тогда
		МассивРасширений = РасширенияКонфигурации.Получить();
		Для Каждого ТекРасширение Из МассивРасширений Цикл
			Если ТекРасширение.Имя = "УАТ_ПодключениеСистемыМониторингаСпутникАвто"
				И ТекРасширение.Версия = "" Тогда
				ИспользуемаяСистемаGPS = ИспользуемаяСистемаGPS.НаименованиеПС;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(ИспользуемаяСистемаGPS) = Тип("СправочникСсылка.уатВнешниеСистемы")
		И ИспользуемаяСистемаGPS.НаименованиеПС = "Waliot" Тогда
				
		НаименованиеДействия = "Справочник.уатТС.МодульОбъекта.ПередЗаписью";
		уатРаботаСРасширениями.ПСМ_ВыполнитьДействияВФорме(НаименованиеДействия, 
		?(ТипЗнч(ИспользуемаяСистемаGPS) = Тип("Строка"), ИспользуемаяСистемаGPS,
		уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ИспользуемаяСистемаGPS, "НаименованиеПС")),, ЭтотОбъект);
				
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЗаписьНомераБортовогоУстройтваТС") Тогда
		АнализИзмененийБортовыхУстройств(ДополнительныеСвойства.ДатаОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЭтоГруппа И Модель.ЭтоГруппа Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если (Не ЭтоГруппа) 
		И (уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП() Или уатОбщегоНазначенияПовтИсп.ВариантПоставкиПРОФ())
		И ПринадлежностьТС = Перечисления.уатПринадлежностьТС.Привлеченное
		И (ВладелецТС = Неопределено Или Не ЗначениеЗаполнено(ВладелецТС)) Тогда
		
		ТекстНСТР = НСтр("en='For vehicle ""%1"" it is necessary to specify owner, element is not recorded.';ru='Для транспортного средства ""%1"" необходимо указать владельца, элемент не записан.'");
		ТекстСообщения = СтрШаблон(ТекстНСТР, Наименование);
		уатОбщегоНазначенияСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения,,, "ВладелецТС", Отказ);
	КонецЕсли;
	
	Если (Не ЭтоГруппа) И ЗначениеЗаполнено(ИспользуемаяСистемаGPS) 
		И ИспользуемаяСистемаGPS <> Справочники.уатВнешниеСистемы._1СЦСМ тогда
		ПроверяемыеРеквизиты.Добавить("ИДвСистемеНавигации");
		ПроверяемыеРеквизиты.Добавить("УчетнаяЗаписьСистемыМониторинга");
	КонецЕсли;
	
	Если Не ЭтоГруппа И уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП() Тогда 
		ТабХарактеристик = Характеристики.Выгрузить(, "Характеристика");
		ТабХарактеристик.Свернуть("Характеристика");
		
		Если Не ТабХарактеристик.Количество() = Характеристики.Количество() Тогда 
			ТекстОшибки = НСтр("ru = 'Таблица характеристик содержит дубли'");
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстОшибки, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если (НЕ ЭтоГруппа) И ДатаВводаВЭксплуатацию <> '00010101' Тогда
		ДатаВводаВЭксплуатацию = '00010101';
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ДатаВыбытия <> '00010101' Тогда
		ДатаВыбытия = '00010101';
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(Организация) Тогда
		Организация = Неопределено;
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(Подразделение) Тогда
		Подразделение = Неопределено;
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(Колонна) Тогда
		Колонна = Неопределено;
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(VIN) Тогда
		VIN = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ЛицензионнаяКарточка) Тогда
		ЛицензионнаяКарточка = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(НомерЛицензионнойКарточки) Тогда
		НомерЛицензионнойКарточки = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(СерияЛицензионнойКарточки) Тогда
		СерияЛицензионнойКарточки = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(НомерСерииЛицензионнойКарточки) Тогда
		НомерСерииЛицензионнойКарточки = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(НомерДвигателя) Тогда
		НомерДвигателя = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(НомерКузова) Тогда
		НомерКузова = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(НомерШасси) Тогда
		НомерШасси = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(НомерКПП) Тогда
		НомерКПП = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ОсновноеСредство) Тогда
		ОсновноеСредство = Справочники.ОсновныеСредства.ПустаяСсылка();
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(КодПоКлассификатору) Тогда
		КодПоКлассификатору = Справочники.уатКлассификаторВидыТранспортныхСредств.ПустаяСсылка();
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(БалансоваяСтоимость) Тогда
		БалансоваяСтоимость = 0;
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(СрокИспользования) Тогда
		СрокИспользования = 0;
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИДвСистемеНавигации) Тогда
		ИДвСистемеНавигации = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИспользуемаяСистемаGPS) Тогда
		ИспользуемаяСистемаGPS = Справочники.уатВнешниеСистемы.ПустаяСсылка();
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(УчетнаяЗаписьСистемыМониторинга) Тогда
		УчетнаяЗаписьСистемыМониторинга = Справочники.уатУчетныеЗаписиСистемыМониторинга.ПустаяСсылка();
	КонецЕсли;  
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(УчетнаяЗаписьMultiGo) Тогда
		УчетнаяЗаписьMultiGo = Справочники.уатУчетныеЗаписиMultiGo.ПустаяСсылка();
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(УчетнаяЗаписьCервисаШтрафов) Тогда
		УчетнаяЗаписьCервисаШтрафов = Справочники.уатУчетныеЗаписиСервисовШтрафов.ПустаяСсылка();
	КонецЕсли;  
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(УчетнаяЗаписьCервисаПарковок) Тогда
		УчетнаяЗаписьCервисаПарковок = Справочники.уатУчетныеЗаписиСервисовПарковок.ПустаяСсылка();
	КонецЕсли;  
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИДвСервисеПарковок) Тогда
		ИДвСервисеПарковок = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИДвСервисеШтрафов) Тогда
		ИДвСервисеШтрафов = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИДвСервисеMultiGo) Тогда
		ИДвСервисеMultiGo = "";
	КонецЕсли;
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИспользуемыйСервисПарковок) Тогда
		ИспользуемыйСервисПарковок = Справочники.уатВнешниеСистемы.ПустаяСсылка();
	КонецЕсли; 
	Если (НЕ ЭтоГруппа) И ЗначениеЗаполнено(ИспользуемыйСервисШтрафов) Тогда
		ИспользуемыйСервисШтрафов = Справочники.уатВнешниеСистемы.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура АнализИзмененийБортовыхУстройств(ДатаОперации)
	
	Если Не ЗначениеЗаполнено(НомерБортовогоУстройстваАвтодор) И Не ЗначениеЗаполнено(НомерБортовогоУстройстваПлатон) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ТекущийИДАвтодор = Ссылка.НомерБортовогоУстройстваАвтодор;
		ТекущийИДПлатон  = Ссылка.НомерБортовогоУстройстваПлатон;
	Иначе
		ТекущийИДАвтодор = Неопределено;
		ТекущийИДПлатон  = Неопределено;
	КонецЕсли;
	
	Если ТекущийИДАвтодор <> НомерБортовогоУстройстваАвтодор Тогда
		
		МенеджерЗаписи = РегистрыСведений.уатИсторияЗакрепленияБортовыхУстройств.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Период 		  			= ДатаОперации;
		МенеджерЗаписи.ТС     		  			= Ссылка;
		МенеджерЗаписи.ВнешняяСистема 			= Справочники.уатВнешниеСистемы.Автодор;
		МенеджерЗаписи.НомерБортовогоУстройства = НомерБортовогоУстройстваАвтодор;
		
		Попытка
			МенеджерЗаписи.Записать();
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	Если ТекущийИДПлатон <> НомерБортовогоУстройстваПлатон Тогда
		
		МенеджерЗаписи = РегистрыСведений.уатИсторияЗакрепленияБортовыхУстройств.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Период 		  			= ДатаОперации;
		МенеджерЗаписи.ТС     		  			= Ссылка;
		МенеджерЗаписи.ВнешняяСистема 			= Справочники.уатВнешниеСистемы.Платон;
		МенеджерЗаписи.НомерБортовогоУстройства = НомерБортовогоУстройстваПлатон;
		
		Попытка
			МенеджерЗаписи.Записать();
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

