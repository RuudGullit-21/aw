
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("НастройкиВнешнийМаршрутизатор", НастройкиВнешнийМаршрутизатор);
	НастройкиМаршрутизации = уатЗащищенныеФункцииСервер_уэ.ПрочитатьНастройкиВнешнийМаршрутизатор(НастройкиВнешнийМаршрутизатор);
	
	Если НастройкиМаршрутизации.НастраиватьСовместимыеГруппы Тогда
		НастраиватьСовместимыеГруппы = Истина;
		ТаблицаГрупп = НастройкиМаршрутизации.ТаблицаСовместимыхГрупп;
	Иначе
		НастраиватьСовместимыеГруппы = Ложь;
		ТаблицаГрупп = НастройкиМаршрутизации.ТаблицаНесовместимыхГрупп;
	КонецЕсли;
	ПустаяГруппаСовместимостиСовместима = НастройкиМаршрутизации.ПустаяГруппаСовместимостиСовместима;
	УстановитьЗаголовокФормы();
	
	Для Каждого ТекГруппаНесовместимости Из ТаблицаГрупп Цикл
		НовСтрокаГруппы = ТаблицаНесовместимыхГрупп.Добавить();
		
		Для Каждого ТекЭлементНесовместимости Из ТекГруппаНесовместимости Цикл 
			ТекGUID = Новый УникальныйИдентификатор(ТекЭлементНесовместимости);
			СсылкаНаЭлемент = Справочники.уатГруппыСовместимости_уэ.ПолучитьСсылку(ТекGUID);
			
			ЭлементОбъект = СсылкаНаЭлемент.ПолучитьОбъект();
			Если ЭлементОбъект = Неопределено Или ЭлементОбъект.ЭтоНовый() Тогда 
				Продолжить;
			КонецЕсли;
			
			НовЭлемент = НовСтрокаГруппы.НесовместимыеЭлементы.Добавить();
			НовЭлемент.ГруппаСовместимости = СсылкаНаЭлемент;
		КонецЦикла;
		
		Если НовСтрокаГруппы.НесовместимыеЭлементы.Количество() = 0 Тогда 
			ТаблицаНесовместимыхГрупп.Удалить(НовСтрокаГруппы);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьПредставленияГруппНесовместимости();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастраиватьСовместимыеГруппыПриИзменении(Элемент)
	
	Если ТаблицаНесовместимыхГрупп.Количество() > 0 Тогда
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("НастраиватьСовместимыеГруппыПриИзмененииОтвет", ЭтаФорма);
		ТекстВопроса = НСтр("ru = 'Подобранные группы будут очищены. Продолжить?'");
		ПоказатьВопрос(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе
		НастраиватьСовместимыеГруппыПриИзмененииЗавершение();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ТаблицаНесовместимыхГруппНесовместимыеЭлементы

&НаКлиенте
Процедура ТаблицаНесовместимыхГруппНесовместимыеЭлементыПриИзменении(Элемент)
	
	ОбновитьПредставленияГруппНесовместимости();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройкиНесовместимости(Команда)
	
	НесовместимыеГруппы = Новый Массив();
	
	Для Каждого ТекГруппа Из ТаблицаНесовместимыхГрупп Цикл 
		ЭлементыСовместимости = Новый Массив();
		
		Для Каждого ТекЭлемент Из ТекГруппа.НесовместимыеЭлементы Цикл 
			Если Не ЗначениеЗаполнено(ТекЭлемент.ГруппаСовместимости) Тогда 
				Продолжить;
			КонецЕсли;
			
			ЭлементыСовместимости.Добавить(Строка(ТекЭлемент.ГруппаСовместимости.УникальныйИдентификатор()));
		КонецЦикла;
		
		Если ЭлементыСовместимости.Количество() Тогда 
			НесовместимыеГруппы.Добавить(ЭлементыСовместимости);
		КонецЕсли;
	КонецЦикла;
	
	НастройкиМаршрутизации = Новый Структура();
	НастройкиМаршрутизации.Вставить("НастраиватьСовместимыеГруппы", НастраиватьСовместимыеГруппы);
	НастройкиМаршрутизации.Вставить("ПустаяГруппаСовместимостиСовместима", ПустаяГруппаСовместимостиСовместима);
	
	Если НастраиватьСовместимыеГруппы Тогда
		НастройкиМаршрутизации.Вставить("ТаблицаСовместимыхГрупп", НесовместимыеГруппы);
		НастройкиМаршрутизации.Вставить("ТаблицаНесовместимыхГрупп", Новый Массив());
	Иначе
		НастройкиМаршрутизации.Вставить("ТаблицаСовместимыхГрупп", Новый Массив());
		НастройкиМаршрутизации.Вставить("ТаблицаНесовместимыхГрупп", НесовместимыеГруппы);
	КонецЕсли;
	
	уатЗащищенныеФункцииСервер_уэ.ЗаписатьНастройкиВнешнийМаршрутизатор(НастройкиВнешнийМаршрутизатор, НастройкиМаршрутизации);
	
	Закрыть(Новый Структура("НастройкиВнешнийМаршрутизатор", НастройкиВнешнийМаршрутизатор));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПредставленияГруппНесовместимости()
	
	Для Каждого ТекГруппа Из ТаблицаНесовместимыхГрупп Цикл 
		ТекГруппа.ПредставлениеГруппы = "";
		
		Для Каждого ТекЭлемент Из ТекГруппа.НесовместимыеЭлементы Цикл 
			Если Не ЗначениеЗаполнено(ТекЭлемент.ГруппаСовместимости) Тогда 
				Продолжить;
			КонецЕсли;
			
			ТекГруппа.ПредставлениеГруппы = ТекГруппа.ПредставлениеГруппы
				+ ?(ТекГруппа.ПредставлениеГруппы = "", "", ", ") + Строка(ТекЭлемент.ГруппаСовместимости);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы();
	
	Если НастраиватьСовместимыеГруппы Тогда
		ЭтаФорма.Заголовок = НСтр("ru = 'Таблица совместимых групп'");
	Иначе
		ЭтаФорма.Заголовок = НСтр("ru = 'Таблица несовместимых групп'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастраиватьСовместимыеГруппыПриИзмененииОтвет(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		НастраиватьСовместимыеГруппыПриИзмененииЗавершение();
	Иначе
		НастраиватьСовместимыеГруппы = Не НастраиватьСовместимыеГруппы;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастраиватьСовместимыеГруппыПриИзмененииЗавершение()
	УстановитьЗаголовокФормы();
	ТаблицаНесовместимыхГрупп.Очистить();
КонецПроцедуры

#КонецОбласти
