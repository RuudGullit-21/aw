
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("НастройкиВнешнийМаршрутизатор", НастройкиВнешнийМаршрутизатор);
	НастройкиМаршрутизации = уатЗащищенныеФункцииСервер_уэ.ПрочитатьНастройкиВнешнийМаршрутизатор(НастройкиВнешнийМаршрутизатор);
	
	ТаблицаГеозон = НастройкиМаршрутизации.ТаблицаНесовместимыхГеозон;
	
	Для Каждого ТекГеозона Из ТаблицаГеозон Цикл
		НовСтрока = ТаблицаНесовместимыхГеозон.Добавить(); 
		
		Для Каждого ТекЭлементНесовместимости Из ТекГеозона Цикл 
			НовЭлемент = НовСтрока.НесовместимыеГеозоны.Добавить();
			НовЭлемент.Геозона = ТекЭлементНесовместимости;
		КонецЦикла;
	КонецЦикла;
	
	ОбновитьПредставленияГруппНесовместимости();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ТаблицаНесовместимыхГруппНесовместимыеЭлементы

&НаКлиенте
Процедура ТаблицаНесовместимыхГеозонНесовместимыеГеозоныПриИзменении(Элемент)
	
	ОбновитьПредставленияГруппНесовместимости();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройкиНесовместимости(Команда)
	
	НесовместимыеГеозоны = Новый Массив();
	
	Для Каждого ТекГеозона Из ТаблицаНесовместимыхГеозон Цикл 
		ТекНесовместимыеГеозоны = Новый Массив();
		Для Каждого ТекЭлемент Из ТекГеозона.НесовместимыеГеозоны Цикл 
			Если НЕ ЗначениеЗаполнено(ТекЭлемент.Геозона)
				ИЛИ ТекНесовместимыеГеозоны.Найти(ТекЭлемент.Геозона) <> Неопределено Тогда 
				Продолжить;
			КонецЕсли;
			
			ТекНесовместимыеГеозоны.Добавить(ТекЭлемент.Геозона);
		КонецЦикла;
		
		Если ТекНесовместимыеГеозоны.Количество() Тогда 
			НесовместимыеГеозоны.Добавить(ТекНесовместимыеГеозоны);
		КонецЕсли;
	КонецЦикла;
	
	НастройкиМаршрутизации = Новый Структура();
	НастройкиМаршрутизации.Вставить("ТаблицаНесовместимыхГеозон", НесовместимыеГеозоны);
	
	уатЗащищенныеФункцииСервер_уэ.ЗаписатьНастройкиВнешнийМаршрутизатор(НастройкиВнешнийМаршрутизатор, НастройкиМаршрутизации);
	
	Закрыть(Новый Структура("НастройкиВнешнийМаршрутизатор", НастройкиВнешнийМаршрутизатор));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПредставленияГруппНесовместимости()
	
	Для Каждого ТекГруппа Из ТаблицаНесовместимыхГеозон Цикл 
		ТекГруппа.ПредставлениеГруппы = "";
		
		Для Каждого ТекЭлемент Из ТекГруппа.НесовместимыеГеозоны Цикл 
			Если Не ЗначениеЗаполнено(ТекЭлемент.Геозона) Тогда 
				Продолжить;
			КонецЕсли;
			
			ТекГруппа.ПредставлениеГруппы = ТекГруппа.ПредставлениеГруппы
				+ ?(ТекГруппа.ПредставлениеГруппы = "", "", ", ") + Строка(ТекЭлемент.Геозона);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

