
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьВыбор(Команда)
	
	Закрыть(ПолучитьМассивТС());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьМассивТС()
	
	ТабТС = РеквизитФормыВЗначение("ВиртуальныеТС");
	ТабТС.Свернуть("ТС", "Количество");
	
	МассивТС = Новый Массив();
	
	Для Каждого ТекТС Из ТабТС Цикл 
		Если ТекТС.Количество = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ДанныеТС = Новый Структура();
		ДанныеТС.Вставить("ТС",                             ТекТС.ТС);
		ДанныеТС.Вставить("КоличествоВиртуальныхТС",        ТекТС.Количество);
		ДанныеТС.Вставить("Грузоподъемность",               ?(ТекТС.ТС.Грузоподъемность <> 0, ТекТС.ТС.Грузоподъемность, ТекТС.ТС.Модель.Грузоподъемность));
		ДанныеТС.Вставить("ОбъемКузова",                    ?(ТекТС.ТС.ОбъемКузова <> 0, ТекТС.ТС.ОбъемКузова, ТекТС.ТС.Модель.ОбъемКузова));
		ДанныеТС.Вставить("СобственныйВес",                 ТекТС.ТС.Модель.СобственныйВес); 
		ДанныеТС.Вставить("МаксимальноеКоличествоУпаковок", ?(ТекТС.ТС.МаксимальноеКоличествоУпаковок <> 0, ТекТС.ТС.МаксимальноеКоличествоУпаковок, ТекТС.ТС.Модель.МаксимальноеКоличествоУпаковок)); 
		ДанныеТС.Вставить("МодельТС",                       ТекТС.ТС.Модель);
		ДанныеТС.Вставить("ГаражТС",                        ТекТС.ТС.Гараж);
		ДанныеТС.Вставить("ГаражТСЛат",                     ТекТС.ТС.Гараж.Лат);
		ДанныеТС.Вставить("ГаражТСЛон",                     ТекТС.ТС.Гараж.Лон);
		ДанныеТС.Вставить("ПринадлежностьТС",               ТекТС.ТС.ПринадлежностьТС);
		ДанныеТС.Вставить("МинимальноеКоличествоУпаковок",  ТекТС.ТС.Модель.МинимальноеКоличествоУпаковок);
		ДанныеТС.Вставить("МинимальныйВесГруза",            ТекТС.ТС.Модель.МинимальныйВесГруза);
		ДанныеТС.Вставить("МинимальныйОбъемГруза",          ТекТС.ТС.Модель.МинимальныйОбъемГруза);
		ДанныеТС.Вставить("ХарактеристикиТС",               Новый Массив());
		
		ДанныеТС.Вставить("ПредставлениеВиртТС",            "(" + Строка(ТекТС.Количество) + ") " + СокрЛП(Строка(ТекТС.ТС)));
		ДанныеТС.Вставить("ПредставлениеТС",                СокрЛП(Строка(ТекТС.ТС)));
		ДанныеТС.Вставить("ПредставлениеМЛ",                "");
		
		Если ТекТС.ТС.ПринадлежностьТС = Перечисления.уатПринадлежностьТС.Привлеченное Тогда 
			ДанныеТС.Вставить("ИндексКартинки", 4); 
		ИначеЕсли ТекТС.ТС.ПринадлежностьТС = Перечисления.уатПринадлежностьТС.Виртуальное Тогда 
			ДанныеТС.Вставить("ИндексКартинки", 6); 
		Иначе 
			ДанныеТС.Вставить("ИндексКартинки", 3); 
		КонецЕсли;
		
		Если Константы.уатЕдиницаИзмеренияВеса.Получить() = Перечисления.уатЕдиницыИзмеренияВеса.Килограмм Тогда 
			ДанныеТС.Грузоподъемность = ДанныеТС.Грузоподъемность * 1000;
		КонецЕсли;
		
		Если Константы.уатЕдиницаИзмеренияОбъема.Получить() = Перечисления.уатЕдиницыИзмеренияОбъема.Литр Тогда 
			ДанныеТС.ОбъемКузова = ДанныеТС.ОбъемКузова * 1000;
		КонецЕсли;
		
		МассивТС.Добавить(ДанныеТС);
	КонецЦикла;
	
	Возврат МассивТС;
	
КонецФункции // ПолучитьМассивТС()

#КонецОбласти