#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, 
		"РегистрационныйНомер, ОрганизацияНаКоторуюПрограммаЗарегистрирована, ИННОрганизации, ИмяПользователя, АдресОтвета, Должность");
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(АдресОтвета) И 
		НЕ ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(АдресОтвета) Тогда
		ТекстОшибки = НСтр("ru = 'Указан некорректный адрес электронной почты.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "АдресОтвета", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РегистрационныйНомер)
		И СтрДлина(РегистрационныйНомер) < 8 Тогда
		ТекстОшибки = НСтр("ru = 'Указан некорректный регистрационный номер.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "РегистрационныйНомер", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацияНаКоторуюПрограммаЗарегистрированаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Организации") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ПараметрыОрганизации = ПараметрыОрганизации(ВыбранноеЗначение);
		ОрганизацияНаКоторуюПрограммаЗарегистрирована = ПараметрыОрганизации.НаименованиеСокращенное;
		ИННОрганизации = ПараметрыОрганизации.ИНН;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОрганизацию(Команда)
	
	ОткрытьФорму("Справочник.Организации.ФормаВыбора", , Элементы.ОрганизацияНаКоторуюПрограммаЗарегистрирована);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("РегистрационныйНомер",                          РегистрационныйНомер);
	РегистрационныеДанные.Вставить("ОрганизацияНаКоторуюПрограммаЗарегистрирована", ОрганизацияНаКоторуюПрограммаЗарегистрирована);
	РегистрационныеДанные.Вставить("ИННОрганизации",                                ИННОрганизации);
	РегистрационныеДанные.Вставить("ИмяПользователя",                               ИмяПользователя);
	РегистрационныеДанные.Вставить("Должность",                                     Должность);
	РегистрационныеДанные.Вставить("АдресОтвета",                                   АдресОтвета);
	
	Закрыть(РегистрационныеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПараметрыОрганизации(Организация)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "НаименованиеСокращенное, ИНН");
	
КонецФункции

#КонецОбласти