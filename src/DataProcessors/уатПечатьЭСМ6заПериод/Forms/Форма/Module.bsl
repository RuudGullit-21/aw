
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// начало блока стандартных операций
	ДопПараметрыОткрытие = Новый Структура("ИмяФормы", ИмяФормы);
	уатЗащищенныеФункцииСервер.уатОбработкаФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	// конец блока стандартных операций
	
	Если НЕ уатОбщегоНазначения.КонфигурацияДляРФ() Тогда
		ТекстСообщения = НСтр("en='Открытие возможно только тогда, когда включен признак использования конфигурации в РФ.';ru='Открытие возможно только тогда, когда включен признак использования конфигурации в РФ.'");
		уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстСообщения, Отказ);
	КонецЕсли;
	
	НачПериода = НачалоГода(ТекущаяДата());
	КонПериода = КонецМесяца(ТекущаяДата());
	ПечататьОбложку = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// начало блока стандартных операций
	уатЗащищенныеФункцииКлиент.уатОбработкаФормаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// конец блока стандартных операций
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяОрганизация");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрВыработки) Тогда
		ПараметрВыработки = ПредопределенноеЗначение("Справочник.уатПараметрыВыработки.ВремяВРаботе");
	КонецЕсли;
	
	Если ВидыПЛ.Количество() = 0 Тогда
		ВидыПЛ.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыПЛ._ЭСМ1"));
		ВидыПЛ.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыПЛ._ЭСМ2"));
		ВидыПЛ.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыПЛ._ЭСМ3"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	ЕстьОшибки = Ложь;
	ТекстОшибки = НСтр("en='Невозможно выполнить операцию:';ru='Невозможно выполнить операцию:'");
	
	Если НЕ ЗначениеЗаполнено(НачПериода) ИЛИ НЕ ЗначениеЗаполнено(КонПериода) Тогда
		ЕстьОшибки = Истина;
		ТекстОшибки = ТекстОшибки + Символы.ПС + " - " + НСтр("en='необходимо заполнить период отчета.';ru='необходимо заполнить период отчета.'");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ЕстьОшибки = Истина;
		ТекстОшибки = ТекстОшибки + Символы.ПС + " - " + НСтр("en='необходимо заполнить организацию.';ru='необходимо заполнить организацию.'");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрВыработки) Тогда
		ЕстьОшибки = Истина;
		ТекстОшибки = ТекстОшибки + Символы.ПС + " - " + НСтр("en='необходимо заполнить параметр выработки.';ru='необходимо заполнить параметр выработки.'");
	КонецЕсли;
	
	флЕстьВидыПЛ = Ложь;
	Для Каждого ТекВидПЛ Из ВидыПЛ Цикл
		Если ЗначениеЗаполнено(ТекВидПЛ.Значение) Тогда
			флЕстьВидыПЛ = Истина;
		КонецЕсли;
	КонецЦикла;
	Если НЕ флЕстьВидыПЛ Тогда
		ЕстьОшибки = Истина;
		ТекстОшибки = ТекстОшибки + Символы.ПС + " - " + НСтр("en='необходимо указать хотя бы один вид путевых листов.';ru='необходимо указать хотя бы один вид путевых листов.'");
	КонецЕсли;	
	
	Если ЕстьОшибки Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ТабДок = СформироватьЭСМЗаПериод();
	ТабДок.Показать("ЭСМ-6");
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПериода(Команда)
	ДиалогПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогПериода.Период.ДатаНачала = НачПериода;
	ДиалогПериода.Период.ДатаОкончания = КонПериода;
	ДиалогПериода.Показать(Новый ОписаниеОповещения("НастройкаПериодаЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПериодаЗавершение(Период, ДополнительныеПараметры) Экспорт
    
    Если Период <> Неопределено Тогда
        НачПериода = Период.ДатаНачала;
        КонПериода = Период.ДатаОкончания;
    КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьЭСМЗаПериод()
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("уатЭСМ6");
	КоличествоСтрокНаСтранице = 16;
	
	// вывод лицевой
	Если ПечататьОбложку Тогда
		ОбластьЛицевая = Макет.ПолучитьОбласть("Лицевая");
		ОбластьЛицевая.Параметры.Организация = Организация;
		ОбластьЛицевая.Параметры.ОрганизацияПредставление = уатОбщегоНазначенияТиповыеСервер.ОписаниеОрганизации(
		уатОбщегоНазначенияСервер.СведенияОЮрФизЛице(Организация, ТекущаяДата()), "ПолноеНаименование, ЮридическийАдрес, Телефоны");
		ОбластьЛицевая.Параметры.КодПоОКПО = Организация.КодПоОКПО;
		ОбластьЛицевая.Параметры.Год = Формат(Год(НачПериода), "ЧГ=");
		ТабДок.Вывести(ОбластьЛицевая);
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатВыработкаТСОбороты.ТС КАК ТС,
	|	уатВыработкаТСОбороты.ОбъектСтроительства КАК ОбъектСтроительства,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, ДЕНЬ) КАК Дата,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, МЕСЯЦ) КАК Месяц,
	|	СУММА(уатВыработкаТСОбороты.КоличествоОборот) КАК ВремяВРаботе,
	|	СУММА(ЕСТЬNULL(уатПредоставленныеУслуги.СуммаРеглОборот, 0)) КАК СтоимостьРабот,
	|	ВЫБОР
	|		КОГДА &ПараметрВыработки = ЗНАЧЕНИЕ(Справочник.уатПараметрыВыработки.ВремяВРаботе)
	|			ТОГДА СУММА(уатВыработкаТСОбороты.КоличествоОборот)
	|		ИНАЧЕ СУММА(ЕСТЬNULL(уатПредоставленныеУслуги.КоличествоОборот, 0))
	|	КОНЕЦ КАК ОбъемРабот,
	|	СУММА(ЕСТЬNULL(уатПредоставленныеУслуги2.СуммаРеглОборот, 0)) КАК СтоимостьЧасов
	|ИЗ
	|	РегистрНакопления.уатВыработкаТС.Обороты(&НачПериода, &КонПериода, Регистратор, ) КАК уатВыработкаТСОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатПредоставленныеУслуги.Обороты(
	|				,
	|				,
	|				,
	|				ПараметрВыработки = &ПараметрВыработки
	|					И ПолучательУслуг = ЗНАЧЕНИЕ(Перечисление.уатПолучателиУслуг.Контрагент)) КАК уатПредоставленныеУслуги
	|		ПО уатВыработкаТСОбороты.ТС = уатПредоставленныеУслуги.ТС
	|			И уатВыработкаТСОбороты.ОбъектСтроительства = уатПредоставленныеУслуги.ОбъектСтроительства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатПредоставленныеУслуги.Обороты(
	|				,
	|				,
	|				,
	|				ПараметрВыработки = ЗНАЧЕНИЕ(Справочник.уатПараметрыВыработки.ВремяВРаботе)
	|					И ПолучательУслуг = ЗНАЧЕНИЕ(Перечисление.уатПолучателиУслуг.Контрагент)) КАК уатПредоставленныеУслуги2
	|		ПО уатВыработкаТСОбороты.ТС = уатПредоставленныеУслуги2.ТС
	|			И уатВыработкаТСОбороты.ОбъектСтроительства = уатПредоставленныеУслуги2.ОбъектСтроительства
	|ГДЕ
	|	уатВыработкаТСОбороты.Регистратор.ВидПЛ В(&ВидыПЛ)
	|	И уатВыработкаТСОбороты.ПараметрВыработки = ЗНАЧЕНИЕ(Справочник.уатПараметрыВыработки.ВремяВРаботе)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, МЕСЯЦ),
	|	уатВыработкаТСОбороты.ТС,
	|	уатВыработкаТСОбороты.ОбъектСтроительства,
	|	НАЧАЛОПЕРИОДА(уатВыработкаТСОбороты.Период, ДЕНЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц
	|ИТОГИ
	|	СУММА(ВремяВРаботе),
	|	СУММА(СтоимостьРабот),
	|	СУММА(ОбъемРабот),
	|	СУММА(СтоимостьЧасов)
	|ПО
	|	Месяц ПЕРИОДАМИ(МЕСЯЦ, &НачПериода, &КонПериода),
	|	ТС,
	|	ОбъектСтроительства,
	|	Дата";
	
	Запрос.УстановитьПараметр("НачПериода", НачПериода);
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонПериода));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПараметрВыработки", ПараметрВыработки);
	Запрос.УстановитьПараметр("ВидыПЛ", ВидыПЛ.ВыгрузитьЗначения());
	
	ВыборкаМесяц = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Месяц", "ВСЕ");
	
	КоличествоСтрокНаСтранице = 16;
	
	Пока ВыборкаМесяц.Следующий() Цикл
		НомерСтроки = 1;
		НомерСтрокиНаСтранице = 1;
		
		ОбластьЧетная = Макет.ПолучитьОбласть("ЧетнаяСтраница");
		ОбластьЧетная.Параметры.Месяц = Формат(ВыборкаМесяц.Месяц, "ДФ=MMMM");
		ОбластьНеЧетная = Макет.ПолучитьОбласть("НеЧетнаяСтраница");
				
		ВыборкаТС = ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаТС.Следующий() Цикл // цикл по ТС
			ВыборкаОбъектСтроительства = ВыборкаТС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			// Если ВыборкаОбъектСтроительства.Следующий() Тогда // раскомментировать для проверки вывода нескольких страниц на месяц
			//Для СчСч = 1 По 17 Цикл
			Пока ВыборкаОбъектСтроительства.Следующий() Цикл //цикл по объектам строительства
				Если НомерСтрокиНаСтранице > КоличествоСтрокНаСтранице Тогда
					НомерСтрокиНаСтранице = 1;
					
					ТабДок.Вывести(ОбластьЧетная);
					ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
					ТабДок.Вывести(ОбластьНеЧетная);
					ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
					
					ОбластьЧетная = Макет.ПолучитьОбласть("ЧетнаяСтраница");
					ОбластьЧетная.Параметры.Месяц = Формат(ВыборкаМесяц.Месяц, "ДФ=MMMM");
					ОбластьНеЧетная = Макет.ПолучитьОбласть("НеЧетнаяСтраница");
				КонецЕсли;
				
				// вывод ТС, объекта
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 1, 5+НомерСтрокиНаСтранице*2, 2).Текст = НомерСтроки;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 3, 5+НомерСтрокиНаСтранице*2, 5).Текст = Строка(ВыборкаТС.ТС);
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 3, 5+НомерСтрокиНаСтранице*2, 5).Расшифровка = ВыборкаТС.ТС;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 6, 5+НомерСтрокиНаСтранице*2, 10).Текст = ВыборкаТС.ТС.Модель;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 6, 5+НомерСтрокиНаСтранице*2, 10).Расшифровка = ВыборкаТС.ТС.Модель;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 11, 5+НомерСтрокиНаСтранице*2, 12).Текст = ВыборкаТС.ТС.ГодВыпуска;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 13, 5+НомерСтрокиНаСтранице*2, 15).Текст = ВыборкаТС.ТС.ГаражныйНомер;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 16, 5+НомерСтрокиНаСтранице*2, 21).Текст = ВыборкаОбъектСтроительства.ОбъектСтроительства;
				ОбластьЧетная.Область(4+НомерСтрокиНаСтранице*2, 16, 5+НомерСтрокиНаСтранице*2, 21).Расшифровка = ВыборкаОбъектСтроительства.ОбъектСтроительства;
				
				// вывод матрицы рабочего времени по дням месяца
				ВыборкаДата = ВыборкаОбъектСтроительства.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				мсвВремяВРаботе = Новый Массив;
				Для Сч = 0 По 30 Цикл
					мсвВремяВРаботе.Добавить(0);
				КонецЦикла;
				Пока ВыборкаДата.Следующий() Цикл // цикл по датам
					ТекДень = День(ВыборкаДата.Дата)-1;
					мсвВремяВРаботе[ТекДень] = мсвВремяВРаботе[ТекДень] + ВыборкаДата.ВремяВРаботе;
				КонецЦикла;
				ВсегоВремя = 0;
				ВсегоДней = 0;
				Для Сч = 0 По 30 Цикл
					Если мсвВремяВРаботе[Сч] = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					ТекДень = Сч + 1;
					Если ТекДень > 15 Тогда
						НомСтрокиТабДока = 5+НомерСтрокиНаСтранице*2;
						ТекДень = ТекДень-15;
					Иначе
						НомСтрокиТабДока = 4+НомерСтрокиНаСтранице*2;
					КонецЕсли;
					ВсегоВремя = ВсегоВремя + мсвВремяВРаботе[Сч];
					ВсегоДней = ВсегоДней + 1;
					ОбластьЧетная.Область(НомСтрокиТабДока, 21 + ТекДень, НомСтрокиТабДока, 21 + ТекДень).Текст = мсвВремяВРаботе[Сч]/3600;
				КонецЦикла;
				
				// вывод итогов для ТС и объекта за месяц
				ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 1, 6+НомерСтрокиНаСтранице, 1).Текст = ВсегоДней;
				ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 4, 6+НомерСтрокиНаСтранице, 4).Текст = ВсегоВремя/3600;
				Если ВсегоВремя <> 0 Тогда
					ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 5, 6+НомерСтрокиНаСтранице, 5).Текст = ВыборкаОбъектСтроительства.СтоимостьЧасов/(ВсегоВремя/3600);
				КонецЕсли;
				ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 6, 6+НомерСтрокиНаСтранице, 6).Текст = ВыборкаОбъектСтроительства.СтоимостьЧасов;
				ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 7, 6+НомерСтрокиНаСтранице, 7).Текст = ПараметрВыработки;
				ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 9, 6+НомерСтрокиНаСтранице, 9).Текст = ВыборкаОбъектСтроительства.СтоимостьРабот;
				КоэффициентВыработки = 0;
				Если ПараметрВыработки.Временный Тогда
					ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 8, 6+НомерСтрокиНаСтранице, 8).Текст = ВыборкаОбъектСтроительства.ОбъемРабот/3600;
					Если ВыборкаТС.ТС.БалансоваяСтоимость <> 0 Тогда
						КоэффициентВыработки = Окр(ВыборкаОбъектСтроительства.ОбъемРабот/3600/ВыборкаТС.ТС.БалансоваяСтоимость, 3);
					КонецЕсли;
				Иначе
					ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 8, 6+НомерСтрокиНаСтранице, 8).Текст = ВыборкаОбъектСтроительства.ОбъемРабот;
					Если ВыборкаТС.ТС.БалансоваяСтоимость <> 0 Тогда
						КоэффициентВыработки = Окр(ВыборкаОбъектСтроительства.ОбъемРабот/ВыборкаТС.ТС.БалансоваяСтоимость, 3);
					КонецЕсли;
				КонецЕсли;
				
				ОбластьНеЧетная.Область(6+НомерСтрокиНаСтранице, 10, 6+НомерСтрокиНаСтранице, 10).Текст = КоэффициентВыработки;
				
				НомерСтроки = НомерСтроки + 1;
				НомерСтрокиНаСтранице = НомерСтрокиНаСтранице + 1;
			КонецЦикла;
			// КонецЕсли; // раскомментировать для проверки вывода нескольких страниц на месяц
			
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьЧетная);
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		ТабДок.Вывести(ОбластьНеЧетная);
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;

	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку     = Ложь;
	Табдок.ТолькоПросмотр = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДок;
	
КонецФункции

#КонецОбласти