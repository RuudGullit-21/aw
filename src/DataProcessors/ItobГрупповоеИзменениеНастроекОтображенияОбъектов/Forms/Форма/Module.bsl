
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектыМониторинга.ТекстЗапроса = ТекстЗапросаДинамический();
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИконкаНаКартеПриИзменении(Элемент)
	АдресИконки = ПолучитьНавигационнуюСсылку(ИконкаНаКарте, "Иконка");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИконкаНаКартеПриИзменении(Элемент)
	АдресДополнительнойИконки = ПолучитьНавигационнуюСсылку(ДополнительнаяИконкаНаКарте, "Иконка");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИконкаНаКартеПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДополнительнаяИконкаНаКартеПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВариантОтображенияТрекаПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЦветМаршрутаНаКартеПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЦветНадписиНаКартеПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНеПоказыватьСтрелкуНаправленияДвиженияПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	Элементы.Книга.ТекущаяСтраница = Элементы.НастройкиОтображения;
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	Элементы.Книга.ТекущаяСтраница = Элементы.ОтборОбъектовМониторинга;
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройки(Команда)
	ПрименитьНастройкиСервер();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЦвет(Команда)
	
	ДиалогВыбораЦвета = Новый ДиалогВыбораЦвета;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманды", "КнопкаВыбратьЦвет");
	ДополнительныеПараметры.Вставить("ИмяРеквизитаХранения", "ЦветМаршрутаНаКарте");
	ДиалогВыбораЦвета.Цвет = Элементы.КнопкаВыбратьЦвет.ЦветФона;
	ДиалогВыбораЦвета.Показать(Новый ОписаниеОповещения("ВыборЦветаЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЦветНадписи(Команда)
	
	ДиалогВыбораЦвета = Новый ДиалогВыбораЦвета;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманды", "ВыбратьЦветНадписи");
	ДополнительныеПараметры.Вставить("ИмяРеквизитаХранения", "ЦветНадписиНаКарте");
	ДиалогВыбораЦвета.Цвет = Элементы.ВыбратьЦветНадписи.ЦветФона;
	ДиалогВыбораЦвета.Показать(Новый ОписаниеОповещения("ВыборЦветаЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыОбъектыМониторинга

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступность()
	
	Элементы.ИконкаНаКарте.Доступность = ИзменитьИконкаНаКарте;
	Элементы.ДополнительнаяИконкаНаКарте.Доступность = ИзменитьДополнительнаяИконкаНаКарте;
	Элементы.ВариантОтображенияТрека.Доступность = ИзменитьВариантОтображенияТрека;
	Элементы.КнопкаВыбратьЦвет.Доступность = ИзменитьЦветМаршрутаНаКарте;
	Элементы.ВыбратьЦветНадписи.Доступность = ИзменитьЦветНадписиНаКарте;
	Элементы.НеПоказыватьСтрелкуНаправленияДвижения.Доступность = ИзменитьНеПоказыватьСтрелкуНаправленияДвижения;
	
	Элементы.ПрименитьНастройки.Доступность = (ИзменитьЦветМаршрутаНаКарте 
											   ИЛИ ИзменитьЦветНадписиНаКарте 
											   ИЛИ ИзменитьИконкаНаКарте 
											   ИЛИ ИзменитьДополнительнаяИконкаНаКарте
											   ИЛИ ИзменитьВариантОтображенияТрека
											   ИЛИ ИзменитьНеПоказыватьСтрелкуНаправленияДвижения);
	
КонецПроцедуры
 
&НаСервере
Функция ТекстЗапросаДинамический()
	
	ТекстЗапроса = "";
	
	МетаданныеОбъектовМониторинга = Новый Массив;
	ВсеРеквизитыСправочников = Новый Массив;
	ЗаполнитьМетаданныеОбъектовМониторинга(МетаданныеОбъектовМониторинга, ВсеРеквизитыСправочников);
	
	МассивСтрок = Новый Массив;
	ЭтоПервыйОбъект = Истина;
	Для каждого ДанныеОбъектаМониторинга Из МетаданныеОбъектовМониторинга Цикл
		Если ЭтоПервыйОбъект Тогда
			МассивСтрок.Добавить(СтрШаблон("ВЫБРАТЬ
										   |	%1.Ссылка КАК ОбъектМониторинга,
										   |	%1.ПометкаУдаления КАК ПометкаУдаления, 
										   |	%1.Наименование КАК Наименование,
										   |	%2 КАК ТипОбъекта", ДанныеОбъектаМониторинга.ИмяОбъекта, ДанныеОбъектаМониторинга.ПредставлениеТипа));
			Для каждого ИмяРеквизита Из ВсеРеквизитыСправочников Цикл
				Если НЕ ДанныеОбъектаМониторинга.РеквизитыОбъекта.Найти(ИмяРеквизита) = Неопределено Тогда
					МассивСтрок.Добавить(СтрШаблон(",
												   |	%1.%2 КАК %2", ДанныеОбъектаМониторинга.ИмяОбъекта, ИмяРеквизита));
				Иначе
					МассивСтрок.Добавить(СтрШаблон(",
												   |	НЕОПРЕДЕЛЕНО КАК %1", ИмяРеквизита));
				КонецЕсли;
			КонецЦикла;
			МассивСтрок.Добавить(СтрШаблон("
										   |ИЗ
										   |	Справочник.%1 КАК %1", ДанныеОбъектаМониторинга.ИмяОбъекта));
			ЭтоПервыйОбъект = Ложь;
		Иначе
			МассивСтрок.Добавить(СтрШаблон("
										   |
										   |ОБЪЕДИНИТЬ ВСЕ
										   |
										   |ВЫБРАТЬ
										   |	%1.Ссылка,
										   |	%1.ПометкаУдаления,
										   |	%1.Наименование,
										   |	%2", ДанныеОбъектаМониторинга.ИмяОбъекта, ДанныеОбъектаМониторинга.ПредставлениеТипа));
			Для каждого ИмяРеквизита Из ВсеРеквизитыСправочников Цикл
				Если НЕ ДанныеОбъектаМониторинга.РеквизитыОбъекта.Найти(ИмяРеквизита) = Неопределено Тогда
					МассивСтрок.Добавить(СтрШаблон(",
												   |	%1.%2", ДанныеОбъектаМониторинга.ИмяОбъекта, ИмяРеквизита));
				Иначе
					МассивСтрок.Добавить(",
										 |	НЕОПРЕДЕЛЕНО");
				КонецЕсли;
			КонецЦикла;
			МассивСтрок.Добавить(СтрШаблон("
										   |ИЗ
										   |	Справочник.%1 КАК %1", ДанныеОбъектаМониторинга.ИмяОбъекта));
		КонецЕсли;
	КонецЦикла;
	ТекстЗапроса = СтрСоединить(МассивСтрок, "");
	
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьМетаданныеОбъектовМониторинга(МетаданныеОбъектовМониторинга, ВсеРеквизитыСправочников)
	
	ТипыОбъектовМониторинга = Метаданные.ОпределяемыеТипы.ItobОбъектМониторинга.Тип.Типы();
	Для каждого ТипОбъектаМониторинга Из ТипыОбъектовМониторинга Цикл
		ОбъектМониторингаСсылка = Новый(ТипОбъектаМониторинга);
		ТекущиеМетаданные = ОбъектМониторингаСсылка.Метаданные(); 
			
		Реквизиты = ТекущиеМетаданные.Реквизиты;
		РеквизитыОбъекта = Новый Массив;
		Для каждого Реквизит Из Реквизиты Цикл
			Если МожноДобавитьРеквизитВОтбор(Реквизит, ВсеРеквизитыСправочников) Тогда
				ВсеРеквизитыСправочников.Добавить(Реквизит.Имя);
				РеквизитыОбъекта.Добавить(Реквизит.Имя);
			КонецЕсли;
		КонецЦикла;
		
		ДанныеОбъектаМониторинга = Новый Структура;
		ДанныеОбъектаМониторинга.Вставить("ИмяОбъекта", ТекущиеМетаданные.Имя);
		ДанныеОбъектаМониторинга.Вставить("РеквизитыОбъекта", РеквизитыОбъекта);
		ДанныеОбъектаМониторинга.Вставить("ПредставлениеТипа", СтрШаблон("""%1""", ТекущиеМетаданные.Синоним));
		
		МетаданныеОбъектовМониторинга.Добавить(ДанныеОбъектаМониторинга);
	КонецЦикла;
	
КонецПроцедуры
 
&НаСервере
Функция МожноДобавитьРеквизитВОтбор(РеквизитМетаданные, ДобавленныеРеквизиты)
	
	Если РеквизитМетаданные.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если РеквизитМетаданные.Тип.СодержитТип(Тип("Строка"))
		 И РеквизитМетаданные.Тип.КвалификаторыСтроки = Новый КвалификаторыСтроки(0) Тогда
		 
		Возврат Ложь;
	КонецЕсли; 
	
	Если СтрНайти(РеквизитМетаданные.Синоним, "(не используется)") > 0 Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если НЕ ДобавленныеРеквизиты.Найти(РеквизитМетаданные.Имя) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	
	Возврат Истина;
	
КонецФункции
 
&НаСервере
Процедура ПрименитьНастройкиСервер()
		
	Схема = Элементы.ОбъектыМониторинга.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.ОбъектыМониторинга.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	Результат = ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Счетчик = 0;
	Для каждого СтрокаРезультата Из Результат Цикл		
		Если НЕ ЗначениеЗаполнено(СтрокаРезультата.ОбъектМониторинга) Тогда
			Продолжить;	
		КонецЕсли; 
		
		Счетчик = Счетчик + 1;
		
		Запись = РегистрыСведений.ItobНастройкиОтображенияОбъектов.СоздатьМенеджерЗаписи();
		Запись.Объект = СтрокаРезультата.ОбъектМониторинга;
		Запись.Прочитать();
		Запись.Объект = СтрокаРезультата.ОбъектМониторинга;
		
		Если ИзменитьЦветМаршрутаНаКарте Тогда
			Запись.ЦветМаршрутаНаКарте = ЦветМаршрутаНаКарте;
		КонецЕсли;
		Если ИзменитьЦветНадписиНаКарте Тогда
			Запись.ЦветНадписиНаКарте = ЦветНадписиНаКарте;
		КонецЕсли;
		Если ИзменитьИконкаНаКарте Тогда
			Запись.ИконкаНаКарте = ИконкаНаКарте;
		КонецЕсли;
		Если ИзменитьДополнительнаяИконкаНаКарте Тогда
			Запись.ДополнительнаяИконкаНаКарте = ДополнительнаяИконкаНаКарте;
		КонецЕсли;
		Если ИзменитьВариантОтображенияТрека Тогда
			Запись.ВариантОтображенияТрека = ВариантОтображенияТрека;
		КонецЕсли;
		Если ИзменитьНеПоказыватьСтрелкуНаправленияДвижения Тогда
			Запись.НеПоказыватьСтрелкуНаправленияДвижения = НеПоказыватьСтрелкуНаправленияДвижения;	
		КонецЕсли; 
		
		Запись.Записать();
	КонецЦикла;
	
	Если Счетчик = 0 Тогда
		ТекстСообщения = "Не найдено подходящих для редактирования объектов";
	Иначе
		ТекстСообщения = "Изменения успешно записаны. Количество модифицированных объектов: " + Строка(Счетчик);	
	КонецЕсли; 
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();

КонецПроцедуры

&НаКлиенте
Процедура ВыборЦветаЗавершение(ВыбранныйЦвет, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЦвет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АбсолютныйЦвет = ItobОбщегоНазначенияВызовСервера.ПолучитьАбсолютныйЦвет(ВыбранныйЦвет);
	
	УстановитьЦветФонаЦветТекстаЭлементуФормыСервер(АбсолютныйЦвет, ДополнительныеПараметры.ИмяЭлементаКоманды);
	
	ЭтотОбъект[ДополнительныеПараметры.ИмяРеквизитаХранения] = ItobОбщегоНазначенияКлиентСервер.ПолучитьЗначениеХраненияЦвета(АбсолютныйЦвет.Красный,
																															  АбсолютныйЦвет.Зеленый,
																															  АбсолютныйЦвет.Синий);
КонецПроцедуры

&НаСервере
Процедура УстановитьЦветФонаЦветТекстаЭлементуФормыСервер(АбсолютныйЦвет, ИмяЭлементаФормы)
	
	Если АбсолютныйЦвет = Неопределено Тогда
		Элементы[ИмяЭлементаФормы].ЦветФона = Новый Цвет;
		Элементы[ИмяЭлементаФормы].ЦветТекста = Новый Цвет;
		
		Возврат;
	КонецЕсли;
	
	Элементы[ИмяЭлементаФормы].ЦветФона = АбсолютныйЦвет;
	Элементы[ИмяЭлементаФормы].ЦветТекста = Новый Цвет(255 - АбсолютныйЦвет.Красный,
													   255 - АбсолютныйЦвет.Зеленый,
													   255 - АбсолютныйЦвет.Синий);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыМониторингаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(Элемент.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;	
	КонецЕсли; 
	
	Если Поле.Имя = "ОбъектыМониторингаОбъектМониторинга" Тогда
		ПоказатьЗначение(, Элемент.ТекущаяСтрока);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти