#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// начало блока стандартных операций
	ДопПараметрыОткрытие = Новый Структура("ИмяФормы", ИмяФормы);
	уатЗащищенныеФункцииСервер.уатОбработкаФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	// конец блока стандартных операций
	
	// временно заблокирована
	ТекстНСТР = НСтр("en='Functional is under development';ru='Функционал находится в стадии разработки'");
	уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстНСТР, Отказ);
	СтандартнаяОбработка = Ложь;
	Возврат;
	
	ТекПользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	
	Если Параметры.Свойство("Прейскурант") Тогда
		Объект.Прейскурант = Параметры.Прейскурант;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// начало блока стандартных операций
	уатЗащищенныеФункцииКлиент.уатОбработкаФормаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// конец блока стандартных операций
	
	РассчитатьСтоимостьУслугТС();
	УстановитьВидимостьНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		НоваяСтрока = Объект.Услуги.Добавить();
		НоваяСтрока.Тариф =  ВыбранноеЗначение.Тариф;
		НоваяСтрока.Количество 	= ВыбранноеЗначение.Количество; 
		НоваяСтрока.Цена 		= ВыбранноеЗначение.Цена;
		НоваяСтрока.СуммаНДС	= 0;
		НоваяСтрока.СтавкаНДС	= уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекПользователь, "ОсновнаяСтавкаНДС");
		РассчитатьСуммуСтроки(НоваяСтрока);
		РассчитатьСуммуНДССтроки(НоваяСтрока);
		РассчитатьСуммуВсегоСтроки(НоваяСтрока);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СуммаВключаетНДСПриИзменении(Элемент)
	Для каждого ТекСтрока Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(ТекСтрока);
		РассчитатьСуммуНДССтроки(ТекСтрока);
		РассчитатьСуммуВсегоСтроки(ТекСтрока);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьНДСПриИзменении(Элемент)
	Для каждого ТекСтрока Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(ТекСтрока);
		РассчитатьСуммуНДССтроки(ТекСтрока);
		РассчитатьСуммуВсегоСтроки(ТекСтрока);
	КонецЦикла;
	УстановитьВидимостьНДС();
КонецПроцедуры

&НаКлиенте
Процедура НадписьДопИнфоНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.Прейскурант) Тогда
		ОткрытьФорму("Справочник.уатПрейскурантыТС.ФормаОбъекта", Новый Структура("Ключ", Объект.Прейскурант));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиТарифПриИзменении(Элемент)
	ТекСтрока = Элементы.Услуги.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуСтроки(ТекСтрока);
	РассчитатьСуммуНДССтроки(ТекСтрока);
	РассчитатьСуммуВсегоСтроки(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.Услуги.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуСтроки(ТекСтрока);
	РассчитатьСуммуНДССтроки(ТекСтрока);
	РассчитатьСуммуВсегоСтроки(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	ТекСтрока = Элементы.Услуги.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуСтроки(ТекСтрока);
	РассчитатьСуммуНДССтроки(ТекСтрока);
	РассчитатьСуммуВсегоСтроки(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)
	ТекСтрока = Элементы.Услуги.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекСтрока.ПроцентСкидкиНаценки = 0;
	
	Если ТекСтрока.Количество = 0 Тогда
		ТекСтрока.Количество = 1;
	КонецЕсли;
	
	ТекСтрока.Цена = ТекСтрока.Сумма / ТекСтрока.Количество;
	
	РассчитатьСуммуНДССтроки(ТекСтрока);
	РассчитатьСуммуВсегоСтроки(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)
	ТекСтрока = Элементы.Услуги.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуНДССтроки(ТекСтрока);
	РассчитатьСуммуВсегоСтроки(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура УслугиПроцентСкидкиНаценкиПриИзменении(Элемент)
	ТекСтрока = Элементы.Услуги.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуСтроки(ТекСтрока);
	РассчитатьСуммуНДССтроки(ТекСтрока);
	РассчитатьСуммуВсегоСтроки(ТекСтрока);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзПрейскуранта(Команда)
	Если Объект.Услуги.Количество() > 0 Тогда
		Ответ = Неопределено;
		
		ТекстНСТР = НСтр("en='Do you want to clear the table before filling?';ru='Очистить таблицу перед заполнением?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗагрузитьИзПрейскурантаЗавершение", ЭтотОбъект), ТекстНСТР, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
		Возврат;	
	КонецЕсли;	
	
	ЗагрузитьИзПрейскурантаФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзПрейскурантаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Отмена Тогда
        Возврат;
    ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
        Объект.Услуги.Очистить();
    КонецЕсли;	
    
    ЗагрузитьИзПрейскурантаФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзПрейскурантаФрагмент()
    
    ОткрытьФорму("Справочник.уатПрейскурантыТС.ФормаВыбора",,,,,, Новый ОписаниеОповещения("ЗагрузитьИзПрейскурантаФрагментЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзПрейскурантаФрагментЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Объект.Прейскурант = Результат;
    Если ЗначениеЗаполнено(Объект.Прейскурант) Тогда
        РассчитатьСтоимостьУслугТС();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ОткрытьФорму("Обработка.уатКалькуляторТарифов.Форма.ФормаПодбора", , ЭтотОбъект,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Функция рассчитывает стоимость услуг
//
&НаКлиенте
Процедура РассчитатьСтоимостьУслугТС()
	
	ФлагРасчета = РассчитатьСтоимостьУслугТССервер();
	Если ФлагРасчета = -1 Тогда
		Возврат;
	КонецЕсли;
	
	// пересчет сумм на клиенте
	Для Каждого ВыбраннаяСтрока Из Объект.Услуги Цикл
		Если уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ВыбраннаяСтрока.Тариф, "ЭтоГруппа") = ИСТИНА Тогда
			Продолжить;
		КонецЕсли;
		
		РассчитатьСуммуСтроки(ВыбраннаяСтрока);
		РассчитатьСуммуНДССтроки(ВыбраннаяСтрока);
		РассчитатьСуммуВсегоСтроки(ВыбраннаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

// Функция рассчитывает стоимость услуг
//
&НаСервере
Функция РассчитатьСтоимостьУслугТССервер()
	
	Результат = 0;
		
	Если ЗначениеЗаполнено(Объект.Прейскурант) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	уатТарифыТС.Ссылка КАК Тариф,
		|	уатТарифыТС.Владелец.Ссылка КАК Прейскурант
		|ИЗ
		|	Справочник.уатТарифыТС КАК уатТарифыТС
		|ГДЕ
		|	уатТарифыТС.Владелец.Ссылка = &Владелец";
		
		Запрос.УстановитьПараметр("Владелец", Объект.Прейскурант);
		ТаблицаПрейскурант = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаПрейскурант.Количество() = 0 Тогда
			НадписьДопИнфо = НСтр("en='Current tariff rate on price list is absent!';ru='Действующие тарифы по прейскуранту отсутствуют!'");
			Возврат -1;
		КонецЕсли;
		
	Иначе //ищем прейскурант
		ТаблицаПрейскурант = уатРасчетыПоТарифам.НайтиПрейскурант(Объект.СписокФильтров);
		Если ТаблицаПрейскурант = Неопределено Тогда
			НадписьДопИнфо = НСтр("en='Price list is not found!';ru='Прейскурант не найден!'");
			Возврат -1;
		КонецЕсли;
		
		Объект.СуммаВключаетНДС = Объект.Прейскурант.СуммаВключаетНДС;
		
	КонецЕсли;
	
	НадписьДопИнфо = СокрЛП(Объект.Прейскурант.Наименование) + " " + СокрЛП(Объект.Прейскурант.Код);
		
    Для Каждого ВыбраннаяСтрока Из ТаблицаПрейскурант Цикл
		Если ВыбраннаяСтрока.Тариф.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Объект.Услуги.Добавить();
		НоваяСтрока.Тариф = ВыбраннаяСтрока.Тариф;
		НоваяСтрока.Количество 	= 1; 
		НоваяСтрока.Цена 		= ВыбраннаяСтрока.Тариф.Тариф;
		НоваяСтрока.СуммаНДС	= 0;
		НоваяСтрока.СтавкаНДС	= уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекПользователь, "ОсновнаяСтавкаНДС");
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции //РасчетПрейскурантов

// Рассчитывает сумму в строке табличной части документа
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа,
//  ДокументОбъект       - объект редактируемого документа,
//  СпособРасчета        - "Со всеми скидками", сумма минус скидки;
//                         "Без учета ручной скидки", сумма минус автоматические скидки;
//                         "Без учета скидок", сумма (Цена * Количество);
//
&НаКлиенте
Процедура РассчитатьСуммуСтроки(СтрокаТабличнойЧасти)
		
	Если уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтрокаТабличнойЧасти.Тариф, "МетодРасчета")
		= ПредопределенноеЗначение("Перечисление.уатМетодыРасчетаПоТарифам.ФиксированнойСуммой")  Тогда
		
		СтрокаТабличнойЧасти.Цена = уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтрокаТабличнойЧасти.Тариф, "Тариф");
		
	Иначе
		ЗначенияГруппировок = Новый Соответствие;
		Если уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтрокаТабличнойЧасти.Тариф, "ПараметрВыработки")
			= ПредопределенноеЗначение("Справочник.уатПараметрыВыработки.ПробегСПассажирами") Тогда
			ЗначенияГруппировок.Вставить(ПредопределенноеЗначение("Справочник.уатПараметрыВыработки.ПробегСГрузом"),
				СтрокаТабличнойЧасти.Количество);
		Иначе
			ЗначенияГруппировок.Вставить(ПредопределенноеЗначение("Справочник.уатПараметрыВыработки.ВремяВРаботе"),
				СтрокаТабличнойЧасти.Количество);
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Цена = уатРасчетыПоТарифам.ЗначениеТарифа(СтрокаТабличнойЧасти.Тариф, ЗначенияГруппировок);
		
	КонецЕсли;	
	
	Сумма = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
	СуммаСкидки = Сумма * СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100;
	СтрокаТабличнойЧасти.Сумма = Сумма - СуммаСкидки;
	
КонецПроцедуры // РассчитатьСуммуСтроки()

&НаКлиенте
Процедура РассчитатьСуммуВсегоСтроки(ТекСтрока)
	ТекСтрока.Всего = ТекСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, ТекСтрока.СуммаНДС);
КонецПроцедуры

// Расчет, исходя из постоянной суммы
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа,
//  ДокументОбъект       - объект редактируемого документа.
//
&НаКлиенте
Процедура РассчитатьСуммуНДССтроки(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.СуммаНДС = уатОбщегоНазначенияТиповыеСервер.уатРассчитатьСуммуНДС(СтрокаТабличнойЧасти.Сумма,
		Объект.УчитыватьНДС, Объект.СуммаВключаетНДС, уатОбщегоНазначенияТиповые.уатПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));

КонецПроцедуры // РассчитатьСуммуНДССтроки()

&НаКлиенте
Процедура УстановитьВидимостьНДС()
	Если Элементы.УслугиСтавкаНДС.Видимость <> Объект.УчитыватьНДС Тогда
		Элементы.УслугиСтавкаНДС.Видимость = Объект.УчитыватьНДС;
		Элементы.УслугиСуммаНДС.Видимость = Объект.УчитыватьНДС;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
