
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторыТерминалов = Неопределено;
	ПредставлениеТСНаКарте = Неопределено;
	Параметры.Свойство("ИдентификаторыТерминалов", ИдентификаторыТерминалов);
	Параметры.Свойство("ПредставлениеТСНаКарте", ПредставлениеТСНаКарте);
	Если ТипЗнч(ИдентификаторыТерминалов) = Тип("Строка") Тогда
	    Отказ = Отказ ИЛИ НЕ ЗаполнитьОбъектыМониторинга(ИдентификаторыТерминалов, ПредставлениеТСНаКарте);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыМониторинга

&НаКлиенте
Процедура ОбъектыМониторингаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Прав(Поле.Имя, 14) = "ПоказатьОбъект" Тогда
		Оповестить("СфокусироватьсяНаВыбранномОбъекте", Элемент.ТекущиеДанные.ОбъектМониторинга); 
	Иначе
		СтрокаПоИдентификатору = ОбъектыМониторинга.НайтиПоИдентификатору(ВыбраннаяСтрока);
		ВыбранныйОбъект = СтрокаПоИдентификатору.ОбъектМониторинга;
		Если ЗначениеЗаполнено(ВыбранныйОбъект) Тогда
			ПоказатьЗначение(, ВыбранныйОбъект);
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьОбъектыМониторинга(ИдентификаторыТерминалов, ТипПредставленияТСНаКарте)
	
	ИдентификаторыТерминалов = СтрРазделить(ИдентификаторыТерминалов, ",");
	
	МассивТерминалов = Новый Массив;
	
	Для каждого ИдентификаторТерминала Из ИдентификаторыТерминалов Цикл
	    МассивТерминалов.Добавить(Справочники.ItobТерминалы.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторТерминала)));
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивТерминалов", МассивТерминалов);
	Запрос.УстановитьПараметр("ТипПредставленияТСНаКарте", ТипПредставленияТСНаКарте);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ItobПривязкиТрекеровСрезПоследних.Объект КАК ОбъектМониторинга,
	|	ItobПривязкиТрекеровСрезПоследних.Терминал КАК Терминал
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	РегистрСведений.ItobПривязкиТрекеров.СрезПоследних(, Терминал В (&МассивТерминалов)) КАК ItobПривязкиТрекеровСрезПоследних
	|ГДЕ
	|	ItobПривязкиТрекеровСрезПоследних.ТерминалУстановлен = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.ОбъектМониторинга КАК ОбъектМониторинга,
	|	втДанные.Терминал КАК Терминал,
	|	ИСТИНА КАК ПоказатьОбъект,
	|	ВЫБОР
	|		КОГДА &ТипПредставленияТСНаКарте = ЗНАЧЕНИЕ(Перечисление.ItobПредставлениеТС.Наименование)
	|			ТОГДА ПРЕДСТАВЛЕНИЕССЫЛКИ(втДанные.ОбъектМониторинга)
	|		КОГДА &ТипПредставленияТСНаКарте = ЗНАЧЕНИЕ(Перечисление.ItobПредставлениеТС.ГосНомер)
	|			ТОГДА ItobСвойстваТранспортныхСредств.ГосНомер
	|		КОГДА &ТипПредставленияТСНаКарте = ЗНАЧЕНИЕ(Перечисление.ItobПредставлениеТС.ГаражныйНомер)
	|			ТОГДА ItobСвойстваТранспортныхСредств.ГаражныйНомер
	|		КОГДА &ТипПредставленияТСНаКарте = ЗНАЧЕНИЕ(Перечисление.ItobПредставлениеТС.НаименованиеТерминала)
	|			ТОГДА ПРЕДСТАВЛЕНИЕССЫЛКИ(втДанные.Терминал)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ПредставлениеТС
	|ИЗ
	|	втДанные КАК втДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ItobСвойстваТранспортныхСредств КАК ItobСвойстваТранспортныхСредств
	|		ПО втДанные.ОбъектМониторинга = ItobСвойстваТранспортныхСредств.ТранспортноеСредство";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбъектыМониторинга.Добавить(), Выборка);
	КонецЦикла;
	
	Количество = Выборка.Количество();
	
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти