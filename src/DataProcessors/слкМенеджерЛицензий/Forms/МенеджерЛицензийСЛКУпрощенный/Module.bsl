
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Обработки.слкМенеджерЛицензий.ПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	
	ЗаполнитьСерииКлючейЗащитыСервер();
	ОбновитьСтатусыСерийКлючейЗащитыСервер("ПодключитьЕслиНеЗапущен");
	
	Элементы.СпособСохраненияНастроек.Доступность = ПравоДоступа("АдминистрированиеДанных", Метаданные);
	
	АктуальнаяВерсияКомпонентыСЛК = НомерАктуальнойВерсииКомпонентыСЛК();
	Элементы.АктуальностьСЛК.Видимость = ЗначениеЗаполнено(АктуальнаяВерсияКомпонентыСЛК)
		И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияКомпонентыСЛК, АктуальнаяВерсияКомпонентыСЛК) < 0;
		
	ПерсональныеНастройки = НЕ Элементы.СпособСохраненияНастроек.Доступность
		Или слкМенеджерЗащитыСервер.ЕстьПерсональныеНастройкиТекущегоПользователя();
	
	Если ЗначениеЗаполнено(ТаблицаСерийКлючейЗащиты) Тогда
		ОбновитьРеквизитыШапки(ЭтотОбъект, ТаблицаСерийКлючейЗащиты[0]);
	КонецЕсли;
	
	Если Элементы.НаименованиеЛицензии.СписокВыбора.Количество() < 2 Тогда
		Элементы.АдресСервераЛицензии.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "слкИзмененСоставЛицензий" Тогда
		ОбновитьСтатусыСерийКлючейЗащитыСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеЛицензииПриИзменении(Элемент)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Серия", СерияЛицензии);
	МассивСтрок = ТаблицаСерийКлючейЗащиты.НайтиСтроки(СтруктураПоиска);
	Если НЕ ЗначениеЗаполнено(МассивСтрок) Тогда
		ОбновитьРеквизитыШапки(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ОбновитьРеквизитыШапки(ЭтотОбъект, МассивСтрок[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура АктуальностьСЛКНажатие(Элемент)
	
	ТекстСообщения = СтрШаблон(НСтр("ru='Используется устаревшая версия компоненты защиты СЛК %1.
	|Для корректной работы необходима компонента защиты версии %2 или выше.
	|
	|Возможные причины:
	|1.Установлена внешняя компонента устаревшей версии (на сервере или локально в случае файловой базы);
	|2.Некорректно выполнено обновление программы.
	|
	|Для решения проблемы обратитесь к администратору.
	|Последняя версия системы защиты доступна по ссылке http://licencecenter.ru/'"),
	ВерсияКомпонентыСЛК, 
	АктуальнаяВерсияКомпонентыСЛК);
	ПоказатьПредупреждение(Неопределено, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервераЛицензииПриИзменении(Элемент)
	
	ОбновитьРеквизитыСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПортСервераЛицензииПриИзменении(Элемент)
	
	ОбновитьРеквизитыСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КодДоступаСервераЛицензииПриИзменении(Элемент)
	
	ОбновитьРеквизитыСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсональныеНастройкиПриИзменении(Элемент)
	СохранятьНастройкиПодключения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)
	
	Если НЕ СохраненыПерсональныеНастройки Тогда
		СохранитьНастройкиМенеджераСерииЗащиты(Неопределено);
	КонецЕсли;
	
	ОписаниеОшибки = "";
	слкМенеджерЗащиты.ПроверитьЛицензиюСеанса(СерияЛицензии, ОписаниеОшибки, "ПодключитьЕслиНеЗапущен");
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		слкМенеджерЗащиты.СообщитьОбОшибкеСЛК(ОписаниеОшибки);
	КонецЕсли;
	
	Оповестить("слкИзмененСоставЛицензий");
	
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	
	ОписаниеОшибки = "";
	слкМенеджерЗащиты.ПроверитьЛицензиюСеанса(СерияЛицензии, ОписаниеОшибки, "Отключить");
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		слкМенеджерЗащиты.СообщитьОбОшибкеСЛК(ОписаниеОшибки);
	КонецЕсли;
	
	Оповестить("слкИзмененСоставЛицензий");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСерииКлючейЗащиты(Команда)
	ЗаполнитьСерииКлючейЗащитыСервер();
	
	Если ЗначениеЗаполнено(ТаблицаСерийКлючейЗащиты) Тогда
		ОбновитьРеквизитыШапки(ЭтотОбъект, ТаблицаСерийКлючейЗащиты[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	слкМенеджерЗащитыКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенныеНастройки(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РасширенныеНастройкиЗакрытиеФормы", ЭтотОбъект);
	ОткрытьФорму("Обработка.слкМенеджерЛицензий.Форма.МенеджерЛицензийСЛК",,,,,, ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСерииКлючейЗащитыСервер(Активные = Истина)
	
	Обработки.слкМенеджерЛицензий.ЗаполнитьСерииКлючейЗащитыСервер(ЭтотОбъект, Активные);
	
	СписокВыбораНаименований = Элементы.НаименованиеЛицензии.СписокВыбора;
	СписокВыбораНаименований.Очистить();
	
	Для Каждого СтрокаСерииЗащиты Из ТаблицаСерийКлючейЗащиты Цикл
		Если СтрНайти(СтрокаСерииЗащиты.Наименование, СтрокаСерииЗащиты.Серия) = 0 Тогда
			СтрокаСерииЗащиты.Наименование = СтрШаблон(НСтр("ru='%2 (%1)'"), СтрокаСерииЗащиты.Серия, СтрокаСерииЗащиты.Наименование);
		КонецЕсли;
		СписокВыбораНаименований.Добавить(СтрокаСерииЗащиты.Серия, СтрокаСерииЗащиты.Наименование);
	КонецЦикла;
	
	ОбновитьСтатусыСерийКлючейЗащитыСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусыСерийКлючейЗащитыСервер(КомандаМенеджераЗащиты = "ПроверитьСтатус")
	
	Обработки.слкМенеджерЛицензий.ОбновитьСтатусыСерийКлючейЗащитыСервер(ЭтотОбъект, КомандаМенеджераЗащиты);
	
	Для Каждого СерияКлючейЗащиты Из ТаблицаСерийКлючейЗащиты Цикл
		Если СерияКлючейЗащиты.Серия = СерияЛицензии Тогда
			ОбновитьРеквизитыШапки(ЭтотОбъект, СерияКлючейЗащиты);
		КонецЕсли;
	КонецЦикла;
	ОбновитьСтатистикуПодключений();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиМенеджераСерииЗащиты(Команда)
	
	СохранитьНастройкиМенеджераСерииЗащитыНаСервере(СерияЛицензии);
	СохраненыПерсональныеНастройки = Истина;
	
	Модифицированность = Ложь;
	ЗаполнитьСерииКлючейЗащитыСервер();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиМенеджераСерииЗащитыНаСервере(Серия)
	
	Обработки.слкМенеджерЛицензий.СохранитьНастройкиМенеджераСерииЗащитыНаСервере(ЭтотОбъект, Серия, ПерсональныеНастройки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьРеквизитыШапки(Форма, СтрокаТаблицы = Неопределено)
	
	СоответствиеРеквизитов = СоответствиеРеквизитовТаблицыИФормы();
	Для Каждого ТекРеквизит Из СоответствиеРеквизитов Цикл
		
		Если СтрокаТаблицы = Неопределено Тогда
			Форма[ТекРеквизит.Ключ] = "";
		Иначе
			Форма[ТекРеквизит.Ключ] = СтрокаТаблицы[ТекРеквизит.Значение];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьРеквизитыСтроки(Форма)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Серия", Форма.СерияЛицензии);
	МассивСтрок = Форма.ТаблицаСерийКлючейЗащиты.НайтиСтроки(СтруктураПоиска);
	Если НЕ ЗначениеЗаполнено(МассивСтрок) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = МассивСтрок[0];
	
	СоответствиеРеквизитов = СоответствиеРеквизитовТаблицыИФормы();
	Для Каждого ТекРеквизит Из СоответствиеРеквизитов Цикл
		СтрокаТаблицы[ТекРеквизит.Значение] = Форма[ТекРеквизит.Ключ];
	КонецЦикла;
	
	Форма.СохраненыПерсональныеНастройки = Ложь;
	
КонецПроцедуры

&НаСервере
Функция НомерАктуальнойВерсииКомпонентыСЛК()
	
	Результат = "";
	
	// Пример синонима "Внешняя компонента СЛК (licenceaddin-3.0.29.10180-template.zip)".
	ОписаниеКомпоненты = Метаданные.ОбщиеМакеты.слкКомпонентаЗащиты.Синоним;
	ИндексНачала = СтрНайти(ОписаниеКомпоненты, "licenceaddin-");
	Если ИндексНачала = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеКомпоненты = СтрЗаменить(ОписаниеКомпоненты, Лев(ОписаниеКомпоненты,ИндексНачала-1), "");
	ОписаниеКомпоненты = СтрЗаменить(ОписаниеКомпоненты, ".zip", "");
	
	ДопустимыеСимволы = "0123456789.";
	ДлинаВерсия = СтрДлина(ОписаниеКомпоненты);
	МассивВерсии = Новый Массив;
	Для Счет = 1 По ДлинаВерсия Цикл
		ТекСимвол = Сред(ОписаниеКомпоненты, Счет,1);
		Если СтрНайти(ДопустимыеСимволы, ТекСимвол) = 0 Тогда
			Продолжить;
		КонецЕсли;
		МассивВерсии.Добавить(ТекСимвол);
	КонецЦикла;
	Результат = СтрСоединить(МассивВерсии);
	
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СоответствиеРеквизитовТаблицыИФормы()

	Результат = Новый Структура;
	Результат.Вставить("СерияЛицензии", "Серия");
	Результат.Вставить("КоличествоКлючейЛицензии", "КоличествоКлючей");
	Результат.Вставить("АдресСервераЛицензии", "Host");
	Результат.Вставить("ПортСервераЛицензии", "Port");
	Результат.Вставить("КодДоступаСервераЛицензии", "КодДоступа");
	Результат.Вставить("ЗапущенЛицензия", "Запущен");

	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатистикуПодключений()
	
	ТекстШаблона = НСтр("ru='Видов лицензий: %1 | Подключено: %2'");

	КоличествоВидовЛицензий = 0;
	ПодключенныеЛицензии = Новый Массив;
	Для Каждого ТекСтрока Из ТаблицаСерийКлючейЗащиты Цикл
		КоличествоВидовЛицензий = КоличествоВидовЛицензий + 1;
		Если НЕ ТекСтрока.Запущен Тогда
			Продолжить;
		КонецЕсли;
		
		ПодключенныеЛицензии.Добавить(ТекСтрока.Серия);
		
	КонецЦикла;
	
	ПодключенныеЛицензииСтрокой = СтрСоединить(ПодключенныеЛицензии, ", ");
	Если НЕ ЗначениеЗаполнено(ПодключенныеЛицензии) Тогда
		ПодключенныеЛицензииСтрокой = НСтр("ru='-'");
	КонецЕсли;
	
	Элементы.ДекорацияСтатистикаПодключений.Заголовок = СтрШаблон(ТекстШаблона, 
	Формат(КоличествоВидовЛицензий,"ЧН=0"),
		ПодключенныеЛицензииСтрокой);
		
	ТекстШаблона = НСтр("ru='Версия используемого модуля компоненты СЛК: %1'");
	Элементы.ВерсияКомпонентыСЛК.Заголовок = СтрШаблон(ТекстШаблона, ВерсияКомпонентыСЛК);
		
КонецПроцедуры

&НаКлиенте
Процедура СохранятьНастройкиПодключения()
	
	СохранитьНастройкиМенеджераСерииЗащиты(Неопределено);
	ОбновитьСтатусыСерийКлючейЗащитыСервер("ПодключитьЕслиНеЗапущен");
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенныеНастройкиЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт
	Закрыть();
КонецПроцедуры

#КонецОбласти
