
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	МенеджерЗащиты = слкМенеджерЗащитыПовтИсп.ПолучитьМенеджерЗащиты(Истина);
	Если МенеджерЗащиты.Количество() = 1 Тогда
		ИспользоватьУпрощеннуюФорму = Истина;
	Иначе
		ИспользоватьУпрощеннуюФорму = Ложь;
	КонецЕсли;
	
	Если ВидФормы = "Форма" Тогда
		слкМенеджерЗащитыПереопределяемый.ПриОбработкеПолученияФормыМенеджераЛицензий(ИспользоватьУпрощеннуюФорму)
	КонецЕсли;
	
	Если ИспользоватьУпрощеннуюФорму Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "МенеджерЛицензийСЛКУпрощенный";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка, Форма) Экспорт
	
	Форма.ВерсияКомпонентыСЛК = слкМенеджерЗащиты.ВерсияКомпонентыСЛК();
	Форма.Элементы.ТаблицаСерийКлючейЗащитыОтключить.Видимость = НЕ слкМенеджерЗащитыПовтИсп.ЭтоКлиентСервернаяБаза();
	
	Форма.СохраненыПерсональныеНастройки = Ложь;
	
	слкМенеджерЗащитыПереопределяемый.МенеджерЛицензийСЛКПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ЗаполнитьСерииКлючейЗащитыСервер(Форма, Активные = Истина) Экспорт
	
	Форма.ТаблицаСерийКлючейЗащиты.Очистить();
	Форма.ТаблицаФайлыДанных.Очистить();
	
	МенеджерЗащиты = слкМенеджерЗащитыПовтИсп.ПолучитьМенеджерЗащиты(Истина);
	Для Каждого СерияКлючейЗащиты Из МенеджерЗащиты Цикл
		
		СтрокаСерииЗащиты = Форма.ТаблицаСерийКлючейЗащиты.Добавить();
		СтрокаСерииЗащиты.Серия = СерияКлючейЗащиты.Ключ;
		СтрокаСерииЗащиты.Наименование = СерияКлючейЗащиты.Значение.Наименование;
		СтрокаСерииЗащиты.ТолькоНаличиеКлюча = СерияКлючейЗащиты.Значение.ТолькоНаличиеКлюча;
		
		ЗаполнитьЗначенияСвойств(СтрокаСерииЗащиты, СерияКлючейЗащиты.Значение.ПараметрыПодключения);
		
		ДобавитьФайлыДанных(Форма, СтрокаСерииЗащиты.Серия, СерияКлючейЗащиты.Значение.ПараметрыПодключения.ФайлыДанных);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьСтатусыСерийКлючейЗащитыСервер(Форма, КомандаМенеджераЗащиты = "ПроверитьСтатус") Экспорт
	
	Для каждого СерияКлючейЗащиты Из Форма.ТаблицаСерийКлючейЗащиты Цикл
		СерияКлючейЗащиты.Запущен = слкМенеджерЗащиты.ПроверитьЛицензиюСеанса(СерияКлючейЗащиты.Серия,, КомандаМенеджераЗащиты);
		
		// Количество ключей по регистрационным номерам
		Если СерияКлючейЗащиты.Запущен Тогда
			РегНомераКлючей = слкМенеджерЗащиты.ПолучитьРегНомераКлючей(СерияКлючейЗащиты.Серия);
			Если ЗначениеЗаполнено(РегНомераКлючей) Тогда
				СерияКлючейЗащиты.КоличествоКлючей = СтрРазделить(РегНомераКлючей, Символы.ПС).Количество();
			Иначе
				СерияКлючейЗащиты.КоличествоКлючей = 0;
			КонецЕсли;
		Иначе
			СерияКлючейЗащиты.КоличествоКлючей = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СохранитьНастройкиМенеджераСерииЗащитыНаСервере(Форма, Серия, ПерсональныеНастройки = Ложь) Экспорт
	
	ПараметрыПодключенияСерииКлючейЗащиты = ПолучитьПараметрыПодключенияСерииКлючейЗащиты(Форма, Серия);
	Если ПараметрыПодключенияСерииКлючейЗащиты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПерсональныеНастройки Тогда
		слкМенеджерЗащитыСервер.СохранитьПерсональныеНастройкиМенеджераСерииЗащиты(Серия, ПараметрыПодключенияСерииКлючейЗащиты);
	Иначе
		слкМенеджерЗащитыСервер.УдалитьПерсональныеНастройкиМенеджераСерииЗащиты(Серия);
		слкМенеджерЗащитыСервер.СохранитьНастройкиМенеджераСерииЗащиты(Серия, ПараметрыПодключенияСерииКлючейЗащиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьФайлыДанных(Форма, Серия, ФайлыДанных)
	
	Для каждого ФайлДанных Из ФайлыДанных Цикл
		СтрокаФайлаЗащиты = Форма.ТаблицаФайлыДанных.Добавить();
		СтрокаФайлаЗащиты.Серия = Серия;
		СтрокаФайлаЗащиты.Макет = ФайлДанных.Ключ;
		СтрокаФайлаЗащиты.Псевдоним = ФайлДанных.Значение;
	КонецЦикла;
	
КонецПроцедуры // ДобавитьФайлыДанных()

Функция ПолучитьПараметрыПодключенияСерииКлючейЗащиты(Форма, Серия)

	ПараметрыПодключения = слкМенеджерЗащитыСервер.ПолучитьНастройкиМенеджераСерииЗащиты();
	
	СтрокиСерииЗащиты = Форма.ТаблицаСерийКлючейЗащиты.НайтиСтроки(Новый Структура("Серия", Серия));
	Если СтрокиСерииЗащиты.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтрокаСерииЗащиты = СтрокиСерииЗащиты[0];
	
	ЗаполнитьЗначенияСвойств(ПараметрыПодключения, СтрокаСерииЗащиты);
	
	// Файлы данных
	ФайлыДанныхСЛК = Новый Соответствие;
	СтрокиФайловДанных = Форма.ТаблицаФайлыДанных.НайтиСтроки(Новый Структура("Серия", СтрокаСерииЗащиты.Серия));
	Для каждого СтрокаФайла Из СтрокиФайловДанных Цикл
		ФайлыДанныхСЛК.Вставить(СтрокаФайла.Макет, СтрокаФайла.Псевдоним);
	КонецЦикла;
	ПараметрыПодключения.ФайлыДанных = ФайлыДанныхСЛК;
	
	Возврат ПараметрыПодключения;
	
КонецФункции

#КонецОбласти

#КонецЕсли