
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаСоздаватьМЛ = Истина;
	
	ЭтоКОРП = уатОбщегоНазначенияПовтИсп.ВариантПоставкиКОРП();
	ЭтоСТД  = уатОбщегоНазначенияПовтИсп.ВариантПоставкиСТД();
	
	Элементы.НастройкаПроверятьНахождениеВЗонеПоФактическомуМестоположениюТС.Видимость = ЭтоКОРП;
	Элементы.НастройкаВремяАктуальностиКоординат.Видимость = ЭтоКОРП;
	Элементы.НастройкаРежимОткрытияКарты.Видимость         = НЕ ЭтоСТД;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВладелецФормы,
			уатАРМКлиент.ПолучитьНастройкиАРМДиспетчера());
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОрганизацияМЛИсточникПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаПечататьМаршрутныеЛистыПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПечататьПЛПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСоздаватьПЛПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПроверятьНахождениеВЗонеПоФактическомуМестоположениюТСПриИзменении(Элемент)
	Если НастройкаПроверятьНахождениеВЗонеПоФактическомуМестоположениюТС Тогда
		Если НастройкаВремяАктуальностиКоординат = 0 Тогда
			НастройкаВремяАктуальностиКоординат = 60;
		КонецЕсли;
	Иначе
		Если НастройкаВремяАктуальностиКоординат <> 0 Тогда
			НастройкаВремяАктуальностиКоординат = 0;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрыЗакрытия = Новый Структура(уатАРМКлиент.ПолучитьНастройкиАРМДиспетчера());
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ЭтотОбъект);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Если Элементы.НастройкаПечататьПЛ.Доступность <> НастройкаСоздаватьПЛ Тогда
		Элементы.НастройкаПечататьПЛ.Доступность  = НастройкаСоздаватьПЛ;
	КонецЕсли;
	Если Элементы.НастройкаКопийПечатныхФормМЛ.Доступность <> НастройкаПечататьМаршрутныеЛисты Тогда
		Элементы.НастройкаКопийПечатныхФормМЛ.Доступность = НастройкаПечататьМаршрутныеЛисты;
	КонецЕсли;
	Если Элементы.НастройкаКопийПечатныхФормПЛ.Доступность <> НастройкаПечататьПЛ Тогда
		Элементы.НастройкаКопийПечатныхФормПЛ.Доступность = НастройкаПечататьПЛ;
	КонецЕсли;
	Если Элементы.НастройкаПечататьСразуНаПринтер.Доступность
		<> (НастройкаПечататьМаршрутныеЛисты ИЛИ НастройкаПечататьПЛ) Тогда
		Элементы.НастройкаПечататьСразуНаПринтер.Доступность = (НастройкаПечататьМаршрутныеЛисты ИЛИ НастройкаПечататьПЛ);
	КонецЕсли;
	Если Элементы.НастройкаПечатнаяФормаМЛ.Доступность <> НастройкаПечататьМаршрутныеЛисты Тогда
		Элементы.НастройкаПечатнаяФормаМЛ.Доступность = НастройкаПечататьМаршрутныеЛисты;
	КонецЕсли;
	Если Элементы.НастройкаПечатнаяФормаПЛ.Доступность <> НастройкаПечататьПЛ Тогда
		Элементы.НастройкаПечатнаяФормаПЛ.Доступность = НастройкаПечататьПЛ;
	КонецЕсли;
	Если Элементы.НастройкаОрганизацияМЛ.Доступность <> (НастройкаОрганизацияМЛИсточник = 2) Тогда
		Элементы.НастройкаОрганизацияМЛ.Доступность = (НастройкаОрганизацияМЛИсточник = 2);
	КонецЕсли;
	Элементы.НастройкаВремяАктуальностиКоординат.Видимость = НастройкаПроверятьНахождениеВЗонеПоФактическомуМестоположениюТС;
	
КонецПроцедуры

#КонецОбласти