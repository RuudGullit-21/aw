#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивОшибок") И ТипЗнч(Параметры.МассивОшибок) = Тип("Массив") Тогда 
		Для Сч = 1 По Параметры.МассивОшибок.Количество() Цикл 
			НоваяОшибка = ТаблицаОшибок.Добавить();
			НоваяОшибка.НомерОшибки    = Сч;
			НоваяОшибка.ОписаниеОшибки = Параметры.МассивОшибок[Сч-1];
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("Пояснение") Тогда 
		Пояснение = Параметры.Пояснение;
	КонецЕсли;
	
	Если Параметры.Свойство("Режим") Тогда 
		УстановитьРежимРаботыФормы(Параметры.Режим);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Закрыть("ОК");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПродолжить(Команда)
	
	Закрыть("Продолжить");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть("Отмена");
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьРежимРаботыФормы(Знач Режим)
	
	Если Режим = "Ошибка" Тогда 
		Элементы.ФормаОК.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаПродолжить.Видимость = Ложь;
		Элементы.ФормаОтмена.Видимость     = Ложь;
		
		Элементы.Пояснение.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
		
	ИначеЕсли Режим = "Предупреждение" Тогда 
		Элементы.ФормаОК.Видимость                 = Ложь;
		Элементы.ФормаПродолжить.КнопкаПоУмолчанию = Истина;
		
		Элементы.Пояснение.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
