#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ОбъектНастройкиПрав") Тогда
		ОбъектНастройкиПрав = Параметры.ОбъектНастройкиПрав;
	КонецЕсли;
	Если Параметры.Свойство("ТипОбъекта") Тогда
		ТипОбъекта = Параметры.ТипОбъекта;
	КонецЕсли;
	
	ОбъектИсточник = ОбъектНастройкиПрав;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		
		ТекстНСТР = НСтр("en='Immediate opening for this object is prohibited!';ru='Непосредственное открытие для данного объекта запрещено!'");
		ПоказатьПредупреждение(Неопределено, ТекстНСТР, 10);
		Возврат;
	КонецЕсли;
	
	НастроитьТипы();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	НастроитьТипы();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если уатОбщегоНазначения.уатЗначениеНеЗаполнено(ОбъектИсточник) Тогда
		ТекстНСТР = НСтр("en='Source object is not selected!';ru='Объект источник не выбран!'");
		ПоказатьПредупреждение(Неопределено, ТекстНСТР);
		Возврат;
	КонецЕсли;
	Если уатОбщегоНазначения.уатЗначениеНеЗаполнено(ОбъектПриемник) Тогда
		ТекстНСТР = НСтр("en='Object receiver is not selected!';ru='Объект приемник не выбран!'");
		ПоказатьПредупреждение(Неопределено, ТекстНСТР);
		Возврат;
	КонецЕсли;
	Если ОбъектИсточник = ОбъектПриемник Тогда
		ТекстНСТР = НСтр("en='Source object and receiver object are the same!';ru='Объект источник и объект приемник совпадают!'");
		ПоказатьПредупреждение(Неопределено, ТекстНСТР);
		Возврат;
	КонецЕсли;
	
	СкопироватьНаборПрав(ОбъектИсточник, ОбъектПриемник);
	
	Если ОбъектПриемник = ОбъектНастройкиПрав Тогда
		ТекстНСТР = НСтр("en=""Update the current user's rights ..."";ru='Обновление прав текущего объекта ...'");
		Состояние(ТекстНСТР);
		ОбновитьПовторноИспользуемыеЗначения();
		Оповестить("ВыполненоКопированиеПравИНастроек");
	КонецЕсли; 
	
	ТекстНСТР = НСтр("en='Permission set of object ""%1"" copied to object ""%2"".';ru='Набор прав объекта ""%1"" скопирован объекту ""%2"".'");
	ТекстНСТР = СтрШаблон(ТекстНСТР, Строка(ОбъектИсточник), Строка(ОбъектПриемник));
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстНСТР);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// настройка типов для полей ввода ОбъектИсточник и ОбъектПриемник
&НаКлиенте
Процедура НастроитьТипы()
	МассивТипов = Новый Массив;
	Если ТипОбъекта = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Организация") Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
	ИначеЕсли ТипОбъекта = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Пользователь")
		И ТипЗнч(ОбъектИсточник) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.ВнешниеПользователи"));
	ИначеЕсли ТипОбъекта = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Пользователь") Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
	ИначеЕсли ТипОбъекта = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Подразделение") Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.ПодразделенияОрганизаций"));
	Иначе
		ТипОбъекта = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Пользователь");
		МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
		ОбъектИсточник = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
	Описание = Новый ОписаниеТипов(МассивТипов);
	Элементы.ОбъектИсточник.ОграничениеТипа = Описание;
	ОбъектИсточник = Описание.ПривестиЗначение(ОбъектИсточник);
	Элементы.ОбъектПриемник.ОграничениеТипа = Описание;
	ОбъектПриемник = Описание.ПривестиЗначение(ОбъектПриемник);
КонецПроцедуры

// копирование набора прав для другого пользователя/организации/подразделения
&НаСервереБезКонтекста
Процедура СкопироватьНаборПрав(ОбъектИсточник, ОбъектПриемник)
	НаборИсточник = РегистрыСведений.уатПраваИНастройки.СоздатьНаборЗаписей();
	НаборИсточник.Отбор.Объект.Установить(ОбъектИсточник);
	НаборИсточник.Прочитать();
	НаборПриемник = РегистрыСведений.уатПраваИНастройки.СоздатьНаборЗаписей();
	НаборПриемник.Отбор.Объект.Установить(ОбъектПриемник);
	НаборПриемник.Очистить();
	Для Каждого Запись Из НаборИсточник Цикл
		НоваяЗапись = НаборПриемник.Добавить();
		НоваяЗапись.Объект = ОбъектПриемник;
		НоваяЗапись.ПравоНастройка = Запись.ПравоНастройка;
		НоваяЗапись.Значение = Запись.Значение;
	КонецЦикла;
	НаборПриемник.Записать();
КонецПроцедуры

#КонецОбласти
