
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("ИмяОбъектаВыбора") Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭтоЗапрос = Истина;
	Если Параметры.ИмяОбъектаВыбора = "АЗС" Тогда
		Заголовок = НСтр("ru = 'Выберите АЗС'");
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	уатАЗС.Ссылка
		|ИЗ
		|	Справочник.уатАЗС КАК уатАЗС
		|ГДЕ
		|	НЕ уатАЗС.ПометкаУдаления
		|	И уатАЗС.ЭтоАЗССклад = &ЭтоАЗССклад
		|УПОРЯДОЧИТЬ ПО
		|	уатАЗС.Наименование");
		Запрос.УстановитьПараметр("ЭтоАЗССклад",
			Параметры.Свойство("ЭтоАЗССклад") И Параметры.ЭтоАЗССклад);
				
	ИначеЕсли Параметры.ИмяОбъектаВыбора = "Прицеп" Тогда
		Заголовок = НСтр("ru = 'Выберите прицеп или оборудование'");
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	уатТС.Ссылка
		|ИЗ
		|	Справочник.уатТС КАК уатТС
		|ГДЕ
		|	НЕ уатТС.ПометкаУдаления
		|	И (уатТС.ВидМоделиТС = ЗНАЧЕНИЕ(Перечисление.уатВидыМоделейТС.Прицеп)
		|	ИЛИ уатТС.ВидМоделиТС = ЗНАЧЕНИЕ(Перечисление.уатВидыМоделейТС.Оборудование))
		|
		|УПОРЯДОЧИТЬ ПО
		|	уатТС.Наименование");
		
	ИначеЕсли Параметры.ИмяОбъектаВыбора = "УсловиеРаботы" Тогда
		Заголовок = НСтр("ru = 'Выберите условие работы'");
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	уатУсловияРаботы.Ссылка
		|ИЗ
		|	Справочник.уатУсловияРаботы КАК уатУсловияРаботы
		|ГДЕ
		|	НЕ уатУсловияРаботы.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	уатУсловияРаботы.Наименование");
				
	ИначеЕсли Параметры.ИмяОбъектаВыбора = "ВидПростоя" Тогда
		Заголовок = НСтр("ru = 'Выберите вид простоя'");
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	уатСостояниеТС.Ссылка
		|ИЗ
		|	Справочник.уатСостояниеТС КАК уатСостояниеТС
		|ГДЕ
		|	НЕ уатСостояниеТС.ПометкаУдаления
		|	И уатСостояниеТС.ВидСостояния = ЗНАЧЕНИЕ(Перечисление.уатВидыСостоянийТС.ВПростое)
		|
		|УПОРЯДОЧИТЬ ПО
		|	уатСостояниеТС.Наименование");
		
	ИначеЕсли Параметры.ИмяОбъектаВыбора = "Список" Тогда
		Если НЕ Параметры.Свойство("СписокОбъектов") Тогда
			СтандартнаяОбработка = Ложь;
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		Заголовок = Параметры.Заголовок;
		ЭтоЗапрос = Ложь;
	Иначе
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЭтоЗапрос Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока        = Список.Добавить();
			НоваяСтрока.Ссылка = Выборка.Ссылка;
		КонецЦикла;
	Иначе
		Если ТипЗнч(Параметры.СписокОбъектов) = Тип("СписокЗначений") Тогда
			Для Каждого ТекСтрока Из Параметры.СписокОбъектов Цикл
				НоваяСтрока        = Список.Добавить();
				НоваяСтрока.Ссылка = ТекСтрока.Значение;
			КонецЦикла;
		Иначе
			НоваяСтрока        = Список.Добавить();
			НоваяСтрока.Ссылка = Параметры.СписокОбъектов;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Закрыть(Элементы.Список.ТекущиеДанные.Ссылка);
КонецПроцедуры

#КонецОбласти
