
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("ИмяОбъектаВыбора") Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ВыбранныйСправочник.Ссылка КАК Ссылка,
		               |	ВыбранныйСправочник.Родитель КАК Родитель,
		               |	ВыбранныйСправочник.Наименование КАК Наименование
		               |ИЗ
		               |	%1 КАК ВыбранныйСправочник
		               |ГДЕ
		               |	НЕ ВыбранныйСправочник.ПометкаУдаления";

	Если Параметры.ИмяОбъектаВыбора = "Контрагент" Тогда
		Заголовок = НСтр("ru = 'Выберите контрагента'");
		НаименованиеСправочника = "Справочник.Контрагенты";
	ИначеЕсли Параметры.ИмяОбъектаВыбора = "ОбъектСтроительства" Тогда
		Заголовок = НСтр("ru = 'Выберите объект строительства'");
		НаименованиеСправочника = "Справочник.уатОбъектыСтроительства";
	Иначе
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, НаименованиеСправочника);
	Список.ТекстЗапроса = ТекстЗапроса + "
	|	И ВыбранныйСправочник.ЭтоГруппа = ЛОЖЬ";
	Дерево.ТекстЗапроса = ТекстЗапроса + "
	|	И ВыбранныйСправочник.ЭтоГруппа = ИСТИНА";
	Список.ОсновнаяТаблица = НаименованиеСправочника;
	Дерево.ОсновнаяТаблица = НаименованиеСправочника;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Закрыть(Элементы.Список.ТекущиеДанные.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииЯчейки(Элемент)
	ТекДанные = Элементы.Дерево.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Родитель", ПредопределенноеЗначение(НаименованиеСправочника + ".ПустаяСсылка"),,,Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Родитель", ТекДанные.Ссылка,,,Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
