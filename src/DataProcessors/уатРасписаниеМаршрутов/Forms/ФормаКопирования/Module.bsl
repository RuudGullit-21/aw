
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Рейс") Тогда
		Рейс = Параметры.Рейс;
		ТекстНСтр = НСтр("ru='Укажите смещение по времени относительно копируемого рейса %1'; en='Copying trip %1. Input time diffetence'");
		ТекстНСтр = СтрШаблон(ТекстНСтр, Рейс.НомерРейса);
		Элементы.ВременноеСмещение.Подсказка = ТекстНСтр;
		ЭтаФорма.Заголовок = НСтр("ru = 'Копирование рейса'; en = 'Copying trip'");
	ИначеЕсли Параметры.Свойство("Выход") Тогда
		Выход = Параметры.Выход;
		ТекстНСтр = НСтр("ru='Укажите смещение по времени относительно копируемого выхода %1'; en='Copying departure %1. Input time diffetence'");
		ТекстНСтр = СтрШаблон(ТекстНСтр, СокрЛП(Выход));
		Элементы.ВременноеСмещение.Подсказка = ТекстНСтр;
		ЭтаФорма.Заголовок = НСтр("ru = 'Копирование выхода'; en = 'Copying departure'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОсновныеДействияФормыОК(Команда)
	Если ЗначениеЗаполнено(Рейс) Тогда
		Результат = Новый Структура("Рейс, НомерНовогоРейса, Смещение", Рейс, НомерНовогоРейса, ВременноеСмещение);
	Иначе
		Результат = Новый Структура("Выход, Смещение", Выход, ВременноеСмещение);
	КонецЕсли;
	Закрыть(Результат);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимость()
	Если ЗначениеЗаполнено(Рейс) Тогда
		Элементы.НомерНовогоРейса.Видимость = Истина;
	Иначе
		Элементы.НомерНовогоРейса.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти