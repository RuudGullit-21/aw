
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТипВнешнейСистемы", ТипВнешнейСистемы);
	
	Если НЕ ЗначениеЗаполнено(ТипВнешнейСистемы) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ВнешняяСистема", ВнешняяСистема);

	НаименованиеПС             = ВнешняяСистема.НаименованиеПС;
	ВнешняяСистемаНаименование = ВнешняяСистема.Наименование;
	
	ТипВнешнейСистемы = Параметры.ТипВнешнейСистемы;
	Если ТипВнешнейСистемы = Перечисления.уатТипыВнешнихСистем.СистемаМониторинга Тогда
		
		Параметры.Свойство("ЗагрузитьПробегИРасходГСМ",       СистемаМониторинга_ЗагрузитьПробегИРасходГСМ);
		Параметры.Свойство("ЗагрузитьДополнительныеСведения", СистемаМониторинга_ЗагрузитьДополнительныеСведения);
		Параметры.Свойство("ЗагрузитьКоординаты",             СистемаМониторинга_ЗагрузитьКоординаты);
		Параметры.Свойство("ИнтервалЗагрузки",                СистемаМониторинга_ИнтервалЗагрузки);
		
		МассивТС = Параметры.МассивТС;
		Для Каждого ТекТС Из МассивТС Цикл
			НоваяСтрока		 = СистемаМониторинга_ТаблТС.Добавить();
			НоваяСтрока.ТС	 = ТекТС;
		КонецЦикла;

		Элементы.СтраницыНастроек.ТекущаяСтраница = Элементы.СтраницаСистемаМониторинга;
		
		Элементы.СистемаМониторинга_ЗаполнитьВсемиТС.Заголовок = 
		СтрШаблон(Нстр("en = 'Все ТС (%1)'; ru = 'Все ТС (%1)'"), ВнешняяСистемаНаименование);
		
		УстановитьСписокВыбораВариантовПериодичностиРегламентногоЗадания();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ПараметрыЗакрытия = Новый Структура();
	
	ПараметрыЗакрытия.Вставить("ЗагрузитьПробегИРасходГСМ",       СистемаМониторинга_ЗагрузитьПробегИРасходГСМ);
	ПараметрыЗакрытия.Вставить("ЗагрузитьДополнительныеСведения", СистемаМониторинга_ЗагрузитьДополнительныеСведения);
	ПараметрыЗакрытия.Вставить("ЗагрузитьКоординаты",             СистемаМониторинга_ЗагрузитьКоординаты);
	ПараметрыЗакрытия.Вставить("ИнтервалЗагрузки",                СистемаМониторинга_ИнтервалЗагрузки);
	
	МассивТС = Новый Массив;
	Для Каждого ТекСтрока Из СистемаМониторинга_ТаблТС Цикл
		МассивТС.Добавить(ТекСтрока.ТС);
	КонецЦикла;
	ПараметрыЗакрытия.Вставить("МассивТС", МассивТС);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Wialon_ТаблТС

&НаКлиенте
Процедура СистемаМониторинга_ТаблТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Состояние(НСтр("ru = 'Добавлено ТС'") + " <" + Строка(ВыбранноеЗначение) + ">",,, БиблиотекаКартинок.Информация32);
	
	НайдСтроки = СистемаМониторинга_ТаблТС.НайтиСтроки(Новый Структура("ТС", ВыбранноеЗначение));
	Если НайдСтроки.Количество() = 0 Тогда 
		НовСтрока	 = СистемаМониторинга_ТаблТС.Добавить();
		НовСтрока.ТС = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СистемаМониторинга_ЗаполнитьВсемиТС(Команда)
	
	Если СистемаМониторинга_ТаблТС.Количество() Тогда 
		ПоказатьВопрос(
			Новый ОписаниеОповещения("СистемаМониторинга_ЗаполнитьВсемиТСОчистка", ЭтотОбъект),
			НСтр("en='You want to clear the table before filling?';ru='Очистить таблицу перед заполнением?'"),
			РежимДиалогаВопрос.ДаНетОтмена
		);
		
	Иначе 
		СистемаМониторинга_ЗаполнитьВсемиТСЗавершение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_ОчиститьТаблицуТС(Команда)
	
	Если СистемаМониторинга_ТаблТС.Количество() Тогда 
		ПоказатьВопрос(
			Новый ОписаниеОповещения("СистемаМониторинга_ОчиститьТаблицуТСОчистка", ЭтотОбъект),
			НСтр("en='You want to clear the table?';ru='Очистить таблицу?'"),
			РежимДиалогаВопрос.ДаНет
		);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_РасширенноеЗаполнение(Команда)
	
	Если СистемаМониторинга_ТаблТС.Количество() Тогда 
		ПоказатьВопрос(
			Новый ОписаниеОповещения("СистемаМониторинга_РасширенноеЗаполнениеОчистка", ЭтотОбъект),
			НСтр("en='You want to clear the table before filling?';ru='Очистить таблицу перед заполнением?'"),
			РежимДиалогаВопрос.ДаНетОтмена
		);
		
	Иначе 
		СистемаМониторинга_РасширенноеЗаполнениеУстановитьОтбор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_ПодобратьТС(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.уатТС.ФормаВыбора", ПараметрыФормы, Элементы.СистемаМониторинга_ТаблТС,
		,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСписокВыбораВариантовПериодичностиРегламентногоЗадания()
	
	ЭлементСписок = Элементы.СистемаМониторинга_ИнтервалЗагрузки;
	ЭлементСписок.СписокВыбора.Очистить();
	
	Если ВнешняяСистема = Справочники.уатВнешниеСистемы.Wialon Тогда 
		ЭлементСписок.СписокВыбора.Добавить("Неделя",  НСтр("en='7 days';ru='7 дней'"));
		ЭлементСписок.СписокВыбора.Добавить("Сутки",   НСтр("en='24 hours';ru='24 часа'"));
		ЭлементСписок.СписокВыбора.Добавить("12часов", НСтр("en='12 hours';ru='12 часов'"));
		ЭлементСписок.СписокВыбора.Добавить("Час",     НСтр("en='1 hour';ru='1 час'"));
		
	ИначеЕсли ВнешняяСистема = Справочники.уатВнешниеСистемы.Omnicomm Тогда
		ЭлементСписок.СписокВыбора.Добавить("Месяц",     НСтр("en='1 month';ru='1 месяц'"));
		ЭлементСписок.СписокВыбора.Добавить("Неделя",    НСтр("en='7 days';ru='7 дней'"));
		ЭлементСписок.СписокВыбора.Добавить("Сутки",     НСтр("en='24 hours';ru='24 часа'"));
		ЭлементСписок.СписокВыбора.Добавить("12часов",   НСтр("en='12 hours';ru='12 часов'"));
		ЭлементСписок.СписокВыбора.Добавить("Час",       НСтр("en='1 hour';ru='1 час'"));
		ЭлементСписок.СписокВыбора.Добавить("Полчаса",   НСтр("en='30 minutes';ru='30 минут'"));
		ЭлементСписок.СписокВыбора.Добавить("15минут",   НСтр("en='15 minutes';ru='15 минут'"));
		
	Иначе 
		ЭлементСписок.СписокВыбора.Добавить("Год",       НСтр("en='12 months';ru='12 месяцев'"));
		ЭлементСписок.СписокВыбора.Добавить("Полугодие", НСтр("en='6 months';ru='6 месяцев'"));
		ЭлементСписок.СписокВыбора.Добавить("Квартал",   НСтр("en='3 months';ru='3 месяцев'"));
		ЭлементСписок.СписокВыбора.Добавить("Месяц",     НСтр("en='1 month';ru='1 месяц'"));
		ЭлементСписок.СписокВыбора.Добавить("Неделя",    НСтр("en='7 days';ru='7 дней'"));
		ЭлементСписок.СписокВыбора.Добавить("Сутки",     НСтр("en='24 hours';ru='24 часа'"));
		ЭлементСписок.СписокВыбора.Добавить("12часов",   НСтр("en='12 hours';ru='12 часов'"));
		ЭлементСписок.СписокВыбора.Добавить("Час",       НСтр("en='1 hour';ru='1 час'"));
		ЭлементСписок.СписокВыбора.Добавить("Полчаса",   НСтр("en='30 minutes';ru='30 минут'"));
		ЭлементСписок.СписокВыбора.Добавить("15минут",   НСтр("en='15 minutes';ru='15 минут'"));
		ЭлементСписок.СписокВыбора.Добавить("10минут",   НСтр("en='10 minutes';ru='10 минут'"));
		ЭлементСписок.СписокВыбора.Добавить("5минут",    НСтр("en='5 minutes';ru='5 минут'"));
		ЭлементСписок.СписокВыбора.Добавить("Минута",    НСтр("en='1 minute';ru='1 минуту'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СистемаМониторинга_ЗаполнитьВсемиТСЗавершение()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ВнешняяСистема", ВнешняяСистема);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	уатТС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.уатТС КАК уатТС
	|ГДЕ
	|	НЕ уатТС.ПометкаУдаления
	|	И НЕ уатТС.ЭтоГруппа
	|	И уатТС.ИспользуемаяСистемаGPS = &ВнешняяСистема
	|	И НЕ уатТС.ИДвСистемеНавигации = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НовСтрока	 = СистемаМониторинга_ТаблТС.Добавить();
		НовСтрока.ТС = Выборка.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_ОчиститьТаблицуТСОчистка(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		СистемаМониторинга_ТаблТС.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_РасширенноеЗаполнениеУстановитьОтбор()
	
	ОткрытьФорму(
		"ОбщаяФорма.уатФормаПодбораТС", 
		Новый Структура("ИсточникПодбора", "ОбщаяФорма_уатНастройкиМониторинга_" + ВнешняяСистемаНаименование + "_ТаблТС"), 
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("СистемаМониторинга_РасширенноеЗаполнениеЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_РасширенноеЗаполнениеОчистка(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		СистемаМониторинга_ТаблТС.Очистить();
		СистемаМониторинга_РасширенноеЗаполнениеУстановитьОтбор();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда 
		СистемаМониторинга_РасширенноеЗаполнениеУстановитьОтбор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_РасширенноеЗаполнениеЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекТС Из РезультатЗакрытия Цикл 
		НовСтрока	 = СистемаМониторинга_ТаблТС.Добавить();
		НовСтрока.ТС = ТекТС;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаМониторинга_ЗаполнитьВсемиТСОчистка(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		СистемаМониторинга_ТаблТС.Очистить();
		СистемаМониторинга_ЗаполнитьВсемиТСЗавершение();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда 
		СистемаМониторинга_ЗаполнитьВсемиТСЗавершение();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
