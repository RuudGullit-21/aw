#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Цех") = Неопределено Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	мсвЗаявок = Параметры.мсвЗаявок;
	
	Документ = мсвЗаявок[0];
	ПериодПредставление = НСтр("en='Period from';ru='Период с'") + " "
		+ Формат(Документ.ДатаНачала, "ДФ='dd.MM.yyyy HH:mm'") + " " + НСтр("en='till';ru='по'") + " "
		+ Формат(Документ.ДатаОкончания, "ДФ='dd.MM.yyyy HH:mm'");
		
	ТС = Документ.ТС;
	НомерТС = Строка(ТС) + " (" + ТС.Модель + ")";
	Для Сч = 1 По мсвЗаявок.Количество()-1 Цикл
		Пересечения.Добавить(мсвЗаявок[Сч]);
	КонецЦикла;
		
	Цех = Параметры.Цех;
	ТекДата = Параметры.ТекДата;
	
	Заголовок = Цех;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерТСНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Новый ОписаниеОповещения("НомерТСНажатиеЗавершение", ЭтотОбъект), ТС);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСНажатиеЗавершение(ДополнительныеПараметры) Экспорт
    Заглушка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДокументНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Новый ОписаниеОповещения("ДокументНажатиеЗавершение", ЭтотОбъект), Документ);
КонецПроцедуры

&НаКлиенте
Процедура ДокументНажатиеЗавершение(ДополнительныеПараметры) Экспорт
    Заглушка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПересеченияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ПоказатьЗначение(Новый ОписаниеОповещения("ПересеченияПередНачаломИзмененияЗавершение", ЭтотОбъект), Элементы.Пересечения.ТекущиеДанные.Значение);
КонецПроцедуры

&НаКлиенте
Процедура ПересеченияПередНачаломИзмененияЗавершение(ДополнительныеПараметры) Экспорт
    Заглушка = Истина;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	ЗапланированныйДок = "";
	Если ЕстьЗапланированные(ТекДата, ЗапланированныйДок) Тогда
		ТекстСообщения = НСтр("en='At the time';ru='На время'") + " " + Формат(ТекДата, "ДФ='dd.MM.yyyy HH:mm'") + " " + НСтр("en='already planned works on';ru='уже запланированы работы по'") + " " + ЗапланированныйДок + "!";
	КонецЕсли;
	
	ТекстНСТР = ТекстСообщения + НСтр("en='Continue planning?';ru='Продолжить планирование?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьДокументЗавершение", ЭтотОбъект),
		ТекстНСТР, РежимДиалогаВопрос.ДаНет, 10, КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Нет ИЛИ Ответ = КодВозвратаДиалога.Таймаут Тогда
        Возврат;
    КонецЕсли;
    
    ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("ДатаНачала", ТекДата);
    ПараметрыФормы.Вставить("ДатаОкончания", ТекДата + 3600);
    ПараметрыФормы.Вставить("Цех", Цех);
    ПараметрыФормы.Вставить("Мастер", уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Цех, "Мастер"));
    Если ТипЗнч(Документ) = Тип("ДокументСсылка.уатЗаявкаНаРемонт") Тогда
        ОткрытьФорму("Документ.уатЗаявкаНаРемонт.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ПараметрыФормы));
    Иначе
        ОткрытьФорму("Документ.уатЗаявкаНаРемонт.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ПараметрыФормы));
    КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЕстьЗапланированные(ТекДата, ЗапланированныйДок)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаРемонт.ДатаНачала,
	|	ЗаявкаНаРемонт.ДатаОкончания,
	|	""ЗнР "" + ЗаявкаНаРемонт.Номер КАК Представление
	|ИЗ
	|	Документ.уатЗаявкаНаРемонт КАК ЗаявкаНаРемонт
	|ГДЕ
	|	&НачПериода МЕЖДУ ЗаявкаНаРемонт.ДатаНачала И ЗаявкаНаРемонт.ДатаОкончания
	|	И ЗаявкаНаРемонт.Проведен
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	уатРемонтныйЛист.ДатаНачала,
	|	уатРемонтныйЛист.ДатаОкончания,
	|	""РЛ "" + уатРемонтныйЛист.Номер КАК Представление
	|ИЗ
	|	Документ.уатРемонтныйЛист КАК уатРемонтныйЛист
	|ГДЕ
	|	&НачПериода МЕЖДУ уатРемонтныйЛист.ДатаНачала И уатРемонтныйЛист.ДатаОкончания
	|	И уатРемонтныйЛист.Проведен");
	Запрос.УстановитьПараметр("НачПериода", ТекДата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗапланированныйДок = Выборка.Представление;
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти
